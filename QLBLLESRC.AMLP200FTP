       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    AMLP200   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2018-OUT-06.
     š*---------------------------------------------------------------*
     š*   APLICACO .......: AML - Anti Money Loundering               *
     š*   OBJECTIVO ......: Este programa destina-se ao Envio de      *
     š*                     Mensagens destinadas ao Temenos           *
     š*   ANALISTA .......: PEDRO GARCIA                              *
     š*   PROGRAMADOR.....:                                           *
     š*===============================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *------------------------------------------------------------------------*
       FILE-CONTROL.
      *------------------------------------------------------------------------*
           SELECT AMLFREQ
                  ASSIGN TO DATABASE-AMLFREQ
                  ORGANIZATION IS SEQUENTIAL
                  ACCESS MODE  IS SEQUENTIAL
                  FILE STATUS  IS GRS-FILE-STATUS.
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
       FD  AMLFREQ.
       01  REG-AMLFREQ.
           COPY DDS-ALL-FORMATS OF AMLFREQ.
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 GRS-FILE-STATUS            PIC XX.
      *
       01 EXECUTA-P001               PIC 9(01).
          88 INICIO-P001             VALUE 0.
          88 FIM-P001                VALUE 1.
      *
       01 PGM-STATUS                 PIC 9(01).
          88 SEM-ERRO                VALUE 0.
          88 COM-ERRO                VALUE 1.
      *
       01 EXISTE-SEPAR               PIC 9(01).
          88 SIM-SEPAR               VALUE 0.
          88 NAO-SEPAR               VALUE 1.
      *
       01 EXISTE-SEPAS               PIC 9(01).
          88 SIM-SEPAS               VALUE 0.
          88 NAO-SEPAS               VALUE 1.
      *
       01 WS-TRATA                   PIC 9(01).
          88 SIM-TRATA               VALUE 1.
          88 NAO-TRATA               VALUE 2.
      *
       01 WS-DIRECTION               PIC 9(01).
          88 INBOUND                 VALUE 1.
          88 OUTBOUND                VALUE 2.
          88 INTERNAL                VALUE 3.
      *
       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(8)    VALUE "AMLP200".

       01 Ws-dtCriacao.
          05 ws-Data         PIC X(10).
          05 ws-SepT         PIC X(1).
          05 ws-HH           PIC X(2).
          05 ws-SepH         PIC X(1).
          05 ws-MM           PIC X(2).
          05 ws-SepM         PIC X(1).
          05 ws-SS           PIC X(2).

     š* FCMMSG.YYYYMMDD.ttttttt.xml.---------------------------
       01 ws-idFich.
          05  ws-idFichAML   PIC X(7) VALUE "FCMMSG.".
          05  ws-idFichANO   PIC X(4) .
          05  ws-idFichMES   PIC X(2) .
          05  ws-idFichDIA   PIC X(2) .
          05  ws-idFichfil1  PIC X(1) .
          05  ws-idFichtrn   PIC X(11) .
          05  ws-idFichfil2  PIC X(1) .
          05  ws-idFichXML   PIC X(8) VALUE "XML".

     š* FCMMSG.YYYYMMDD.ttttttt.xml.---------------------------
       01 ws-idPath.
          05  ws-directory   PIC X(13) VALUE "/AML/REQUEST/".
          05  ws-idPathAML   PIC X(7)  VALUE "FCMMSG.".
          05  ws-idPathANO   PIC X(4) .
          05  ws-idPathMES   PIC X(2) .
          05  ws-idPathDIA   PIC X(2) .
          05  ws-idPathfil1  PIC X(1) .
          05  ws-idPathtrn   PIC X(7) .
          05  ws-idPathfil2  PIC X(1) .
          05  ws-idPathXML   PIC X(8) VALUE "XML".

     š* ------------------------------------------------------
       01 WS-STRING.
          05 WS-STRING-CHR         PIC X           OCCURS 300.
       01 WS-VAL-TAG-A             PIC X(80).
       01 WS-NOME-TAG              PIC X(80).
       01 WS-VAL-TAG-L             PIC S9(4)       COMP   VALUE 0.
       01 WS-NOM-TAG-L             PIC S9(4)       COMP   VALUE 0.
       01 WS-COUNT-CSV             PIC S9(1).
       01 WS-STRING-OPDOA3-1       PIC X(35).
       01 WS-STRING-OPDOA3-2       PIC X(35).
       01 WS-STRING-OPDOA3-3       PIC X(35).
       01 WS-STRING-OPDOA4-1       PIC X(35).
       01 WS-STRING-OPDOA4-2       PIC X(35).
       01 WS-STRING-OPDOA4-3       PIC X(35).
       01 WS-STRING-CSV.
          05 WS-STRING-CHR-CSV     PIC X           OCCURS 35.
     š* ------------------------------------------------------
     š* Determina Comprimento de uma String
       01 WS-POS-I                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-S                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-XML               PIC S9(4)       COMP   VALUE 0.
       01 WS-STRING-L              PIC S9(4)       COMP   VALUE 0.
       01 W-NUM                    PIC 9(3).
       01 Edit-Nbr-Tx PIC      -----9,99999.
       01 Edit-Nbr-Mn PIC      --------9,99.
       01 Edit-Nbr-In PIC      -----------9.

     š* Campos de Caracteres Invalidos
        01 CHAR-TESTE                PIC X.
           88 CARACTER-VALIDO      VALUE
      *                             - letras minusculas
                                          "a", "b", "c", "d", "e",
                                          "f", "g", "h", "i", "j",
                                          "k", "l", "m", "n", "o",
                                          "p", "q", "r", "s", "t",
                                          "u", "v", "w", "x", "y",
                                          "z",
      *                             - letras maiusculas
                                          "A", "B", "C", "D", "E",
                                          "F", "G", "H", "I", "J",
                                          "K", "L", "M", "N", "O",
                                          "P", "Q", "R", "S", "T",
                                          "U", "V", "W", "X", "Y",
                                          "Z",
      *                             - numeros
                                          "0", "1", "2", "3", "4",
                                          "5", "6", "7", "8", "9",
      *                             - outros
                                          " ", "/", "-", "?", ":",
                                          "(", ")", ".", ",",
                                          "+".

      * "'" passou a ser considerado invalido

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
     š* Parametros p/ Interface
       01 WS-ERRO                  PIC X(10).
     š* Área de Ligação para o FTP
      ** 01 AMLL001M-FICH-DESTINO   PIC  X(45).
         01 AMLL001M-FICH-ORIGEM    PIC  X(15).
         01 AMLL001M-FICH-DESTINO   PIC  X(50).
         01 AMLL001M-ERRO-FTP       PIC  X(01).
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".
       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA       PIC 9(04).
           03  FILLER             PIC X VALUE "-".
           03  WS-MESIN-DIA       PIC 9(02).
           03  FILLER             PIC X VALUE "-".
           03  WS-DIAIN-DIA       PIC 9(02).

       01 WS-NURACI               PIC X(07).
       01 WS-LPDTAA               PIC X(04).
       01 WS-LPDTMM               PIC X(02).
       01 WS-LPDTJJ               PIC X(02).
     š*
     š* Campos para Screening
     š*
     š* Cliente do Banco
       01 WS-CONTA-CLI.
          05 WS-RACINE-CLI        PIC X(7).
          05 WS-SEP1-CLI          PIC X(1).
          05 WS-GENRE-CLI         PIC X(3).
          05 WS-SEP2-CLI          PIC X(1).
          05 WS-RUB-CLI           PIC X(3).
          05 WS-SEP3-CLI          PIC X(1).
          05 WS-MON-CLI           PIC X(3).
       01 WS-NAME-CLI             PIC X(150).
       01 WS-COUNTRY-CLI          PIC X(2).
       01 WS-BKNAME-CLI           PIC X(30).
       01 WS-ADDRESS-CLI          PIC X(150).
       01 WS-CITY-CLI             PIC X(150).
       01 WS-CLIENTID             PIC X(07).
     š* Ordenante
       01 WS-IBAN-ORD             PIC X(30).
     š* OBeneficiário
       01 WS-IBAN-BEN              PIC X(30).
     š* Correspondente
       01 WS-BIC-COR               PIC X(30).
       01 WS-COUNTRY-COR           PIC X(2).
       01 WS-BKNAME-COR            PIC X(30).
     š* Contraparte
       01 WS-NAME-CR               PIC X(150).
       01 WS-BIC-CR                PIC X(30).
       01 WS-COUNTRY-CR            PIC X(2).
       01 WS-BKNAME-CR             PIC X(30).
       01 WS-ADDRESS-CR            PIC X(150).
       01 WS-CITY-CR               PIC X(150).
     š* Tratamento de Nome Completo
       01 WS-OPERADOR              PIC X(07).
       01 WS-TRFTYPE               PIC X(04).
       01 WS-VALOR                 PIC S9(13)V9(2).
       01 WS-RACINE                PIC X(7).
       01 WS-IBAN                  PIC X(30).
       01 WS-MON                   PIC X(3).
       01 WS-INFOMSG               PIC X(40).
       01 WS-DESCTYPE              PIC X(40).
       01 WS-TRFDATE.
          05 WS-TRFDATE-YYYY       PIC X(4).
          05 WS-TRFDATE-MM         PIC X(2).
          05 WS-TRFDATE-DD         PIC X(2).
     š* W- ADDRESS Base
       01 WS-ADDRESS               PIC X(250).
       01 WS-CITY                  PIC X(250).
       01 WS-POS-ADDRESS           PIC S9(4)   COMP   VALUE 0.

       01 WS-MON-ALFA              PIC X(03).
       01 WS-TBCODE                PIC X(03).
       01 WS-REFCODE               PIC S9(11) COMP-3.

       01 WS-POS-NOMI              PIC S9(4)   COMP   VALUE 0.
       01 WS-POS-NOMIS             PIC S9(4)   COMP   VALUE 0.
       01 WS-NOM-L                 PIC S9(4)   COMP   VALUE 0.
       01 WS-NOM.
          03 W-NOM                 PIC X       OCCURS 35.
       01 WS-POS-PRNMI             PIC S9(4)   COMP   VALUE 0.
       01 WS-POS-PRNMIS            PIC S9(4)   COMP   VALUE 0.
       01 WS-PRNM-L                PIC S9(4)   COMP   VALUE 0.
       01 WS-PRNM.
          03 W-PRNM                PIC X       OCCURS 35.
       01 WS-NOM-COMPLETO          PIC X(80).

     š* ------------------------------------------------------
     š* Linha com XML gerado HardCode pelo programa
       01 XML-OUTPUT       PIC X(1000).

     š* Copy : Tabela de AML
       01  R-AMLJ200.
           COPY DDS-ALL-FORMATS OF AMLJ200.

     š* Copy : Tabela de AML
       01  R-AMLJSEPAR.
           COPY DDS-ALL-FORMATS OF AMLJSEPAR.

     š* Copy : Tabela de AML
       01  R-AMLJSEPAS.
           COPY DDS-ALL-FORMATS OF AMLJSEPAS.

     š* Copy : Tabela de Clientes
       01  R-FDBCLI.
           COPY DDS-ALL-FORMATS OF FDBCLI.

     š* Copy : Tabela de Other Numbering
       01  R-FDBNUM.
           COPY DDS-ALL-FORMATS OF FDBNUM.

     š* Copy : Tabela de Clientes
       01  R-FDBTAB.
           COPY DDS-ALL-FORMATS OF FDBTAB.

     š* Copy : Tabela de AMLT900A
       01  R-AMLT900A.
           COPY DDS-ALL-FORMATS OF AMLT900A.

           EXEC SQL DECLARE AMLREQ CURSOR FOR
           SELECT    OPID
                  ,  LPLREF
                  ,  LPSTAT
                  ,  LPDTJJ
                  ,  LPDTMM
                  ,  LPDTAA
                  ,  LPTIME
                  ,  LPLOPR
                  ,  OPRFEX
                  ,  OPDBRC
                  ,  OPDBGR
                  ,  OPDBRU
                  ,  OPDBNM
                  ,  OPCRRC
                  ,  OPCRGR
                  ,  OPCRRU
                  ,  OPCRNM
                  ,  OPRECP
                  ,  OPDBMT
                  ,  OPDBMC
                  ,  OPCBMC
                  ,  OPDNMC
                  ,  OPDOA1
                  ,  OPDOA2
                  ,  OPDOA3
                  ,  OPDOA4
                  ,  OPDBLI
                  ,  OPCRLI
                  ,  FILENOT
                  ,  TMSTAT
                  ,  AMLSTAT
                  ,  TRANREL
                  ,  TMTREL
                FROM AMLJ200
                WHERE  AMLSTAT= " "

           END-EXEC.

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       PROCEDURE DIVISION.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P001-INICIO
           PERFORM P100-CAPTURA-TRANSF
           PERFORM P100-OPEN-CURSOR-AMLREQ
           PERFORM P200-FETCH-CURSOR-AMLREQ
           PERFORM P300-PROCESSO-GERAL
                           UNTIL FIM-P001
           PERFORM P400-CLOSE-CURSOR-AMLREQ
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P001-INICIO.
     š*---------------------------------------------------------------------
           SET  INICIO-P001         TO  TRUE.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.
           MOVE WS-ANOIN-DIA    TO WS-LPDTAA
           MOVE WS-MESIN-DIA    TO WS-LPDTMM
           MOVE WS-DIAIN-DIA    TO WS-LPDTJJ
           INITIALIZE         WS-CONTA-CLI
                              WS-NAME-CLI        WS-NAME-CR
                              WS-IBAN-ORD        WS-IBAN-BEN
                              WS-BIC-COR         WS-BIC-CR
                              WS-BKNAME-CLI      WS-BKNAME-CR
                              WS-ADDRESS-CLI     WS-ADDRESS-CR
                              WS-CLIENTID        WS-INFOMSG
                              WS-OPERADOR        WS-RACINE
                              WS-TRFTYPE         WS-IBAN
                              WS-VALOR           WS-MON
                              WS-MON-ALFA        WS-TRFDATE
                              WS-TBCODE          WS-DESCTYPE
                              WS-CITY-CLI        WS-CITY-CR
                      REPLACING ALPHANUMERIC BY SPACES
                                    NUMERIC BY ZEROS.
     š*---------------------------------------------------------------------
       P100-CAPTURA-TRANSF.
     š*---------------------------------------------------------------------
     š*-
           EXEC SQL
                INSERT INTO AMLJ200
                SELECT     OPID
                          ,LPLREF
                          ,LPSTAT
                          ,LPDTJJ
                          ,LPDTMM
                          ,LPDTAA
                          ,LPTIME
                          ,LPLOPR
                          ,OPRFEX
                          ,OPDBRC
                          ,OPDBGR
                          ,OPDBRU
                          ,OPDBNM
                          ,OPCRRC
                          ,OPCRGR
                          ,OPCRRU
                          ,OPCRNM
                          ,OPRECP
                          ,OPDBMT
                          ,OPDBMC
                          ,OPCBMC
                          ,OPDNMC
                          ,OPDOA1
                          ,OPDOA2
                          ,OPDOA3
                          ,OPDOA4
                          ,OPDBLI
                          ,OPCRLI
                          ," "
                          ,CURRENT TIMESTAMP
                          ," "
                          , 0
                          ,"0001-01-01-00.00.00.000000"
                    FROM FDBLOP A
                      WHERE OPID IN
                      ("101" , "123" , "125" , "135" , "136" , "137")
                      AND LPDTAA = :WS-LPDTAA
                      AND LPDTMM = :WS-LPDTMM
                      AND LPDTJJ = :WS-LPDTJJ
                      AND NOT EXISTS ( SELECT 1
                                       FROM AMLJ200 B
                                       WHERE A.LPLREF = B.LPLREF)
           END-EXEC.
     š*-
     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-AMLREQ.
     š*---------------------------------------------------------------------
     š*-
           EXEC SQL
                OPEN AMLREQ
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P001         TO TRUE
           WHEN OTHER
              SET     FIM-P001         TO TRUE
              MOVE "AMLREQ"         TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P200-FETCH-CURSOR-AMLREQ.
     š*****************************************************************
     š*-
           EXEC SQL FETCH AMLREQ
              INTO  :OPID
                  , :LPLREF
                  , :LPSTAT
                  , :LPDTJJ
                  , :LPDTMM
                  , :LPDTAA
                  , :LPTIME
                  , :LPLOPR
                  , :OPRFEX
                  , :OPDBRC
                  , :OPDBGR
                  , :OPDBRU
                  , :OPDBNM
                  , :OPCRRC
                  , :OPCRGR
                  , :OPCRRU
                  , :OPCRNM
                  , :OPRECP
                  , :OPDBMT
                  , :OPDBMC
                  , :OPCBMC
                  , :OPDNMC
                  , :OPDOA1
                  , :OPDOA2
                  , :OPDOA3
                  , :OPDOA4
                  , :OPDBLI
                  , :OPCRLI
                  , :FILENOT
                  , :TMSTAT
                  , :AMLSTAT
                  , :TRANREL
                  , :TMTREL
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             SET SEM-ERRO TO TRUE
             MOVE LPLREF  TO WS-REFCODE
     š*-
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P001          TO TRUE
           WHEN OTHER
              SET   FIM-P001         TO TRUE
              MOVE "AMLREQ"       TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P300-PROCESSO-GERAL.
     š*****************************************************************
     š*- BIC Code BBI - BNFIPTPL
     š*-
            SET NAO-SEPAR   TO TRUE
            SET NAO-SEPAS   TO TRUE
            SET SIM-TRATA   TO TRUE

     š*- Se for uma Transferência SEPA "123 "
            IF  OPID OF R-AMLJ200  =  "123"
               PERFORM P400-GET-AMLJSEPAR
            END-IF
     š*- Se for uma Transferência SEPA "125" Ou "135"
            IF  OPID OF R-AMLJ200  =  "125" OR "135"
               PERFORM P400-GET-AMLJSEPAS
            END-IF
     š*-
           IF SIM-TRATA
              PERFORM P400-SPLIT-OPDOA3-INFO
              PERFORM P400-SPLIT-OPDOA4-INFO
           END-IF.

           IF SIM-TRATA

           EVALUATE TRUE
     š*- 100 - Transferencia Interna
             WHEN OPID OF R-AMLJ200  =   "100"
               MOVE "Trans. Interna"
                                         TO WS-DESCTYPE
               PERFORM P400-INTERNAL-INFO
     š*- 101 - Transferencia Interb. Saida  SWIFT/ TARGET
     š*-     A Crédito deve ser sempre 9 ( Nostrum ) OPCRRC (1:1) = "9"
             WHEN OPID OF R-AMLJ200  =   "101"
               MOVE "Transf. Interb. SWIFT/AT2 Saida"
                                         TO WS-DESCTYPE
               PERFORM P400-OUTBOUND-INFO
     š*- 102 - Transferencia Swift/AT2 Entrada
             WHEN OPID OF R-AMLJ200  =   "102"
               MOVE "Transf. Swift/AT2 Entrada"
                                         TO WS-DESCTYPE
               PERFORM P400-INBOUND-INFO
     š*- 123 - Transferencia SEPA Entrada
             WHEN OPID OF R-AMLJ200  =   "123"
               MOVE "Transf. SEPA Entrada"
                                         TO WS-DESCTYPE
               PERFORM P400-INBOUND-INFO
     š*- 125 - Transferencia SEPA Enviada
             WHEN OPID OF R-AMLJ200  =   "125"
               MOVE "Transf. SEPA Saida"
                                         TO WS-DESCTYPE
               PERFORM P400-OUTBOUND-INFO
     š*- 135 -Transf. TARGET Interbancaria com Comissões Saida (EUR)
             WHEN OPID OF R-AMLJ200  =   "135"
               MOVE "Transf. Interb. AT2 de Saida"
                                         TO WS-DESCTYPE
               SET OUTBOUND TO TRUE
               PERFORM P400-OUTBOUND-INFO
     š*- Transferencia TARGET normal com Comissões Saida (MOEDA)
             WHEN OPID OF R-AMLJ200  =   "136"
               IF OPCRRC (1:1) = "9"
                 MOVE "Transf. Interb. AT2 de Saida"
                                         TO WS-DESCTYPE
                 PERFORM P400-OUTBOUND-INFO
               ELSE
                 MOVE "Transf. Interb. AT2 de Entrada"
                                         TO WS-DESCTYPE
                 PERFORM P400-INBOUND-INFO
               END-IF
     š*- 137 - Transferencia TARGET urgente Com Comissões Saida
             WHEN OPID OF R-AMLJ200  =   "137"
               IF OPCRRC (1:1) = "9"
                 MOVE "Transf. AT2 Urgente de Saida"
                                         TO WS-DESCTYPE
                 PERFORM P400-OUTBOUND-INFO
               ELSE
                 MOVE "Transf. AT2 Urgente de Entrada"
                                         TO WS-DESCTYPE
                 SET INBOUND TO TRUE
                 PERFORM P400-INBOUND-INFO
               END-IF
             WHEN OTHER
               CONTINUE
           END-EVALUATE
           END-IF

           IF SIM-TRATA
              PERFORM P400-GERA-REQ-XML
              PERFORM P450-FTP-FILE
      ****    PERFORM P450-COPY-FILE
              PERFORM P400-UPDATE-STATUS
              IF SIM-SEPAS
                 PERFORM P400-UPDATE-SEPAS
              END-IF
              IF SIM-SEPAR
                 PERFORM P400-UPDATE-SEPAR
              END-IF
              PERFORM P400-AMLFREQ-CLOSE
           END-IF.
           PERFORM P200-FETCH-CURSOR-AMLREQ.
     š*-
      **     set FIM-P001  to true.
     š*----------------------------------------------------------------
       P400-INTERNAL-INFO.
     š*----------------------------------------------------------------
           SET INTERNAL TO TRUE
     š*- ------Ordenante
           MOVE OPDBRC OF R-AMLJ200  TO  WS-RACINE
           MOVE WS-RACINE            TO  WS-CLIENTID
           MOVE OPDBRC OF R-AMLJ200  TO  WS-RACINE-CLI
           MOVE OPDBGR OF R-AMLJ200  TO  WS-GENRE-CLI
           MOVE OPDBRU OF R-AMLJ200  TO  WS-RUB-CLI
           MOVE OPDBNM OF R-AMLJ200  TO  WS-MON-CLI
           MOVE "."                  TO  WS-SEP1-CLI
                                         WS-SEP2-CLI
                                         WS-SEP3-CLI
           PERFORM P300-GET-FDBCLI
           PERFORM P300-GET-ADDRESS
           PERFORM P300-GET-CITY
           PERFORM P300-GET-NOME
           MOVE WS-NOM-COMPLETO      TO  WS-NAME-CLI
           MOVE WS-ADDRESS           TO  WS-ADDRESS-CLI
           MOVE WS-CITY              TO  WS-CITY-CLI
     š*- ------Cliente Beneficiário (Creditado)
           MOVE OPCRRC OF R-AMLJ200  TO  WS-RACINE
           PERFORM P300-GET-IBAN
           MOVE WS-IBAN              TO  WS-IBAN-BEN
           PERFORM P300-GET-FDBCLI
           PERFORM P300-GET-ADDRESS
           PERFORM P300-GET-CITY
           PERFORM P300-GET-NOME
           MOVE WS-NOM-COMPLETO      TO  WS-NAME-CR
           MOVE WS-ADDRESS           TO  WS-ADDRESS-CR
           MOVE WS-CITY              TO  WS-CITY-CR
           PERFORM P300-GET-IBAN
           MOVE WS-IBAN              TO  WS-IBAN-ORD
     š*- ------Montante
           MOVE OPDBMT OF R-AMLJ200  TO  WS-VALOR
     š*- ------Operador
           MOVE LPLOPR OF R-AMLJ200  TO  WS-OPERADOR
     š*- ------Data
           MOVE LPDTAA OF R-AMLJ200  TO  WS-TRFDATE-YYYY
           MOVE LPDTMM OF R-AMLJ200  TO  WS-TRFDATE-MM
           MOVE LPDTJJ OF R-AMLJ200  TO  WS-TRFDATE-DD.
     š*-
     š*----------------------------------------------------------------
       P400-OUTBOUND-INFO.
     š*----------------------------------------------------------------
     š*- OPDOA1 - NIB  Destinatário
     š*- OPDOA2 - Nome Destinatário
     š*- OPDOA3 - BIC Code ; Nome Banco Destino
     š*- OPDOA4 - BIC Code ; Nome Banco Correspondente
           SET OUTBOUND TO TRUE
     š*- ------ID Cliente
           MOVE OPDBRC OF R-AMLJ200  TO WS-RACINE
           MOVE WS-RACINE            TO  WS-CLIENTID
     š*- ------Conta Cliente
           MOVE OPDBRC OF R-AMLJ200  TO  WS-RACINE-CLI
           MOVE OPDBGR OF R-AMLJ200  TO  WS-GENRE-CLI
           MOVE OPDBRU OF R-AMLJ200  TO  WS-RUB-CLI
           MOVE OPDBNM OF R-AMLJ200  TO  WS-MON-CLI
           MOVE "."                  TO  WS-SEP1-CLI
                                         WS-SEP2-CLI
                                         WS-SEP3-CLI
           PERFORM P300-GET-FDBCLI
           PERFORM P300-GET-ADDRESS
           PERFORM P300-GET-CITY
           PERFORM P300-GET-NOME
           MOVE WS-NOM-COMPLETO      TO  WS-NAME-CLI
           MOVE WS-ADDRESS           TO  WS-ADDRESS-CLI
           MOVE WS-CITY              TO  WS-CITY-CLI
     š*- ------Ordenante
           PERFORM P300-GET-IBAN
           MOVE WS-IBAN              TO  WS-IBAN-ORD
     š*- ------Montante
           MOVE OPDBMT OF R-AMLJ200  TO  WS-VALOR
     š*- ------Operador
           MOVE LPLOPR OF R-AMLJ200  TO  WS-OPERADOR
     š*- ------Data
           MOVE LPDTAA OF R-AMLJ200  TO  WS-TRFDATE-YYYY
           MOVE LPDTMM OF R-AMLJ200  TO  WS-TRFDATE-MM
           MOVE LPDTJJ OF R-AMLJ200  TO  WS-TRFDATE-DD
     š*- ------Beneficiário
           MOVE OPDOA1               TO  WS-IBAN-BEN
           MOVE OPDOA2 OF R-AMLJ200  TO  WS-NAME-CR
           MOVE WS-STRING-OPDOA3-1   TO  WS-BKNAME-CR
           MOVE WS-STRING-OPDOA3-2   TO  WS-BIC-CR
           MOVE WS-STRING-OPDOA3-2(5:1) TO WS-COUNTRY-CR
           IF SIM-SEPAS
              MOVE SMSGINFO             TO WS-INFOMSG
              MOVE SBICBEN              TO WS-BIC-CR
              MOVE SIBANBEN             TO WS-IBAN-BEN
              MOVE SNOMEBEN             TO WS-NAME-CR
              MOVE SPAYPUR              TO WS-TRFTYPE
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-INBOUND-INFO.
     š*----------------------------------------------------------------
     š*- OPDOA1 - NIB  Destinatário
     š*- OPDOA2 - Nome Destinatário
     š*- OPDOA3 - BIC Code ; Nome Banco Destino
     š*- OPDOA4 - BIC Code ; Nome Banco Correspondente
           SET INBOUND  TO TRUE
     š*- ------ID Cliente
           MOVE OPCRRC OF R-AMLJ200  TO WS-RACINE
           MOVE WS-RACINE            TO WS-CLIENTID
     š*- ------Conta Cliente
           MOVE OPCRRC OF R-AMLJ200  TO  WS-RACINE-CLI
           MOVE OPCRGR OF R-AMLJ200  TO  WS-GENRE-CLI
           MOVE OPCRRU OF R-AMLJ200  TO  WS-RUB-CLI
           MOVE OPCRNM OF R-AMLJ200  TO  WS-MON-CLI
           MOVE "."                  TO  WS-SEP1-CLI
                                         WS-SEP2-CLI
                                         WS-SEP3-CLI
           PERFORM P300-GET-FDBCLI
           PERFORM P300-GET-ADDRESS
           PERFORM P300-GET-CITY
           PERFORM P300-GET-NOME
           MOVE WS-NOM-COMPLETO      TO  WS-NAME-CR
           MOVE WS-ADDRESS           TO  WS-ADDRESS-CR
           MOVE WS-CITY              TO  WS-CITY-CR
     š*- ------Beneficiário
           PERFORM P300-GET-IBAN
           MOVE WS-IBAN              TO  WS-IBAN-BEN
     š*- ------Montante
           MOVE OPDBMT OF R-AMLJ200  TO  WS-VALOR
     š*- ------Operador
           MOVE LPLOPR OF R-AMLJ200  TO  WS-OPERADOR
     š*- ------Data
           MOVE LPDTAA OF R-AMLJ200  TO  WS-TRFDATE-YYYY
           MOVE LPDTMM OF R-AMLJ200  TO  WS-TRFDATE-MM
           MOVE LPDTJJ OF R-AMLJ200  TO  WS-TRFDATE-DD.
     š*- ------Ordenante / Contraparte
           MOVE OPDOA1               TO  WS-IBAN-ORD
           MOVE OPDOA2 OF R-AMLJ200  TO  WS-NAME-CR
           MOVE WS-STRING-OPDOA3-1   TO  WS-BKNAME-CR
           MOVE WS-STRING-OPDOA3-2   TO  WS-BIC-CR
           MOVE WS-STRING-OPDOA3-2(5:1) TO WS-COUNTRY-CR
           IF SIM-SEPAR
              MOVE RMSGINFO             TO WS-INFOMSG
              MOVE RBICORD              TO WS-BIC-CR
              MOVE RIBANORD             TO WS-BKNAME-CR
              MOVE RNAMEORD             TO WS-NAME-CR
              MOVE RPAYPUR              TO WS-TRFTYPE
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-SPLIT-OPDOA3-INFO.
     š*----------------------------------------------------------------
     š*-
           MOVE SPACES TO WS-STRING-OPDOA3-1
                          WS-STRING-OPDOA3-2
                          WS-STRING-OPDOA3-3
           MOVE ZERO   TO WS-COUNT-CSV
           MOVE OPDOA3 OF R-AMLJ200  TO WS-STRING-CSV
           MOVE ZEROS TO WS-POS-I  WS-POS-S  WS-STRING-L
           PERFORM P400-SCAN-OPDOA3-CSV
                   VARYING W-NUM FROM 1 BY 1
                   UNTIL W-NUM > LENGTH OF WS-STRING-CSV.

           MOVE W-NUM TO WS-POS-S
           ADD 1 TO WS-POS-I
           EVALUATE WS-COUNT-CSV
           WHEN 0
             MOVE OPDOA3 OF R-AMLJ200 TO WS-STRING-OPDOA3-1
           WHEN 1
             COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I - 1
             IF WS-STRING-L GREATER THAN ZERO
                 MOVE WS-STRING-CSV(WS-POS-I:WS-STRING-L)
                                      TO WS-STRING-OPDOA3-2
             END-IF
           WHEN 2
             COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I  - 1
             IF WS-STRING-L GREATER THAN ZERO
                 MOVE WS-STRING-CSV(WS-POS-I:WS-STRING-L)
                                      TO WS-STRING-OPDOA3-3
             END-IF
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-SCAN-OPDOA3-CSV.
     š*----------------------------------------------------------------
     š*-
           IF  WS-STRING-CHR-CSV(W-NUM)  = ";"
               EVALUATE WS-COUNT-CSV
                WHEN   0
                  MOVE 1     TO WS-POS-I
                  MOVE W-NUM TO WS-POS-S
                  COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I
                  IF WS-STRING-L GREATER THAN ZERO
                    MOVE WS-STRING-CSV(1:WS-STRING-L)
                        TO WS-STRING-OPDOA3-1
                  END-IF
                  ADD 1 TO WS-COUNT-CSV
                  MOVE W-NUM TO WS-POS-I
                WHEN  1
                  MOVE W-NUM TO WS-POS-S
                  ADD 1 TO WS-POS-S
                  COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I
                  IF WS-STRING-L GREATER THAN ZERO
                    MOVE WS-STRING-CSV(1:WS-STRING-L)
                        TO WS-STRING-OPDOA3-2
                  END-IF
                  ADD 1 TO WS-COUNT-CSV
                  MOVE W-NUM TO WS-POS-I
                WHEN   2
                  MOVE W-NUM TO WS-POS-S
                  ADD 1 TO WS-POS-S
                  COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I
                  IF WS-STRING-L GREATER THAN ZERO
                    MOVE WS-STRING-CSV(1:WS-STRING-L)
                        TO WS-STRING-OPDOA3-3
                  END-IF
               END-EVALUATE
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-SPLIT-OPDOA4-INFO.
     š*----------------------------------------------------------------
     š*-
           MOVE SPACES TO WS-STRING-OPDOA4-1
                          WS-STRING-OPDOA4-2
                          WS-STRING-OPDOA4-3
           MOVE ZERO   TO WS-COUNT-CSV
           MOVE OPDOA4 OF R-AMLJ200  TO WS-STRING-CSV
           MOVE ZEROS TO WS-POS-I  WS-POS-S  WS-STRING-L
           PERFORM P400-SCAN-OPDOA4-CSV
                   VARYING W-NUM FROM 1 BY 1
                   UNTIL W-NUM > LENGTH OF WS-STRING-CSV.

           MOVE W-NUM TO WS-POS-S
           ADD 1 TO WS-POS-I
           EVALUATE WS-COUNT-CSV
           WHEN 0
             MOVE OPDOA4 OF R-AMLJ200 TO WS-STRING-OPDOA4-1
           WHEN 1
             COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I - 1
             IF WS-STRING-L GREATER THAN ZERO
                 MOVE WS-STRING-CSV(WS-POS-I:WS-STRING-L)
                                      TO WS-STRING-OPDOA4-2
             END-IF
           WHEN 2
             COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I - 1
             IF WS-STRING-L GREATER THAN ZERO
                 MOVE WS-STRING-CSV(WS-POS-I:WS-STRING-L)
                                      TO WS-STRING-OPDOA4-3
             END-IF
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-SCAN-OPDOA4-CSV.
     š*----------------------------------------------------------------
     š*-
           IF  WS-STRING-CHR-CSV(W-NUM)  = ";"
               EVALUATE WS-COUNT-CSV
                WHEN  0
                  MOVE 1     TO WS-POS-I
                  MOVE W-NUM TO WS-POS-S
                  COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I
                  IF WS-STRING-L GREATER THAN ZERO
                    MOVE WS-STRING-CSV(1:WS-STRING-L)
                        TO WS-STRING-OPDOA4-1
                  END-IF
                  ADD 1 TO WS-COUNT-CSV
                  MOVE W-NUM TO WS-POS-I
                WHEN  1
                  MOVE W-NUM TO WS-POS-S
                  ADD 1 TO WS-POS-S
                  COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I
                  IF WS-STRING-L GREATER THAN ZERO
                    MOVE WS-STRING-CSV(1:WS-STRING-L)
                        TO WS-STRING-OPDOA4-2
                  END-IF
                  ADD 1 TO WS-COUNT-CSV
                  MOVE W-NUM TO WS-POS-I
                WHEN  2
                  MOVE W-NUM TO WS-POS-S
                  ADD 1 TO WS-POS-S
                  COMPUTE WS-STRING-L  = WS-POS-S - WS-POS-I
                  IF WS-STRING-L GREATER THAN ZERO
                    MOVE WS-STRING-CSV(1:WS-STRING-L)
                        TO WS-STRING-OPDOA4-3
                  END-IF
               END-EVALUATE
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-GERA-REQ-XML.
     š*----------------------------------------------------------------
     š*-
           PERFORM P300-AMLFREQ-OPEN.
           IF SEM-ERRO
             INITIALIZE REG-AMLFREQ XML-OUTPUT
             PERFORM  P300-GERA-REQ-HEADER
             PERFORM  P300-GERA-REQ-PAYLOAD
           END-IF.

           PERFORM P400-AMLFREQ-CLOSE.
     š*-
     š*---------------------------------------------------------------------
       P300-GERA-REQ-HEADER.
     š*---------------------------------------------------------------------
     š*-
           INITIALIZE XML-OUTPUT
     š*-XML Doc Declaration
           MOVE "<?xml version='1.0' encoding='UTF-8'?>"
                                              TO XML-OUTPUT.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-scan request OPEN
           MOVE "<scan_request version='1'>"  TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-amount
           MOVE  "amount"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.

           MOVE  WS-VALOR            TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn         TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG-N
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-cost_center
           MOVE  "cost_center"       TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-currency Alfa  ( FDBTAB = 40 )
           MOVE WS-MON-CLI            TO  WS-MON
           PERFORM P300-MOEDA-ALFA
           MOVE  "currency"          TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE WS-MON-ALFA          TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-direction
           EVALUATE TRUE
           WHEN  INBOUND
             MOVE "<direction>I</direction>"   TO XML-OUTPUT
           WHEN  OUTBOUND
             MOVE "<direction>O</direction>"   TO XML-OUTPUT
           WHEN  INTERNAL
             MOVE "<direction>N</direction>"   TO XML-OUTPUT
           WHEN OTHER
             MOVE "<direction>I</direction>"   TO XML-OUTPUT
           END-EVALUATE
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-gatway
           MOVE "<gateway>OLY</gateway>"     TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-mandator
           MOVE "<mandator>BBI</mandator>"   TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-mandator
           MOVE "<message_format>XML</message_format>" TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-message_type
           MOVE "<message_type>FT</message_type>" TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-receiver
           MOVE "<receiver>AS400</receiver>"        TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-receiver
           MOVE "<risk_type>ALERT.TRN.COFI</risk_type>" TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-sender
           MOVE "<sender>AS400</sender>"            TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-transaction_number
           MOVE "transaction_number"        TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG
           MOVE  LPLREF OF R-AMLJ200       TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*-value_date
           MOVE "value_date"                TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-TRFDATE                 TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-
     š*---------------------------------------------------------------------
       P300-GERA-REQ-PAYLOAD.
     š*---------------------------------------------------------------------
     š*-
           INITIALIZE XML-OUTPUT
     š*-payload
           MOVE "<payload>"                   TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-CDATA
           MOVE "<![CDATA[<funds_transfer>"   TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-operator
           MOVE "operator"                 TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-OPERADOR               TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-ClientId
           MOVE "clientID"                TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-CLIENTID            TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-AccountNr
           MOVE "accountNr"                TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-CONTA-CLI               TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Amount
           MOVE "amount"                   TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-VALOR                  TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn               TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG-N.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Iban Order
           MOVE "ibanOrderer"               TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-IBAN-ORD                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Iban Beneficiary
           MOVE "ibanBeneficiary"          TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-IBAN-BEN                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Bic Counterpart
           MOVE "bicConterpart"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-BIC-CR                 TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Bic Correspondent
           MOVE "bicCorrespondent"         TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-BIC-COR                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Name of The Bank Counterpart
           MOVE "bankNameCR"               TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-BKNAME-CR              TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Name of The Bank Correspondent
           MOVE "bankNameCorrespondent"    TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-BKNAME-COR             TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Name of Counterpart Person
           MOVE "nameContrapart"           TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-NAME-CR                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Name of Client
           MOVE "nameClient"               TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-NAME-CLI                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-address of Counterpart
           MOVE "addressCounterpart"       TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-ADDRESS-CR             TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-address of Client
           MOVE "addressClient"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-ADDRESS-CLI             TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-City of CounterPart
           MOVE "cityCounterpart"          TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-CITY-CR                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-City of Client
           MOVE "cityClient"               TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-CITY-CLI                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Country of Counterpart
           MOVE "countryCounterpart"       TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-COUNTRY-CR             TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Country of Client
           MOVE "countryClient"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-COUNTRY-CLI             TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Transaction Type Description
           MOVE "TransType"                TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-COUNTRY-CLI             TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-OPID
           MOVE "OPID"                     TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  OPID OF R-AMLJ200         TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-transaction_number
           MOVE "transactionNumber"       TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG
           MOVE  LPLREF OF R-AMLJ200       TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Comunication
           MOVE "Communication"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-INFOMSG                 TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-Payment Purpose
           MOVE "transferType"             TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG.
           MOVE  WS-TRFTYPE                TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.
           PERFORM P300-CLOSE-TAG.
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-funds_transfer
           MOVE "</funds_transfer>]]>"        TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-payload
           MOVE "</payload>"                  TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.
     š*-scan_request
           MOVE "</scan_request>"             TO XML-OUTPUT
           MOVE XML-OUTPUT TO REG-AMLFREQ
           PERFORM P300-AMLFREQ-WRITE.

     š*---------------------------------------------------------------------
       P450-FTP-FILE.
     š*---------------------------------------------------------------------
           INITIALIZE AMLL001M-FICH-ORIGEM
                      AMLL001M-FICH-DESTINO
                      AMLL001M-ERRO-FTP
                      REPLACING ALPHANUMERIC BY SPACES
                                    NUMERIC BY ZEROS.

           MOVE "AMLFREQ"              TO AMLL001M-FICH-ORIGEM

           MOVE "FCMMSG."              TO WS-idFichAML
           MOVE WS-ANOIN-DIA           TO WS-idFichANO
           MOVE WS-MESIN-DIA           TO WS-idFichMES
           MOVE WS-DIAIN-DIA           TO WS-idFichDIA
           MOVE LPLREF OF R-AMLJ200    TO ws-idFichtrn
           MOVE "."                    TO WS-idFichfil1
           MOVE "."                    TO WS-idFichfil2
           MOVE "XML"                  TO WS-idFichXML
           MOVE ws-idFich              TO AMLL001M-FICH-DESTINO

           CALL "AMLCFTP2" USING AMLL001M-FICH-ORIGEM
                                AMLL001M-FICH-DESTINO
                                    AMLL001M-ERRO-FTP

           IF AMLL001M-ERRO-FTP NOT EQUAL SPACES
              MOVE "001"                TO W-FILE-CODE
              MOVE WS-ERRO              TO MSG-ERRO-FS
              MOVE "AMLCFTP2"           TO W-OBJECT-NAME
              MOVE "FTP"                TO W-CMDEXEC
              MOVE "P450-FTP-FILE"      TO W-PARAGRAFO
              PERFORM P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P450-COPY-FILE.
     š*---------------------------------------------------------------------

           INITIALIZE AMLL001M-FICH-DESTINO
                      REPLACING ALPHANUMERIC BY SPACES
                                    NUMERIC BY ZEROS.

           MOVE "/AML/REQUEST/"        TO ws-directory
           MOVE "FCMMSG."              TO ws-idPathAML
           MOVE WS-ANOIN-DIA           TO ws-idPathANO
           MOVE WS-MESIN-DIA           TO ws-idPathMES
           MOVE WS-DIAIN-DIA           TO ws-idPathDIA
           MOVE LPLREF OF R-AMLJ200    TO ws-idPathtrn
           MOVE "."                    TO ws-idPathfil1
           MOVE "."                    TO ws-idPathfil2
           MOVE "XML"                  TO ws-idPathXML

           MOVE ws-idPath              TO AMLL001M-FICH-DESTINO

           CALL "AMLCREQ" USING AMLL001M-FICH-DESTINO.

     š*-
     š*----------------------------------------------------------------
       P400-UPDATE-STATUS.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
                 UPDATE AMLJ200
                 SET TMSTAT   = CURRENT TIMESTAMP
                   , AMLSTAT = "REQ"
                 WHERE LPLREF = :LPLREF
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "AMLJ200"           TO W-OBJECT-NAME
              SET CMD-UPDATE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-UPDATE-SEPAS.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
                 UPDATE AMLJSEPAS
                 SET SAMLSTAT = "OK"
                 WHERE DECIMAL(SREFCODE , 11, 0 ) = :WS-REFCODE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "AMLSEPAS"          TO W-OBJECT-NAME
              SET CMD-UPDATE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-UPDATE-SEPAR.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
                 UPDATE AMLJSEPAR
                 SET RAMLSTAT = "OK"
                 WHERE DECIMAL(RREFCODE , 11, 0 ) = :WS-REFCODE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "AMLJSEPAR"         TO W-OBJECT-NAME
              SET CMD-UPDATE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*---------------------------------------------------------------------
       P300-AMLFREQ-OPEN.
     š*---------------------------------------------------------------------
           OPEN OUTPUT AMLFREQ
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-OPENED
               CONTINUE
           ELSE
               SET  FIM-P001             TO TRUE
               MOVE "AMLP200"            TO W-PROGRAMA
               MOVE "P300-AMLFREQ-OPEN"  TO W-PARAGRAFO
               MOVE "AMLFREQ"            TO W-OBJECT-NAME
               MOVE "OPEN"               TO W-CMDEXEC
               MOVE SPACES               TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P400-AMLFREQ-CLOSE.
     š*---------------------------------------------------------------------
           CLOSE AMLFREQ
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-CLOSED
               CONTINUE
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "AMLP200"             TO W-PROGRAMA
               MOVE "P400-AMLFREQ-CLOSE"  TO W-PARAGRAFO
               MOVE "AMLFREQ"             TO W-OBJECT-NAME
               MOVE "CLOSE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P300-AMLFREQ-WRITE.
     š*---------------------------------------------------------------------
           INSPECT REG-AMLFREQ
           REPLACING ALL "&" BY "e" .
           WRITE REG-AMLFREQ
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO       OR
               GRS-DUP-KEY
                INITIALIZE REG-AMLFREQ
                INITIALIZE XML-OUTPUT
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "AMLP200"             TO W-PROGRAMA
               MOVE "P300-AMLFREQ-WRITE"  TO W-PARAGRAFO
               MOVE "AMLFREQ"             TO W-OBJECT-NAME
               MOVE "WRITE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.

           MOVE 1 TO WS-POS-XML.
     š*-
     š*----------------------------------------------------------------
       P300-GET-IBAN.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
                SELECT  NUREFE
                 INTO   :WS-IBAN
                FROM   FDBNUM A
                 WHERE A.NURACI = :WS-RACINE
                  AND  A.NUTYPE = "020"
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN SQLCODE-NOTFOUND
              MOVE SPACES TO WS-IBAN
           WHEN OTHER
              MOVE "NUM-IBAN"          TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
           MOVE ZEROS TO SQLCODE W-SQLCODE, W-EDTSQLC.
     š*-
     š*----------------------------------------------------------------
       P300-GET-NOME.
     š*----------------------------------------------------------------
     š*-
           MOVE ZEROS TO WS-POS-NOMI
                         WS-POS-PRNMI
                         WS-POS-PRNMIS
                         WS-POS-NOMIS

     š*- NOME PROPRIO
           MOVE  CLPRNM    OF R-FDBCLI   TO WS-PRNM
           INSPECT  WS-PRNM
                   TALLYING WS-POS-PRNMIS   FOR LEADING SPACE.

           INSPECT FUNCTION REVERSE(WS-PRNM)
                   TALLYING WS-POS-PRNMI    FOR LEADING SPACE.

           COMPUTE  WS-PRNM-L   = LENGTH OF WS-PRNM - WS-POS-PRNMI.
           COMPUTE  WS-PRNM-L   = WS-PRNM-L - WS-POS-PRNMIS

           IF LENGTH OF WS-PRNM = WS-POS-PRNMIS
              COMPUTE WS-POS-PRNMIS = 0
           END-IF
           IF WS-POS-PRNMIS = 0
             ADD 1 TO WS-POS-PRNMIS
           END-IF

     š*-APELIDO
           MOVE  CLNOM     OF R-FDBCLI   TO WS-NOM
           INSPECT WS-NOM
                   TALLYING WS-POS-NOMIS    FOR LEADING SPACE.
           INSPECT FUNCTION REVERSE(WS-NOM)
                   TALLYING WS-POS-NOMI     FOR LEADING SPACE.

           COMPUTE  WS-NOM-L    = LENGTH OF WS-NOM  -  WS-POS-NOMI.
           COMPUTE  WS-NOM-L    = WS-NOM-L    - WS-POS-NOMIS

           IF LENGTH OF WS-NOM  = WS-POS-NOMIS
              COMPUTE WS-POS-NOMIS   = 0
           END-IF
           IF WS-POS-NOMIS  = 0
             ADD 1 TO WS-POS-NOMIS
           END-IF

     š*-CONSTROI NOME
           MOVE SPACES                    TO WS-NOM-COMPLETO
           IF WS-PRNM-L > 0
             MOVE WS-PRNM(WS-POS-PRNMIS: WS-PRNM-L)
                                          TO WS-NOM-COMPLETO
           END-IF
           IF WS-NOM-L  > 0
             IF WS-PRNM-L > 0
               ADD 2 TO WS-PRNM-L
               MOVE WS-NOM(WS-POS-NOMIS: WS-NOM-L)
                            TO WS-NOM-COMPLETO(WS-PRNM-L:WS-NOM-L)
             ELSE
               MOVE WS-NOM(WS-POS-NOMIS: WS-NOM-L)
                            TO WS-NOM-COMPLETO(WS-POS-NOMIS:WS-NOM-L)
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P300-GET-FDBCLI.
     š*----------------------------------------------------------------
     š*-
     š*- Obtem Informação Mestre de Clientes
     š*-
           INITIALIZE R-FDBCLI
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           EXEC SQL
                SELECT  CLRACI
                     ,  CLETAT
                     ,  CLDTOU
                     ,  CLDTMU
                     ,  CLDTAN
                     ,  CLNBRM
                     ,  CLOPRN
                     ,  CLGRPE
                     ,  CLAGNT
                     ,  CLCENT
                     ,  CLGERA
                     ,  CLETCV
                     ,  CLNOM
                     ,  CLPRNM
                     ,  CLORIG
                     ,  CLTYPE
                     ,  CLNOMC
                     ,  CLDTNA
                     ,  CLDTDC
                     ,  CLSRCD
                     ,  CLLNGE
                     ,  CLCDEX
                     ,  CLDESI
                     ,  CLAD01
                     ,  CLAD02
                     ,  CLAD03
                     ,  CLAD04
                     ,  CLAD05
                     ,  CLAD06
                     ,  CLDOMI
                     ,  CLREGI
                     ,  CLNATI
                     ,  CLACTI
                     ,  CLSECT
                     ,  CLRGMA
                     ,  CLSYMP
                     ,  CLMONE
                     ,  CLMONP
                     ,  CLTLPH
                     ,  CLTLEX
                     ,  CLTLFX
                     ,  CLSWFT
                     ,  CLSIC
                     ,  CLAUTR
                     ,  CLGIRO
                     ,  CLPROF
                     ,  CLGEST
                     ,  CLOBJE
                     ,  CLGRPG
                     ,  CLGER2
                     ,  CLGER3
                     ,  CLNACP
                     ,  CLDOMR

                INTO   :CLRACI
                     , :CLETAT
                     , :CLDTOU
                     , :CLDTMU
                     , :CLDTAN
                     , :CLNBRM
                     , :CLOPRN
                     , :CLGRPE
                     , :CLAGNT
                     , :CLCENT
                     , :CLGERA
                     , :CLETCV
                     , :CLNOM
                     , :CLPRNM
                     , :CLORIG
                     , :CLTYPE
                     , :CLNOMC
                     , :CLDTNA
                     , :CLDTDC
                     , :CLSRCD
                     , :CLLNGE
                     , :CLCDEX
                     , :CLDESI
                     , :CLAD01
                     , :CLAD02
                     , :CLAD03
                     , :CLAD04
                     , :CLAD05
                     , :CLAD06
                     , :CLDOMI
                     , :CLREGI
                     , :CLNATI
                     , :CLACTI
                     , :CLSECT
                     , :CLRGMA
                     , :CLSYMP
                     , :CLMONE
                     , :CLMONP
                     , :CLTLPH
                     , :CLTLEX
                     , :CLTLFX
                     , :CLSWFT
                     , :CLSIC
                     , :CLAUTR
                     , :CLGIRO
                     , :CLPROF
                     , :CLGEST
                     , :CLOBJE
                     , :CLGRPG
                     , :CLGER2
                     , :CLGER3
                     , :CLNACP
                     , :CLDOMR
                FROM  FDBCLI A
                WHERE A.CLRACI = :WS-RACINE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN SQLCODE-NOTFOUND
              CONTINUE
           WHEN OTHER
              MOVE "FDBCLI "           TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P300-GET-ADDRESS.
     š*----------------------------------------------------------------
     š*-
           MOVE 1 TO WS-POS-ADDRESS
           IF CLAD03 OF R-FDBCLI  NOT = " "
             MOVE CLAD03 OF R-FDBCLI TO WS-STRING
             PERFORM P300-COMPRIMENTO
             STRING  WS-STRING(1: WS-STRING-L )
             DELIMITED BY SIZE INTO WS-ADDRESS
             WITH POINTER WS-POS-ADDRESS.

           IF CLAD04 OF R-FDBCLI NOT = " "
             MOVE CLAD04 OF R-FDBCLI  TO WS-STRING
             PERFORM P300-COMPRIMENTO
             ADD 1 TO WS-STRING-L
             STRING  WS-STRING(1: WS-STRING-L )
             DELIMITED BY SIZE INTO WS-ADDRESS
             WITH POINTER WS-POS-ADDRESS.

           IF CLAD05 OF R-FDBCLI NOT = " "
             MOVE CLAD05 OF R-FDBCLI  TO WS-STRING
             PERFORM P300-COMPRIMENTO
             ADD 1 TO WS-STRING-L
             STRING  WS-STRING(1: WS-STRING-L )
             DELIMITED BY SIZE INTO WS-ADDRESS
             WITH POINTER WS-POS-ADDRESS.

           IF CLAD06 OF R-FDBCLI NOT = " "
             MOVE CLAD06 OF R-FDBCLI  TO WS-STRING
             PERFORM P300-COMPRIMENTO
             ADD 1 TO WS-STRING-L
             STRING  WS-STRING(1: WS-STRING-L )
             DELIMITED BY SIZE INTO WS-ADDRESS
             WITH POINTER WS-POS-ADDRESS.
     š*-
     š*----------------------------------------------------------------
       P300-GET-CITY.
     š*----------------------------------------------------------------
     š*-
             IF CLAD05 OF R-FDBCLI NOT = " "
                MOVE CLAD05 OF R-FDBCLI  TO WS-CITY
             END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-GET-AMLJSEPAR.
     š*----------------------------------------------------------------
     š*-
     š*- Obtem Informação Sepa Recebidas
     š*-
           INITIALIZE R-AMLJSEPAR
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                SELECT
                        RTRFDATE
                     ,  RREFCODE
                     ,  RIEREF
                     ,  RIDPACS8
                     ,  RTXID
                     ,  RPAYPUR
                     ,  RDESPAYPUR
                     ,  RCDOPER
                     ,  RDESCDOPER
                     ,  RBICORD
                     ,  RIBANORD
                     ,  RNAMEORD
                     ,  RMSGINFO
                     ,  RIBAN
                     ,  RAMOUNT
                     ,  RESTADO
                     ,  RCONTA
                     ,  RAMLSTAT

                INTO
                       :RTRFDATE
                     , :RREFCODE
                     , :RIEREF
                     , :RIDPACS8
                     , :RTXID
                     , :RPAYPUR
                     , :RDESPAYPUR
                     , :RCDOPER
                     , :RDESCDOPER
                     , :RBICORD
                     , :RIBANORD
                     , :RNAMEORD
                     , :RMSGINFO
                     , :RIBAN
                     , :RAMOUNT
                     , :RESTADO
                     , :RCONTA
                     , :RAMLSTAT
                FROM  AMLJSEPAR A
                WHERE DECIMAL(A.RREFCODE , 11, 0 ) = :WS-REFCODE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET SIM-SEPAR        TO TRUE
              SET SIM-TRATA        TO TRUE
           WHEN SQLCODE-NOTFOUND
              SET NAO-TRATA        TO TRUE
           WHEN OTHER
              SET NAO-TRATA            TO TRUE
              MOVE "AMLJSEPAR"         TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-GET-AMLJSEPAS.
     š*----------------------------------------------------------------
     š*-
     š*- Obtem Informação Sepa Recebidas
     š*-
           INITIALIZE R-AMLJSEPAS
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                SELECT
                        STRFDATE
                     ,  SREFCODE
                     ,  SORGCODE
                     ,  SREQID
                     ,  SPAYPUR
                     ,  SORIG
                     ,  SLOTE
                     ,  SNAME
                     ,  SNIB
                     ,  SMSGINFO
                     ,  SBICBEN
                     ,  SNIBBEN
                     ,  SIBANBEN
                     ,  SNOMEBEN
                     ,  SAMOUNT
                     ,  SCDCURR
                     ,  SFASE
                     ,  SESTADO
                     ,  SAMLSTAT

                INTO
                       :STRFDATE
                     , :SREFCODE
                     , :SORGCODE
                     , :SREQID
                     , :SPAYPUR
                     , :SORIG
                     , :SLOTE
                     , :SNAME
                     , :SNIB
                     , :SMSGINFO
                     , :SBICBEN
                     , :SNIBBEN
                     , :SIBANBEN
                     , :SNOMEBEN
                     , :SAMOUNT
                     , :SCDCURR
                     , :SFASE
                     , :SESTADO
                     , :SAMLSTAT
                FROM  AMLJSEPAS A
                WHERE DECIMAL(A.SREFCODE , 11, 0 ) = :WS-REFCODE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET SIM-SEPAS     TO TRUE
              SET SIM-TRATA     TO TRUE
           WHEN SQLCODE-NOTFOUND
              SET NAO-TRATA     TO TRUE
           WHEN OTHER
              SET NAO-TRATA            TO TRUE
              MOVE "AMLJSEPAS"         TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P400-CLOSE-CURSOR-AMLREQ.
     š*****************************************************************
     š*-
           EXEC SQL
               CLOSE AMLREQ
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET     FIM-P001         TO TRUE
           WHEN OTHER
              SET     FIM-P001         TO TRUE
              MOVE "CURSOR-AMLREQ"     TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P300-MOEDA-ALFA.
     š*----------------------------------------------------------------
     š*- Descodifica MON  pela Tabela 040
     š*-
           INITIALIZE  R-FDBTAB
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           MOVE WS-MON  TO WS-TBCODE
           MOVE WS-MON  TO WS-MON-ALFA

           EXEC SQL
                 SELECT SUBSTR(A.TBCOMP, 1, 3 )
                  INTO :WS-MON-ALFA
                  FROM  FDBTAB A
                  WHERE A.TBID   = "040"
                   AND  A.TBCODE = :WS-TBCODE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "FDBTAB"            TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.
     š*---------------------------------------------------------------------
       P300-ATRIBUTE.
     š*---------------------------------------------------------------------
     š*-Trata Valor Atributo
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF WS-STRING  NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.
     š*---------------------------------------------------------------------
       P300-GERA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome da TAG
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L
             MOVE WS-STRING    TO WS-NOME-TAG

             STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "</"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-DATA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-DATA-TAG-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A( WS-POS-S: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-ATRIBUTE-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(WS-POS-S: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML

           END-IF.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING
                "</"
                WS-NOME-TAG(1: WS-NOM-TAG-L)
                ">"

           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-END-TAG.
     š*---------------------------------------------------------------------
           STRING ">"
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-COMPRIMENTO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING
     š*- WY -Comprimento Final
           MOVE ZEROES TO WS-POS-I.
           INSPECT FUNCTION REVERSE(WS-STRING)
                   TALLYING WS-POS-I      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = LENGTH OF WS-STRING  -  WS-POS-I.
     š*---------------------------------------------------------------------
       P300-PRIMEIRO-ESPACO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING(1:WS-STRING-L)
           MOVE ZEROES TO WS-POS-S.
           INSPECT WS-STRING(1:WS-STRING-L)
                   TALLYING WS-POS-S      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = WS-STRING-L -  WS-POS-S.
           ADD 1 TO WS-POS-S.
     š*---------------------------------------------------------------------
       P300-RETIRA-CARAC-ESPECIAIS.
     š*---------------------------------------------------------------------
     š*-Retira Caracters Invalidos de WS-STRING
           PERFORM P300-UTLS001-TESTA
                   VARYING W-NUM FROM 1 BY 1
                   UNTIL W-NUM > LENGTH OF WS-STRING.

     š*---------------------------------------------------------------------
       P300-UTLS001-TESTA.
     š*---------------------------------------------------------------------
           MOVE WS-STRING-CHR(W-NUM)  TO CHAR-TESTE.
           IF   NOT CARACTER-VALIDO
                PERFORM  P300-UTLS001-ALTERA
           END-IF.

     š*---------------------------------------------------------------------
       P300-UTLS001-ALTERA.
     š*---------------------------------------------------------------------
           EVALUATE  TRUE

            WHEN WS-STRING-CHR(W-NUM) = "à" OR
                 WS-STRING-CHR(W-NUM) = "á" OR
                 WS-STRING-CHR(W-NUM) = "â" OR
                 WS-STRING-CHR(W-NUM) = "ã" OR
                 WS-STRING-CHR(W-NUM) = "ä" OR
                 WS-STRING-CHR(W-NUM) = "ª"
              MOVE "a" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ç"
              MOVE "c" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "é" OR
                 WS-STRING-CHR(W-NUM) = "è" OR
                 WS-STRING-CHR(W-NUM) = "ê" OR
                 WS-STRING-CHR(W-NUM) = "ë"
              MOVE "e" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "í" OR
                 WS-STRING-CHR(W-NUM) = "ì" OR
                 WS-STRING-CHR(W-NUM) = "ï"
              MOVE "i" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ñ"
              MOVE "n" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ó" OR
                 WS-STRING-CHR(W-NUM) = "ò" OR
                 WS-STRING-CHR(W-NUM) = "õ" OR
                 WS-STRING-CHR(W-NUM) = "ö"
              MOVE "o" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ú" OR
                 WS-STRING-CHR(W-NUM) = "ù" OR
                 WS-STRING-CHR(W-NUM) = "û" OR
                 WS-STRING-CHR(W-NUM) = "ü"
              MOVE "u" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Á" OR
                 WS-STRING-CHR(W-NUM) = "À" OR
                 WS-STRING-CHR(W-NUM) = "Ã" OR
                 WS-STRING-CHR(W-NUM) = "Â" OR
                 WS-STRING-CHR(W-NUM) = "Ä"
              MOVE "A" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ç"
              MOVE "C" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "É" OR
                 WS-STRING-CHR(W-NUM) = "È" OR
                 WS-STRING-CHR(W-NUM) = "Ê" OR
                 WS-STRING-CHR(W-NUM) = "Ë"
              MOVE "E" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Í" OR
                 WS-STRING-CHR(W-NUM) = "Ì" OR
                 WS-STRING-CHR(W-NUM) = "Ï"
              MOVE "I" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ñ"
              MOVE "N" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ó" OR
                 WS-STRING-CHR(W-NUM) = "Ò" OR
                 WS-STRING-CHR(W-NUM) = "Õ" OR
                 WS-STRING-CHR(W-NUM) = "Ô" OR
                 WS-STRING-CHR(W-NUM) = "Ö"
              MOVE "O" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ú" OR
                 WS-STRING-CHR(W-NUM) = "Ù" OR
                 WS-STRING-CHR(W-NUM) = "Û" OR
                 WS-STRING-CHR(W-NUM) = "Ü"
              MOVE "U" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "\"
              MOVE "/" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "[" OR
                 WS-STRING-CHR(W-NUM) = "{"
              MOVE "(" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "]" OR
                 WS-STRING-CHR(W-NUM) = "}"
              MOVE ")" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = """" OR
                 WS-STRING-CHR(W-NUM) = "«"  OR
                 WS-STRING-CHR(W-NUM) = "»"
              MOVE "'" TO WS-STRING-CHR(W-NUM)

            WHEN OTHER
              MOVE " " TO WS-STRING-CHR(W-NUM)

           END-EVALUATE.


     š*---------------------------------------------------------------------
       P990-ERRO-FS.
     š*---------------------------------------------------------------------
           EVALUATE TRUE
           WHEN GRS-READ-DUP-KEY
                  STRING "ERRO DE DADOS: CHAVE DUPLICADA NO FICHEIRO <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-FS
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ ERRO    <"
                      W-EDTFILC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-FS
           END-EVALUATE.
     š*
           INITIALIZE   AMLT900A
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           SET  COM-ERRO        TO TRUE
           MOVE  1              TO ERRRTC   OF  AMLT900A
           MOVE W-FILE-CODE     TO ERRCOD   OF  AMLT900A
           MOVE MSG-ERRO-FS     TO ERRMSG   OF  AMLT900A
           MOVE "AMLP200"       TO ERRPRG   OF  AMLT900A
           MOVE "AML"           TO ERRAPL   OF  AMLT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  AMLT900A
           MOVE W-CMDEXEC       TO ERRCMD   OF  AMLT900A
           MOVE W-PARAGRAFO     TO ERRFLD   OF  AMLT900A

           PERFORM    P995-INSERT-ERROS.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   AMLT900A
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  AMLT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  AMLT900A
           MOVE "SQL"           TO ERRAPL   OF  AMLT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  AMLT900A
           MOVE  2              TO ERRRTC   OF  AMLT900A

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "AMLP200"      TO  ERRPRG OF AMLT900A.
           MOVE   WS-NURACI      TO  RACINE OF AMLT900A.

           EXEC SQL  INSERT
                INTO AMLT900A
                VALUES  (
                           :AMLT900A.RACINE
                         , :AMLT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :AMLT900A.ERRRTC
                         , :AMLT900A.ERRCOD
                         , :AMLT900A.ERRMSG
                         , :AMLT900A.ERRPRG
                         , :AMLT900A.ERRAPL
                         , :AMLT900A.ERROBJ
                         , :AMLT900A.ERRFLD
                         , :AMLT900A.ERRCMD
                         , :AMLT900A.ERRKEY1
                         , :AMLT900A.ERRVAL1
                         , :AMLT900A.ERRKEY2
                         , :AMLT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN OTHER
              MOVE "AMLT900A"       TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.

     š*****************************************************************
       P999-FIMPGM.
     š*****************************************************************
           EXEC SQL    COMMIT    HOLD         END-EXEC
           EXIT PROGRAM.
           STOP RUN.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA AMLP200
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
