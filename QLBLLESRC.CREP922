      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP922.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP922
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP922"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-IDREGSEL.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  FILLER              PIC X(69).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVGRFUNC-I   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVGRFUNC-O   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           10  KV-NRMNUPRC-DFLT        PIC 99      VALUE 1.
           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB972.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET906 OF CRET906.
           COPY DDS-CRET908 OF CRET908.
           COPY DDS-CRET909 OF CRET909.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-IDREGSEL     TO WS-IDREGSEL
           IF  CREB000-MSGDLG-P > 1
           OR  CREB000-IDCANALC = CREB950-IDCANALC-RM
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB972-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB972-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN(CREB000-INITDLG AND NOT CREB000-PROCLOV)
           OR   CREB000-PROCIDT
               PERFORM P2100-INICIALIZA
               IF  CREB000-PROCNLF
               AND(CREB000-PROC-OK OR  CREB000-MSGINFO)
               AND CREB000-IDREG-OK
                   PERFORM P2500-PROC-REGEXIST
               END-IF
           WHEN CREB000-PROCLOV
               PERFORM P2200-PROC-LISTVALS
           WHEN CREB000-IDREG-NV
               PERFORM P2300-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2400-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2500-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2600-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2700-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2800-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB972-MSGDIALG
           SET  CREB000-IDREG-NF TO TRUE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCIDT OR CREB000-PROCSEL
               PERFORM P2110-READ-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2120-INIT-NOVOREG
           END-EVALUATE
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2110-READ-REGEXIST         SECTION.

           MOVE WS-IDGRFUNC OF WS-IDREGSEL   TO CREB972-IDGRFUNC-V
           MOVE WS-IDAPLICA OF WS-IDREGSEL   TO CREB972-IDAPLICA-V
           PERFORM P9000-READ-CRET909

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2111-FRMT-MSGDIALG
           WHEN OTHER
               SET  CREB950-FUNCAO-INTERRP   TO TRUE
               SET  CREB000-MSGFUNC          TO TRUE
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               IF  CREB972-IDGRFUNC-V = CREB000-IDGRFUNC
               AND CREB972-IDAPLICA-V = CREB000-IDAPLICA
                   SET  CREB000-FUNCANUL-OFF TO TRUE
               END-IF
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
               AND NOT CREB950-FUNCAO-INTERRP
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           END-IF
           .
       P2110-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2111-FRMT-MSGDIALG         SECTION.

           MOVE NRMNUPRC OF CRET909       TO CREB972-NRMNUPRC-V
           MOVE NVACESSO OF CRET909       TO CREB972-NVACESSO-V
           MOVE DHINISIT OF CRET909       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB972-DHINISIT-V
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DATA-X08
           EVALUATE TRUE
           WHEN CREB950-DATA-N08    <= CREB000-DTPRCAPL
               MOVE MCSITEXE OF CRET909   TO CREB972-MCSITEXE-V
           WHEN MCSITEXE OF CRET909  = KV-MCSITEXE-ACTV
               MOVE KV-MCSITEXE-INIB      TO CREB972-MCSITEXE-V
           WHEN OTHER
               MOVE KV-MCSITEXE-ACTV      TO CREB972-MCSITEXE-V
           END-EVALUATE
           PERFORM P9000-READ-SITEXECU
           MOVE DHCRIREG OF CRET909       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB972-DHCRIREG-V
           IF  DHMDFREG OF CRET909 NOT = CREB950-TMSTPMIN
               MOVE DHMDFREG OF CRET909   TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14      TO CREB972-DHMDFREG-V
           END-IF
           MOVE DHMDFREG OF CRET909       TO CREB000-DHMDFREG
           PERFORM P9000-READ-CRET908

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE DSGRFUNC OF CRET908   TO CREB972-DSGRFUNC-V
               PERFORM P9000-READ-CRET901
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE NMAPLICA OF CRET901 TO CREB972-NMAPLICA-V
           END-IF
           .
       P2111-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2120-INIT-NOVOREG          SECTION.

           MOVE WS-IDGRFUNC OF WS-IDREGSEL TO CREB972-IDGRFUNC-V
           MOVE WS-IDAPLICA OF WS-IDREGSEL TO CREB972-IDAPLICA-V
           MOVE KV-NRMNUPRC-DFLT           TO CREB972-NRMNUPRC-V
           MOVE KV-MCSITEXE-ACTV           TO CREB972-MCSITEXE-V
           PERFORM P9000-READ-SITEXECU

           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PERMIT-DET
           PERFORM P9000-NDISPL-DET
           .
       P2120-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2210-FRMT-PARMSLDV
           ELSE
               PERFORM P2220-RECB-IDSELLDV
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDGRFUNC"
               MOVE CREB972-IDGRFUNC-V  TO WS-IDGRFUNC OF WS-LVGRFUNC-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVGRFUNC-I
               SET  CREB972-IDGRFUNC-PC TO TRUE
           WHEN "IDAPLICA"
               MOVE CREB972-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB972-IDAPLICA-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDGRFUNC"
           AND  WS-IDGRFUNC OF WS-LVGRFUNC-O NOT = SPACES
               MOVE WS-IDGRFUNC OF WS-LVGRFUNC-O TO CREB972-IDGRFUNC-V
               SET  CREB972-IDGRFUNC-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB972-IDAPLICA-V
               SET  CREB972-IDAPLICA-PC          TO TRUE
           END-EVALUATE
           .
       P2220-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB972-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB972-NRMNUPRC-V   TO CREB999-NRMNUPRC
           MOVE CREB972-IDGRFUNC-V   TO CREB999-IDGRFUNC
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO CREB000-PARMSINP
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB972-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           MOVE CREB972-IDGRFUNC-V TO WS-STRING80
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF CREB972-IDGRFUNC-V
             OR   (WS-CHRSTR80(WS-ARRYNDX1) = SPACE OR "*" OR "?")
               CONTINUE
           END-PERFORM

           PERFORM
           VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF CREB972-IDGRFUNC-V
             OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
               CONTINUE
           END-PERFORM

           EVALUATE TRUE
           WHEN CREB972-IDGRFUNC-V       = SPACES
               SET  CREB972-IDGRFUNC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN WS-ARRYNDX1             <= LENGTH OF CREB972-IDGRFUNC-V
               SET  CREB972-IDGRFUNC-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB972-IDAPLICA-V       = SPACES
               SET  CREB972-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               PERFORM P2510-VRIF-NIVEL01
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           PERFORM P9000-VLID-IDGRFUNC

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VLID-IDAPLICA
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-READ-CRET909
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-OK
               SET  CREB972-IDGRFUNC-PC    TO TRUE
               SET  CREB950-REGIST-JEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
               SET  CREB000-IDREG-NF       TO TRUE
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2520-VRIF-NIVEL02
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-MSGDIALG         SECTION.

           PERFORM P2530-FRMT-MSGDIALG
           .
       P2430-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE CREB972-VARSOUT
               PERFORM P2510-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2520-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2530-FRMT-MSGDIALG
               END-IF
           ELSE
               PERFORM P2540-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2500-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2510-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB972-NRMNUPRC-V = ZERO
               SET  CREB972-NRMNUPRC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB972-NVACESSO-V = ZERO
               SET  CREB972-NVACESSO-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2510-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2520-VRIF-NIVEL02          SECTION.

           PERFORM P9000-VRIF-CRET906
           .
       P2520-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2530-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2530-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2540-VRIF-RELDPEND         SECTION.

           CONTINUE
           .
       P2540-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2600-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2710-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2720-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2730-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-EXEC-TRNCRIAR         SECTION.

           PERFORM P9000-FRMT-CRET909-CHAV
           PERFORM P9000-FRMT-CRET909-ATRB
           PERFORM P9000-INSR-CRET909

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE CREB972-IDGRFUNC-V TO WS-IDGRFUNC OF WS-IDREGSEL
               MOVE CREB972-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           END-IF
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-CRET909-CHAV
           PERFORM P9000-FRMT-CRET909-ATRB
           PERFORM P9000-UPDT-CRET909
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-EXEC-TRNANULA         SECTION.

           PERFORM P9000-FRMT-CRET909-CHAV
           PERFORM P9000-DELT-CRET909
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-IDREG-NF
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
           WHEN CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-EVALUATE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
           OR   CREB000-IDCANALC = CREB950-IDCANALC-RM
               CONTINUE
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB972-MSGDIALG
           END-EVALUATE
           MOVE CREB972-MSGDIALG       TO CREB000-MSGDLG-V
                                         (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL   TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB972-IDGRFUNC-UP TO TRUE
           SET  CREB972-IDAPLICA-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB972-IDGRFUNC-PR TO TRUE
           SET  CREB972-IDAPLICA-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  CREB972-NRMNUPRC-UP     TO TRUE
           SET  CREB972-NVACESSO-UP     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  CREB972-NRMNUPRC-PR TO TRUE
           SET  CREB972-NVACESSO-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  CREB972-DSSITEXE-PR TO TRUE
           SET  CREB972-DHINISIT-PR TO TRUE
           SET  CREB972-DHCRIREG-PR TO TRUE
           SET  CREB972-DHMDFREG-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           IF  CREB000-PROCCRT
           OR (CREB000-PROCVRF AND CREB000-IDREG-NF)
               SET  CREB972-DSSITEXE-PR TO TRUE
           ELSE
               SET  CREB972-DSSITEXE-ND TO TRUE
           END-IF
           SET  CREB972-DHINISIT-ND     TO TRUE
           SET  CREB972-DHCRIREG-ND     TO TRUE
           SET  CREB972-DHMDFREG-ND     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET909          SECTION.

           MOVE CREB972-IDGRFUNC-V TO IDGRFUNC OF CRET909
           MOVE CREB972-IDAPLICA-V TO IDAPLICA OF CRET909
           EXEC SQL SELECT NRMNUPRC
                         , NVACESSO
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , DHCRIREG
                      INTO :CRET909.NRMNUPRC
                         , :CRET909.NVACESSO
                         , :CRET909.MCSITEXE
                         , :CRET909.DHINISIT
                         , :CRET909.DHMDFREG
                         , :CRET909.DHCRIREG
                      FROM CRET909
                     WHERE IDGRFUNC = :CRET909.IDGRFUNC
                       AND IDAPLICA = :CRET909.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP922.CRET909" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDGRFUNC         SECTION.

           PERFORM P9000-READ-CRET908

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSGRFUNC OF CRET908    TO CREB972-DSGRFUNC-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB972-IDGRFUNC-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET908          SECTION.

           MOVE CREB972-IDGRFUNC-V TO IDGRFUNC OF CRET908
           EXEC SQL SELECT DSGRFUNC
                      INTO :CRET908.DSGRFUNC
                      FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSGRFUNC OF CRET908
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP922.CRET908" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDAPLICA         SECTION.

           PERFORM P9000-READ-CRET901

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE NMAPLICA OF CRET901    TO CREB972-NMAPLICA-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB972-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           MOVE CREB972-IDAPLICA-V TO IDAPLICA OF CRET901
           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO NMAPLICA OF CRET901
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP922.CRET901" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET906          SECTION.

           MOVE CREB000-IDAPLICA    TO IDAPLICA OF CRET901
           MOVE CREB972-IDAPLICA-V  TO IDAPLICA OF CRET906
           MOVE CREB972-NRMNUPRC-V  TO NRMNUPRC OF CRET906
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET906
                     WHERE IDAPLICA = :CRET906.IDAPLICA
                       AND NRMNUPRC = :CRET906.NRMNUPRC
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB972-NRMNUPRC-PC    TO TRUE
               SET  CREB950-MNUPRC-NEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP922.EXIST01"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB972-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB972-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB972-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET909-CHAV     SECTION.

           MOVE CREB972-IDGRFUNC-V     TO IDGRFUNC OF CRET909
           MOVE CREB972-IDAPLICA-V     TO IDAPLICA OF CRET909
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET909
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET909-ATRB     SECTION.

           MOVE CREB972-NRMNUPRC-V        TO NRMNUPRC OF CRET909
           MOVE CREB972-NVACESSO-V        TO NVACESSO OF CRET909
           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB972-MCSITEXE-V    TO MCSITEXE OF CRET909
               MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB000-DTPRCAPL      TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08 TO CREB950-DTHR-X14
               MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
               MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET909
               MOVE CREB000-IDUTILIZ      TO IDUTLCRI OF CRET909
               MOVE CREB950-TMSTPMIN      TO DHMDFREG OF CRET909
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET909
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-CRET909          SECTION.

           EXEC SQL INSERT INTO CRET909
                         ( IDGRFUNC
                         , IDAPLICA
                         , NRMNUPRC
                         , NVACESSO
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , IDUTLMDF
                         , DHCRIREG
                         , IDUTLCRI
                         )
                    VALUES
                         ( :CRET909.IDGRFUNC
                         , :CRET909.IDAPLICA
                         , :CRET909.NRMNUPRC
                         , :CRET909.NVACESSO
                         , :CRET909.MCSITEXE
                         , :CRET909.DHINISIT
                         , :CRET909.DHMDFREG
                         , :CRET909.IDUTLMDF
                         , CURRENT TIMESTAMP
                         , :CRET909.IDUTLCRI
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP922.CRET909" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET909          SECTION.

           EXEC SQL UPDATE CRET909
                       SET NRMNUPRC = :CRET909.NRMNUPRC
                         , NVACESSO = :CRET909.NVACESSO
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET909.IDUTLMDF
                     WHERE IDGRFUNC = :CRET909.IDGRFUNC
                       AND IDAPLICA = :CRET909.IDAPLICA
                       AND DHMDFREG = :CRET909.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP922.CRET909"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET909          SECTION.

           EXEC SQL DELETE FROM CRET909
                     WHERE IDGRFUNC = :CRET909.IDGRFUNC
                       AND IDAPLICA = :CRET909.IDAPLICA
                       AND DHMDFREG = :CRET909.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP922.CRET909"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP922 ------------------------------------*
