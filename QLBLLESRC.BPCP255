      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.     BPCP255.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: BPCP255
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "BPCE255"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT BPCE255
                  ASSIGN        TO WORKSTATION-BPCE255-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-BPCE255
                  CONTROL-AREA  IS CA-BPCE255.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  BPCE255
           LABEL RECORD OMITTED.
       01  BPCE255-HDR.
           COPY DDS-BPCE255HDR OF BPCE255.
       01  BPCE255-D01.
           COPY DDS-BPCE255D01 OF BPCE255.
       01  BPCE255-FTR.
           COPY DDS-BPCE255FTR OF BPCE255.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-BPCE255                  PIC 99.
           88  BPCE255-OK                          VALUE ZERO.

       01  CA-BPCE255.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.

       01  WS-EDITVARS.
           05  ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDDIALOG            PIC X(10).
               10  REGLISTA                                OCCURS 11.
                   15  NROPCLIN        PIC 9.
                   15  IDDLGLIN        PIC X(10).
                   15  TYPERLIN        PIC X(02).
                   15  DTDEMFINLIN     PIC X(10).
                   15  DTAVRISCLIN     PIC X(10).
                   15  TPPDENTLIN      PIC X(03).
                   15  PDENTLIN        PIC 9(3)V9(5).
           05  ED-CREEVARS-I   REDEFINES ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDDIALOG            PIC X(10).
               10  REGLISTA                                OCCURS 11.
                   15  NROPCLIN        PIC 9.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "BPCE255".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "BPCE255HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "BPCE255D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "BPCE255FTR".
           05  KV-IDDIALOG-HI          PIC 99      VALUE 25.
           05  KV-IDDIALOG-PC          PIC 99      VALUE 26.
           05  KV-IDDIALOG-PR          PIC 99      VALUE 27.
           05  KV-SUBSET01.
               10  KV-NROPCL01-PC      PIC 99      VALUE 31.
               10  KV-NROPCL01-PR      PIC 99      VALUE 32.
               10  KV-NROPCL02-PC      PIC 99      VALUE 33.
               10  KV-NROPCL02-PR      PIC 99      VALUE 34.
               10  KV-NROPCL03-PC      PIC 99      VALUE 35.
               10  KV-NROPCL03-PR      PIC 99      VALUE 36.
               10  KV-NROPCL04-PC      PIC 99      VALUE 37.
               10  KV-NROPCL04-PR      PIC 99      VALUE 38.
               10  KV-NROPCL05-PC      PIC 99      VALUE 39.
               10  KV-NROPCL05-PR      PIC 99      VALUE 40.
               10  KV-NROPCL06-PC      PIC 99      VALUE 41.
               10  KV-NROPCL06-PR      PIC 99      VALUE 42.
               10  KV-NROPCL07-PC      PIC 99      VALUE 43.
               10  KV-NROPCL07-PR      PIC 99      VALUE 44.
               10  KV-NROPCL08-PC      PIC 99      VALUE 45.
               10  KV-NROPCL08-PR      PIC 99      VALUE 46.
               10  KV-NROPCL09-PC      PIC 99      VALUE 47.
               10  KV-NROPCL09-PR      PIC 99      VALUE 48.
               10  KV-NROPCL10-PC      PIC 99      VALUE 49.
               10  KV-NROPCL10-PR      PIC 99      VALUE 50.
               10  KV-NROPCL11-PC      PIC 99      VALUE 51.
               10  KV-NROPCL11-PR      PIC 99      VALUE 52.
           05  FILLER      REDEFINES KV-SUBSET01           OCCURS 11.
               10  KV-NROPCLIN-PC      PIC 99.
               10  KV-NROPCLIN-PR      PIC 99.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY BPCB255.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-BPCE255, CA-BPCE255
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT BPCE255-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O BPCE255

           IF  NOT BPCE255-OK
               MOVE FS-BPCE255      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO BPCB255-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  BPCE255-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  BPCE255-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  BPCE255-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE BPCB255-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  BPCB255-IDDIALOG-PR
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PR)     TO TRUE
           END-IF

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > BPCB255-NRSELLIN-MAX
               IF  BPCB255-NROPCLIN-PR(WS-ARRYNDX1)
                   MOVE KV-NROPCLIN-PR(WS-ARRYNDX1) TO WS-ARRYNDX2
                   SET  CREB000-INDICDLG-ON (WS-ARRYNDX2)    TO TRUE
               END-IF
           END-PERFORM
      *
           EVALUATE TRUE
           WHEN BPCB255-IDDIALOG-PC
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PC)     TO TRUE
           WHEN BPCB255-PCURSOR > BPCB255-NRPARAMS-INP
               COMPUTE WS-ARRYNDX1 = 24 + (BPCB255-NRPARAMS-INP * 3)
                                   +(BPCB255-PCURSOR
                                   - BPCB255-NRPARAMS-INP) * 2 - 1
               SET  CREB000-INDICDLG-ON (WS-ARRYNDX1)        TO TRUE
           WHEN BPCB255-IDDIALOG-UP
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PC)     TO TRUE
           WHEN BPCB255-NROPCLIN-UP(1)
               SET  CREB000-INDICDLG-ON (KV-NROPCL01-PC)     TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN BPCB255-IDDIALOG-PC
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-HI)     TO TRUE
           END-EVALUATE
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO BPCE255-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF BPCE255HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF BPCE255HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF BPCE255HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF BPCE255HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF BPCE255HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF BPCE255HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF BPCE255HDR-O
           MOVE CREB000-NRENTLST    TO NRENTLST OF BPCE255HDR-O
           MOVE CREB000-DSOPCSEL    TO DSOPCSEL OF BPCE255HDR-O
           WRITE BPCE255-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT BPCE255-OK
               MOVE FS-BPCE255      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO BPCE255-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF BPCE255FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF BPCE255FTR-O
           WRITE BPCE255-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT BPCE255-OK
               MOVE FS-BPCE255      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO ED-CREEVARS-O
           MOVE BPCB255-IDDIALOG-V  TO IDDIALOG OF ED-CREEVARS-O
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > BPCB255-NRSELLIN-MAX
               MOVE BPCB255-NROPCLIN-V       (WS-ARRYNDX1)
                 TO NROPCLIN    OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE BPCB255-IDDLGLIN-V       (WS-ARRYNDX1)
                 TO IDDLGLIN    OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE BPCB255-TYPERLIN-V       (WS-ARRYNDX1)
                 TO TYPERLIN    OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE BPCB255-DTDEMFINLIN-V    (WS-ARRYNDX1)
                 TO DTDEMFINLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE BPCB255-DTAVRISCLIN-V    (WS-ARRYNDX1)
                 TO DTAVRISCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE BPCB255-TPPDENTLIN-V     (WS-ARRYNDX1)
                 TO TPPDENTLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE BPCB255-PDENTLIN-V       (WS-ARRYNDX1)
                 TO PDENTLIN   OF ED-CREEVARS-O(WS-ARRYNDX1)
           END-PERFORM
           MOVE ED-CREEVARS-O       TO BPCE255D01-O
           WRITE BPCE255-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT BPCE255-OK
               MOVE FS-BPCE255      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ BPCE255
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  BPCE255-OK
               MOVE BPCE255D01-I              TO ED-CREEVARS-I
               MOVE IDDIALOG OF ED-CREEVARS-I TO BPCB255-IDDIALOG-V
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > BPCB255-NRSELLIN-MAX
                   MOVE NROPCLIN OF ED-CREEVARS-I(WS-ARRYNDX1)
                     TO BPCB255-NROPCLIN-V       (WS-ARRYNDX1)
               END-PERFORM
               MOVE IDVARCSR OF ED-CREEVARS-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-BPCE255    TO CREB000-IDTECLAF
           ELSE
               MOVE FS-BPCE255                TO CREB000-CDRETORN
               MOVE KV-IDDIALOG               TO CREB000-NMOBJECT
               SET  CREB000-READ              TO TRUE
               SET  CREB000-MSGDLOG           TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE BPCE255

           IF  NOT BPCE255-OK
               MOVE FS-BPCE255      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA BPCP255 ------------------------------------*
