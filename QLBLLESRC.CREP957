      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP957.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP957
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE957"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE957
                  ASSIGN        TO WORKSTATION-CREE957-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE957
                  CONTROL-AREA  IS CA-CREE957.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE957
           LABEL RECORD OMITTED.
       01  CREE957-HDR.
           COPY DDS-CREE957HDR OF CREE957.
       01  CREE957-D01.
           COPY DDS-CREE957D01 OF CREE957.
       01  CREE957-FTR.
           COPY DDS-CREE957FTR OF CREE957.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE957                  PIC 99.
           88  CREE957-OK                          VALUE ZERO.

       01  CA-CREE957.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.

       01  WS-EDITVARS.
           05  ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDPRCAPL            PIC X(08).
               10  DSPRCAPL            PIC X(32).
               10  IDAPLICA            PIC X(03).
               10  NMAPLICA            PIC X(34).
               10  IDDIALOG            PIC X(08).
               10  IDTIPDLG            PIC XX.
               10  DSTIPDLG            PIC X(19).
               10  MCSITEXE            PIC X.
               10  DSSITEXE            PIC X(07).
               10  IDPRCDEP            PIC X(08).
               10  REGLISTA                                OCCURS 9.
                   15  NROPCLIN        PIC 9.
                   15  IDPRCLIN        PIC X(08).
                   15  NMPRCLIN        PIC X(32).
                   15  IDAPLLIN        PIC X(03).
                   15  IDGSTLIN        PIC X(08).
                   15  IDDLGLIN        PIC X(08).
                   15  TPDLGLIN        PIC XX.
                   15  STEXELIN        PIC X.
           05  ED-CREEVARS-I   REDEFINES ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDPRCAPL            PIC X(08).
               10  DSPRCAPL            PIC X(32).
               10  IDAPLICA            PIC X(03).
               10  NMAPLICA            PIC X(34).
               10  IDDIALOG            PIC X(08).
               10  IDTIPDLG            PIC XX.
               10  DSTIPDLG            PIC X(19).
               10  MCSITEXE            PIC X.
               10  DSSITEXE            PIC X(07).
               10  IDPRCDEP            PIC X(08).
               10  REGLISTA                                OCCURS 9.
                   15  NROPCLIN        PIC 9.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE957".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE957HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE957D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE957FTR".
           05  KV-IDAPLICA-HI          PIC 99      VALUE 25.
           05  KV-IDAPLICA-PC          PIC 99      VALUE 26.
           05  KV-IDAPLICA-PR          PIC 99      VALUE 27.
           05  KV-IDPRCAPL-HI          PIC 99      VALUE 28.
           05  KV-IDPRCAPL-PC          PIC 99      VALUE 29.
           05  KV-IDPRCAPL-PR          PIC 99      VALUE 30.
           05  KV-DSPRCAPL-HI          PIC 99      VALUE 31.
           05  KV-DSPRCAPL-PC          PIC 99      VALUE 32.
           05  KV-DSPRCAPL-PR          PIC 99      VALUE 33.
           05  KV-IDDIALOG-HI          PIC 99      VALUE 34.
           05  KV-IDDIALOG-PC          PIC 99      VALUE 35.
           05  KV-IDDIALOG-PR          PIC 99      VALUE 36.
           05  KV-IDTIPDLG-HI          PIC 99      VALUE 37.
           05  KV-IDTIPDLG-PC          PIC 99      VALUE 38.
           05  KV-IDTIPDLG-PR          PIC 99      VALUE 39.
           05  KV-MCSITEXE-HI          PIC 99      VALUE 40.
           05  KV-MCSITEXE-PC          PIC 99      VALUE 41.
           05  KV-MCSITEXE-PR          PIC 99      VALUE 42.
           05  KV-IDPRCDEP-HI          PIC 99      VALUE 43.
           05  KV-IDPRCDEP-PC          PIC 99      VALUE 44.
           05  KV-IDPRCDEP-PR          PIC 99      VALUE 45.
           05  KV-SUBSET01.
               10  KV-NROPCL01-PC      PIC 99      VALUE 46.
               10  KV-NROPCL01-PR      PIC 99      VALUE 47.
               10  KV-NROPCL02-PC      PIC 99      VALUE 48.
               10  KV-NROPCL02-PR      PIC 99      VALUE 49.
               10  KV-NROPCL03-PC      PIC 99      VALUE 50.
               10  KV-NROPCL03-PR      PIC 99      VALUE 51.
               10  KV-NROPCL04-PC      PIC 99      VALUE 52.
               10  KV-NROPCL04-PR      PIC 99      VALUE 53.
               10  KV-NROPCL05-PC      PIC 99      VALUE 54.
               10  KV-NROPCL05-PR      PIC 99      VALUE 55.
               10  KV-NROPCL06-PC      PIC 99      VALUE 56.
               10  KV-NROPCL06-PR      PIC 99      VALUE 57.
               10  KV-NROPCL07-PC      PIC 99      VALUE 58.
               10  KV-NROPCL07-PR      PIC 99      VALUE 59.
               10  KV-NROPCL08-PC      PIC 99      VALUE 60.
               10  KV-NROPCL08-PR      PIC 99      VALUE 61.
               10  KV-NROPCL09-PC      PIC 99      VALUE 62.
               10  KV-NROPCL09-PR      PIC 99      VALUE 63.
           05  FILLER      REDEFINES KV-SUBSET01           OCCURS 9.
               10  KV-NROPCLIN-PC      PIC 99.
               10  KV-NROPCLIN-PR      PIC 99.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB957.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE957, CA-CREE957
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE957-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE957

           IF  NOT CREE957-OK
               MOVE FS-CREE957      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB957-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE957-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE957-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE957-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB957-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB957-IDAPLICA-PR
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PR)     TO TRUE
           END-IF
      *
           IF  CREB957-IDPRCAPL-PR
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PR)     TO TRUE
           END-IF
      *
           IF  CREB957-DSPRCAPL-PR
               SET  CREB000-INDICDLG-ON (KV-DSPRCAPL-PR)     TO TRUE
           END-IF
      *
           IF  CREB957-IDDIALOG-PR
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PR)     TO TRUE
           END-IF
      *
           IF  CREB957-IDTIPDLG-PR
               SET  CREB000-INDICDLG-ON (KV-IDTIPDLG-PR)     TO TRUE
           END-IF
      *
           IF  CREB957-MCSITEXE-PR
               SET  CREB000-INDICDLG-ON (KV-MCSITEXE-PR)     TO TRUE
           END-IF
      *
           IF  CREB957-IDPRCDEP-PR
               SET  CREB000-INDICDLG-ON (KV-IDPRCDEP-PR)     TO TRUE
           END-IF
      *
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
               IF  CREB957-NROPCLIN-PR(WS-ARRYNDX1)
                   MOVE KV-NROPCLIN-PR(WS-ARRYNDX1) TO WS-ARRYNDX2
                   SET  CREB000-INDICDLG-ON (WS-ARRYNDX2)    TO TRUE
               END-IF
           END-PERFORM
      *
           EVALUATE TRUE
           WHEN CREB957-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC)     TO TRUE
           WHEN CREB957-IDPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PC)     TO TRUE
           WHEN CREB957-DSPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-DSPRCAPL-PC)     TO TRUE
           WHEN CREB957-IDDIALOG-PC
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PC)     TO TRUE
           WHEN CREB957-IDTIPDLG-PC
               SET  CREB000-INDICDLG-ON (KV-IDTIPDLG-PC)     TO TRUE
           WHEN CREB957-MCSITEXE-PC
               SET  CREB000-INDICDLG-ON (KV-MCSITEXE-PC)     TO TRUE
           WHEN CREB957-IDPRCDEP-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCDEP-PC)     TO TRUE
           WHEN CREB957-PCURSOR > CREB957-NRPARAMS-INP
               COMPUTE WS-ARRYNDX1 = 24 + (CREB957-NRPARAMS-INP * 3)
                                   +(CREB957-PCURSOR
                                   - CREB957-NRPARAMS-INP) * 2 - 1
               SET  CREB000-INDICDLG-ON (WS-ARRYNDX1)        TO TRUE
           WHEN CREB957-IDAPLICA-UP
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC)     TO TRUE
           WHEN CREB957-NROPCLIN-UP(1)
               SET  CREB000-INDICDLG-ON (KV-NROPCL01-PC)     TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB957-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-HI)     TO TRUE
           WHEN CREB957-IDPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-HI)     TO TRUE
           WHEN CREB957-DSPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-DSPRCAPL-HI)     TO TRUE
           WHEN CREB957-IDDIALOG-PC
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-HI)     TO TRUE
           WHEN CREB957-IDTIPDLG-PC
               SET  CREB000-INDICDLG-ON (KV-IDTIPDLG-HI)     TO TRUE
           WHEN CREB957-MCSITEXE-PC
               SET  CREB000-INDICDLG-ON (KV-MCSITEXE-HI)     TO TRUE
           WHEN CREB957-IDPRCDEP-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCDEP-HI)     TO TRUE
           END-EVALUATE
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE957-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE957HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE957HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE957HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE957HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE957HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE957HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE957HDR-O
           MOVE CREB000-NRENTLST    TO NRENTLST OF CREE957HDR-O
           MOVE CREB000-DSOPCSEL    TO DSOPCSEL OF CREE957HDR-O
           WRITE CREE957-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE957-OK
               MOVE FS-CREE957      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE957-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE957FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE957FTR-O
           WRITE CREE957-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE957-OK
               MOVE FS-CREE957      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO ED-CREEVARS-O
           MOVE CREB957-IDAPLICA-V  TO IDAPLICA OF ED-CREEVARS-O
           MOVE CREB957-NMAPLICA-V  TO NMAPLICA OF ED-CREEVARS-O
           MOVE CREB957-IDPRCAPL-V  TO IDPRCAPL OF ED-CREEVARS-O
           MOVE CREB957-DSPRCAPL-V  TO DSPRCAPL OF ED-CREEVARS-O
           MOVE CREB957-IDDIALOG-V  TO IDDIALOG OF ED-CREEVARS-O
           MOVE CREB957-IDTIPDLG-V  TO IDTIPDLG OF ED-CREEVARS-O
           MOVE CREB957-DSTIPDLG-V  TO DSTIPDLG OF ED-CREEVARS-O
           MOVE CREB957-MCSITEXE-V  TO MCSITEXE OF ED-CREEVARS-O
           MOVE CREB957-DSSITEXE-V  TO DSSITEXE OF ED-CREEVARS-O
           MOVE CREB957-IDPRCDEP-V  TO IDPRCDEP OF ED-CREEVARS-O
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
               MOVE CREB957-NROPCLIN-V       (WS-ARRYNDX1)
                 TO NROPCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-IDPRCLIN-V       (WS-ARRYNDX1)
                 TO IDPRCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-NMPRCLIN-V       (WS-ARRYNDX1)
                 TO NMPRCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-IDAPLLIN-V       (WS-ARRYNDX1)
                 TO IDAPLLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-IDGSTLIN-V       (WS-ARRYNDX1)
                 TO IDGSTLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-IDDLGLIN-V       (WS-ARRYNDX1)
                 TO IDDLGLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-TPDLGLIN-V       (WS-ARRYNDX1)
                 TO TPDLGLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB957-STEXELIN-V       (WS-ARRYNDX1)
                 TO STEXELIN OF ED-CREEVARS-O(WS-ARRYNDX1)
           END-PERFORM
           MOVE ED-CREEVARS-O       TO CREE957D01-O
           WRITE CREE957-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE957-OK
               MOVE FS-CREE957      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE957
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE957-OK
               MOVE CREE957D01-I              TO ED-CREEVARS-I
               MOVE IDAPLICA OF ED-CREEVARS-I TO CREB957-IDAPLICA-V
               MOVE IDPRCAPL OF ED-CREEVARS-I TO CREB957-IDPRCAPL-V
               MOVE DSPRCAPL OF ED-CREEVARS-I TO CREB957-DSPRCAPL-V
               MOVE IDDIALOG OF ED-CREEVARS-I TO CREB957-IDDIALOG-V
               MOVE IDTIPDLG OF ED-CREEVARS-I TO CREB957-IDTIPDLG-V
               MOVE MCSITEXE OF ED-CREEVARS-I TO CREB957-MCSITEXE-V
               MOVE IDPRCDEP OF ED-CREEVARS-I TO CREB957-IDPRCDEP-V
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
                   MOVE NROPCLIN OF ED-CREEVARS-I(WS-ARRYNDX1)
                     TO CREB957-NROPCLIN-V       (WS-ARRYNDX1)
               END-PERFORM
               MOVE IDVARCSR OF ED-CREEVARS-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE957    TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE957                TO CREB000-CDRETORN
               MOVE KV-IDDIALOG               TO CREB000-NMOBJECT
               SET  CREB000-READ              TO TRUE
               SET  CREB000-MSGDLOG           TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE957

           IF  NOT CREE957-OK
               MOVE FS-CREE957      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP957 ------------------------------------*
