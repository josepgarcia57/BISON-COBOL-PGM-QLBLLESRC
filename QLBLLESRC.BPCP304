      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.     BPCP304.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: BPCP304
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "BPCP304"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY BPCB002.
           COPY CREB950.

       01 WS-POS-NOMI                PIC S9(4)   COMP   VALUE 0.
       01 WS-NOM-L                   PIC S9(4)   COMP   VALUE 0.
       01 WS-NOM.
          03 W-NOM                   PIC X       OCCURS 35.
       01 WS-POS-PRNMI               PIC S9(4)   COMP   VALUE 0.
       01 WS-PRNM-L                  PIC S9(4)   COMP   VALUE 0.
       01 WS-PRNM.
          03 W-PRNM                  PIC X       OCCURS 35.
       01 WS-NOM-COMPLETO            PIC X(80).

       01  WS-VARSTRAB.
           05  WS-IDREGSEL.
               10  WS-IDDIALOG         PIC X(32).
               10  WS-IDAUXPROT        PIC X(32).
               10  FILLER              PIC X(16).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVCTAB-I
               REDEFINES WS-PARMSLDV.
               10  WS-CTAB             PIC X(03).
               10  FILLER              PIC X(77).
           05  WS-LVCCOD-O
               REDEFINES WS-PARMSLDV.
               10  WS-COD              PIC X(20).
               10  FILLER              PIC X(60).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-NRPGSHRZ-DFLT        PIC 9       VALUE 1.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY BPCB354.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-BPCTPFIN2 OF BPCTPFIN2.
       01  R-FDBCLI.
           COPY DDS-ALL-FORMATS OF FDBCLI.

       01  WS-NURACI                    PIC X(07).
       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-IDREGSEL     TO WS-IDREGSEL
           IF  CREB000-MSGDLG-P > 1
           OR  CREB000-IDCANALC = CREB950-IDCANALC-RM
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO BPCB354-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, BPCB354-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN(CREB000-INITDLG AND NOT CREB000-PROCLOV)
           OR   CREB000-PROCIDT
               PERFORM P2100-INICIALIZA
               IF  CREB000-PROCNLF
               AND(CREB000-PROC-OK OR  CREB000-MSGINFO)
               AND CREB000-IDREG-OK
                   PERFORM P2500-PROC-REGEXIST
               END-IF
           WHEN CREB000-PROCLOV
               PERFORM P2200-PROC-LISTVALS
           WHEN CREB000-IDREG-NV
               PERFORM P2300-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2400-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2500-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2600-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2700-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2800-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE BPCB354-MSGDIALG
           SET  CREB000-IDREG-NF TO TRUE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCIDT OR CREB000-PROCSEL
               PERFORM P2110-READ-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2120-INIT-NOVOREG
           END-EVALUATE
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2110-READ-REGEXIST         SECTION.

           MOVE WS-IDDIALOG  OF WS-IDREGSEL TO BPCB354-IDINST-V
           MOVE WS-IDAUXPROT OF WS-IDREGSEL TO BPCB354-IDPROT-V
           PERFORM P9000-READ-BPCTPFIN2

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2111-FRMT-MSGDIALG
           WHEN OTHER
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
               AND NOT CREB950-FUNCAO-INTERRP
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           END-IF
           .
       P2110-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2111-FRMT-MSGDIALG         SECTION.

           MOVE IDINST     OF BPCTPFIN2     TO BPCB354-IDINST-V
           MOVE IDPROT     OF BPCTPFIN2     TO BPCB354-IDPROT-V
           MOVE VALPROT    OF BPCTPFIN2     TO BPCB354-VALPROT-V
           MOVE CREDPRIOR  OF BPCTPFIN2     TO BPCB354-CREDPRIOR-V
           MOVE INDEXECPRT OF BPCTPFIN2     TO BPCB354-INDEXECPRT-V
           MOVE VALEXECPRT OF BPCTPFIN2     TO BPCB354-VALEXECPRT-V
           MOVE TMSCRIA    OF BPCTPFIN2     TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO       TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14            TO BPCB354-DHCRIREG-V
           IF  TMSALT   OF BPCTPFIN2 NOT = CREB950-TMSTPMIN
               MOVE TMSALT   OF BPCTPFIN2 TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14      TO BPCB354-DHMDFREG-V
           END-IF
           MOVE TMSALT   OF BPCTPFIN2     TO CREB000-DHMDFREG

      *- Carrega Descritivos na Consulta                               *
           PERFORM P9000-DESC-IDINST
           PERFORM P9000-DESC-INDEXECPRT
           .
       P2111-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2120-INIT-NOVOREG          SECTION.

           MOVE WS-IDDIALOG OF WS-IDREGSEL TO BPCB354-IDINST-V
      *    MOVE KV-NRPGSHRZ-DFLT           TO BPCB354-NRPGSHRZ-V

           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PERMIT-DET
           .
       P2120-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2210-FRMT-PARMSLDV
           ELSE
               PERFORM P2220-RECB-IDSELLDV
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *-------------------------------                                 *
      *- Obtenção da Lista de Códigos                                  *
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "INDEXECPRT"
               MOVE CREB950-TBP-EEXECG  TO WS-CTAB
               SET  BPCB354-INDEXECPRT-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2210-EXIT.                 EXIT.
      *------------------------------------------------                *
      *- Recebe Valor Escolhido na Lista de Valores                    *
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "INDEXECPRT"
           AND  WS-COD  OF WS-LVCCOD-O   NOT = SPACES
               MOVE WS-COD  OF WS-LVCCOD-O TO BPCB354-INDEXECPRT-V
               SET  BPCB354-INDEXECPRT-PC        TO TRUE
           END-EVALUATE
           .
       P2220-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE BPCB354-IDINST-V     TO CREB999-IDDIALOG
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO CREB000-PARMSINP
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE BPCB354-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           MOVE BPCB354-IDINST-V   TO WS-STRING80
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF BPCB354-IDINST-V
             OR   (WS-CHRSTR80(WS-ARRYNDX1) = SPACE OR "*" OR "?")
               CONTINUE
           END-PERFORM

           PERFORM
           VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF BPCB354-IDINST-V
             OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
               CONTINUE
           END-PERFORM

           EVALUATE TRUE
           WHEN BPCB354-IDINST-V  = SPACES
               SET  BPCB354-IDINST-PC      TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN WS-ARRYNDX1        <= LENGTH OF BPCB354-IDINST-V
               SET  BPCB354-IDINST-PC      TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               PERFORM P2510-VRIF-NIVEL01
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           PERFORM P9000-READ-BPCTPFIN2

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-OK
               SET  BPCB354-IDINST-PC      TO TRUE
               SET  CREB950-REGIST-JEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
               SET  CREB000-IDREG-NF       TO TRUE
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2520-VRIF-NIVEL02
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2430-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE BPCB354-VARSOUT
               PERFORM P2510-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2520-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2530-FRMT-MSGDIALG
               END-IF
           ELSE
               CONTINUE
      *        PERFORM P2540-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2500-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2510-VRIF-NIVEL01          SECTION.

      *    EVALUATE TRUE
      *    WHEN BPCB354-IDAPLICA-V = SPACES
      *        SET  BPCB354-IDAPLICA-PC    TO TRUE
      *        SET  CREB950-PCAMPO-OBRIGAT TO TRUE
      *        SET  CREB000-MSGFUNC        TO TRUE
      *    END-EVALUATE
           continue
           .
       P2510-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2520-VRIF-NIVEL02          SECTION.

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-IDINST
           END-IF.

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-INDEXECPRT
           END-IF.

       P2520-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2530-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2530-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2600-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2710-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2720-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2730-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-EXEC-TRNCRIAR         SECTION.

           PERFORM P9000-FRMT-BPCTPFIN2-CHAV
           PERFORM P9000-FRMT-BPCTPFIN2-ATRB
           PERFORM P9000-INSR-BPCTPFIN2

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
      *        MOVE BPCB354-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
               MOVE BPCB354-IDINST-V   TO WS-IDDIALOG OF WS-IDREGSEL
           END-IF
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-BPCTPFIN2-CHAV
           PERFORM P9000-FRMT-BPCTPFIN2-ATRB
           PERFORM P9000-UPDT-BPCTPFIN2
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-EXEC-TRNANULA         SECTION.

           PERFORM P9000-FRMT-BPCTPFIN2-CHAV
           PERFORM P9000-DELT-BPCTPFIN2
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-IDREG-NF
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
           WHEN CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-EVALUATE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
           OR   CREB000-IDCANALC = CREB950-IDCANALC-RM
               CONTINUE
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF BPCB354-MSGDIALG
           END-EVALUATE
           MOVE BPCB354-MSGDIALG       TO CREB000-MSGDLG-V
                                         (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL   TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  BPCB354-IDINST-UP   TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  BPCB354-IDINST-PR   TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  BPCB354-VALPROT-UP    TO TRUE
           SET  BPCB354-CREDPRIOR-UP  TO TRUE
           SET  BPCB354-INDEXECPRT-UP TO TRUE
           SET  BPCB354-VALEXECPRT-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  BPCB354-IDINST-PR     TO TRUE
           SET  BPCB354-VALPROT-PR    TO TRUE
           SET  BPCB354-CREDPRIOR-PR  TO TRUE
           SET  BPCB354-INDEXECPRT-PR TO TRUE
           SET  BPCB354-VALEXECPRT-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  BPCB354-DHCRIREG-PR TO TRUE
           SET  BPCB354-DHMDFREG-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           SET  BPCB354-DHCRIREG-ND TO TRUE
           SET  BPCB354-DHMDFREG-ND TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-BPCTPFIN2          SECTION.

           MOVE BPCB354-IDINST-V      TO IDINST   OF BPCTPFIN2
           MOVE BPCB354-IDPROT-V      TO IDPROT   OF BPCTPFIN2

           EXEC SQL SELECT IDCONT
                         , IDINST
                         , IDPROT
                         , VALPROT
                         , CREDPRIOR
                         , INDEXECPRT
                         , VALEXECPRT
                         , TMSCRIA
                         , USERCRIA
                         , TMSALT
                         , USERALT
                      INTO :BPCTPFIN2.IDCONT
                         , :BPCTPFIN2.IDINST
                         , :BPCTPFIN2.IDPROT
                         , :BPCTPFIN2.VALPROT
                         , :BPCTPFIN2.CREDPRIOR
                         , :BPCTPFIN2.INDEXECPRT
                         , :BPCTPFIN2.VALEXECPRT
                         , :BPCTPFIN2.TMSCRIA
                         , :BPCTPFIN2.USERCRIA
                         , :BPCTPFIN2.TMSALT
                         , :BPCTPFIN2.USERALT
                      FROM BPCTPFIN2
                     WHERE IDINST   = :BPCTPFIN2.IDINST
                     AND   IDPROT   = :BPCTPFIN2.IDPROT       END-EXEC.

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "BPCP304.BPCTPFIN2" TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-IDINST        SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           MOVE  BPCB354-IDINST-V(1:7) TO WS-NURACI

           EXEC SQL
                SELECT  CLRACI
                     ,  CLNOM
                     ,  CLPRNM
                INTO   :CLRACI
                     , :CLNOM
                     , :CLPRNM
                FROM  FDBCLI A
                WHERE A.CLRACI = :WS-NURACI
           END-EXEC.

           MOVE SQLCODE TO CREB950-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-OK
              PERFORM P9000-NOME
              MOVE WS-NOM-COMPLETO      TO BPCB354-NMRACINE-V
           WHEN SQLCODE-NOTFOUND
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB354-IDINST-PC       TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "CREP909.FDBCLI"    TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE.

       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NOME  SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           MOVE  CLNOM     OF R-FDBCLI   TO WS-NOM
           MOVE  CLPRNM    OF R-FDBCLI   TO WS-PRNM
           MOVE ZEROS TO WS-POS-NOMI WS-POS-PRNMI

           INSPECT FUNCTION REVERSE(WS-NOM)
                   TALLYING WS-POS-NOMI     FOR LEADING SPACE.
           COMPUTE  WS-NOM-L    = LENGTH OF WS-NOM  -  WS-POS-NOMI.

           INSPECT FUNCTION REVERSE(WS-PRNM)
                   TALLYING WS-POS-PRNMI    FOR LEADING SPACE.
           COMPUTE  WS-PRNM-L   = LENGTH OF WS-PRNM - WS-POS-PRNMI.

           MOVE SPACES                    TO WS-NOM-COMPLETO
           IF WS-PRNM-L > 0
             MOVE WS-PRNM(1 :WS-PRNM-L)   TO WS-NOM-COMPLETO
           END-IF
           IF WS-NOM-L  > 0
             ADD 2 TO WS-PRNM-L
             MOVE WS-NOM(1:WS-NOM-L)
                             TO WS-NOM-COMPLETO(WS-PRNM-L:WS-NOM-L)
           END-IF.
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-INDEXECPRT       SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           INITIALIZE BPCB002-LKPARMS

           MOVE CREB950-TBP-EEXECG   TO BPCB002-CTAB
           MOVE BPCB354-INDEXECPRT-V TO BPCB002-CACC

           CALL "BPCS002" USING BPCB002-LKPARMS

           IF BPCB002-PROC-OK
              MOVE BPCB002-CAMPO1          TO BPCB354-NMINDEXEC-V
              IF BPCB002-CAMPO1 = SPACES
                 MOVE BPCB002-DESC         TO BPCB354-NMINDEXEC-V
              END-IF
           ELSE
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB354-INDEXECPRT-PC   TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-BPCTPFIN2-CHAV     SECTION.

           MOVE BPCB354-IDINST-V       TO IDINST   OF BPCTPFIN2
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO TMSALT   OF BPCTPFIN2
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-BPCTPFIN2-ATRB     SECTION.

           MOVE BPCB354-IDINST-V       TO IDINST     OF BPCTPFIN2
           MOVE BPCB354-IDPROT-V       TO IDPROT     OF BPCTPFIN2
           MOVE BPCB354-IDINST-V       TO IDINST     OF BPCTPFIN2
           MOVE BPCB354-IDPROT-V       TO IDPROT     OF BPCTPFIN2
           MOVE BPCB354-VALPROT-V      TO VALPROT    OF BPCTPFIN2
           MOVE BPCB354-CREDPRIOR-V    TO CREDPRIOR  OF BPCTPFIN2
           MOVE BPCB354-INDEXECPRT-V   TO INDEXECPRT OF BPCTPFIN2
           MOVE BPCB354-VALEXECPRT-V   TO VALEXECPRT OF BPCTPFIN2

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB000-IDUTILIZ   TO USERCRIA OF BPCTPFIN2
               MOVE CREB950-TMSTPMIN   TO TMSCRIA  OF BPCTPFIN2
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ   TO USERALT  OF BPCTPFIN2
           END-EVALUATE
           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-BPCTPFIN2          SECTION.

           EXEC SQL INSERT INTO BPCTPFIN2
                         ( IDCONT
                         , IDINST
                         , IDPROT
                         , VALPROT
                         , CREDPRIOR
                         , INDEXECPRT
                         , VALEXECPRT
                         , TMSCRIA
                         , USERCRIA
                         , TMSALT
                         , USERALT
                         )
                    VALUES
                         ( :BPCTPFIN2.IDCONT
                         , :BPCTPFIN2.IDINST
                         , :BPCTPFIN2.IDPROT
                         , :BPCTPFIN2.VALPROT
                         , :BPCTPFIN2.CREDPRIOR
                         , :BPCTPFIN2.INDEXECPRT
                         , :BPCTPFIN2.VALEXECPRT
                         , CURRENT TIMESTAMP
                         , :BPCTPFIN2.USERCRIA
                         , :BPCTPFIN2.TMSALT
                         , :BPCTPFIN2.USERALT
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "BPCP304.BPCTPFIN2" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-BPCTPFIN2          SECTION.

           EXEC SQL UPDATE BPCTPFIN2
                       SET IDCONT      =:BPCTPFIN2.IDCONT
                         , IDINST      =:BPCTPFIN2.IDINST
                         , IDPROT      =:BPCTPFIN2.IDPROT
                         , VALPROT     =:BPCTPFIN2.VALPROT
                         , CREDPRIOR   =:BPCTPFIN2.CREDPRIOR
                         , INDEXECPRT  =:BPCTPFIN2.INDEXECPRT
                         , VALEXECPRT  =:BPCTPFIN2.VALEXECPRT
                         , TMSALT      = CURRENT TIMESTAMP
                         , USERALT     = :BPCTPFIN2.USERALT
                     WHERE IDINST   = :BPCTPFIN2.IDINST
                       AND TMSALT   = :BPCTPFIN2.TMSALT       END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "BPCP304.BPCTPFIN2"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-BPCTPFIN2          SECTION.

           EXEC SQL DELETE FROM BPCTPFIN2
                     WHERE IDINST   = :BPCTPFIN2.IDINST
                       AND TMSALT   = :BPCTPFIN2.TMSALT        END-EXEC.

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "BPCP304.BPCTPFIN2"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA BPCP304 ------------------------------------*
