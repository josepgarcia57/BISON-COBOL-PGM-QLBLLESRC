      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP916.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP916
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP916"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-NRMNUPRC         PIC 99.
               10  WS-IDPRCORI         PIC X(08).
               10  WS-IDPRCDST         PIC X(08).
               10  FILLER              PIC X(59).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).
           05  WS-IDREGSEL.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-NRMNUPRC         PIC 99.
               10  WS-IDPRCORI         PIC X(08).
               10  WS-NROPCMNU         PIC 99.
               10  FILLER              PIC X(65).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVPRCAPL-I   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDTIPDLG         PIC XX.
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPRCAPL-O   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).

           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.
           05  WS-COUNTER1             PIC S9(4)       BINARY.

           05  CS-QMNUNVG              PIC X.
               88  QMNUNVG-CLSE                    VALUE "C".
               88  QMNUNVG-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB966.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET904 OF CRET904.
           COPY DDS-CRET906 OF CRET906.
           COPY DDS-CRET907 OF CRET907.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDAPLICA         PIC X(03).
               10  DB-NRMNUPRC         PIC S9(2)       COMP-3.
               10  DB-IDPRCORI         PIC X(08).
               10  DB-IDPRCDST         PIC X(08).
           05  DB-SNPARAMS.
               10  DB-IDAPLFLT         PIC X.
                   88  IDAPLFLT-ON                 VALUE "S".
                   88  IDAPLFLT-OFF                VALUE "N".
               10  DB-NRMNUFLT         PIC X.
                   88  NRMNUFLT-ON                 VALUE "S".
                   88  NRMNUFLT-OFF                VALUE "N".
               10  DB-IDORIFLT         PIC X.
                   88  IDORIFLT-ON                 VALUE "S".
                   88  IDORIFLT-OFF                VALUE "N".
               10  DB-IDDSTFLT         PIC X.
                   88  IDDSTFLT-ON                 VALUE "S".
                   88  IDDSTFLT-OFF                VALUE "N".
           05  DB-IDREGLST.
               10  DB-IDAPLICA         PIC X(03).
               10  DB-NRMNUPRC         PIC S9(2)       COMP-3.
               10  DB-IDPRCORI         PIC X(08).
               10  DB-NROPCMNU         PIC S9(2)       COMP-3.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QMNUNVG CURSOR FOR
                    SELECT IDAPLICA
                         , NRMNUPRC
                         , IDPRCORI
                         , NROPCMNU
                         , IDPRCDST
                      FROM CRET907
                     WHERE(IDAPLICA = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"      = :DB-IDAPLFLT)
                       AND(NRMNUPRC = :DB-PARMSINP.DB-NRMNUPRC
                        OR "N"      = :DB-NRMNUFLT)
                       AND(IDPRCORI = :DB-PARMSINP.DB-IDPRCORI
                        OR "N"      = :DB-IDORIFLT)
                       AND(IDPRCDST = :DB-PARMSINP.DB-IDPRCDST
                        OR "N"      = :DB-IDDSTFLT)
                       AND(
                          (IDAPLICA > :DB-IDREGLST.DB-IDAPLICA)
                        OR(IDAPLICA = :DB-IDREGLST.DB-IDAPLICA
                       AND NRMNUPRC > :DB-IDREGLST.DB-NRMNUPRC)
                        OR(IDAPLICA = :DB-IDREGLST.DB-IDAPLICA
                       AND NRMNUPRC = :DB-IDREGLST.DB-NRMNUPRC
                       AND IDPRCORI > :DB-IDREGLST.DB-IDPRCORI)
                        OR(IDAPLICA = :DB-IDREGLST.DB-IDAPLICA
                       AND NRMNUPRC = :DB-IDREGLST.DB-NRMNUPRC
                       AND IDPRCORI = :DB-IDREGLST.DB-IDPRCORI
                       AND NROPCMNU > :DB-IDREGLST.DB-NROPCMNU)
                          )
                     ORDER BY 1, 2, 3, 4
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-PARMSINP     TO WS-PARMSINP
           IF  CREB000-MSGDLG-P > ZERO
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB966-MSGDIALG
           END-IF
           IF  CREB000-IDREGLST-MIN
               INITIALIZE DB-IDREGLST
           ELSE
               MOVE CREB000-IDREGLST TO DB-IDREGLST
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB966-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCSEL
               PERFORM P2600-PREP-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2700-PREP-NOVOREG
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB966-MSGDIALG, WS-PARMSINP
           SET  CREB000-IDREG-NF TO TRUE
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB966-MSGDIALG
           MOVE WS-IDAPLICA OF WS-PARMSINP TO CREB966-IDAPLICA-V
           MOVE WS-NRMNUPRC OF WS-PARMSINP TO CREB966-NRMNUPRC-V
           MOVE WS-IDPRCORI OF WS-PARMSINP TO CREB966-IDPRCORI-V
           MOVE WS-IDPRCDST OF WS-PARMSINP TO CREB966-IDPRCDST-V
           SET  CREB000-IDREG-NF           TO TRUE
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDAPLICA"
               MOVE CREB966-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB966-IDAPLICA-PC TO TRUE
           WHEN "IDPRCORI"
               MOVE CREB966-IDPRCORI-V  TO WS-IDPRCAPL OF WS-LVPRCAPL-I
               MOVE CREB966-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPRCAPL-I
               SET  CREB966-IDPRCORI-PC TO TRUE
           WHEN "IDPRCDST"
               MOVE CREB966-IDPRCDST-V  TO WS-IDPRCAPL OF WS-LVPRCAPL-I
               MOVE CREB966-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVPRCAPL-I
               MOVE CREB950-MCDLGPRN    TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPRCAPL-I
               SET  CREB966-IDPRCDST-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB966-IDAPLICA-V
               SET  CREB966-IDAPLICA-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDPRCORI"
           AND  WS-IDPRCAPL OF WS-LVPRCAPL-O NOT = SPACES
               MOVE WS-IDPRCAPL OF WS-LVPRCAPL-O TO CREB966-IDPRCORI-V
               SET  CREB966-IDPRCORI-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDPRCDST"
           AND  WS-IDPRCAPL OF WS-LVPRCAPL-O NOT = SPACES
               MOVE WS-IDPRCAPL OF WS-LVPRCAPL-O TO CREB966-IDPRCDST-V
               SET  CREB966-IDPRCDST-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB966-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               IF  CREB966-NRMNUPRC-V = ZERO
                   SET  CREB000-FUNCCRIA-OFF TO TRUE
               END-IF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           IF (CREB966-IDAPLICA-V = SPACES OR CREB950-MCPESQGN)
           AND CREB966-NRMNUPRC-V > ZERO
               SET  CREB966-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           IF  CREB966-IDAPLICA-V NOT = SPACES AND CREB950-MCPESQGN
               MOVE CREB966-IDAPLICA-V         TO IDAPLICA OF CRET901
               PERFORM P9000-READ-CRET901

               EVALUATE TRUE
               WHEN SQLCODE-OK
                   MOVE NMAPLICA OF CRET901    TO CREB966-NMAPLICA-V
               WHEN SQLCODE-NOTFOUND
                   SET  CREB966-IDAPLICA-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB966-NRMNUPRC-V > ZERO
               MOVE CREB966-IDAPLICA-V         TO IDAPLICA OF CRET906
               MOVE CREB966-NRMNUPRC-V         TO NRMNUPRC OF CRET906
               PERFORM P9000-VRIF-CRET906
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB966-IDPRCORI-V NOT = SPACES AND CREB950-MCPESQGN)
               MOVE CREB966-IDPRCORI-V         TO IDPRCAPL OF CRET903
               PERFORM P9000-READ-IDPRCAPL

               EVALUATE TRUE
               WHEN SQLCODE-OK
                   MOVE DSPRCAPL OF CRET903    TO CREB966-NMPRCORI-V
               WHEN SQLCODE-NOTFOUND
                   SET  CREB966-IDPRCORI-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB966-IDPRCDST-V NOT = SPACES AND CREB950-MCPESQGN)
               MOVE CREB966-IDPRCDST-V         TO IDPRCAPL OF CRET903
               PERFORM P9000-READ-CRET903

               EVALUATE TRUE
               WHEN SQLCODE-OK
                   MOVE DSPRCAPL OF CRET903    TO CREB966-NMPRCDST-V
               WHEN SQLCODE-NOTFOUND
                   SET  CREB966-IDPRCDST-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB966-IDAPLICA-V TO WS-IDAPLICA OF WS-PARMSINP
           MOVE CREB966-NRMNUPRC-V TO WS-NRMNUPRC OF WS-PARMSINP
           MOVE CREB966-IDPRCORI-V TO WS-IDPRCORI OF WS-PARMSINP
           MOVE CREB966-IDPRCDST-V TO WS-IDPRCDST OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           INITIALIZE DB-PARMSINP, CRET907, CREB966-VARSDET
           IF  CREB966-IDAPLICA-V = SPACES OR CREB950-MCPESQGN
               SET  IDAPLFLT-OFF       TO TRUE
           ELSE
               MOVE CREB966-IDAPLICA-V TO DB-IDAPLICA OF DB-PARMSINP
               SET  IDAPLFLT-ON        TO TRUE
           END-IF

           IF  CREB966-NRMNUPRC-V = ZERO
               SET  NRMNUFLT-OFF       TO TRUE
           ELSE
               MOVE CREB966-NRMNUPRC-V TO DB-NRMNUPRC OF DB-PARMSINP
               SET  NRMNUFLT-ON        TO TRUE
           END-IF

           IF  CREB966-IDPRCORI-V = SPACES OR CREB950-MCPESQGN
               SET  IDORIFLT-OFF       TO TRUE
           ELSE
               MOVE CREB966-IDPRCORI-V TO DB-IDPRCORI OF DB-PARMSINP
               SET  IDORIFLT-ON        TO TRUE
           END-IF

           IF  CREB966-IDPRCDST-V = SPACES OR CREB950-MCPESQGN
               SET  IDDSTFLT-OFF       TO TRUE
           ELSE
               MOVE CREB966-IDPRCDST-V TO DB-IDPRCDST OF DB-PARMSINP
               SET  IDDSTFLT-ON        TO TRUE
           END-IF
           SET  QMNUNVG-CLSE           TO TRUE
           PERFORM P2441-CONT-QMNUNVG

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-NRENTLST = ZERO
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               PERFORM P2442-OPEN-QMNUNVG
           END-EVALUATE

           IF  QMNUNVG-OPEN
               PERFORM P2443-FTCH-QMNUNVG

               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
                 OR    CREB000-IDREGLST-MAX
                 OR    CREB000-PROC-ERR
                   PERFORM P2444-LOAD-LSTSEL

                   IF  CREB000-PROC-OK OR CREB000-MSGINFO
                       PERFORM P2443-FTCH-QMNUNVG
                   END-IF
               END-PERFORM

               PERFORM P2445-CLSE-QMNUNVG
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND NOT CREB000-IDREGLST-MAX
               MOVE DB-IDREGLST TO CREB000-IDREGLST
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QMNUNVG          SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET907
                     WHERE(IDAPLICA = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"      = :DB-IDAPLFLT)
                       AND(NRMNUPRC = :DB-PARMSINP.DB-NRMNUPRC
                        OR "N"      = :DB-NRMNUFLT)
                       AND(IDPRCORI = :DB-PARMSINP.DB-IDPRCORI
                        OR "N"      = :DB-IDORIFLT)
                       AND(IDPRCDST = :DB-PARMSINP.DB-IDPRCDST
                        OR "N"      = :DB-IDDSTFLT)
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               MOVE DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP916.QMNUNVG" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2442-OPEN-QMNUNVG          SECTION.

           EXEC SQL OPEN  QMNUNVG                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QMNUNVG-OPEN      TO TRUE
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP916.QMNUNVG" TO CREB000-NMOBJECT
               SET  CREB000-OPEN-CS   TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2442-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2443-FTCH-QMNUNVG          SECTION.

           EXEC SQL FETCH QMNUNVG
                     INTO :CRET907.IDAPLICA
                        , :CRET907.NRMNUPRC
                        , :CRET907.IDPRCORI
                        , :CRET907.NROPCMNU
                        , :CRET907.IDPRCDST                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK     TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE      TO CREB000-CDRETORN
               MOVE "CREP916.QMNUNVG"    TO CREB000-NMOBJECT
               SET  CREB000-FTCH-CS      TO TRUE
               SET  CREB000-MSGSGBD      TO TRUE
           END-EVALUATE
           .
       P2443-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2444-LOAD-LSTSEL           SECTION.

           MOVE IDAPLICA OF CRET907 TO CREB966-IDAPLLIN-V(WS-ARRYNDX1)
                                       DB-IDAPLICA OF DB-IDREGLST
           MOVE NRMNUPRC OF CRET907 TO CREB966-NRMNULIN-V(WS-ARRYNDX1)
                                       DB-NRMNUPRC OF DB-IDREGLST
           MOVE IDPRCORI OF CRET907 TO CREB966-IDORILIN-V(WS-ARRYNDX1)
                                       DB-IDPRCORI OF DB-IDREGLST
           MOVE NROPCMNU OF CRET907 TO CREB966-OPCMNLIN-V(WS-ARRYNDX1)
                                       DB-NROPCMNU OF DB-IDREGLST
           MOVE IDPRCDST OF CRET907 TO CREB966-IDDSTLIN-V(WS-ARRYNDX1)
                                       IDPRCAPL OF CRET903
           PERFORM P9000-READ-CRET903

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE DSPRCAPL OF CRET903
                                    TO CREB966-NMDSTLIN-V(WS-ARRYNDX1)
           END-IF
           .
       P2444-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2445-CLSE-QMNUNVG          SECTION.

           EXEC SQL CLOSE QMNUNVG                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP916.QMNUNVG"      TO CREB000-NMOBJECT
               SET  CREB000-CLSE-CS        TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P2445-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB966-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB966-NRMNUPRC-V   TO CREB999-NRMNUPRC
           MOVE CREB966-IDPRCORI-V   TO CREB999-IDPRCAPL
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PREP-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-IDREGSEL = SPACES
               PERFORM P2610-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2620-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2630-FRMT-IDREGSEL
               END-IF
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2640-FRMT-IDPRCDST
           END-IF
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           INITIALIZE CREB000-NRLINSMC
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
             OR    CREB000-PROC-ERR
               MOVE CREB966-NROPCLIN-V(WS-ARRYNDX1) TO WS-ARRYNDX2
               EVALUATE TRUE
               WHEN WS-ARRYNDX2 = ZERO
                   CONTINUE
               WHEN CREB000-INDICOPC-ON(WS-ARRYNDX2)
                   ADD  1 TO CREB000-NRLINSMC
               WHEN OTHER
                   COMPUTE CREB966-PCURSOR = CREB966-NRPARAMS-INP
                                           + WS-ARRYNDX1
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-PERFORM

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-NRLINSMC = ZERO
               SET  CREB966-NROPCL01-PC        TO TRUE
               SET  CREB950-SELECC-NINFORM     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL CREB966-NROPCLIN-V(WS-ARRYNDX1) > ZERO
               CONTINUE
           END-PERFORM
           MOVE CREB966-NROPCLIN-V(WS-ARRYNDX1) TO CREB000-IDOPCSEL
           COMPUTE CREB966-PCURSOR = CREB966-NRPARAMS-INP + WS-ARRYNDX1
           INITIALIZE CREB966-NROPCLIN-V(WS-ARRYNDX1)
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-IDREGSEL         SECTION.

           MOVE CREB966-IDAPLLIN-V(WS-ARRYNDX1)
                                   TO WS-IDAPLICA OF WS-IDREGSEL
           MOVE CREB966-NRMNULIN-V(WS-ARRYNDX1)
                                   TO WS-NRMNUPRC OF WS-IDREGSEL
           MOVE CREB966-IDORILIN-V(WS-ARRYNDX1)
                                   TO WS-IDPRCORI OF WS-IDREGSEL
           MOVE CREB966-OPCMNLIN-V(WS-ARRYNDX1)
                                   TO WS-NROPCMNU OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2630-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2640-FRMT-IDPRCDST         SECTION.

           CONTINUE
           .
       P2640-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PREP-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB966-VARSOUT
           PERFORM P2710-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2720-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2730-FRMT-IDREGSEL
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB966-IDAPLICA-V = SPACES
               SET  CREB966-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB966-IDAPLICA-V = CREB950-MCPESQGN
               SET  CREB966-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB966-NRMNUPRC-V = ZERO
               SET  CREB966-NRMNUPRC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           PERFORM P2420-VRIF-NIVEL02
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL
           IF  CREB966-IDAPLICA-V NOT = CREB950-MCPESQGN
               MOVE CREB966-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           END-IF
           MOVE CREB966-NRMNUPRC-V     TO WS-NRMNUPRC OF WS-IDREGSEL
           IF  CREB966-IDPRCORI-V NOT = CREB950-MCPESQGN
               MOVE CREB966-IDPRCORI-V TO WS-IDPRCORI OF WS-IDREGSEL
           END-IF
           MOVE WS-IDREGSEL            TO CREB000-IDREGSEL
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN OTHER
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB966-MSGDIALG TO CREB000-MSGDLG-L
           END-EVALUATE
           MOVE CREB966-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)

           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB966-IDAPLICA-UP TO TRUE
           SET  CREB966-NRMNUPRC-UP TO TRUE
           SET  CREB966-IDPRCORI-UP TO TRUE
           SET  CREB966-IDPRCDST-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB966-IDAPLICA-PR TO TRUE
           SET  CREB966-NRMNUPRC-PR TO TRUE
           SET  CREB966-IDPRCORI-PR TO TRUE
           SET  CREB966-IDPRCDST-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
             OR    CREB966-IDORILIN-V(WS-ARRYNDX1) = SPACES
               SET  CREB966-NROPCLIN-UP(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
               SET  CREB966-NROPCLIN-PR(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO NMAPLICA OF CRET901
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP916.CRET901" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET906          SECTION.

           MOVE CREB000-IDAPLICA TO IDAPLICA OF CRET901
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET906
                     WHERE IDAPLICA = :CRET906.IDAPLICA
                       AND NRMNUPRC = :CRET906.NRMNUPRC
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB966-NRMNUPRC-PC    TO TRUE
               SET  CREB950-MNUPRC-NEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP916.EXIST01"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-IDPRCAPL         SECTION.

           PERFORM P9000-READ-CRET903

           IF  SQLCODE-NOTFOUND
               PERFORM P9000-READ-CRET904
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903          SECTION.

           EXEC SQL SELECT DSPRCAPL
                      INTO :CRET903.DSPRCAPL
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSPRCAPL OF CRET903
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP916.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET904          SECTION.

           EXEC SQL SELECT DSPRCAPL
                      INTO :CRET903.DSPRCAPL
                      FROM CRET904
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSPRCAPL OF CRET903
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP916.CRET904" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP916 ------------------------------------*
