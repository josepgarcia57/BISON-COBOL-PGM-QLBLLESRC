      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.     BPCP256.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE CRC
      * PROGRAMA .: BPCP256
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "BPCE256"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT BPCE256
                  ASSIGN        TO WORKSTATION-BPCE256-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-BPCE256
                  CONTROL-AREA  IS CA-BPCE256.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  BPCE256
           LABEL RECORD OMITTED.
       01  BPCE256-HDR.
           COPY DDS-BPCE256HDR OF BPCE256.
       01  BPCE256-D01.
           COPY DDS-BPCE256D01 OF BPCE256.
       01  BPCE256-FTR.
           COPY DDS-BPCE256FTR OF BPCE256.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-BPCE256                  PIC 99.
           88  BPCE256-OK                          VALUE ZERO.

       01  CA-BPCE256.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "BPCE256".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "BPCE256HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "BPCE256D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "BPCE256FTR".
           05  KV-IDENT-HI             PIC 99      VALUE 25.
           05  KV-IDENT-PC             PIC 99      VALUE 26.
           05  KV-IDENT-PR             PIC 99      VALUE 27.
           05  KV-TYPER-HI             PIC 99      VALUE 28.
           05  KV-TYPER-PC             PIC 99      VALUE 29.
           05  KV-TYPER-PR             PIC 99      VALUE 30.
           05  KV-REFEXTENT-HI         PIC 99      VALUE 31.
           05  KV-REFEXTENT-PC         PIC 99      VALUE 32.
           05  KV-REFEXTENT-PR         PIC 99      VALUE 33.
           05  KV-DTAVRISCO-HI         PIC 99      VALUE 34.
           05  KV-DTAVRISCO-PC         PIC 99      VALUE 35.
           05  KV-DTAVRISCO-PR         PIC 99      VALUE 36.
           05  KV-TIPOPDENT-HI         PIC 99      VALUE 37.
           05  KV-TIPOPDENT-PC         PIC 99      VALUE 38.
           05  KV-TIPOPDENT-PR         PIC 99      VALUE 39.
           05  KV-TPAVALR-HI           PIC 99      VALUE 40.
           05  KV-TPAVALR-PC           PIC 99      VALUE 41.
           05  KV-TPAVALR-PR           PIC 99      VALUE 42.
           05  KV-SISAVALR-HI          PIC 99      VALUE 43.
           05  KV-SISAVALR-PC          PIC 99      VALUE 44.
           05  KV-SISAVALR-PR          PIC 99      VALUE 45.
           05  KV-NOTACENT-HI          PIC 99      VALUE 46.
           05  KV-NOTACENT-PC          PIC 99      VALUE 47.
           05  KV-NOTACENT-PR          PIC 99      VALUE 48.
           05  KV-DTDEMOFIN-HI         PIC 99      VALUE 49.
           05  KV-DTDEMOFIN-PC         PIC 99      VALUE 50.
           05  KV-DTDEMOFIN-PR         PIC 99      VALUE 51.
           05  KV-PDENT-HI             PIC 99      VALUE 52.
           05  KV-PDENT-PC             PIC 99      VALUE 53.
           05  KV-PDENT-PR             PIC 99      VALUE 54.
           05  KV-MODIRBENT-HI         PIC 99      VALUE 55.
           05  KV-MODIRBENT-PC         PIC 99      VALUE 56.
           05  KV-MODIRBENT-PR         PIC 99      VALUE 57.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 60.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 60.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY BPCB256.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-BPCE256, CA-BPCE256
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT BPCE256-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O BPCE256

           IF  NOT BPCE256-OK
               MOVE FS-BPCE256      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO BPCB256-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  BPCE256-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  BPCE256-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  BPCE256-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE BPCB256-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.


           IF  BPCB256-IDENT-PR
               SET  CREB000-INDICDLG-ON (KV-IDENT-PR )    TO TRUE
           END-IF
      *
           IF  BPCB256-TYPER-PR
               SET  CREB000-INDICDLG-ON (KV-TYPER-PR )    TO TRUE
           END-IF
      *
           IF  BPCB256-DTAVRISCO-PR
               SET  CREB000-INDICDLG-ON (KV-DTAVRISCO-PR) TO TRUE
           END-IF
      *
           IF  BPCB256-REFEXTENT-PR
               SET  CREB000-INDICDLG-ON (KV-REFEXTENT-PR) TO TRUE
           END-IF
      *
           IF  BPCB256-TIPOPDENT-PR
               SET  CREB000-INDICDLG-ON (KV-TIPOPDENT-PR) TO TRUE
           END-IF
      *
           IF  BPCB256-TPAVALR-PR
               SET  CREB000-INDICDLG-ON (KV-TPAVALR-PR)  TO TRUE
           END-IF
      *
           IF  BPCB256-SISAVALR-PR
               SET  CREB000-INDICDLG-ON (KV-SISAVALR-PR) TO TRUE
           END-IF
      *
           IF  BPCB256-NOTACENT-PR
               SET  CREB000-INDICDLG-ON (KV-NOTACENT-PR)  TO TRUE
           END-IF
      *
           IF  BPCB256-DTDEMOFIN-PR
               SET  CREB000-INDICDLG-ON (KV-DTDEMOFIN-PR) TO TRUE
           END-IF
      *
           IF   BPCB256-PDENT-PR
               SET  CREB000-INDICDLG-ON (KV-PDENT-PR )   TO TRUE
           END-IF
      *
           IF   BPCB256-MODIRBENT-PR
               SET  CREB000-INDICDLG-ON (KV-MODIRBENT-PR ) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN BPCB256-IDENT-UP
               SET  CREB000-INDICDLG-ON   (KV-IDENT-PC)      TO TRUE
           WHEN BPCB256-TYPER-UP
               SET  CREB000-INDICDLG-ON   (KV-TYPER-PC)      TO TRUE
           WHEN BPCB256-REFEXTENT-UP
               SET  CREB000-INDICDLG-ON   (KV-REFEXTENT-PC)  TO TRUE
           WHEN BPCB256-DTAVRISCO-PC
               SET  CREB000-INDICDLG-ON   (KV-DTAVRISCO-PC)  TO TRUE
           WHEN BPCB256-TIPOPDENT-PC
               SET  CREB000-INDICDLG-ON   (KV-TIPOPDENT-PC)  TO TRUE
           WHEN BPCB256-TPAVALR-PC
               SET  CREB000-INDICDLG-ON   (KV-TPAVALR-PC)    TO TRUE
           WHEN BPCB256-SISAVALR-PC
               SET  CREB000-INDICDLG-ON   (KV-SISAVALR-PC)   TO TRUE
           WHEN BPCB256-NOTACENT-PC
               SET  CREB000-INDICDLG-ON   (KV-NOTACENT-PC)   TO TRUE
           WHEN BPCB256-DTDEMOFIN-PC
               SET  CREB000-INDICDLG-ON   (KV-DTDEMOFIN-PC)  TO TRUE
           WHEN BPCB256-PDENT-PC
               SET  CREB000-INDICDLG-ON   (KV-PDENT-PC)      TO TRUE
           WHEN BPCB256-MODIRBENT-PC
               SET  CREB000-INDICDLG-ON   (KV-MODIRBENT-PC)  TO TRUE
           WHEN BPCB256-IDENT-PC
               SET  CREB000-INDICDLG-ON   (KV-IDENT-PC)     TO TRUE
           WHEN BPCB256-TYPER-PC
               SET  CREB000-INDICDLG-ON   (KV-TYPER-PC)     TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN BPCB256-IDENT-PC
               SET  CREB000-INDICDLG-ON   (KV-IDENT-HI)      TO TRUE
           WHEN BPCB256-TYPER-PC
               SET  CREB000-INDICDLG-ON   (KV-TYPER-HI)      TO TRUE
           WHEN BPCB256-REFEXTENT-PC
               SET  CREB000-INDICDLG-ON   (KV-REFEXTENT-HI)  TO TRUE
           WHEN BPCB256-DTAVRISCO-PC
               SET  CREB000-INDICDLG-ON   (KV-DTAVRISCO-HI)  TO TRUE
           WHEN BPCB256-TIPOPDENT-PC
               SET  CREB000-INDICDLG-ON   (KV-TIPOPDENT-HI)  TO TRUE
           WHEN BPCB256-TPAVALR-PC
               SET  CREB000-INDICDLG-ON   (KV-TPAVALR-HI)    TO TRUE
           WHEN BPCB256-SISAVALR-PC
               SET  CREB000-INDICDLG-ON   (KV-SISAVALR-HI)   TO TRUE
           WHEN BPCB256-NOTACENT-PC
               SET  CREB000-INDICDLG-ON   (KV-NOTACENT-HI)   TO TRUE
           WHEN BPCB256-DTDEMOFIN-PC
               SET  CREB000-INDICDLG-ON   (KV-DTDEMOFIN-HI)  TO TRUE
           WHEN BPCB256-PDENT-PC
               SET  CREB000-INDICDLG-ON   (KV-PDENT-HI)      TO TRUE
           WHEN BPCB256-MODIRBENT-PC
               SET  CREB000-INDICDLG-ON   (KV-MODIRBENT-HI)  TO TRUE
           WHEN BPCB256-MODIRBENT-PC
               SET  CREB000-INDICDLG-ON   (KV-MODIRBENT-HI)  TO TRUE
           END-EVALUATE
      *
           IF  BPCB256-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  BPCB256-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO BPCE256-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF BPCE256HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF BPCE256HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF BPCE256HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF BPCE256HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF BPCE256HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF BPCE256HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF BPCE256HDR-O
           WRITE BPCE256-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT BPCE256-OK
               MOVE FS-BPCE256      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO BPCE256-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF BPCE256FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF BPCE256FTR-O
           WRITE BPCE256-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT BPCE256-OK
               MOVE FS-BPCE256      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES               TO BPCE256-D01
           MOVE BPCB256-IDENT-V      TO IDENT      OF BPCE256D01-O
           MOVE BPCB256-TYPER-V      TO TYPER      OF BPCE256D01-O
           MOVE BPCB256-REFEXTENT-V  TO REFEXTENT  OF BPCE256D01-O
           MOVE BPCB256-DTAVRISCO-V  TO DTAVRISCO  OF BPCE256D01-O
           MOVE BPCB256-DTDEMOFIN-V  TO DTDEMOFIN  OF BPCE256D01-O
           MOVE BPCB256-PDENT-V      TO PDENT      OF BPCE256D01-O
           MOVE BPCB256-TPAVALR-V    TO TPAVALR    OF BPCE256D01-O
           MOVE BPCB256-SISAVALR-V   TO SISAVALR   OF BPCE256D01-O
           MOVE BPCB256-MODIRBENT-V  TO MODIRBENT  OF BPCE256D01-O
           MOVE BPCB256-NOTACENT-V   TO NOTACENT   OF BPCE256D01-O
           MOVE BPCB256-DHCRIREG-V   TO DHCRIREG   OF BPCE256D01-O
           MOVE BPCB256-DHMDFREG-V   TO DHMDFREG   OF BPCE256D01-O
           WRITE BPCE256-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT BPCE256-OK
               MOVE FS-BPCE256      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ BPCE256
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  BPCE256-OK
               MOVE IDENT       OF BPCE256D01-I TO BPCB256-IDENT-V
               MOVE TYPER       OF BPCE256D01-I TO BPCB256-TYPER-V
               MOVE REFEXTENT   OF BPCE256D01-I TO BPCB256-REFEXTENT-V
               MOVE DTAVRISCO   OF BPCE256D01-I TO BPCB256-DTAVRISCO-V
               MOVE DTDEMOFIN   OF BPCE256D01-I TO BPCB256-DTDEMOFIN-V
               MOVE PDENT       OF BPCE256D01-I TO BPCB256-PDENT-V
               MOVE TIPOPDENT   OF BPCE256D01-I TO BPCB256-TIPOPDENT-V
               MOVE TPAVALR     OF BPCE256D01-I TO BPCB256-TPAVALR-V
               MOVE SISAVALR    OF BPCE256D01-I TO BPCB256-SISAVALR-V
               MOVE MODIRBENT   OF BPCE256D01-I TO BPCB256-MODIRBENT-V
               MOVE NOTACENT    OF BPCE256D01-I TO BPCB256-NOTACENT-V
               MOVE IDVARCSR    OF BPCE256D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF    OF CA-BPCE256   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-BPCE256               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE BPCE256

           IF  NOT BPCE256-OK
               MOVE FS-BPCE256      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA BPCP256 ------------------------------------*
