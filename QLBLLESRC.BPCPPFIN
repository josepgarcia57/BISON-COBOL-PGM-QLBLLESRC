       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BPCPPFIN   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BPC - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este programa destina-se a Extrair Info    *
     š*                     Mensal Financeira a enviar ao B.P.
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 EXECUTA-P001               PIC 9(01).
          88 INICIO-P001             VALUE 0.
          88 FIM-P001                VALUE 1.
      *
       01 NR-ADM                     PIC 9(06) VALUE ZEROS.
       01 NR-READ                    PIC 9(06) VALUE ZEROS.
       01 NR-PROC                    PIC 9(06) VALUE ZEROS.
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(8)    VALUE "BPCPPFIN".

       01 Ws-dtCriacao.
          05 ws-Data         PIC X(10).
          05 ws-SepT         PIC X(1).
          05 ws-HH           PIC X(2).
          05 ws-SepH         PIC X(1).
          05 ws-MM           PIC X(2).
          05 ws-SepM         PIC X(1).
          05 ws-SS           PIC X(2).

     š* BCB.BdP.SSSSSSSSS.TTTT.AAAAMMDD.hhmmss.xml-------------
       01 ws-idFich.
          05  ws-idFichBCB   PIC X(4) VALUE "CRC.".
          05  ws-Bdp         PIC X(4) VALUE "Bdp.".
          05  ws-idFichPPP   PIC X(5) VALUE "0063.".
          05  ws-idFichPPP   PIC X(5) VALUE "0063.".
          05  ws-idFichTTTT  PIC X(5) VALUE "CINA.".
          05  ws-idFichANO   PIC X(4) .
          05  ws-idFichMES   PIC X(2) .
          05  ws-idFichDIA   PIC X(2) .
          05  ws-FILLER1     PIC X(1) VALUE ".".
          05  ws-idFichHH    PIC X(2) .
          05  ws-idFichMI    PIC X(2) .
          05  ws-idFichSS    PIC X(2) .
          05  ws-FILLER1     PIC X(1) VALUE ".".
          05  ws-idFichXML   PIC X(8) VALUE "XML".

     š* Instrumento de Crédito
       01 WS-IDINST                PIC X(32).
       01 WX-IDINT  REDEFINES WS-IDINST.
          05 WS-RACINE-INST        PIC X(07).
          05 WS-FILLER1            PIC X(01).
          05 WS-GRE-INST           PIC X(03).
          05 WS-FILLER2            PIC X(01).
          05 WS-RUB-INST           PIC X(03).
          05 WS-FILLER3            PIC X(01).
          05 WS-MON-INST           PIC X(03).
          05 WS-FILLER4            PIC X(01).
          05 WS-CTID-INST          PIC X(03).
          05 WS-FILLER5            PIC X(01).
          05 WS-CTCONT-INST        PIC X(07).

     š* Contrato
       01 WS-IDCONT                PIC X(32).
       01 WX-IDCONT REDEFINES WS-IDCONT.
          05 WS-CTR-CONT           PIC X(03).
          05 WS-FILLER1-CONT       PIC X(01).
          05 WS-SEQ-CONT           PIC X(07).

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
     š* Parametros p/ Interface Clientes
           COPY BPCBPFIN.
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".
       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA         PIC 9(04).
           03  FILLER               PIC X VALUE "-".
           03  WS-MESIN-DIA         PIC 9(02).
           03  FILLER               PIC X VALUE "-".
           03  WS-DIAIN-DIA         PIC 9(02).

       01  WS-DTREF                 PIC X(10).

     š* Copy : Tabela de Controlo
       01  R-TBAT.
           COPY DDS-ALL-FORMATS OF BPCTBAT.

     š* Copy : Tabela de Controlo
       01  R-JCNTL.
           COPY DDS-ALL-FORMATS OF BPCJCNTL.

     š* Copy : Tabela de Controlo
       01  R-JCCIN.
           COPY DDS-ALL-FORMATS OF BPCJCCIN.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.

       01 WS-NURACI                PIC X(07).
       01 WS-CPGRE                 PIC X(03).
       01 WS-CPRUB                 PIC X(03).
       01 WS-CPMON                 PIC X(03).

           EXEC SQL DECLARE CURCCIN CURSOR FOR
                SELECT  IDCONT
                     ,  IDINST
                FROM  BPCJCCIN A
           END-EXEC.

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       PROCEDURE DIVISION.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P001-INICIO
           PERFORM P002-DATA-REF
           IF WS-DTREF NOT EQUAL "0001-01-01"
             PERFORM P002-REG-CONTROLO
     š*- Trata Info Financeira
             PERFORM P500-INSERT-HIST-BPCJPFIN
             PERFORM P500-DELETE-BPCJPFIN
     š*- Trata Intervenientes
             PERFORM P500-INSERT-HIST-BPCJPFIN1
             PERFORM P500-DELETE-BPCJPFIN1
     š*- Trata Protecções
             PERFORM P500-INSERT-HIST-BPCJPFIN2
             PERFORM P500-DELETE-BPCJPFIN2
     š*- Ciclo
             PERFORM P100-OPEN-CURSOR-CURCCIN
             PERFORM P200-FETCH-CURSOR-CURCCIN
             PERFORM P300-PROCESSO-GERAL
                             UNTIL FIM-P001
             PERFORM P400-CLOSE-CURSOR-CURCCIN
             PERFORM P400-UPDATES-FINAIS.
             PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P001-INICIO.
     š*---------------------------------------------------------------------
           SET  INICIO-P001         TO  TRUE.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.
     š*---------------------------------------------------------------------
       P002-DATA-REF.
     š*---------------------------------------------------------------------

           EXEC SQL
                SELECT  DTREF
                INTO   :WS-DTREF
                FROM BPCTBAT
                WHERE CDEMP = "BBI"
                 AND NMPROC = "BPC"
                 AND NMPROG = "BPCC002"
                 AND INDACT = "A"
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              MOVE "0001-01-01"      TO WS-DTREF
           WHEN OTHER
              MOVE "0001-01-01"      TO WS-DTREF
              MOVE "BPCTBAT"         TO W-OBJECT-NAME
              SET   CMD-SELECT       TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P002-REG-CONTROLO.
     š*---------------------------------------------------------------------
           INITIALIZE  BPCJCNTL
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

     š*-Ws-dtCriacao                                                   -----
           MOVE WS-DATA-DIA       TO  ws-Data
           MOVE "T"               TO  ws-SepT.
           MOVE S-SYSTMST(12:2  ) TO  ws-HH
           MOVE S-SYSTMST(15:2  ) TO  ws-MM
           MOVE S-SYSTMST(18:2  ) TO  ws-SS
           MOVE ":"               TO  ws-SepH
           MOVE ":"               TO  ws-SepM
           MOVE WS-DTREF          TO  DTREF    OF BPCJCNTL

     š*-NOMEFICH                                                       -----
     š*-TTTT pode ser CCNT e RSPC                                      -----
     š*-CRC.BdP.PPPP(PP).TTTT.AAAAMMDD.hhmmss.xml  ----------------    -----
           MOVE WS-ANOIN-DIA      TO  ws-idFichANO.
           MOVE WS-MESIN-DIA      TO  ws-idFichMES.
           MOVE WS-DIAIN-DIA      TO  ws-idFichDia.
           MOVE S-SYSTMST(12:2  ) TO  ws-idFichHH
           MOVE S-SYSTMST(15:2  ) TO  ws-idFichMI
           MOVE S-SYSTMST(18:2  ) TO  ws-idFichSS.

           MOVE "0063"         TO   ENTOBS     OF BPCJCNTL.
           MOVE "0063"         TO   ENTREPORT  OF BPCJCNTL.
           MOVE "0001"         TO   IDDEST     OF BPCJCNTL.
           MOVE Ws-dtCriacao   TO   DTCRIACAO  OF BPCJCNTL.
           MOVE ws-idFich      TO   NOMEFICH   OF BPCJCNTL.
           MOVE "ENV"          TO   ESTADO     OF BPCJCNTL.
           MOVE WS-TMS-NULA    TO   TMSRET     OF BPCJCNTL.
           MOVE "PFIN"         TO   REPTYPE    OF BPCJCNTL.

           PERFORM P001-VRFY-RETORNO
           PERFORM P003-INSERT-BPCJCNTL.

     š*---------------------------------------------------------------------
       P001-VRFY-RETORNO.
     š*---------------------------------------------------------------------

     š*- Inicializa Envio ESTADO RET                                   -----
           EXEC SQL
             UPDATE  BPCJPFIN
             SET   BPENV   = " "
                 , TMSALT  = CURRENT TIMESTAMP
                 , USERALT = "BPCPPFIN"
             WHERE BPENV   = "E"
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN SQLCODE-NOTFOUND
              CONTINUE
           WHEN OTHER
              MOVE "BPCJPFIN"        TO W-OBJECT-NAME
              SET CMD-UPDATE         TO TRUE
              PERFORM  P990-ERRO-DB2
              SET SIM-ERRO           TO TRUE
           END-EVALUATE.

     š*- Inicializa Envio ESTADO RET                                   -----
           EXEC SQL
             UPDATE  BPCJCNTL
             SET   ESTADO  = "RET"
             WHERE ESTADO  = "ENV"
              AND  REPTYPE = "PFIN"
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN SQLCODE-NOTFOUND
              CONTINUE
           WHEN OTHER
              MOVE "BPCJCNTL"        TO W-OBJECT-NAME
              SET CMD-UPDATE         TO TRUE
              PERFORM  P990-ERRO-DB2
              SET SIM-ERRO           TO TRUE
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P003-INSERT-BPCJCNTL.
     š*---------------------------------------------------------------------
     š*-
           EXEC SQL
                 INSERT INTO BPCJCNTL
                 VALUES ( :BPCJCNTL.DTREF
                        , :BPCJCNTL.REPTYPE
                        , :BPCJCNTL.ESTADO
                        , :BPCJCNTL.ENTOBS
                        , :BPCJCNTL.ENTREPORT
                        , :BPCJCNTL.IDDEST
                        , :BPCJCNTL.DTCRIACAO
                        , :BPCJCNTL.NOMEFICH
                        , :BPCJCNTL.NREC
                        , :BPCJCNTL.NREJ
                        , :BPCJCNTL.NINT
                        , :BPCJCNTL.NALERTA
                        , CURRENT TIMESTAMP
                        , :BPCJCNTL.TMSRET )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCNTL"          TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURCCIN.
     š*---------------------------------------------------------------------
     š*-
           INITIALIZE  R-JCCIN
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURCCIN
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P001         TO TRUE
           WHEN OTHER
              SET     FIM-P001         TO TRUE
              MOVE "CURCCIN"           TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P200-FETCH-CURSOR-CURCCIN.
     š*****************************************************************
     š*-
           EXEC SQL FETCH CURCCIN
                     INTO
                       :BPCJCCIN.IDCONT
                     , :BPCJCCIN.IDINST
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
             MOVE IDCONT OF BPCJCCIN  TO WS-IDCONT
             MOVE IDINST OF BPCJCCIN  TO WS-IDINST
     š*-
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P001          TO TRUE
           WHEN OTHER
              SET   FIM-P001         TO TRUE
              MOVE "CURCCIN"         TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P300-PROCESSO-GERAL.
     š*****************************************************************
     š*-
     š*-  Obtem Informação Financeira Instrumentos
           IF SQLCODE-OK
              PERFORM P400-CALL-BPCSPFIN
           END-IF.
     š*- Trata Intervenientes
           IF SQLCODE-OK
              PERFORM P400-CALL-BPCSPFIN1
           END-IF.
     š*- Trata Protecções
           IF SQLCODE-OK
              PERFORM P400-CALL-BPCSPFIN2
           END-IF.
           PERFORM P200-FETCH-CURSOR-CURCCIN.
     š*-
     š*----------------------------------------------------------------
       P400-CALL-BPCSPFIN.
     š*----------------------------------------------------------------
           INITIALIZE              BPCBPFIN-LKPARMS.
           MOVE IDCONT OF BPCJCCIN TO BPCBPFIN-IDCONT
           MOVE IDINST OF BPCJCCIN TO BPCBPFIN-IDINST
           MOVE WS-RACINE-INST     TO BPCBPFIN-RACINE
           MOVE WS-GRE-INST        TO BPCBPFIN-CPGRE
           MOVE WS-RUB-INST        TO BPCBPFIN-CPRUB
           MOVE WS-MON-INST        TO BPCBPFIN-CPMON
           MOVE WS-CTR-CONT        TO BPCBPFIN-CTID
           MOVE WS-SEQ-CONT        TO BPCBPFIN-CTCONT
           MOVE WS-DTREF           TO BPCBPFIN-DTPROC

           CALL "BPCSPFIN" USING   BPCBPFIN-LKPARMS
           IF BPCBPFIN-PROC-OK
              CONTINUE
           ELSE
             SET  FIM-P001          TO TRUE
             INITIALIZE   KYCT900A
             MOVE BPCBPFIN-DESCERR  TO ERRMSG   OF  KYCT900A
             MOVE BPCBPFIN-CODERR   TO ERRCOD   OF  KYCT900A
             MOVE "BPCSPFIN"        TO ERRPRG   OF  KYCT900A
             MOVE "BPC"             TO ERRAPL   OF  KYCT900A
             MOVE BPCBPFIN-IDMSGEXE TO ERROBJ   OF  KYCT900A
             MOVE  1                TO ERRRTC   OF  KYCT900A
             PERFORM P995-INSERT-ERROS
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-CALL-BPCSPFIN1.
     š*----------------------------------------------------------------
     š*- Trata Intervenientes
           INITIALIZE             BPCBPFIN-LKPARMS.
           MOVE IDCONT OF BPCJCCIN TO BPCBPFIN-IDCONT
           MOVE IDINST OF BPCJCCIN TO BPCBPFIN-IDINST
           MOVE WS-NURACI          TO BPCBPFIN-RACINE
           MOVE WS-CPGRE           TO BPCBPFIN-CPGRE
           MOVE WS-CPRUB           TO BPCBPFIN-CPRUB
           MOVE WS-CPMON           TO BPCBPFIN-CPMON
           MOVE WS-CTR-CONT        TO BPCBPFIN-CTID
           MOVE WS-SEQ-CONT        TO BPCBPFIN-CTCONT
           MOVE WS-DTREF           TO BPCBPFIN-DTPROC
           CALL "BPCSPFIN1" USING   BPCBPFIN-LKPARMS
           IF BPCBPFIN-PROC-OK
              CONTINUE
           ELSE
             SET  FIM-P001            TO TRUE
             INITIALIZE   KYCT900A
             MOVE BPCBPFIN-DESCERR  TO ERRMSG   OF  KYCT900A
             MOVE BPCBPFIN-CODERR   TO ERRCOD   OF  KYCT900A
             MOVE "BPCSPFIN1"       TO ERRPRG   OF  KYCT900A
             MOVE "BPC"             TO ERRAPL   OF  KYCT900A
             MOVE BPCBPFIN-IDMSGEXE TO ERROBJ   OF  KYCT900A
             MOVE  1                TO ERRRTC   OF  KYCT900A
             PERFORM P995-INSERT-ERROS
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P400-CALL-BPCSPFIN2.
     š*----------------------------------------------------------------
     š*- Trata Protecções
           INITIALIZE             BPCBPFIN-LKPARMS.
           MOVE IDCONT OF BPCJCCIN TO BPCBPFIN-IDCONT
           MOVE IDINST OF BPCJCCIN TO BPCBPFIN-IDINST
           MOVE WS-NURACI          TO BPCBPFIN-RACINE
           MOVE WS-CPGRE           TO BPCBPFIN-CPGRE
           MOVE WS-CPRUB           TO BPCBPFIN-CPRUB
           MOVE WS-CPMON           TO BPCBPFIN-CPMON
           MOVE WS-CTR-CONT        TO BPCBPFIN-CTID
           MOVE WS-SEQ-CONT        TO BPCBPFIN-CTCONT
           MOVE WS-DTREF           TO BPCBPFIN-DTPROC
           CALL "BPCSPFIN2" USING   BPCBPFIN-LKPARMS
           IF BPCBPFIN-PROC-OK
              CONTINUE
           ELSE
             SET  FIM-P001            TO TRUE
             INITIALIZE   KYCT900A
             MOVE BPCBPFIN-DESCERR  TO ERRMSG   OF  KYCT900A
             MOVE BPCBPFIN-CODERR   TO ERRCOD   OF  KYCT900A
             MOVE "BPCSPFIN2"       TO ERRPRG   OF  KYCT900A
             MOVE "BPC"             TO ERRAPL   OF  KYCT900A
             MOVE BPCBPFIN-IDMSGEXE TO ERROBJ   OF  KYCT900A
             MOVE  1                TO ERRRTC   OF  KYCT900A
             PERFORM P995-INSERT-ERROS
           END-IF.
     š*-
     š*****************************************************************
       P400-CLOSE-CURSOR-CURCCIN.
     š*****************************************************************
     š*-
           EXEC SQL
               CLOSE CURCCIN
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET     FIM-P001         TO TRUE
           WHEN OTHER
              SET     FIM-P001         TO TRUE
              MOVE "CURSOR-CURCCIN"    TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-INSERT-HIST-BPCJPFIN.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação
     š*-
           EXEC SQL
                 INSERT INTO BPCJHPFIN
                 SELECT DTREF
                    ,   BPENV
                    ,   INFFININST
                    ,   IDCONT
                    ,   IDINST
                    ,   TAA
                    ,   TAN
                    ,   COMDESPBAL
                    ,   COMDESPEXP
                    ,   CREDALARG
                    ,   CREDCONV
                    ,   DTACTTXJUR
                    ,   DTESTINC
                    ,   DTINSTVENC
                    ,   ESTINC
                    ,   INSTFINAL
                    ,   JURCORR
                    ,   JURVENCBAL
                    ,   JURVENCEXT
                    ,   MONTABATV
                    ,   MPOTIRREV
                    ,   MPOTREV
                    ,   MREEMBANT
                    ,   MONTTRANSF
                    ,   MONTVENC
                    ,   MONTVIVO
                    ,   TPREEMBANT
                    ,   VALPREST
                    ,   TMSCRIA
                    ,   USERCRIA
                    ,   TMSALT
                    ,   USERALT
                    ,   CURRENT TIMESTAMP
                 FROM  BPCJPFIN
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "BPCJHPFIN"         TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-DELETE-BPCJPFIN.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação
     š*-
           EXEC SQL
                 DELETE
                 FROM  BPCJPFIN
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "BPCJPFIN"          TO W-OBJECT-NAME
              SET CMD-DELETE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-INSERT-HIST-BPCJPFIN1.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação de Intervenientes
     š*-
           EXEC SQL
                 INSERT INTO BPCJHPFIN1
                 SELECT IDCONT
                     ,  IDINST
                     ,  IDENT
                     ,  TYPER
                     ,  TPRESPENT
                     ,  MTOTENT
                     ,  MVENCENT
                     ,  MPOTRVENT
                     ,  MPOTIRVENT
                     ,  MABATENT
                     ,  VPRESTENT
                     ,  TMSCRIA
                     ,  USERCRIA
                     ,  TMSALT
                     ,  USERALT
                     ,  CURRENT TIMESTAMP
                 FROM  BPCJPFIN1
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "BPCJHPFIN"         TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-DELETE-BPCJPFIN1.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação de Intervenientes
     š*-
           EXEC SQL
                 DELETE
                 FROM  BPCJPFIN1
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "BPCJPFIN1"         TO W-OBJECT-NAME
              SET CMD-DELETE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-INSERT-HIST-BPCJPFIN2.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação de Protecções
     š*-
           EXEC SQL
                 INSERT INTO BPCJHPFIN2
                 SELECT IDCONT
                     ,  IDINST
                     ,  IDPROT
                     ,  VALPROT
                     ,  CREDPRIOR
                     ,  INDEXECPRT
                     ,  VALEXECPRT
                     ,  TMSCRIA
                     ,  USERCRIA
                     ,  TMSALT
                     ,  USERALT
                     ,  CURRENT TIMESTAMP
                 FROM  BPCJPFIN2
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "BPCJHPFIN2"        TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-DELETE-BPCJPFIN2.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação Protecções
     š*-
           EXEC SQL
                 DELETE
                 FROM  BPCJPFIN2
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "BPCJPFIN1"         TO W-OBJECT-NAME
              SET CMD-DELETE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-UPDATES-FINAIS.
     š*----------------------------------------------------------------

     š*- Data de Alterção de Estado de Incumprimento a Vencido
     š*- Se não Houver outra é a Data de VEncimento
           EXEC SQL
             UPDATE  BPCJPFIN
               SET DTESTINC =  DTINSTVENC
             WHERE DTESTINC = "0001-01-01"
             AND  DTINSTVENC <> "0001-01-01"
             AND  ESTINC     <> "000"
           END-EXEC.

     š*- Data Mais Recente em que Entrou em Incumprimento
     š*- Se não Houver outra
           EXEC SQL
             UPDATE  BPCJPFIN
               SET DTINSTVENC = DTESTINC
             WHERE DTESTINC  <> "0001-01-01"
             AND  DTINSTVENC =  "0001-01-01"
             AND  ESTINC     <> "000"
           END-EXEC.

     š*- Se não Estiver Vencido ambas as datas Inexistente
     š*- Se não Houver outra
           EXEC SQL
             UPDATE  BPCJPFIN
               SET DTINSTVENC = "0001-01-01"
                ,  DTESTINC   = "9999-12-31"
             WHERE ESTINC    =  "000"
           END-EXEC.

     š*- As datas 9993-12-31 devem ser Substituidas por
     š*- Data Incio de Instrumento
           EXEC SQL
             UPDATE  BPCJPFIN A
               SET A.DTESTINC   = ( SELECT DTINIINST
                                  FROM BPCJCCIN B
                                  WHERE A.IDCONT = B.IDCONT
                                    AND A.IDINST = B.IDINST )
             WHERE DTESTINC   = "9999-12-31"
              AND EXISTS (  SELECT 1
                            FROM BPCJCCIN B
                            WHERE A.IDCONT = B.IDCONT
                              AND A.IDINST = B.IDINST )
           END-EXEC.

     š*- Para Taxa Indexada Colocara a Data de Actualização
           EXEC SQL
             UPDATE  BPCJPFIN A
               SET A.DTACTTXJUR = "9999-12-31"
             WHERE EXISTS (  SELECT 1
                             FROM BPCJCCIN B
                             WHERE A.IDCONT   = B.IDCONT
                               AND A.IDINST   = B.IDINST
                               AND B.TPTXJUR  = "002"  )
           END-EXEC.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BPCPPFIN"      TO  ERRPRG OF KYCT900A.
           MOVE   WS-NURACI      TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "KYCT900A"       TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.


     š*****************************************************************
       P999-FIMPGM.
     š*****************************************************************
           EXEC SQL    COMMIT    HOLD         END-EXEC
           EXIT PROGRAM.
           STOP RUN.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BPCPPFIN
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
