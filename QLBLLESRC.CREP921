      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP921.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP921
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP921"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-NRMNUPRC         PIC 99.
               10  WS-MCSITEXE         PIC X.
               10  FILLER              PIC X(66).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).
           05  WS-IDREGSEL.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  FILLER              PIC X(69).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVPERFFN-I   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPERFFN-O   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.
           05  WS-COUNTER1             PIC S9(4)       BINARY.

           05  CS-QMNUFUN              PIC X.
               88  QMNUFUN-CLSE                    VALUE "C".
               88  QMNUFUN-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-SITEXECU-LS.
               10  FILLER              PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  FILLER              PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB971.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET908 OF CRET908.
           COPY DDS-CRET909 OF CRET909.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDGRFUNC.
                   49  DB-IDGRFUNC-L   PIC S9(4)       BINARY.
                   49  DB-IDGRFUNC-V   PIC X(08).
               10  DB-IDAPLICA         PIC X(03).
               10  DB-NRMNUPRC         PIC S9(2)       COMP-3.
               10  DB-MCSITEXE         PIC X.
           05  DB-SNPARAMS.
               10  DB-IDAPLFLT         PIC X.
                   88  IDAPLFLT-ON                 VALUE "S".
                   88  IDAPLFLT-OFF                VALUE "N".
               10  DB-NRMNUFLT         PIC X.
                   88  NRMNUFLT-ON                 VALUE "S".
                   88  NRMNUFLT-OFF                VALUE "N".
               10  DB-STEXEFLT         PIC X.
                   88  STEXEFLT-ON                 VALUE "S".
                   88  STEXEFLT-OFF                VALUE "N".
           05  DB-IDREGLST.
               10  DB-IDGRFUNC         PIC X(08).
               10  DB-IDAPLICA         PIC X(03).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QMNUFUN CURSOR FOR
                    SELECT IDGRFUNC
                         , IDAPLICA
                         , NRMNUPRC
                         , NVACESSO
                         , MCSITEXE
                      FROM CRET909
                     WHERE IDGRFUNC LIKE :DB-PARMSINP.DB-IDGRFUNC
                       AND(IDAPLICA    = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"         = :DB-IDAPLFLT)
                       AND(NRMNUPRC    = :DB-PARMSINP.DB-NRMNUPRC
                        OR "N"         = :DB-NRMNUFLT)
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                       AND(
                          (IDGRFUNC    > :DB-IDREGLST.DB-IDGRFUNC)
                        OR(IDGRFUNC    = :DB-IDREGLST.DB-IDGRFUNC
                       AND IDAPLICA    > :DB-IDREGLST.DB-IDAPLICA)
                          )
                     ORDER BY 1, 2
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-PARMSINP     TO WS-PARMSINP
           IF  CREB000-MSGDLG-P > ZERO
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB971-MSGDIALG
           END-IF
           IF  CREB000-IDREGLST-MIN
               INITIALIZE DB-IDREGLST
           ELSE
               MOVE CREB000-IDREGLST TO DB-IDREGLST
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB971-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCSEL
               PERFORM P2600-PREP-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2700-PREP-NOVOREG
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB971-MSGDIALG, WS-PARMSINP
           SET  CREB000-IDREG-NF TO TRUE
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB971-MSGDIALG
           MOVE WS-IDAPLICA OF WS-PARMSINP TO CREB971-IDAPLICA-V
           MOVE WS-IDGRFUNC OF WS-PARMSINP TO CREB971-IDGRFUNC-V
           MOVE WS-NRMNUPRC OF WS-PARMSINP TO CREB971-NRMNUPRC-V
           MOVE WS-MCSITEXE OF WS-PARMSINP TO CREB971-MCSITEXE-V
           SET  CREB000-IDREG-NF TO TRUE
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDAPLICA"
               MOVE CREB971-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB971-IDAPLICA-PC TO TRUE
           WHEN "IDGRFUNC"
               MOVE CREB971-IDGRFUNC-V  TO WS-IDGRFUNC OF WS-LVPERFFN-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPERFFN-I
               SET  CREB971-IDGRFUNC-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB971-IDAPLICA-V
               SET  CREB971-IDAPLICA-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDGRFUNC"
           AND  WS-IDGRFUNC OF WS-LVPERFFN-O NOT = SPACES
               MOVE WS-IDGRFUNC OF WS-LVPERFFN-O TO CREB971-IDGRFUNC-V
               SET  CREB971-IDGRFUNC-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB971-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN(CREB971-IDAPLICA-V     = SPACES OR CREB950-MCPESQGN)
           AND  CREB971-NRMNUPRC-V     > ZERO
               SET  CREB971-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB971-IDGRFUNC-V NOT = SPACES AND CREB950-MCPESQGN
               MOVE CREB971-IDGRFUNC-V TO WS-STRING80
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB971-IDGRFUNC-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) = SPACE
                   CONTINUE
               END-PERFORM

               PERFORM
               VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB971-IDGRFUNC-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
                   CONTINUE
               END-PERFORM

               IF  WS-ARRYNDX1 <= LENGTH OF CREB971-IDGRFUNC-V
                   SET  CREB971-IDGRFUNC-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-IF
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           IF  CREB971-IDGRFUNC-V NOT = SPACES AND CREB950-MCPESQGN
               PERFORM P9000-VRIF-IDGRFUNC
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB971-IDAPLICA-V NOT = SPACES AND CREB950-MCPESQGN)
               PERFORM P9000-VLID-IDAPLICA
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB971-MCSITEXE-V NOT = SPACE  AND CREB950-MCPESQGN)
               PERFORM P9000-VLID-MCSITEXE
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB971-IDGRFUNC-V TO WS-IDGRFUNC OF WS-PARMSINP
           MOVE CREB971-IDAPLICA-V TO WS-IDAPLICA OF WS-PARMSINP
           MOVE CREB971-NRMNUPRC-V TO WS-NRMNUPRC OF WS-PARMSINP
           MOVE CREB971-MCSITEXE-V TO WS-MCSITEXE OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           INITIALIZE DB-PARMSINP, CRET909, CREB971-VARSDET
           IF  CREB971-IDGRFUNC-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-IDGRFUNC-V
               MOVE  1                 TO DB-IDGRFUNC-L
           ELSE
               MOVE CREB971-IDGRFUNC-V TO DB-IDGRFUNC-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-IDGRFUNC-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-IDGRFUNC-L
                      FROM LENGTH OF DB-IDGRFUNC-V BY -1
                     UNTIL DB-IDGRFUNC-L = ZERO
                     OR    WS-CHRSTR80(DB-IDGRFUNC-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-IDGRFUNC-V TO DB-IDGRFUNC-L
               END-IF
           END-IF

           IF  CREB971-IDAPLICA-V = SPACES OR CREB950-MCPESQGN
               SET  IDAPLFLT-OFF       TO TRUE
           ELSE
               MOVE CREB971-IDAPLICA-V TO DB-IDAPLICA OF DB-PARMSINP
               SET  IDAPLFLT-ON        TO TRUE
           END-IF

           IF  CREB971-NRMNUPRC-V = ZERO
               SET  NRMNUFLT-OFF       TO TRUE
           ELSE
               MOVE CREB971-NRMNUPRC-V TO DB-NRMNUPRC OF DB-PARMSINP
               SET  NRMNUFLT-ON        TO TRUE
           END-IF

           IF  CREB971-MCSITEXE-V = SPACE  OR CREB950-MCPESQGN
               SET  STEXEFLT-OFF       TO TRUE
           ELSE
               MOVE CREB971-MCSITEXE-V TO DB-MCSITEXE OF DB-PARMSINP
               SET  STEXEFLT-ON        TO TRUE
           END-IF
           SET  QMNUFUN-CLSE           TO TRUE
           PERFORM P2441-CONT-QMNUFUN

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-NRENTLST = ZERO
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               PERFORM P2442-OPEN-QMNUFUN
           END-EVALUATE

           IF  QMNUFUN-OPEN
               PERFORM P2443-FTCH-QMNUFUN

               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB971-NRSELLIN-MAX
                 OR    CREB000-IDREGLST-MAX
                 OR    CREB000-PROC-ERR
                   PERFORM P2444-LOAD-LSTSEL

                   IF  CREB000-PROC-OK OR CREB000-MSGINFO
                       PERFORM P2443-FTCH-QMNUFUN
                   END-IF
               END-PERFORM

               PERFORM P2445-CLSE-QMNUFUN
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND NOT CREB000-IDREGLST-MAX
               MOVE DB-IDREGLST TO CREB000-IDREGLST
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QMNUFUN          SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET909
                     WHERE IDGRFUNC LIKE :DB-PARMSINP.DB-IDGRFUNC
                       AND(IDAPLICA    = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"         = :DB-IDAPLFLT)
                       AND(NRMNUPRC    = :DB-PARMSINP.DB-NRMNUPRC
                        OR "N"         = :DB-NRMNUFLT)
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               MOVE DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP921.QMNUFUN" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2442-OPEN-QMNUFUN          SECTION.

           EXEC SQL OPEN  QMNUFUN                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QMNUFUN-OPEN      TO TRUE
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP921.QMNUFUN" TO CREB000-NMOBJECT
               SET  CREB000-OPEN-CS   TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2442-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2443-FTCH-QMNUFUN          SECTION.

           EXEC SQL FETCH QMNUFUN
                     INTO :CRET909.IDGRFUNC
                        , :CRET909.IDAPLICA
                        , :CRET909.NRMNUPRC
                        , :CRET909.NVACESSO
                        , :CRET909.MCSITEXE                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK     TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE      TO CREB000-CDRETORN
               MOVE "CREP921.QMNUFUN"    TO CREB000-NMOBJECT
               SET  CREB000-FTCH-CS      TO TRUE
               SET  CREB000-MSGSGBD      TO TRUE
           END-EVALUATE
           .
       P2443-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2444-LOAD-LSTSEL           SECTION.

           MOVE IDGRFUNC OF CRET909 TO CREB971-IDGRFLIN-V(WS-ARRYNDX1)
                                       DB-IDGRFUNC OF DB-IDREGLST
           MOVE IDAPLICA OF CRET909 TO CREB971-IDAPLLIN-V(WS-ARRYNDX1)
                                       DB-IDAPLICA OF DB-IDREGLST
           MOVE NRMNUPRC OF CRET909 TO CREB971-NRMNULIN-V(WS-ARRYNDX1)
           MOVE NVACESSO OF CRET909 TO CREB971-NVACSLIN-V(WS-ARRYNDX1)
           MOVE MCSITEXE OF CRET909 TO CREB971-STEXELIN-V(WS-ARRYNDX1)
           IF  IDGRFUNC OF CRET909 NOT = IDGRFUNC OF CRET908
               MOVE IDGRFUNC OF CRET909 TO IDGRFUNC OF CRET908
               PERFORM P9000-READ-CRET908
           END-IF
           MOVE DSGRFUNC OF CRET908 TO CREB971-DSGRFLIN-V(WS-ARRYNDX1)
           .
       P2444-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2445-CLSE-QMNUFUN          SECTION.

           EXEC SQL CLOSE QMNUFUN                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP921.QMNUFUN"      TO CREB000-NMOBJECT
               SET  CREB000-CLSE-CS        TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P2445-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB971-IDGRFUNC-V   TO CREB999-IDGRFUNC
           MOVE CREB971-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB971-NRMNUPRC-V   TO CREB999-NRMNUPRC
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PREP-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-IDREGSEL = SPACES
               PERFORM P2610-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2620-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2630-FRMT-IDREGSEL
               END-IF
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2640-FRMT-IDPRCDST
           END-IF
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           INITIALIZE CREB000-NRLINSMC
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB971-NRSELLIN-MAX
             OR    CREB000-PROC-ERR
               MOVE CREB971-NROPCLIN-V(WS-ARRYNDX1) TO WS-ARRYNDX2
               EVALUATE TRUE
               WHEN WS-ARRYNDX2 = ZERO
                   CONTINUE
               WHEN CREB000-INDICOPC-ON(WS-ARRYNDX2)
                   ADD  1 TO CREB000-NRLINSMC
               WHEN OTHER
                   COMPUTE CREB971-PCURSOR = CREB971-NRPARAMS-INP
                                           + WS-ARRYNDX1
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-PERFORM

           IF  CREB000-PROC-OK
           AND CREB000-NRLINSMC = ZERO
               SET  CREB971-NROPCL01-PC        TO TRUE
               SET  CREB950-SELECC-NINFORM     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL CREB971-NROPCLIN-V(WS-ARRYNDX1) > ZERO
               CONTINUE
           END-PERFORM
           MOVE CREB971-NROPCLIN-V(WS-ARRYNDX1) TO CREB000-IDOPCSEL
           COMPUTE CREB971-PCURSOR = CREB971-NRPARAMS-INP + WS-ARRYNDX1
           INITIALIZE CREB971-NROPCLIN-V(WS-ARRYNDX1)
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL
           MOVE CREB971-IDGRFLIN-V(WS-ARRYNDX1)
                                   TO WS-IDGRFUNC OF WS-IDREGSEL
           MOVE CREB971-IDAPLLIN-V(WS-ARRYNDX1)
                                   TO WS-IDAPLICA OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2630-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2640-FRMT-IDPRCDST         SECTION.

           CONTINUE
           .
       P2640-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PREP-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2710-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2720-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2730-FRMT-IDREGSEL
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           PERFORM P2410-VRIF-NIVEL01
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           PERFORM P2420-VRIF-NIVEL02
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL, WS-COUNTER1
           INSPECT CREB971-IDGRFUNC-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               MOVE CREB971-IDGRFUNC-V TO WS-IDGRFUNC OF WS-IDREGSEL
           END-IF

           IF  CREB971-IDAPLICA-V NOT = CREB950-MCPESQGN
               MOVE CREB971-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           END-IF

           MOVE WS-IDREGSEL            TO CREB000-IDREGSEL
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN OTHER
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB971-MSGDIALG TO CREB000-MSGDLG-L
           END-EVALUATE
           MOVE CREB971-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)

           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB971-IDGRFUNC-UP TO TRUE
           SET  CREB971-IDAPLICA-UP TO TRUE
           SET  CREB971-NRMNUPRC-UP TO TRUE
           SET  CREB971-MCSITEXE-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB971-IDGRFUNC-PR TO TRUE
           SET  CREB971-IDAPLICA-PR TO TRUE
           SET  CREB971-NRMNUPRC-PR TO TRUE
           SET  CREB971-MCSITEXE-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB971-NRSELLIN-MAX
             OR    CREB971-IDGRFLIN-V(WS-ARRYNDX1) = SPACES
               SET  CREB971-NROPCLIN-UP(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB971-NRSELLIN-MAX
               SET  CREB971-NROPCLIN-PR(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-IDGRFUNC         SECTION.

           INITIALIZE WS-COUNTER1
           INSPECT CREB971-IDGRFUNC-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               PERFORM P9000-VLID-IDGRFUNC
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDGRFUNC         SECTION.

           MOVE CREB971-IDGRFUNC-V TO IDGRFUNC OF CRET908
           PERFORM P9000-READ-CRET908

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSGRFUNC OF CRET908    TO CREB971-DSGRFUNC-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB971-IDGRFUNC-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET908          SECTION.

           EXEC SQL SELECT DSGRFUNC
                      INTO :CRET908.DSGRFUNC
                      FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF       TO DSGRFUNC OF CRET908
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP921.CRET908"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDAPLICA         SECTION.

           MOVE CREB971-IDAPLICA-V TO IDAPLICA OF CRET901
           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE NMAPLICA OF CRET901    TO CREB971-NMAPLICA-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB971-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP921.CRET901"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-MCSITEXE         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB971-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB971-DSSITEXE-V
           ELSE
               SET  CREB971-MCSITEXE-PC         TO TRUE
               SET  CREB950-SITEXE-INVALID      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP921 ------------------------------------*
