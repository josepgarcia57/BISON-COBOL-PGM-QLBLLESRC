      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP910.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP910
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP910"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-NMVARDLG         PIC X(10).
               10  WS-DSVALVAR         PIC X(30).
               10  FILLER              PIC X(32).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).
           05  WS-IDREGSEL.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-NMVARDLG         PIC X(10).
               10  FILLER              PIC X(62).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVPRCAPL-I   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDTIPDLG         PIC XX.
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPRCAPL-O   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.
           05  WS-COUNTER1             PIC S9(4)       BINARY.

           05  CS-QLSTVAL              PIC X.
               88  QLSTVAL-CLSE                    VALUE "C".
               88  QLSTVAL-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDTIPDLG-MNTA        PIC XX      VALUE "MA".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB960.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET904 OF CRET904.
           COPY DDS-CRET905 OF CRET905.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDPRCAPL.
                   49  DB-IDPRCAPL-L   PIC S9(4)       BINARY.
                   49  DB-IDPRCAPL-V   PIC X(08).
               10  DB-NMVARDLG.
                   49  DB-NMVARDLG-L   PIC S9(4)       BINARY.
                   49  DB-NMVARDLG-V   PIC X(10).
               10  DB-DSVALVAR.
                   49  DB-DSVALVAR-L   PIC S9(4)       BINARY.
                   49  DB-DSVALVAR-V   PIC X(30).
           05  DB-IDREGLST.
               10  DB-IDPRCAPL         PIC X(08).
               10  DB-MCARADLG         PIC X.
               10  DB-NMVARDLG         PIC X(10).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QLSTVAL CURSOR FOR
                    SELECT IDPRCAPL
                         , MCARADLG
                         , NMVARDLG
                      FROM CRET905
                     WHERE IDPRCAPL LIKE :DB-PARMSINP.DB-IDPRCAPL
                       AND NMVARDLG LIKE :DB-PARMSINP.DB-NMVARDLG
                       AND DSVALVAR LIKE :DB-PARMSINP.DB-DSVALVAR
                       AND(
                          (IDPRCAPL    > :DB-IDREGLST.DB-IDPRCAPL)
                        OR(IDPRCAPL    = :DB-IDREGLST.DB-IDPRCAPL
                       AND MCARADLG    < :DB-IDREGLST.DB-MCARADLG)
                        OR(IDPRCAPL    = :DB-IDREGLST.DB-IDPRCAPL
                       AND MCARADLG    = :DB-IDREGLST.DB-MCARADLG
                       AND NMVARDLG    > :DB-IDREGLST.DB-NMVARDLG)
                          )
                     ORDER BY 1, 2 DESC, 3
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-PARMSINP     TO WS-PARMSINP
           IF  CREB000-MSGDLG-P > ZERO
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB960-MSGDIALG
           END-IF
           IF  CREB000-IDREGLST-MIN
               INITIALIZE DB-IDREGLST
           ELSE
               MOVE CREB000-IDREGLST TO DB-IDREGLST
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB960-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCSEL
               PERFORM P2600-PREP-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2700-PREP-NOVOREG
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB960-MSGDIALG, WS-PARMSINP
           SET  CREB000-IDREG-NF TO TRUE
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB960-MSGDIALG
           MOVE WS-IDPRCAPL OF WS-PARMSINP TO CREB960-IDPRCAPL-V
           MOVE WS-NMVARDLG OF WS-PARMSINP TO CREB960-NMVARDLG-V
           MOVE WS-DSVALVAR OF WS-PARMSINP TO CREB960-DSVALVAR-V
           SET  CREB000-IDREG-NF           TO TRUE
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDPRCAPL"
               MOVE CREB960-IDPRCAPL-V  TO WS-IDPRCAPL OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-IDAPLICA OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPRCAPL-I
               SET  CREB960-IDPRCAPL-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDPRCAPL"
           AND  WS-IDPRCAPL OF WS-LVPRCAPL-O NOT = SPACES
               MOVE WS-IDPRCAPL OF WS-LVPRCAPL-O TO CREB960-IDPRCAPL-V
               SET  CREB960-IDPRCAPL-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB960-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           IF  CREB960-IDPRCAPL-V NOT = SPACES AND CREB950-MCPESQGN
               MOVE CREB960-IDPRCAPL-V TO WS-STRING80
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB960-IDPRCAPL-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) = SPACE
                   CONTINUE
               END-PERFORM

               PERFORM
               VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB960-IDPRCAPL-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
                   CONTINUE
               END-PERFORM

               IF  WS-ARRYNDX1 <= LENGTH OF CREB960-IDPRCAPL-V
                   SET  CREB960-IDPRCAPL-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-IF
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB960-NMVARDLG-V NOT = SPACES AND CREB950-MCPESQGN)
               MOVE CREB960-NMVARDLG-V TO WS-STRING80
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB960-NMVARDLG-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) = SPACE
                   CONTINUE
               END-PERFORM

               PERFORM
               VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB960-NMVARDLG-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
                   CONTINUE
               END-PERFORM

               IF  WS-ARRYNDX1 <= LENGTH OF CREB960-NMVARDLG-V
                   SET  CREB960-NMVARDLG-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-IF
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           IF  CREB960-IDPRCAPL-V NOT = SPACES AND CREB950-MCPESQGN
               PERFORM P9000-VRIF-IDPRCAPL
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB960-IDPRCAPL-V TO WS-IDPRCAPL OF WS-PARMSINP
           MOVE CREB960-NMVARDLG-V TO WS-NMVARDLG OF WS-PARMSINP
           MOVE CREB960-DSVALVAR-V TO WS-DSVALVAR OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           INITIALIZE DB-PARMSINP, CRET905, CREB960-VARSDET
           IF  CREB960-IDPRCAPL-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-IDPRCAPL-V
               MOVE  1                 TO DB-IDPRCAPL-L
           ELSE
               MOVE CREB960-IDPRCAPL-V TO DB-IDPRCAPL-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-IDPRCAPL-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-IDPRCAPL-L
                     FROM LENGTH OF DB-IDPRCAPL-V BY -1
                    UNTIL DB-IDPRCAPL-L = ZERO
                    OR    WS-CHRSTR80(DB-IDPRCAPL-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-IDPRCAPL-V TO DB-IDPRCAPL-L
               END-IF
           END-IF

           IF  CREB960-NMVARDLG-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-NMVARDLG-V
               MOVE  1                 TO DB-NMVARDLG-L
           ELSE
               MOVE CREB960-NMVARDLG-V TO DB-NMVARDLG-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-NMVARDLG-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-NMVARDLG-L
                     FROM LENGTH OF DB-NMVARDLG-V BY -1
                    UNTIL DB-NMVARDLG-L = ZERO
                    OR    WS-CHRSTR80(DB-NMVARDLG-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-NMVARDLG-V TO DB-NMVARDLG-L
               END-IF
           END-IF

           IF  CREB960-DSVALVAR-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-DSVALVAR-V
               MOVE  1                 TO DB-DSVALVAR-L
           ELSE
               MOVE CREB960-DSVALVAR-V TO DB-DSVALVAR-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-DSVALVAR-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-DSVALVAR-L
                     FROM LENGTH OF DB-DSVALVAR-V BY -1
                    UNTIL DB-DSVALVAR-L = ZERO
                    OR    WS-CHRSTR80(DB-DSVALVAR-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-DSVALVAR-V TO DB-DSVALVAR-L
               END-IF
           END-IF
           SET  QLSTVAL-CLSE           TO TRUE
           PERFORM P2441-CONT-QLSTVAL

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-NRENTLST = ZERO
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               PERFORM P2442-OPEN-QLSTVAL
           END-EVALUATE

           IF  QLSTVAL-OPEN
               PERFORM P2443-FTCH-QLSTVAL

               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
                 OR    CREB000-IDREGLST-MAX
                 OR    CREB000-PROC-ERR
                   PERFORM P2444-LOAD-LSTSEL

                   IF  CREB000-PROC-OK OR CREB000-MSGINFO
                       PERFORM P2443-FTCH-QLSTVAL
                   END-IF
               END-PERFORM

               PERFORM P2445-CLSE-QLSTVAL
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND NOT CREB000-IDREGLST-MAX
               MOVE DB-IDREGLST TO CREB000-IDREGLST
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QLSTVAL          SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET905
                     WHERE IDPRCAPL LIKE :DB-PARMSINP.DB-IDPRCAPL
                       AND NMVARDLG LIKE :DB-PARMSINP.DB-NMVARDLG
                       AND DSVALVAR LIKE :DB-PARMSINP.DB-DSVALVAR
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               MOVE DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP910.QLSTVAL" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2442-OPEN-QLSTVAL          SECTION.

           EXEC SQL OPEN  QLSTVAL                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QLSTVAL-OPEN      TO TRUE
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP910.QLSTVAL" TO CREB000-NMOBJECT
               SET  CREB000-OPEN-CS   TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2442-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2443-FTCH-QLSTVAL          SECTION.

           EXEC SQL FETCH QLSTVAL
                     INTO :CRET905.IDPRCAPL
                        , :CRET905.MCARADLG
                        , :CRET905.NMVARDLG                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK     TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE      TO CREB000-CDRETORN
               MOVE "CREP910.QLSTVAL"    TO CREB000-NMOBJECT
               SET  CREB000-FTCH-CS      TO TRUE
               SET  CREB000-MSGSGBD      TO TRUE
           END-EVALUATE
           .
       P2443-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2444-LOAD-LSTSEL           SECTION.

           MOVE IDPRCAPL OF CRET905 TO CREB960-IDPRCLIN-V(WS-ARRYNDX1)
                                       DB-IDPRCAPL OF DB-IDREGLST
                                       IDPRCAPL OF CRET903
           MOVE MCARADLG OF CRET905 TO CREB960-MCARALIN-V(WS-ARRYNDX1)
                                       DB-MCARADLG OF DB-IDREGLST
           MOVE NMVARDLG OF CRET905 TO CREB960-NMVARLIN-V(WS-ARRYNDX1)
                                       DB-NMVARDLG OF DB-IDREGLST
           PERFORM P9000-READ-IDPRCAPL

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE DSPRCAPL OF CRET903
                                    TO CREB960-NMPRCLIN-V(WS-ARRYNDX1)
               MOVE IDDIALOG OF CRET903
                                    TO CREB960-IDDLGLIN-V(WS-ARRYNDX1)
               MOVE IDTIPDLG OF CRET903
                                    TO CREB960-TPDLGLIN-V(WS-ARRYNDX1)
           END-IF
           .
       P2444-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2445-CLSE-QLSTVAL          SECTION.

           EXEC SQL CLOSE QLSTVAL                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP910.QLSTVAL"      TO CREB000-NMOBJECT
               SET  CREB000-CLSE-CS        TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P2445-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB960-IDPRCAPL-V   TO CREB999-IDPRCAPL
           MOVE CREB960-NMVARDLG-V   TO CREB999-NMVARDLG
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PREP-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-IDREGSEL = SPACES
               PERFORM P2610-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2620-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2630-FRMT-IDREGSEL
               END-IF
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2640-FRMT-IDPRCDST
           END-IF
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           INITIALIZE CREB000-NRLINSMC
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
             OR    CREB000-PROC-ERR
               MOVE CREB960-NROPCLIN-V(WS-ARRYNDX1) TO WS-ARRYNDX2
               EVALUATE TRUE
               WHEN WS-ARRYNDX2 = ZERO
                   CONTINUE
               WHEN CREB000-INDICOPC-ON(WS-ARRYNDX2)
                   ADD  1 TO CREB000-NRLINSMC
               WHEN OTHER
                   COMPUTE CREB960-PCURSOR = CREB960-NRPARAMS-INP
                                           + WS-ARRYNDX1
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-PERFORM

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-NRLINSMC = ZERO
               SET  CREB960-NROPCL01-PC        TO TRUE
               SET  CREB950-SELECC-NINFORM     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL CREB960-NROPCLIN-V(WS-ARRYNDX1) > ZERO
               CONTINUE
           END-PERFORM
           MOVE CREB960-NROPCLIN-V(WS-ARRYNDX1) TO CREB000-IDOPCSEL
           COMPUTE CREB960-PCURSOR = CREB960-NRPARAMS-INP + WS-ARRYNDX1
           INITIALIZE CREB960-NROPCLIN-V(WS-ARRYNDX1)
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL
           MOVE CREB960-IDPRCLIN-V(WS-ARRYNDX1)
                                   TO WS-IDPRCAPL OF WS-IDREGSEL
           MOVE CREB960-NMVARLIN-V(WS-ARRYNDX1)
                                   TO WS-NMVARDLG OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2630-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2640-FRMT-IDPRCDST         SECTION.

           CONTINUE
           .
       P2640-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PREP-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2710-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2720-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2730-FRMT-IDREGSEL
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           PERFORM P2410-VRIF-NIVEL01
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           PERFORM P2420-VRIF-NIVEL02
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL, WS-COUNTER1
           INSPECT CREB960-IDPRCAPL-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               MOVE CREB960-IDPRCAPL-V TO WS-IDPRCAPL OF WS-IDREGSEL
           END-IF

           INITIALIZE WS-COUNTER1
           INSPECT CREB960-NMVARDLG-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               MOVE CREB960-NMVARDLG-V TO WS-NMVARDLG OF WS-IDREGSEL
           END-IF

           MOVE WS-IDREGSEL            TO CREB000-IDREGSEL
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN OTHER
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB960-MSGDIALG TO CREB000-MSGDLG-L
           END-EVALUATE
           MOVE CREB960-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)

           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB960-IDPRCAPL-UP TO TRUE
           SET  CREB960-NMVARDLG-UP TO TRUE
           SET  CREB960-DSVALVAR-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB960-IDPRCAPL-PR TO TRUE
           SET  CREB960-NMVARDLG-PR TO TRUE
           SET  CREB960-DSVALVAR-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
             OR    CREB960-IDPRCLIN-V(WS-ARRYNDX1) = SPACES
               SET  CREB960-NROPCLIN-UP(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
               SET  CREB960-NROPCLIN-PR(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-IDPRCAPL         SECTION.

           INITIALIZE WS-COUNTER1
           INSPECT CREB960-IDPRCAPL-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               PERFORM P9000-VLID-IDPRCAPL
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDPRCAPL         SECTION.

           MOVE CREB960-IDPRCAPL-V         TO IDPRCAPL OF CRET903
           PERFORM P9000-READ-IDPRCAPL

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSPRCAPL OF CRET903    TO CREB960-DSPRCAPL-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB960-IDPRCAPL-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-IDPRCAPL         SECTION.

           PERFORM P9000-READ-CRET903

           IF  SQLCODE-NOTFOUND
               PERFORM P9000-READ-CRET904
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903          SECTION.

           EXEC SQL SELECT DSPRCAPL
                         , IDTIPDLG
                         , IDDIALOG
                      INTO :CRET903.DSPRCAPL
                         , :CRET903.IDTIPDLG
                         , :CRET903.IDDIALOG
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP910.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET904          SECTION.

           EXEC SQL SELECT DSPRCAPL
                         , IDDIALOG
                      INTO :CRET903.DSPRCAPL
                         , :CRET903.IDDIALOG
                      FROM CRET904
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE KV-IDTIPDLG-MNTA  TO IDTIPDLG OF CRET903
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSPRCAPL OF CRET903
               MOVE SPACES            TO IDTIPDLG OF CRET903
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP910.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP910 ------------------------------------*
