       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BPCPCPRTX   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BPC - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este programa destina-se a Gerar o XML     *
     š*                     a enviar ao Banco de Portugal             *
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *------------------------------------------------------------------------*
       FILE-CONTROL.
      *------------------------------------------------------------------------*
           SELECT BPCFCPRT
                  ASSIGN TO DATABASE-BPCFCPRT
                  ORGANIZATION IS SEQUENTIAL
                  ACCESS MODE  IS SEQUENTIAL
                  FILE STATUS  IS GRS-FILE-STATUS.
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
       FD  BPCFCPRT.
       01  REG-BPCFCPRT.
           COPY DDS-ALL-FORMATS OF BPCFCPRT.
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 GRS-FILE-STATUS            PIC XX.

       01 EXECUTA-P001               PIC 9(01).
          88 INICIO-P001             VALUE 0.
          88 FIM-P001                VALUE 1.

       01 EXECUTA-P002               PIC 9(01).
          88 INICIO-P002             VALUE 0.
          88 FIM-P002                VALUE 1.
      *
       01 CICLO-SEQ                  PIC 9(01).
          88 CICLO-1                 VALUE 0.
          88 CICLO-N                 VALUE 1.

       01 CICLO-DOC                  PIC 9(01).
          88 CICLO-DOC-1             VALUE 0.
          88 CICLO-DOC-N             VALUE 1.
      *
       01 TRATA-INFO                 PIC 9(01).
          88 SIM-TRATA               VALUE 1.
          88 NAO-TRATA               VALUE 0.

       01 PGM-STATUS                 PIC 9(01).
          88 SEM-ERRO                VALUE 0.
          88 COM-ERRO                VALUE 1.

       01 NR-ADM                     PIC 9(06) VALUE ZEROS.
       01 NR-READ                    PIC 9(06) VALUE ZEROS.
       01 NR-PROC                    PIC 9(06) VALUE ZEROS.
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(8)    VALUE "BPCPCPRTX".

       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA         PIC 9(04).
           03  FILLER               PIC X VALUE "-".
           03  WS-MESIN-DIA         PIC 9(02).
           03  FILLER               PIC X VALUE "-".
           03  WS-DIAIN-DIA         PIC 9(02).
     š* ------------------------------------------------------
     š* Linha com XML gerado HardCode pelo programa
       01 XML-OUTPUT       PIC X(1000).

       01 Ws-dtCriacao.
          05 ws-Data         PIC X(10).
          05 ws-SepT         PIC X(1).
          05 ws-HH           PIC X(2).
          05 ws-SepH         PIC X(1).
          05 ws-MM           PIC X(2).
          05 ws-SepM         PIC X(1).
          05 ws-SS           PIC X(2).

     š* ------------------------------------------------------
       01 WS-STRING.
          05 WS-STRING-CHR         PIC X           OCCURS 300.
       01 WS-VAL-TAG-A             PIC X(80).
       01 WS-NOME-TAG              PIC X(80).
       01 WS-VAL-TAG-L             PIC S9(4)       COMP   VALUE 0.
       01 WS-NOM-TAG-L             PIC S9(4)       COMP   VALUE 0.
     š* ------------------------------------------------------
     š* Determina Comprimento de uma String
       01 WS-POS-I                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-S                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-XML               PIC S9(4)       COMP   VALUE 0.
       01 WS-STRING-L              PIC S9(4)       COMP   VALUE 0.
       01 W-NUM                    PIC 9(3).
       01 Edit-Nbr-Tx PIC      -----9,99999.
       01 Edit-Nbr-Mn PIC      --------9,99.

     š* Campos de Caracteres Invalidos
        01 CHAR-TESTE                PIC X.
           88 CARACTER-VALIDO      VALUE
      *                             - letras minusculas
                                          "a", "b", "c", "d", "e",
                                          "f", "g", "h", "i", "j",
                                          "k", "l", "m", "n", "o",
                                          "p", "q", "r", "s", "t",
                                          "u", "v", "w", "x", "y",
                                          "z",
      *                             - letras maiusculas
                                          "A", "B", "C", "D", "E",
                                          "F", "G", "H", "I", "J",
                                          "K", "L", "M", "N", "O",
                                          "P", "Q", "R", "S", "T",
                                          "U", "V", "W", "X", "Y",
                                          "Z",
      *                             - numeros
                                          "0", "1", "2", "3", "4",
                                          "5", "6", "7", "8", "9",
      *                             - outros
                                          " ", "/", "-", "?", ":",
                                          "(", ")", ".", ",",
                                          "+".

      * "'" passou a ser considerado invalido

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
     š* Criação de Ficheiro de Destino
       01 WS-BIBORIG               PIC X(10).
       01 WS-FICHORIG              PIC X(10).
       01 WS-BIBDEST               PIC X(10).
       01 WS-FICHDEST              PIC X(10).
       01 WS-ERRO                  PIC X(10).
     š* Área de Ligação para o FTP
       01 BPCL001M-FICH-ORIGEM    PIC  X(15).
       01 BPCL001M-FICH-DESTINO   PIC  X(50).
       01 BPCL001M-ERRO-BPCPFTP   PIC  X(01).
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".

     š* Copy : Tabela de Clientes
           EXEC SQL
               INCLUDE BPCHCPRTN
           END-EXEC.

     š* Copy : Tabela XMP BP - CENT - Comunicação de Entidades
       01  R-BPCFCPRT.
           COPY DDS-ALL-FORMATS OF BPCFCPRT.

     š* Copy : Tabela de Controlo
       01  R-JCNTL.
           COPY DDS-ALL-FORMATS OF BPCJCNTL.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.

       01 WS-NURACI                PIC X(07).

     š* Cursor Principal
           EXEC SQL DECLARE CURJCPRT CURSOR FOR
            SELECT    INFTPROT
                   ,  BPENV
                   ,  IDPROT
                   ,  DTREFPROT
                   ,  IDENT
                   ,  TYPER
                   ,  TPPROT
                   ,  REFEXTPROT
                   ,  VALPROT
                   ,  TPVALPROT
                   ,  DTMATPROT
                   ,  PAISLPROT
                   ,  REGLOCPROT
                   ,  DTULTAVAL
                   ,  TPAVAL
                   ,  VALORIPROT
                   ,  DTVALPRTO
                   ,  HIERQPROT
                   ,  PRECOAQUIS
                   ,  NUMREGPROT
                   ,  STATEXPROT
                   ,  DTEXECPROT
                   ,  VALACEXEC
                   ,  TMSCRIA
                   ,  USERCRIA
                   ,  TMSALT
                   ,  USERALT
                FROM  BPCJCPRT A
                WHERE BPENV = "E"
           END-EXEC.

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       PROCEDURE DIVISION.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P002-INICIO.

           PERFORM P002-REG-CONTROLO.

           PERFORM P100-OPEN-CURSOR-CURJCPRT.
           PERFORM P200-FETCH-CURSOR-CURJCPRT.
           PERFORM P300-PROCESSO-GERAL
                           UNTIL FIM-P001
           PERFORM P400-CLOSE-CURSOR-CURJCPRT
           IF NR-PROC NOT EQUAL ZERO
             PERFORM P300-CLOSE-TAG-comProt
             PERFORM P300-CLOSE-TAG-conteudo
             PERFORM P300-CLOSE-TAG-crc
             PERFORM P400-BPCFCPRT-CLOSE
             PERFORM P450-FTP-FILE
           END-IF
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P002-INICIO.
     š*---------------------------------------------------------------------
           SET  INICIO-P001         TO  TRUE.
           SET  CICLO-1             TO  TRUE.
           EXEC SQL
              DELETE FROM BPCFCPRT
           END-EXEC.

           MOVE 1 TO WS-POS-XML
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.

     š*---------------------------------------------------------------------
       P002-REG-CONTROLO.
     š*---------------------------------------------------------------------
           INITIALIZE  BPCJCNTL
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           PERFORM P300-GET-BPCJCNTL.
     š*---------------------------------------------------------------------
       P300-GET-BPCJCNTL.
     š*---------------------------------------------------------------------
     š*- Último Envio
           SET SIM-TRATA TO TRUE

           EXEC SQL
             SELECT     X.DTREF
                     ,  X.REPTYPE
                     ,  X.ESTADO
                     ,  X.ENTOBS
                     ,  X.ENTREPORT
                     ,  X.IDDEST
                     ,  X.DTCRIACAO
                     ,  X.NOMEFICH
                     ,  X.NREC
                     ,  X.NREJ
                     ,  X.NINT
                     ,  X.NALERTA
                     ,  X.TMSCRIA
                     ,  X.TMSRET
             INTO      :BPCJCNTL.DTREF
                     , :BPCJCNTL.REPTYPE
                     , :BPCJCNTL.ESTADO
                     , :BPCJCNTL.ENTOBS
                     , :BPCJCNTL.ENTREPORT
                     , :BPCJCNTL.IDDEST
                     , :BPCJCNTL.DTCRIACAO
                     , :BPCJCNTL.NOMEFICH
                     , :BPCJCNTL.NREC
                     , :BPCJCNTL.NREJ
                     , :BPCJCNTL.NINT
                     , :BPCJCNTL.NALERTA
                     , :BPCJCNTL.TMSCRIA
                     , :BPCJCNTL.TMSRET
             FROM  BPCJCNTL  X
                WHERE TMSCRIA = ( SELECT MAX(TMSCRIA)
                                  FROM BPCJCNTL Y
                                  WHERE REPTYPE = "CPRT" )
                AND   REPTYPE = "CPRT"
                AND   ESTADO  IN( "ENV" , "RET" )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
              IF ESTADO OF BPCJCNTL = "RET"
                 SET NAO-TRATA TO TRUE
              END-IF
              MOVE S-SYSTMST(1:10) TO WS-DATA-DIA
              MOVE DTCRIACAO OF BPCJCNTL TO Ws-dtCriacao
           WHEN OTHER
              MOVE "BPCCPRTX"        TO W-OBJECT-NAME
              SET CMD-SELECT         TO TRUE
              PERFORM  P990-ERRO-DB2
              SET NAO-TRATA TO TRUE
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURJCPRT.
     š*---------------------------------------------------------------------
           INITIALIZE  NEWCPRT-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURJCPRT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P001         TO TRUE
           WHEN OTHER
              SET  FIM-P001            TO TRUE
              MOVE "CURJCPRT"          TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P200-FETCH-CURSOR-CURJCPRT.
     š*---------------------------------------------------------------------

           EXEC SQL FETCH CURJCPRT
                     INTO  :NEWCPRT-INFTPROT
                         , :NEWCPRT-BPENV
                         , :NEWCPRT-IDPROT
                         , :NEWCPRT-DTREFPROT
                         , :NEWCPRT-IDENT
                         , :NEWCPRT-TYPER
                         , :NEWCPRT-TPPROT
                         , :NEWCPRT-REFEXTPROT
                         , :NEWCPRT-VALPROT
                         , :NEWCPRT-TPVALPROT
                         , :NEWCPRT-DTMATPROT
                         , :NEWCPRT-PAISLPROT
                         , :NEWCPRT-REGLOCPROT
                         , :NEWCPRT-DTULTAVAL
                         , :NEWCPRT-TPAVAL
                         , :NEWCPRT-VALORIPROT
                         , :NEWCPRT-DTVALPRTO
                         , :NEWCPRT-HIERQPROT
                         , :NEWCPRT-PRECOAQUIS
                         , :NEWCPRT-NUMREGPROT
                         , :NEWCPRT-STATEXPROT
                         , :NEWCPRT-DTEXECPROT
                         , :NEWCPRT-VALACEXEC
                         , :NEWCPRT-TMSCRIA
                         , :NEWCPRT-USERCRIA
                         , :NEWCPRT-TMSALT
                         , :NEWCPRT-USERALT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P001          TO TRUE
           WHEN OTHER
              SET   FIM-P001         TO TRUE
              MOVE "CURJCPRT"        TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-PROCESSO-GERAL.
     š*---------------------------------------------------------------------
           IF CICLO-1
              PERFORM P300-BPCFCPRT-OPEN
              IF SEM-ERRO
                INITIALIZE REG-BPCFCPRT  XML-OUTPUT
                PERFORM P300-OPEN-TAG-crc
                PERFORM P300-GERA-TAG-controlo
                PERFORM P300-OPEN-TAG-conteudo
                PERFORM P300-OPEN-TAG-comProt
                SET CICLO-N TO TRUE
              END-IF
           END-IF
           IF SEM-ERRO
              PERFORM P300-OPEN-TAG-infProt
              PERFORM P200-FETCH-CURSOR-CURJCPRT
           END-IF.
           PERFORM P300-CLOSE-TAG-infProt.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-crc.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT
     š*-XML Doc Declaration
           MOVE "<?xml version='1.0' encoding='ISO-8859-1'?>"
                       TO XML-OUTPUT.

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*-CRC Tag Declaration
           MOVE "<crc versao='1.0' >"    TO XML-OUTPUT

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-controlo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "controlo"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "entObserv"             TO  WS-NOME-TAG
           MOVE   ENTOBS     OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "entReport"             TO  WS-NOME-TAG
           MOVE  ENTREPORT   OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtCriacao"             TO  WS-NOME-TAG
           MOVE   DTCRIACAO  OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idDest"                TO  WS-NOME-TAG
           MOVE   IDDEST    OF  BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idFichRelac"           TO  WS-NOME-TAG
           MOVE   spaces                 TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           PERFORM P300-END-TAG

           MOVE  "controlo"              TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-conteudo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "conteudo"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-comProt.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT.

           MOVE  "comProt"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "xmlns:xsi"         TO  WS-NOME-TAG
           MOVE  "http://www.w3.org/2001/XMLSchema-instance"
                                     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "xsi:noNamespaceSchemaLocation"
                                     TO  WS-NOME-TAG
           MOVE  "file:/C:/crc_bdp_schema.xsd"
                                     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-infProt.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT
     š*- Type - EI-Insert/EU-Update/ED-Delete

           MOVE  "infProt"           TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "xsi:type"          TO  WS-NOME-TAG
           MOVE   NEWCPRT-INFTPROT   TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idProt"                TO  WS-NOME-TAG
           MOVE   NEWCPRT-IDPROT         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtRefProt"         TO  WS-NOME-TAG
           MOVE   NEWCPRT-DTREFPROT  TO  WS-VAL-TAG-A
           IF NEWCPRT-DTREFPROT NOT EQUAL "0001-01-01" AND
              NEWCPRT-DTREFPROT NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "tpProt"            TO  WS-NOME-TAG
           MOVE   NEWCPRT-TPPROT     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "refExtProt"        TO  WS-NOME-TAG
           MOVE   NEWCPRT-REFEXTPROT TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "valProt"           TO  WS-NOME-TAG
           MOVE   NEWCPRT-VALPROT    TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "tpValProt"         TO  WS-NOME-TAG
           MOVE   NEWCPRT-TPVALPROT  TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtMatProt"         TO  WS-NOME-TAG
           MOVE   NEWCPRT-DTMATPROT  TO  WS-VAL-TAG-A
           IF NEWCPRT-DTMATPROT NOT EQUAL "0001-01-01" AND
              NEWCPRT-DTMATPROT NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "paisLocProt"       TO  WS-NOME-TAG
           MOVE   NEWCPRT-PAISLPROT  TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "regLocProt"        TO  WS-NOME-TAG
           MOVE   NEWCPRT-REGLOCPROT TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtUltAval"            TO  WS-NOME-TAG
           MOVE   NEWCPRT-DTULTAVAL     TO  WS-VAL-TAG-A
           IF NEWCPRT-DTULTAVAL   NOT EQUAL "0001-01-01" AND
              NEWCPRT-DTULTAVAL   NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "tpAval"               TO  WS-NOME-TAG
           MOVE   NEWCPRT-TPAVAL        TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "valOriProt"           TO  WS-NOME-TAG
           MOVE   NEWCPRT-VALORIPROT    TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "dtValOriProt"         TO  WS-NOME-TAG
           MOVE   NEWCPRT-DTVALPRTO     TO  WS-VAL-TAG-A
           IF NEWCPRT-DTVALPRTO   NOT EQUAL "0001-01-01" AND
              NEWCPRT-DTVALPRTO   NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "hierqProt"            TO  WS-NOME-TAG
           MOVE   NEWCPRT-HIERQPROT     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           IF NEWCPRT-TPPROT  =    "1310"
                              OR   "1311"
                              OR   "1312"
                              OR   "1320"
                              OR   "1321"
                              OR   "1322"
                              OR   "1330"
                              OR   "1331"
                              OR   "1332"
                              OR   "1340"
                              OR   "1341"
                              OR   "1342"
                              OR   "1350"
                              OR   "1351"
                              OR   "1352"
                              OR   "1360"
                              OR   "1361"
                              OR   "1362"
                              OR   "1410"
                              OR   "1420"
                              OR   "1430"
                              OR   "1500"
               MOVE  "precoAquisImovel"     TO  WS-NOME-TAG
               MOVE   NEWCPRT-PRECOAQUIS    TO  Edit-Nbr-Mn
               MOVE  Edit-Nbr-Mn            TO  WS-VAL-TAG-A
               PERFORM P300-ATRIBUTE-N
             END-IF.

           MOVE  "numRegProt"           TO  WS-NOME-TAG
           MOVE   NEWCPRT-NUMREGPROT    TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "estExecProt"          TO  WS-NOME-TAG
           MOVE   NEWCPRT-STATEXPROT    TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtExecProtr"          TO  WS-NOME-TAG
           MOVE   NEWCPRT-DTEXECPROT    TO  WS-VAL-TAG-A
           IF NEWCPRT-DTEXECPROT  NOT EQUAL "0001-01-01" AND
              NEWCPRT-DTEXECPROT  NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           IF  NEWCPRT-VALACEXEC NOT EQUAL ZERO
             MOVE  "valAcumExecProt"      TO  WS-NOME-TAG
             MOVE   NEWCPRT-VALACEXEC     TO  Edit-Nbr-Mn
             MOVE  Edit-Nbr-Mn            TO  WS-VAL-TAG-A
             PERFORM P300-ATRIBUTE-N
           END-IF.

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.

           PERFORM P300-GERA-TAG-idEnt.

     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-infProt.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "infProt"                 TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-comProt.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "comProt"                TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-conteudo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "conteudo"              TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-crc.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "crc"                   TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
       P300-BPCFCPRT-OPEN.
     š*---------------------------------------------------------------------
           OPEN OUTPUT BPCFCPRT
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-OPENED
               CONTINUE
           ELSE
               SET  FIM-P001             TO TRUE
               MOVE "BPCPCPRTX"            TO W-PROGRAMA
               MOVE "P300-BPCFCPRT-OPEN" TO W-PARAGRAFO
               MOVE "BPCFCPRT"           TO W-OBJECT-NAME
               MOVE "OPEN"               TO W-CMDEXEC
               MOVE SPACES               TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P400-BPCFCPRT-CLOSE.
     š*---------------------------------------------------------------------
           CLOSE BPCFCPRT
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-CLOSED
               CONTINUE
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "BPCPCPRTX"             TO W-PROGRAMA
               MOVE "P300-BPCFCPRT-CLOSE" TO W-PARAGRAFO
               MOVE "BPCFCPRT"            TO W-OBJECT-NAME
               MOVE "CLOSE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P450-FTP-FILE.
     š*---------------------------------------------------------------------
           INITIALIZE BPCL001M-FICH-ORIGEM
                      BPCL001M-FICH-DESTINO
                      BPCL001M-ERRO-BPCPFTP
                      REPLACING ALPHANUMERIC BY SPACES
                                    NUMERIC BY ZEROS.

           MOVE  "BPCFCPRT"            TO BPCL001M-FICH-ORIGEM
           MOVE  NOMEFICH  OF BPCJCNTL TO BPCL001M-FICH-DESTINO

           CALL "BPCCFTP" USING BPCL001M-FICH-ORIGEM
                                BPCL001M-FICH-DESTINO
                                BPCL001M-ERRO-BPCPFTP

           IF BPCL001M-ERRO-BPCPFTP NOT EQUAL SPACES
             MOVE "001"                     TO W-FILE-CODE
             MOVE WS-ERRO                   TO MSG-ERRO-FS
             MOVE "BPCCFTP"                 TO W-OBJECT-NAME
             MOVE "FTP"                     TO W-CMDEXEC
             MOVE "P450-FTP-FILE"           TO W-PARAGRAFO
             PERFORM P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P300-BPCFCPRT-WRITE.
     š*---------------------------------------------------------------------
           WRITE REG-BPCFCPRT
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO       OR
               GRS-DUP-KEY
                INITIALIZE REG-BPCFCPRT
                ADD 1 TO NR-PROC
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "BPCPCPRTX"             TO W-PROGRAMA
               MOVE "P300-BPCFCPRT-WRITE" TO W-PARAGRAFO
               MOVE "BPCFCPRT"            TO W-OBJECT-NAME
               MOVE "WRITE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.

           MOVE 1 TO WS-POS-XML.

     š*---------------------------------------------------------------------
       P400-CLOSE-CURSOR-CURJCPRT.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJCPRT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P001            TO TRUE
           WHEN OTHER
              SET  FIM-P001            TO TRUE
              MOVE "CURJCPRT"          TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-idEnt.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

     š*- i1 - Residente  / i2 - Não Residente
           MOVE   "idEnt"                     TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE   "xsi:type"                  TO  WS-NOME-TAG
           MOVE   NEWCPRT-TYPER               TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE   "idEnt"                     TO  WS-NOME-TAG
           MOVE   NEWCPRT-IDENT               TO  WS-VAL-TAG-A

           PERFORM P300-END-TAG.

           MOVE   NEWCPRT-IDENT               TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.

           MOVE  "idEnt"                TO  WS-NOME-TAG
           MOVE  SPACES                 TO  WS-VAL-TAG-A
           PERFORM P300-CLOSE-TAG.

           MOVE XML-OUTPUT TO REG-BPCFCPRT
           PERFORM P300-BPCFCPRT-WRITE.

     š*---------------------------------------------------------------------
       P300-OPEN-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-ATRIBUTE.
     š*---------------------------------------------------------------------
     š*-Trata Valor Atributo
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF WS-STRING  NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-GERA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome da TAG
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L
             MOVE WS-STRING    TO WS-NOME-TAG

             STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "</"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-DATA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-DATA-TAG-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A( WS-POS-S: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-ATRIBUTE-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(WS-POS-S: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML

           END-IF.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING
                "</"
                WS-NOME-TAG(1: WS-NOM-TAG-L)
                ">"

           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-END-TAG.
     š*---------------------------------------------------------------------
           STRING ">"
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.
     š*---------------------------------------------------------------------
       P410-CLOSE-CURSOR-CURJCPRT1.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJCPRT1
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P002            TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJCPRT1"         TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-COMPRIMENTO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING
     š*- WY -Comprimento Final
           MOVE ZEROES TO WS-POS-I.
           INSPECT FUNCTION REVERSE(WS-STRING)
                   TALLYING WS-POS-I      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = LENGTH OF WS-STRING  -  WS-POS-I.
     š*---------------------------------------------------------------------
       P300-PRIMEIRO-ESPACO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING(1:WS-STRING-L)
           MOVE ZEROES TO WS-POS-S.
           INSPECT WS-STRING(1:WS-STRING-L)
                   TALLYING WS-POS-S      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = WS-STRING-L -  WS-POS-S.
           ADD 1 TO WS-POS-S.
     š*---------------------------------------------------------------------
       P300-RETIRA-CARAC-ESPECIAIS.
     š*---------------------------------------------------------------------
     š*-Retira Caracters Invalidos de WS-STRING
           PERFORM P300-UTLS001-TESTA
                   VARYING W-NUM FROM 1 BY 1
                   UNTIL W-NUM > LENGTH OF WS-STRING.

     š*---------------------------------------------------------------------
       P300-UTLS001-TESTA.
     š*---------------------------------------------------------------------
           MOVE WS-STRING-CHR(W-NUM)  TO CHAR-TESTE.
           IF   NOT CARACTER-VALIDO
                PERFORM  P300-UTLS001-ALTERA
           END-IF.

     š*---------------------------------------------------------------------
       P300-UTLS001-ALTERA.
     š*---------------------------------------------------------------------
           EVALUATE  TRUE

            WHEN WS-STRING-CHR(W-NUM) = "à" OR
                 WS-STRING-CHR(W-NUM) = "á" OR
                 WS-STRING-CHR(W-NUM) = "â" OR
                 WS-STRING-CHR(W-NUM) = "ã" OR
                 WS-STRING-CHR(W-NUM) = "ä" OR
                 WS-STRING-CHR(W-NUM) = "ª"
              MOVE "a" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ç"
              MOVE "c" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "é" OR
                 WS-STRING-CHR(W-NUM) = "è" OR
                 WS-STRING-CHR(W-NUM) = "ê" OR
                 WS-STRING-CHR(W-NUM) = "ë"
              MOVE "e" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "í" OR
                 WS-STRING-CHR(W-NUM) = "ì" OR
                 WS-STRING-CHR(W-NUM) = "ï"
              MOVE "i" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ñ"
              MOVE "n" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ó" OR
                 WS-STRING-CHR(W-NUM) = "ò" OR
                 WS-STRING-CHR(W-NUM) = "õ" OR
                 WS-STRING-CHR(W-NUM) = "ö"
              MOVE "o" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ú" OR
                 WS-STRING-CHR(W-NUM) = "ù" OR
                 WS-STRING-CHR(W-NUM) = "û" OR
                 WS-STRING-CHR(W-NUM) = "ü"
              MOVE "u" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Á" OR
                 WS-STRING-CHR(W-NUM) = "À" OR
                 WS-STRING-CHR(W-NUM) = "Ã" OR
                 WS-STRING-CHR(W-NUM) = "Â" OR
                 WS-STRING-CHR(W-NUM) = "Ä"
              MOVE "A" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ç"
              MOVE "C" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "É" OR
                 WS-STRING-CHR(W-NUM) = "È" OR
                 WS-STRING-CHR(W-NUM) = "Ê" OR
                 WS-STRING-CHR(W-NUM) = "Ë"
              MOVE "E" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Í" OR
                 WS-STRING-CHR(W-NUM) = "Ì" OR
                 WS-STRING-CHR(W-NUM) = "Ï"
              MOVE "I" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ñ"
              MOVE "N" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ó" OR
                 WS-STRING-CHR(W-NUM) = "Ò" OR
                 WS-STRING-CHR(W-NUM) = "Õ" OR
                 WS-STRING-CHR(W-NUM) = "Ô" OR
                 WS-STRING-CHR(W-NUM) = "Ö"
              MOVE "O" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ú" OR
                 WS-STRING-CHR(W-NUM) = "Ù" OR
                 WS-STRING-CHR(W-NUM) = "Û" OR
                 WS-STRING-CHR(W-NUM) = "Ü"
              MOVE "U" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "\"
              MOVE "/" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "[" OR
                 WS-STRING-CHR(W-NUM) = "{"
              MOVE "(" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "]" OR
                 WS-STRING-CHR(W-NUM) = "}"
              MOVE ")" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = """" OR
                 WS-STRING-CHR(W-NUM) = "«"  OR
                 WS-STRING-CHR(W-NUM) = "»"
              MOVE "'" TO WS-STRING-CHR(W-NUM)

            WHEN OTHER
              MOVE " " TO WS-STRING-CHR(W-NUM)

           END-EVALUATE.

     š*---------------------------------------------------------------------
       P990-ERRO-FS.
     š*---------------------------------------------------------------------
           EVALUATE TRUE
           WHEN GRS-READ-DUP-KEY
                  STRING "ERRO DE DADOS: CHAVE DUPLICADA NO FICHEIRO <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-FS
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ ERRO    <"
                      W-EDTFILC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-FS
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           SET  COM-ERRO        TO TRUE
           MOVE  1              TO ERRRTC   OF  KYCT900A
           MOVE W-FILE-CODE     TO ERRCOD   OF  KYCT900A
           MOVE MSG-ERRO-FS     TO ERRMSG   OF  KYCT900A
           MOVE "BPCPCPRTX"       TO ERRPRG   OF  KYCT900A
           MOVE "BPC"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE W-CMDEXEC       TO ERRCMD   OF  KYCT900A
           MOVE W-PARAGRAFO     TO ERRFLD   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           SET  COM-ERRO        TO TRUE
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BPCPCPRTX"      TO  ERRPRG OF KYCT900A.
           MOVE   WS-NURACI      TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "KYCT900A"       TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P999-FIMPGM.
     š*---------------------------------------------------------------------
           EXIT PROGRAM.
           STOP RUN.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BPCPCPRTX
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

