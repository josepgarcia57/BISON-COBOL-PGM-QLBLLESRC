      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP951.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP951
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE951"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE951
                  ASSIGN        TO WORKSTATION-CREE951-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE951
                  CONTROL-AREA  IS CA-CREE951.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE951
           LABEL RECORD OMITTED.
       01  CREE951-HDR.
           COPY DDS-CREE951HDR OF CREE951.
       01  CREE951-D01.
           COPY DDS-CREE951D01 OF CREE951.
       01  CREE951-FTR.
           COPY DDS-CREE951FTR OF CREE951.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE951                  PIC 99.
           88  CREE951-OK                          VALUE ZERO.

       01  CA-CREE951.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE951".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE951HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE951D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE951FTR".
           05  KV-IDAPLICA-HI          PIC 99      VALUE 25.
           05  KV-IDAPLICA-PC          PIC 99      VALUE 26.
           05  KV-IDAPLICA-PR          PIC 99      VALUE 27.
           05  KV-NMAPLICA-HI          PIC 99      VALUE 28.
           05  KV-NMAPLICA-PC          PIC 99      VALUE 29.
           05  KV-NMAPLICA-PR          PIC 99      VALUE 30.
           05  KV-DSMODEXP-ND          PIC 99      VALUE 31.
           05  KV-DTEXEONL-HI          PIC 99      VALUE 32.
           05  KV-DTEXEONL-PC          PIC 99      VALUE 33.
           05  KV-DTEXEONL-PR          PIC 99      VALUE 34.
           05  KV-DTEXEBTC-HI          PIC 99      VALUE 35.
           05  KV-DTEXEBTC-PC          PIC 99      VALUE 36.
           05  KV-DTEXEBTC-PR          PIC 99      VALUE 37.
           05  KV-DHINISIT-ND          PIC 99      VALUE 38.
           05  KV-DTINISIT-HI          PIC 99      VALUE 39.
           05  KV-DTINISIT-PC          PIC 99      VALUE 40.
           05  KV-DHINISIT-PR          PIC 99      VALUE 41.
           05  KV-HRINISIT-HI          PIC 99      VALUE 42.
           05  KV-HRINISIT-PC          PIC 99      VALUE 43.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 44.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 44.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB951.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE951, CA-CREE951
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE951-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE951

           IF  NOT CREE951-OK
               MOVE FS-CREE951      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB951-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE951-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE951-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE951-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB951-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB951-IDAPLICA-PR
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PR) TO TRUE
           END-IF
      *
           IF  CREB951-NMAPLICA-PR
               SET  CREB000-INDICDLG-ON (KV-NMAPLICA-PR) TO TRUE
           END-IF
      *
           IF  CREB951-DTEXEONL-PR
               SET  CREB000-INDICDLG-ON (KV-DTEXEONL-PR) TO TRUE
           END-IF
      *
           IF  CREB951-DTEXEBTC-PR
               SET  CREB000-INDICDLG-ON (KV-DTEXEBTC-PR) TO TRUE
           END-IF
      *
           IF  CREB951-DHINISIT-PR
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-PR) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN CREB951-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC) TO TRUE
           WHEN CREB951-NMAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-NMAPLICA-PC) TO TRUE
           WHEN CREB951-DTEXEONL-PC
               SET  CREB000-INDICDLG-ON (KV-DTEXEONL-PC) TO TRUE
           WHEN CREB951-DTEXEBTC-PC
               SET  CREB000-INDICDLG-ON (KV-DTEXEBTC-PC) TO TRUE
           WHEN CREB951-DTINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-PC) TO TRUE
           WHEN CREB951-HRINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-HRINISIT-PC) TO TRUE
           WHEN CREB951-IDAPLICA-UP
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC) TO TRUE
           WHEN CREB951-NMAPLICA-UP
               SET  CREB000-INDICDLG-ON (KV-NMAPLICA-PC) TO TRUE
           WHEN CREB951-DTEXEONL-UP
               SET  CREB000-INDICDLG-ON (KV-DTEXEONL-PC) TO TRUE
           WHEN CREB951-DTEXEBTC-UP
               SET  CREB000-INDICDLG-ON (KV-DTEXEONL-PC) TO TRUE
           WHEN CREB951-DHINISIT-UP
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-PC) TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK OR CREB000-MSGINFO
               CONTINUE
           WHEN CREB951-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-HI) TO TRUE
           WHEN CREB951-NMAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-NMAPLICA-HI) TO TRUE
           WHEN CREB951-DTEXEONL-PC
               SET  CREB000-INDICDLG-ON (KV-DTEXEONL-HI) TO TRUE
           WHEN CREB951-DTEXEBTC-PC
               SET  CREB000-INDICDLG-ON (KV-DTEXEBTC-HI) TO TRUE
           WHEN CREB951-DTINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-HI) TO TRUE
           WHEN CREB951-HRINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-HRINISIT-HI) TO TRUE
           END-EVALUATE
      *
           IF  CREB951-DSMODEXP-ND
               SET  CREB000-INDICDLG-ON (KV-DSMODEXP-ND) TO TRUE
           END-IF
      *
           IF  CREB951-DHINISIT-ND
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-PR) TO TRUE
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-ND) TO TRUE
           END-IF
      *
           IF  CREB951-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  CREB951-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE951-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE951HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE951HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE951HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE951HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE951HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE951HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE951HDR-O
           WRITE CREE951-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE951-OK
               MOVE FS-CREE951      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE951-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE951FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE951FTR-O
           WRITE CREE951-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE951-OK
               MOVE FS-CREE951      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO CREE951-D01
           MOVE CREB951-IDAPLICA-V  TO IDAPLICA OF CREE951D01-O
           MOVE CREB951-NMAPLICA-V  TO NMAPLICA OF CREE951D01-O
           MOVE CREB951-DSMODEXP-V  TO DSMODEXP OF CREE951D01-O
           MOVE CREB951-DTEXEONL-V  TO DTEXEONL OF CREE951D01-O
           MOVE CREB951-DTEXEBTC-V  TO DTEXEBTC OF CREE951D01-O
           MOVE CREB951-DSSITEXE-V  TO DSSITEXE OF CREE951D01-O
           MOVE CREB951-DHINISIT-V  TO WS-DTHR-N14
           MOVE WS-DATA-N08         TO DTINISIT OF CREE951D01-O
           MOVE WS-HORA-N06         TO HRINISIT OF CREE951D01-O
           MOVE CREB951-DHCRIREG-V  TO DHCRIREG OF CREE951D01-O
           MOVE CREB951-DHMDFREG-V  TO DHMDFREG OF CREE951D01-O
           WRITE CREE951-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE951-OK
               MOVE FS-CREE951      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE951
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE951-OK
               MOVE IDAPLICA OF CREE951D01-I TO CREB951-IDAPLICA-V
               MOVE NMAPLICA OF CREE951D01-I TO CREB951-NMAPLICA-V
               MOVE DTEXEONL OF CREE951D01-I TO CREB951-DTEXEONL-V
               MOVE DTEXEBTC OF CREE951D01-I TO CREB951-DTEXEBTC-V
               MOVE DTINISIT OF CREE951D01-I TO WS-DATA-N08
               MOVE HRINISIT OF CREE951D01-I TO WS-HORA-N06
               MOVE WS-DTHR-N14              TO CREB951-DHINISIT-V
               MOVE IDVARCSR OF CREE951D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE951   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE951               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE951

           IF  NOT CREE951-OK
               MOVE FS-CREE951      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP951 ------------------------------------*
