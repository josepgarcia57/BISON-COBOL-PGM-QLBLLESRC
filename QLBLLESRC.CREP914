      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP914.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP914
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP914"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-NRMNUPRC         PIC 99.
               10  WS-NVSBMNU1         PIC 99.
               10  WS-NVSBMNU2         PIC 99.
               10  FILLER              PIC X(71).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).
           05  WS-IDREGSEL.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-NRMNUPRC         PIC 99.
               10  WS-NVSBMNU1         PIC 99.
               10  WS-NVSBMNU2         PIC 99.
               10  WS-NROPCMNU         PIC 99.
               10  FILLER              PIC X(69).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).

           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.
           05  WS-COUNTER1             PIC S9(4)       BINARY.

           05  CS-QMNUPRC              PIC X.
               88  QMNUPRC-CLSE                    VALUE "C".
               88  QMNUPRC-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB964.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET906 OF CRET906.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDAPLICA         PIC X(03).
               10  DB-NRMNUPRC         PIC S9(2)       COMP-3.
               10  DB-NVSUBMNU         PIC S9(4)       COMP-3.
           05  DB-IDREGLST.
               10  DB-NROPCMNU         PIC S9(2)       COMP-3.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QMNUPRC CURSOR FOR
                    SELECT NROPCMNU
                         , IDPRCAPL
                         , NMSUBMNU
                      FROM CRET906
                     WHERE IDAPLICA = :DB-PARMSINP.DB-IDAPLICA
                       AND NRMNUPRC = :DB-PARMSINP.DB-NRMNUPRC
                       AND NVSUBMNU = :DB-PARMSINP.DB-NVSUBMNU
                       AND NROPCMNU > :DB-IDREGLST.DB-NROPCMNU
                     ORDER BY 1
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-PARMSINP     TO WS-PARMSINP
           IF  CREB000-MSGDLG-P > ZERO
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB964-MSGDIALG
           END-IF
           IF  CREB000-IDREGLST-MIN
               INITIALIZE DB-IDREGLST
           ELSE
               MOVE CREB000-IDREGLST TO DB-IDREGLST
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB964-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCSEL
               PERFORM P2600-PREP-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2700-PREP-NOVOREG
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB964-MSGDIALG, WS-PARMSINP
           SET  CREB000-IDREG-NF TO TRUE
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB964-MSGDIALG
           MOVE WS-IDAPLICA OF WS-PARMSINP TO CREB964-IDAPLICA-V
           MOVE WS-NRMNUPRC OF WS-PARMSINP TO CREB964-NRMNUPRC-V
           MOVE WS-NVSBMNU1 OF WS-PARMSINP TO CREB964-NVSBMNU1-V
           MOVE WS-NVSBMNU2 OF WS-PARMSINP TO CREB964-NVSBMNU2-V
           SET  CREB000-IDREG-NF           TO TRUE
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDAPLICA"
               MOVE CREB964-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB964-IDAPLICA-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB964-IDAPLICA-V
               SET  CREB964-IDAPLICA-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB964-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB964-IDAPLICA-V = SPACES
               SET  CREB964-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB964-IDAPLICA-V = CREB950-MCPESQGN
               SET  CREB964-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB964-NRMNUPRC-V = ZERO
               SET  CREB964-NRMNUPRC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB964-NVSBMNU1-V > CREB950-OPCMNMAX
               SET  CREB964-NVSBMNU1-PC    TO TRUE
               SET  CREB950-VCAMPO-SUPMAXI TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB964-NVSBMNU2-V > CREB950-OPCMNMAX
               SET  CREB964-NVSBMNU2-PC    TO TRUE
               SET  CREB950-VCAMPO-SUPMAXI TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           MOVE CREB964-IDAPLICA-V         TO IDAPLICA OF CRET901
           PERFORM P9000-READ-CRET901

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE NMAPLICA OF CRET901    TO CREB964-NMAPLICA-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB964-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE CREB964-IDAPLICA-V         TO IDAPLICA OF CRET906
               MOVE CREB964-NRMNUPRC-V         TO NRMNUPRC OF CRET906
               MOVE CREB964-NVSBMNU1-V         TO NVSUBMNU OF CRET906
               MOVE CREB964-NVSBMNU2-V         TO NROPCMNU OF CRET906
               PERFORM P9000-READ-CRET906

               EVALUATE TRUE
               WHEN CREB000-PROC-ERR
                   CONTINUE
               WHEN SQLCODE-OK
               AND  IDPRCAPL OF CRET906 = SPACES
                   MOVE NMSUBMNU OF CRET906    TO CREB964-NMSUBMNU-V
               WHEN OTHER
                   SET  CREB964-NVSBMNU1-PC    TO TRUE
                   SET  CREB950-MNUPRC-NEXISTE TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB964-IDAPLICA-V TO WS-IDAPLICA OF WS-PARMSINP
           MOVE CREB964-NRMNUPRC-V TO WS-NRMNUPRC OF WS-PARMSINP
           MOVE CREB964-NVSBMNU1-V TO WS-NVSBMNU1 OF WS-PARMSINP
           MOVE CREB964-NVSBMNU2-V TO WS-NVSBMNU2 OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           INITIALIZE DB-PARMSINP, CRET906, CREB964-VARSDET
           MOVE CREB964-IDAPLICA-V TO DB-IDAPLICA OF DB-PARMSINP
           MOVE CREB964-NRMNUPRC-V TO DB-NRMNUPRC OF DB-PARMSINP
           COMPUTE DB-NVSUBMNU OF DB-PARMSINP
                 = CREB964-NVSBMNU1-V * 100 + CREB964-NVSBMNU2-V
           SET  QMNUPRC-CLSE       TO TRUE
           PERFORM P2441-CONT-QMNUPRC

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-NRENTLST = ZERO
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               PERFORM P2442-OPEN-QMNUPRC
           END-EVALUATE

           IF  QMNUPRC-OPEN
               PERFORM P2443-FTCH-QMNUPRC

               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB964-NRSELLIN-MAX
                 OR    CREB000-IDREGLST-MAX
                 OR    CREB000-PROC-ERR
                   PERFORM P2444-LOAD-LSTSEL

                   IF  CREB000-PROC-OK OR CREB000-MSGINFO
                       PERFORM P2443-FTCH-QMNUPRC
                   END-IF
               END-PERFORM

               PERFORM P2445-CLSE-QMNUPRC
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND NOT CREB000-IDREGLST-MAX
               MOVE DB-IDREGLST TO CREB000-IDREGLST
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QMNUPRC          SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET906
                     WHERE IDAPLICA = :DB-PARMSINP.DB-IDAPLICA
                       AND NRMNUPRC = :DB-PARMSINP.DB-NRMNUPRC
                       AND NVSUBMNU = :DB-PARMSINP.DB-NVSUBMNU
                       AND NROPCMNU > 0
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               MOVE DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP914.QMNUPRC" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2442-OPEN-QMNUPRC          SECTION.

           EXEC SQL OPEN  QMNUPRC                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QMNUPRC-OPEN      TO TRUE
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP914.QMNUPRC" TO CREB000-NMOBJECT
               SET  CREB000-OPEN-CS   TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2442-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2443-FTCH-QMNUPRC          SECTION.

           EXEC SQL FETCH QMNUPRC
                     INTO :CRET906.NROPCMNU
                        , :CRET906.IDPRCAPL
                        , :CRET906.NMSUBMNU                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK     TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE      TO CREB000-CDRETORN
               MOVE "CREP914.QMNUPRC"    TO CREB000-NMOBJECT
               SET  CREB000-FTCH-CS      TO TRUE
               SET  CREB000-MSGSGBD      TO TRUE
           END-EVALUATE
           .
       P2443-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2444-LOAD-LSTSEL           SECTION.

           MOVE NROPCMNU OF CRET906 TO CREB964-OPCMNLIN-V(WS-ARRYNDX1)
                                       DB-NROPCMNU OF DB-IDREGLST
           MOVE IDPRCAPL OF CRET906 TO CREB964-IDPRCLIN-V(WS-ARRYNDX1)
           IF  IDPRCAPL OF CRET906 = SPACES
               MOVE NMSUBMNU OF CRET906
                                    TO CREB964-NMPRCLIN-V(WS-ARRYNDX1)
           ELSE
               PERFORM P9000-READ-CRET903

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   MOVE DSPRCAPL OF CRET903
                                    TO CREB964-NMPRCLIN-V(WS-ARRYNDX1)
                   MOVE IDDIALOG OF CRET903
                                    TO CREB964-IDDLGLIN-V(WS-ARRYNDX1)
                   MOVE IDTIPDLG OF CRET903
                                    TO CREB964-TPDLGLIN-V(WS-ARRYNDX1)
                   MOVE NVAUTMIN OF CRET903
                                    TO CREB964-NVEXELIN-V(WS-ARRYNDX1)
                   MOVE MCSITEXE OF CRET903
                                    TO CREB964-STEXELIN-V(WS-ARRYNDX1)
               END-IF
           END-IF
           .
       P2444-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2445-CLSE-QMNUPRC          SECTION.

           EXEC SQL CLOSE QMNUPRC                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP914.QMNUPRC"      TO CREB000-NMOBJECT
               SET  CREB000-CLSE-CS        TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P2445-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB964-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB964-NRMNUPRC-V   TO CREB999-NRMNUPRC
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PREP-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-IDREGSEL = SPACES
               PERFORM P2610-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2620-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2630-FRMT-IDREGSEL
               END-IF
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2640-FRMT-IDPRCDST
           END-IF
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           INITIALIZE CREB000-NRLINSMC
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB964-NRSELLIN-MAX
             OR    CREB000-PROC-ERR
               MOVE CREB964-NROPCLIN-V(WS-ARRYNDX1) TO WS-ARRYNDX2
               EVALUATE TRUE
               WHEN WS-ARRYNDX2 = ZERO
                   CONTINUE
               WHEN CREB000-INDICOPC-ON(WS-ARRYNDX2)
                   ADD  1 TO CREB000-NRLINSMC
               WHEN OTHER
                   COMPUTE CREB964-PCURSOR = CREB964-NRPARAMS-INP
                                           + WS-ARRYNDX1
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-PERFORM

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-NRLINSMC = ZERO
               SET  CREB964-NROPCL01-PC        TO TRUE
               SET  CREB950-SELECC-NINFORM     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL CREB964-NROPCLIN-V(WS-ARRYNDX1) > ZERO
               CONTINUE
           END-PERFORM
           MOVE CREB964-NROPCLIN-V(WS-ARRYNDX1) TO CREB000-IDOPCSEL
           COMPUTE CREB964-PCURSOR = CREB964-NRPARAMS-INP + WS-ARRYNDX1
           INITIALIZE CREB964-NROPCLIN-V(WS-ARRYNDX1)
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-IDREGSEL         SECTION.

           MOVE CREB964-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           MOVE CREB964-NRMNUPRC-V TO WS-NRMNUPRC OF WS-IDREGSEL
           MOVE CREB964-NVSBMNU1-V TO WS-NVSBMNU1 OF WS-IDREGSEL
           MOVE CREB964-NVSBMNU2-V TO WS-NVSBMNU2 OF WS-IDREGSEL
           MOVE CREB964-OPCMNLIN-V(WS-ARRYNDX1)
                                   TO WS-NROPCMNU OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2630-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2640-FRMT-IDPRCDST         SECTION.

           CONTINUE
           .
       P2640-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PREP-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB964-VARSOUT
           PERFORM P2710-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2720-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2730-FRMT-IDREGSEL
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           PERFORM P2410-VRIF-NIVEL01
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           PERFORM P2420-VRIF-NIVEL02
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL
           MOVE CREB964-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           MOVE CREB964-NRMNUPRC-V TO WS-NRMNUPRC OF WS-IDREGSEL
           MOVE CREB964-NVSBMNU1-V TO WS-NVSBMNU1 OF WS-IDREGSEL
           MOVE CREB964-NVSBMNU2-V TO WS-NVSBMNU2 OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN OTHER
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB964-MSGDIALG TO CREB000-MSGDLG-L
           END-EVALUATE
           MOVE CREB964-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)

           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB964-IDAPLICA-UP TO TRUE
           SET  CREB964-NRMNUPRC-UP TO TRUE
           SET  CREB964-NVSBMNU1-UP TO TRUE
           SET  CREB964-NVSBMNU2-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB964-IDAPLICA-PR TO TRUE
           SET  CREB964-NRMNUPRC-PR TO TRUE
           SET  CREB964-NVSBMNU1-PR TO TRUE
           SET  CREB964-NVSBMNU2-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB964-NRSELLIN-MAX
             OR    CREB964-OPCMNLIN-V(WS-ARRYNDX1) = ZEROS
               SET  CREB964-NROPCLIN-UP(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB964-NRSELLIN-MAX
               SET  CREB964-NROPCLIN-PR(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO NMAPLICA OF CRET901
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP914.CRET901" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET906          SECTION.

           EXEC SQL SELECT IDPRCAPL
                         , NMSUBMNU
                      INTO :CRET906.IDPRCAPL
                         , :CRET906.NMSUBMNU
                      FROM CRET906
                     WHERE IDAPLICA = :CRET906.IDAPLICA
                       AND NRMNUPRC = :CRET906.NRMNUPRC
                       AND NVSUBMNU = :CRET906.NVSUBMNU
                       AND NROPCMNU = :CRET906.NROPCMNU        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO NMSUBMNU OF CRET906
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP914.CRET906" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903          SECTION.

           MOVE IDPRCAPL OF CRET906   TO IDPRCAPL OF CRET903
           EXEC SQL SELECT DSPRCAPL
                         , IDTIPDLG
                         , IDDIALOG
                         , NVAUTMIN
                         , MCSITEXE
                      INTO :CRET903.DSPRCAPL
                         , :CRET903.IDTIPDLG
                         , :CRET903.IDDIALOG
                         , :CRET903.NVAUTMIN
                         , :CRET903.MCSITEXE
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSPRCAPL OF CRET903
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP914.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP914 ------------------------------------*
