       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BPCPPRINX   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BPC - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este programa destina-se a Gerar o XML     *
     š*                     a enviar ao Banco de Portugal             *
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *------------------------------------------------------------------------*
       FILE-CONTROL.
      *------------------------------------------------------------------------*
           SELECT BPCFPRIN
                  ASSIGN TO DATABASE-BPCFPRIN
                  ORGANIZATION IS SEQUENTIAL
                  ACCESS MODE  IS SEQUENTIAL
                  FILE STATUS  IS GRS-FILE-STATUS.
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
       FD  BPCFPRIN.
       01  REG-BPCFPRIN.
           COPY DDS-ALL-FORMATS OF BPCFPRIN.
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 GRS-FILE-STATUS            PIC XX.

       01 EXECUTA-P001               PIC 9(01).
          88 INICIO-P001             VALUE 0.
          88 FIM-P001                VALUE 1.

       01 EXECUTA-P002               PIC 9(01).
          88 INICIO-P002             VALUE 0.
          88 FIM-P002                VALUE 1.

       01 EXECUTA-P003               PIC 9(01).
          88 INICIO-P003             VALUE 0.
          88 FIM-P003                VALUE 1.
      *
       01 CICLO-SEQ                  PIC 9(01).
          88 CICLO-1                 VALUE 0.
          88 CICLO-N                 VALUE 1.

       01 CICLO-Prt                  PIC 9(01).
          88 CICLO-Prt-1             VALUE 0.
          88 CICLO-Prt-N             VALUE 1.

       01 CICLO-Rin                  PIC 9(01).
          88 CICLO-Rin-1             VALUE 0.
          88 CICLO-Rin-N             VALUE 1.
      *
       01 TRATA-INFO                 PIC 9(01).
          88 SIM-TRATA               VALUE 1.
          88 NAO-TRATA               VALUE 0.

       01 PGM-STATUS                 PIC 9(01).
          88 SEM-ERRO                VALUE 0.
          88 COM-ERRO                VALUE 1.

       01 NR-ADM                     PIC 9(06) VALUE ZEROS.
       01 NR-READ                    PIC 9(06) VALUE ZEROS.
       01 NR-PROC                    PIC 9(06) VALUE ZEROS.
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(9)    VALUE "BPCPPRINX".

       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA         PIC 9(04).
           03  FILLER               PIC X VALUE "-".
           03  WS-MESIN-DIA         PIC 9(02).
           03  FILLER               PIC X VALUE "-".
           03  WS-DIAIN-DIA         PIC 9(02).
     š* ------------------------------------------------------
     š* Linha com XML gerado HardCode pelo programa
       01 XML-OUTPUT       PIC X(1000).

       01 Ws-dtCriacao.
          05 ws-Data         PIC X(10).
          05 ws-SepT         PIC X(1).
          05 ws-HH           PIC X(2).
          05 ws-SepH         PIC X(1).
          05 ws-MM           PIC X(2).
          05 ws-SepM         PIC X(1).
          05 ws-SS           PIC X(2).

     š* ------------------------------------------------------
       01 WS-STRING.
          05 WS-STRING-CHR         PIC X           OCCURS 300.
       01 WS-VAL-TAG-A             PIC X(80).
       01 WS-NOME-TAG              PIC X(80).
       01 WS-VAL-TAG-L             PIC S9(4)       COMP   VALUE 0.
       01 WS-NOM-TAG-L             PIC S9(4)       COMP   VALUE 0.
     š* ------------------------------------------------------
     š* Determina Comprimento de uma String
       01 WS-POS-I                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-S                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-XML               PIC S9(4)       COMP   VALUE 0.
       01 WS-STRING-L              PIC S9(4)       COMP   VALUE 0.
       01 W-NUM                    PIC 9(3).
       01 Edit-Nbr-Tx PIC      -----9,99999.
       01 Edit-Nbr-Mn PIC      --------9,99.

     š* Campos de Caracteres Invalidos
        01 CHAR-TESTE                PIC X.
           88 CARACTER-VALIDO      VALUE
      *                             - letras minusculas
                                          "a", "b", "c", "d", "e",
                                          "f", "g", "h", "i", "j",
                                          "k", "l", "m", "n", "o",
                                          "p", "q", "r", "s", "t",
                                          "u", "v", "w", "x", "y",
                                          "z",
      *                             - letras maiusculas
                                          "A", "B", "C", "D", "E",
                                          "F", "G", "H", "I", "J",
                                          "K", "L", "M", "N", "O",
                                          "P", "Q", "R", "S", "T",
                                          "U", "V", "W", "X", "Y",
                                          "Z",
      *                             - numeros
                                          "0", "1", "2", "3", "4",
                                          "5", "6", "7", "8", "9",
      *                             - outros
                                          " ", "/", "-", "?", ":",
                                          "(", ")", ".", ",",
                                          "+".

      * "'" passou a ser considerado invalido

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
     š* Criação de Ficheiro de Destino
       01 WS-BIBORIG               PIC X(10).
       01 WS-FICHORIG              PIC X(10).
       01 WS-BIBDEST               PIC X(10).
       01 WS-FICHDEST              PIC X(10).
       01 WS-ERRO                  PIC X(10).
     š* Área de Ligação para o FTP
       01 BPCL001M-FICH-ORIGEM    PIC  X(15).
       01 BPCL001M-FICH-DESTINO   PIC  X(50).
       01 BPCL001M-ERRO-BPCPFTP   PIC  X(01).
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".

     š* Copy : Tabela de Exposições de Risco
           EXEC SQL
               INCLUDE BPCHPRIN
           END-EXEC.

     š* Copy : Tabela de Protecções de Exposição
           EXEC SQL
               INCLUDE BPCHPRIN1
           END-EXEC.

     š* Copy : Tabela XMP BP - PRIN - Comunicação de Exposições
       01  R-BPCFPRIN.
           COPY DDS-ALL-FORMATS OF BPCFPRIN.

     š* Copy : Tabela de Controlo
       01  R-JCNTL.
           COPY DDS-ALL-FORMATS OF BPCJCNTL.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.
     š* Contrato
       01 WS-IDCONT                PIC X(32).
     š* Instrumento de Crédito
       01 WS-IDINST                PIC X(32).

     š* Cursor Principal / Contratos Instrumentos com Exposições
           EXEC SQL DECLARE CURJPR CURSOR FOR
            SELECT    DTREF
                   ,  infRInst
                   ,  IDCONT
                   ,  IDINST
                FROM  BPCJPRIN A
                WHERE BPENV = "E"
                 AND  IDEXP <> " "
                GROUP BY DTREF
                       , infRInst
                       , IDCONT
                       , IDINST
           END-EXEC.

     š* Cursor Principal / Exposções
           EXEC SQL DECLARE CURJPRIN CURSOR FOR
            SELECT    DTREF
                   ,  BPENV
                   ,  infRInst
                   ,  IDCONT
                   ,  IDINST
                   ,  IDEXP
                   ,  TPEXP
                   ,  CLASEXPCRR
                   ,  METCALCFPR
                   ,  VAJUSTCOLF
                   ,  MPONDEXPR
                   ,  RISCOPOND
                   ,  LGDPERECN
                   ,  LGDREC
                   ,  VALEXP
                   ,  PREFCONV
                   ,  MONTPERESP
                   ,  EXPAJPME
                   ,  FCONVCRED
                   ,  TMSCRIA
                   ,  USERCRIA
                   ,  TMSALT
                   ,  USERALT
                FROM  BPCJPRIN A
                WHERE BPENV = "E"
                  AND IDCONT = :WS-IDCONT
                  AND IDINST = :WS-IDINST
           END-EXEC.

     š* Cursor Principal / Protecções das Exposições
           EXEC SQL DECLARE CURJPRIN1 CURSOR FOR
            SELECT    IDCONT
                   ,  IDINST
                   ,  IDEXP
                   ,  IDPROT
                FROM  BPCJPRIN1 A
               WHERE IDCONT = :WS-IDCONT
                 AND IDINST = :WS-IDINST
                 AND IDEXP  = :PRIN-IDEXP
           END-EXEC.

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       PROCEDURE DIVISION.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P002-INICIO.

           PERFORM P002-REG-CONTROLO.

           PERFORM P100-OPEN-CURSOR-CURJPR.
           PERFORM P200-FETCH-CURSOR-CURJPR.
           PERFORM P300-PROCESSO-GERAL
                           UNTIL FIM-P001
           PERFORM P400-CLOSE-CURSOR-CURJPR
           IF NR-PROC NOT EQUAL ZERO
             PERFORM P300-CLOSE-TAG-comInfInst
             PERFORM P300-CLOSE-TAG-conteudo
             PERFORM P300-CLOSE-TAG-crc
             PERFORM P400-BPCFPRIN-CLOSE
             PERFORM P450-FTP-FILE
           END-IF
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P002-INICIO.
     š*---------------------------------------------------------------------
           SET  INICIO-P001         TO  TRUE.
           SET  CICLO-1             TO  TRUE.
           EXEC SQL
              DELETE FROM BPCFPRIN
           END-EXEC.

           MOVE 1 TO WS-POS-XML
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.
     š*---------------------------------------------------------------------
       P002-REG-CONTROLO.
     š*---------------------------------------------------------------------
           INITIALIZE  BPCJCNTL
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           PERFORM P300-GET-BPCJCNTL.
     š*---------------------------------------------------------------------
       P300-GET-BPCJCNTL.
     š*---------------------------------------------------------------------
     š*- Último Envio
           SET SIM-TRATA TO TRUE

           EXEC SQL
             SELECT     X.DTREF
                     ,  X.REPTYPE
                     ,  X.ESTADO
                     ,  X.ENTOBS
                     ,  X.ENTREPORT
                     ,  X.IDDEST
                     ,  X.DTCRIACAO
                     ,  X.NOMEFICH
                     ,  X.NREC
                     ,  X.NREJ
                     ,  X.NINT
                     ,  X.NALERTA
                     ,  X.TMSCRIA
                     ,  X.TMSRET
             INTO      :BPCJCNTL.DTREF
                     , :BPCJCNTL.REPTYPE
                     , :BPCJCNTL.ESTADO
                     , :BPCJCNTL.ENTOBS
                     , :BPCJCNTL.ENTREPORT
                     , :BPCJCNTL.IDDEST
                     , :BPCJCNTL.DTCRIACAO
                     , :BPCJCNTL.NOMEFICH
                     , :BPCJCNTL.NREC
                     , :BPCJCNTL.NREJ
                     , :BPCJCNTL.NINT
                     , :BPCJCNTL.NALERTA
                     , :BPCJCNTL.TMSCRIA
                     , :BPCJCNTL.TMSRET
             FROM  BPCJCNTL  X
                WHERE TMSCRIA = ( SELECT MAX(TMSCRIA)
                                  FROM BPCJCNTL Y
                                  WHERE REPTYPE = "PRIN" )
                AND   REPTYPE = "PRIN"
                AND   ESTADO  IN( "ENV" , "RET" )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
              IF ESTADO OF BPCJCNTL = "RET"
                 SET NAO-TRATA TO TRUE
              END-IF
              MOVE S-SYSTMST(1:10) TO WS-DATA-DIA
              MOVE DTCRIACAO OF BPCJCNTL TO Ws-dtCriacao
           WHEN OTHER
              MOVE "BPCPPRINX"       TO W-OBJECT-NAME
              SET CMD-SELECT         TO TRUE
              PERFORM  P990-ERRO-DB2
              SET NAO-TRATA TO TRUE
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURJPR.
     š*---------------------------------------------------------------------
           INITIALIZE  PRIN-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURJPR
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P001         TO TRUE
           WHEN OTHER
              SET  FIM-P001            TO TRUE
              MOVE "CURJPR"            TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P200-FETCH-CURSOR-CURJPR.
     š*---------------------------------------------------------------------

           EXEC SQL FETCH CURJPR
                     INTO  :PRIN-DTREF
                         , :PRIN-infRInst
                         , :PRIN-IDCONT
                         , :PRIN-IDINST
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
             MOVE PRIN-IDCONT        TO WS-IDCONT
             MOVE PRIN-IDINST        TO WS-IDINST
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P001          TO TRUE
           WHEN OTHER
              SET   FIM-P001         TO TRUE
              MOVE "CURJPR"          TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURJPRIN.
     š*---------------------------------------------------------------------
           INITIALIZE  PRIN-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           EXEC SQL
                OPEN CURJPRIN
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P002         TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJPRIN"          TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P200-FETCH-CURSOR-CURJPRIN.
     š*---------------------------------------------------------------------

           EXEC SQL FETCH CURJPRIN
                     INTO  :PRIN-DTREF
                         , :PRIN-BPENV
                         , :PRIN-infRInst
                         , :PRIN-IDCONT
                         , :PRIN-IDINST
                         , :PRIN-IDEXP
                         , :PRIN-TPEXP
                         , :PRIN-CLASEXPCRR
                         , :PRIN-METCALCFPR
                         , :PRIN-VAJUSTCOLF
                         , :PRIN-MPONDEXPR
                         , :PRIN-RISCOPOND
                         , :PRIN-LGDPERECN
                         , :PRIN-LGDREC
                         , :PRIN-VALEXP
                         , :PRIN-PREFCONV
                         , :PRIN-MONTPERESP
                         , :PRIN-EXPAJPME
                         , :PRIN-FCONVCRED
                         , :PRIN-TMSCRIA
                         , :PRIN-USERCRIA
                         , :PRIN-TMSALT
                         , :PRIN-USERALT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P002          TO TRUE
           WHEN OTHER
              SET   FIM-P002         TO TRUE
              MOVE "CURJPRIN"        TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURJPRIN1.
     š*---------------------------------------------------------------------
           INITIALIZE  PRIN1-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURJPRIN1
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P003         TO TRUE
           WHEN OTHER
              SET  FIM-P003            TO TRUE
              MOVE "CURJPRIN1"         TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P200-FETCH-CURSOR-CURJPRIN1.
     š*---------------------------------------------------------------------

           EXEC SQL FETCH CURJPRIN1
                     INTO  :PRIN1-IDCONT
                         , :PRIN1-IDINST
                         , :PRIN1-IDEXP
                         , :PRIN1-IDPROT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P003          TO TRUE
           WHEN OTHER
              SET   FIM-P003         TO TRUE
              MOVE "CURJPRIN1"       TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-PROCESSO-GERAL.
     š*---------------------------------------------------------------------
           IF CICLO-1
              PERFORM P300-BPCFPRIN-OPEN
              IF SEM-ERRO
                INITIALIZE REG-BPCFPRIN  XML-OUTPUT
                PERFORM P300-OPEN-TAG-crc
                PERFORM P300-GERA-TAG-controlo
                PERFORM P300-OPEN-TAG-conteudo
                PERFORM P300-OPEN-TAG-comInfInst
                SET CICLO-N TO TRUE
              END-IF
           END-IF
           IF SEM-ERRO
              PERFORM P300-OPEN-TAG-infPerInst
     š*- Gera lstInfRInst
              PERFORM P300-PROCESSO-infRInst
     š*-Instrumento Seguinte                                           -----
              PERFORM P200-FETCH-CURSOR-CURJPR
           END-IF.
           IF CICLO-RIn-N
             PERFORM P300-CLOSE-TAG-infRInst
             PERFORM P300-CLOSE-TAG-lstinfRInst
           END-IF
           PERFORM P300-CLOSE-TAG-infPerInst.
     š*---------------------------------------------------------------------
       P300-PROCESSO-infRInst.
     š*---------------------------------------------------------------------
           SET CICLO-RIn-1 TO TRUE
           PERFORM P100-OPEN-CURSOR-CURJPRIN
           PERFORM P200-FETCH-CURSOR-CURJPRIN
           PERFORM P300-GERA-TAG-infRInst
                           UNTIL FIM-P002
           PERFORM P400-CLOSE-CURSOR-CURJPRIN
           PERFORM P300-PROCESSO-protExp.
     š*---------------------------------------------------------------------
       P300-PROCESSO-protExp.
     š*---------------------------------------------------------------------
           SET CICLO-Prt-1 TO TRUE
           PERFORM P100-OPEN-CURSOR-CURJPRIN1
           PERFORM P200-FETCH-CURSOR-CURJPRIN1
           PERFORM P300-GERA-TAG-protExp
                           UNTIL FIM-P003
           PERFORM P400-CLOSE-CURSOR-CURJPRIN1
           IF CICLO-Prt-N
             PERFORM P300-CLOSE-TAG-protExp
           END-IF.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-crc.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT
     š*-XML Doc Declaration
           MOVE "<?xml version='1.0' encoding='ISO-8859-1'?>"
                       TO XML-OUTPUT.

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*-CRC Tag Declaration
           MOVE "<crc versao='1.0' >"    TO XML-OUTPUT

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-controlo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "controlo"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "entObserv"             TO  WS-NOME-TAG
           MOVE   ENTOBS     OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "entReport"             TO  WS-NOME-TAG
           MOVE  ENTREPORT   OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtCriacao"             TO  WS-NOME-TAG
           MOVE   DTCRIACAO  OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idDest"                TO  WS-NOME-TAG
           MOVE   IDDEST    OF  BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idFichRelac"           TO  WS-NOME-TAG
           MOVE   spaces                 TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           PERFORM P300-END-TAG

           MOVE  "controlo"              TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-infPerInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT
     š*- Type - EI-Insert/EU-Update/ED-Delete

           MOVE  "infPerInst"           TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "idCont"                TO  WS-NOME-TAG
           MOVE   PRIN-IDCONT            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idInst"                TO  WS-NOME-TAG
           MOVE   PRIN-IDInst            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-infRInst.
     š*---------------------------------------------------------------------
           IF CICLO-RIn-1
              SET CICLO-RIn-N TO TRUE
              PERFORM P300-OPEN-TAG-lstInfRInst
           END-IF

           INITIALIZE XML-OUTPUT
     š*- Type - EI-Insert/EU-Update/ED-Delete

           MOVE  "infRInst"             TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "xsi:type"             TO  WS-NOME-TAG
           MOVE   PRIN-infRInst         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idExp"                TO  WS-NOME-TAG
           MOVE   PRIN-IDEXP            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE.

           MOVE  "tpExp"                TO  WS-NOME-TAG
           MOVE   PRIN-tpExp            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE.

           MOVE  "classExpCRR"          TO  WS-NOME-TAG
           MOVE   PRIN-CLASEXPCRR       TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE.

           MOVE  "metCalcFinsPrud"      TO  WS-NOME-TAG
           MOVE   PRIN-METCALCFPR       TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE.

           MOVE  "valAjustColFin"       TO  WS-NOME-TAG
           MOVE   PRIN-VAJUSTCOLF       TO  Edit-Nbr-Mn
           MOVE   Edit-Nbr-Mn           TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "montPondExpRisco"     TO  WS-NOME-TAG
           MOVE   PRIN-MPONDEXPR        TO  Edit-Nbr-Mn
           MOVE   Edit-Nbr-Mn           TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "riscoPond"            TO  WS-NOME-TAG
           MOVE   PRIN-riscoPond        TO  Edit-Nbr-Tx
           MOVE   Edit-Nbr-Tx           TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "LGDPerEcN"            TO  WS-NOME-TAG
           MOVE   PRIN-LGDPerEcN        TO  Edit-Nbr-Tx
           MOVE   Edit-Nbr-Tx           TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "LGDRec"               TO  WS-NOME-TAG
           MOVE   PRIN-LGDRec           TO  Edit-Nbr-Tx
           MOVE   Edit-Nbr-Tx           TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "valExp"               TO  WS-NOME-TAG
           MOVE   PRIN-valExp           TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "preFConv"             TO  WS-NOME-TAG
           MOVE   PRIN-PREFCONV         TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "montPerEsp"           TO  WS-NOME-TAG
           MOVE   PRIN-montPerEsp       TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "expPMERedRC"          TO  WS-NOME-TAG
           MOVE   PRIN-EXPAJPME         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE.

           MOVE  "fConvCred"            TO  WS-NOME-TAG
           MOVE   PRIN-fConvCred        TO  Edit-Nbr-Tx
           MOVE  Edit-Nbr-Tx            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE   "infRInst"            TO  WS-NOME-TAG
           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
           PERFORM P200-FETCH-CURSOR-CURJPRIN.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-lstInfRInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstInfRInst"           TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.

     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-InfRInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE   "infRInst"            TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG.

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-lstInfRInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstInfRInst"                 TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-protExp.
     š*---------------------------------------------------------------------
           IF CICLO-Prt-1
              SET CICLO-Prt-N TO TRUE
              PERFORM P300-OPEN-TAG-lstProtExp
           END-IF

           INITIALIZE XML-OUTPUT

           MOVE  "protExp"                TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "idProt"                 TO  WS-NOME-TAG
           MOVE   PRIN1-idProt            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE   "protExp"               TO  WS-NOME-TAG
           PERFORM P300-END-TAG

           MOVE   "protExp"               TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
           PERFORM P200-FETCH-CURSOR-CURJPRIN1.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-lstProtExp.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstProtExp"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG
           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-protExp.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstProtExp"                  TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-infPerInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "infPerInst"                 TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-ComInfInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "comInfInst"                TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-conteudo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "conteudo"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-comInfInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT.

           MOVE  "comInfInst"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "xmlns:xsi"         TO  WS-NOME-TAG
           MOVE  "http://www.w3.org/2001/XMLSchema-instance"
                                     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "xsi:noNamespaceSchemaLocation"
                                     TO  WS-NOME-TAG
           MOVE  "file:/C:/crc_bdp_schema.xsd"
                                     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtRef"             TO  WS-NOME-TAG
           MOVE   PRIN-DTREF         TO  WS-VAL-TAG-A
           IF PRIN-DTREF     NOT EQUAL "0001-01-01" AND
              PRIN-DTREF     NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-conteudo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "conteudo"              TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-crc.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "crc"                   TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFPRIN
           PERFORM P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
       P300-BPCFPRIN-OPEN.
     š*---------------------------------------------------------------------
           OPEN OUTPUT BPCFPRIN
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-OPENED
               CONTINUE
           ELSE
               SET  FIM-P001             TO TRUE
               MOVE "BPCPPRINX"            TO W-PROGRAMA
               MOVE "P300-BPCFPRIN-OPEN" TO W-PARAGRAFO
               MOVE "BPCFPRIN"           TO W-OBJECT-NAME
               MOVE "OPEN"               TO W-CMDEXEC
               MOVE SPACES               TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P400-BPCFPRIN-CLOSE.
     š*---------------------------------------------------------------------
           CLOSE BPCFPRIN
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-CLOSED
               CONTINUE
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "BPCPPRINX"             TO W-PROGRAMA
               MOVE "P300-BPCFPRIN-CLOSE" TO W-PARAGRAFO
               MOVE "BPCFPRIN"            TO W-OBJECT-NAME
               MOVE "CLOSE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P450-FTP-FILE.
     š*---------------------------------------------------------------------
           INITIALIZE BPCL001M-FICH-ORIGEM
                      BPCL001M-FICH-DESTINO
                      BPCL001M-ERRO-BPCPFTP
                      REPLACING ALPHANUMERIC BY SPACES
                                    NUMERIC BY ZEROS.

           MOVE  "BPCFPRIN"            TO BPCL001M-FICH-ORIGEM
           MOVE  NOMEFICH  OF BPCJCNTL TO BPCL001M-FICH-DESTINO

           CALL "BPCCFTP" USING BPCL001M-FICH-ORIGEM
                                BPCL001M-FICH-DESTINO
                                BPCL001M-ERRO-BPCPFTP

           IF BPCL001M-ERRO-BPCPFTP NOT EQUAL SPACES
             MOVE "001"                     TO W-FILE-CODE
             MOVE WS-ERRO                   TO MSG-ERRO-FS
             MOVE "BPCCFTP"                 TO W-OBJECT-NAME
             MOVE "FTP"                     TO W-CMDEXEC
             MOVE "P450-FTP-FILE"           TO W-PARAGRAFO
             PERFORM P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P300-BPCFPRIN-WRITE.
     š*---------------------------------------------------------------------
           WRITE REG-BPCFPRIN
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO       OR
               GRS-DUP-KEY
                INITIALIZE REG-BPCFPRIN
                ADD 1 TO NR-PROC
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "BPCPPRINX"             TO W-PROGRAMA
               MOVE "P300-BPCFPRIN-WRITE" TO W-PARAGRAFO
               MOVE "BPCFPRIN"            TO W-OBJECT-NAME
               MOVE "WRITE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.

           MOVE 1 TO WS-POS-XML.

     š*---------------------------------------------------------------------
       P400-CLOSE-CURSOR-CURJPR.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJPR
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P001            TO TRUE
           WHEN OTHER
              SET  FIM-P001            TO TRUE
              MOVE "CURJPR"            TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P400-CLOSE-CURSOR-CURJPRIN.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJPRIN
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P002            TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJPRIN"          TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P400-CLOSE-CURSOR-CURJPRIN1.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJPRIN1
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P003            TO TRUE
           WHEN OTHER
              SET  FIM-P003            TO TRUE
              MOVE "CURJPRIN1"         TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P300-OPEN-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-ATRIBUTE.
     š*---------------------------------------------------------------------
     š*-Trata Valor Atributo
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF WS-STRING  NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-GERA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome da TAG
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L
             MOVE WS-STRING    TO WS-NOME-TAG

             STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "</"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-DATA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-DATA-TAG-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A( WS-POS-S: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-ATRIBUTE-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(WS-POS-S: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML

           END-IF.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING
                "</"
                WS-NOME-TAG(1: WS-NOM-TAG-L)
                ">"

           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-END-TAG.
     š*---------------------------------------------------------------------
           STRING ">"
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.
     š*---------------------------------------------------------------------
       P410-CLOSE-CURSOR-CURJPRIN1.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJPRIN1
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P002            TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJPRIN1"         TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-COMPRIMENTO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING
     š*- WY -Comprimento Final
           MOVE ZEROES TO WS-POS-I.
           INSPECT FUNCTION REVERSE(WS-STRING)
                   TALLYING WS-POS-I      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = LENGTH OF WS-STRING  -  WS-POS-I.
     š*---------------------------------------------------------------------
       P300-PRIMEIRO-ESPACO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING(1:WS-STRING-L)
           MOVE ZEROES TO WS-POS-S.
           INSPECT WS-STRING(1:WS-STRING-L)
                   TALLYING WS-POS-S      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = WS-STRING-L -  WS-POS-S.
           ADD 1 TO WS-POS-S.
     š*---------------------------------------------------------------------
       P300-RETIRA-CARAC-ESPECIAIS.
     š*---------------------------------------------------------------------
     š*-Retira Caracters Invalidos de WS-STRING
           PERFORM P300-UTLS001-TESTA
                   VARYING W-NUM FROM 1 BY 1
                   UNTIL W-NUM > LENGTH OF WS-STRING.

     š*---------------------------------------------------------------------
       P300-UTLS001-TESTA.
     š*---------------------------------------------------------------------
           MOVE WS-STRING-CHR(W-NUM)  TO CHAR-TESTE.
           IF   NOT CARACTER-VALIDO
                PERFORM  P300-UTLS001-ALTERA
           END-IF.

     š*---------------------------------------------------------------------
       P300-UTLS001-ALTERA.
     š*---------------------------------------------------------------------
           EVALUATE  TRUE

            WHEN WS-STRING-CHR(W-NUM) = "à" OR
                 WS-STRING-CHR(W-NUM) = "á" OR
                 WS-STRING-CHR(W-NUM) = "â" OR
                 WS-STRING-CHR(W-NUM) = "ã" OR
                 WS-STRING-CHR(W-NUM) = "ä" OR
                 WS-STRING-CHR(W-NUM) = "ª"
              MOVE "a" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ç"
              MOVE "c" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "é" OR
                 WS-STRING-CHR(W-NUM) = "è" OR
                 WS-STRING-CHR(W-NUM) = "ê" OR
                 WS-STRING-CHR(W-NUM) = "ë"
              MOVE "e" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "í" OR
                 WS-STRING-CHR(W-NUM) = "ì" OR
                 WS-STRING-CHR(W-NUM) = "ï"
              MOVE "i" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ñ"
              MOVE "n" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ó" OR
                 WS-STRING-CHR(W-NUM) = "ò" OR
                 WS-STRING-CHR(W-NUM) = "õ" OR
                 WS-STRING-CHR(W-NUM) = "ö"
              MOVE "o" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ú" OR
                 WS-STRING-CHR(W-NUM) = "ù" OR
                 WS-STRING-CHR(W-NUM) = "û" OR
                 WS-STRING-CHR(W-NUM) = "ü"
              MOVE "u" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Á" OR
                 WS-STRING-CHR(W-NUM) = "À" OR
                 WS-STRING-CHR(W-NUM) = "Ã" OR
                 WS-STRING-CHR(W-NUM) = "Â" OR
                 WS-STRING-CHR(W-NUM) = "Ä"
              MOVE "A" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ç"
              MOVE "C" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "É" OR
                 WS-STRING-CHR(W-NUM) = "È" OR
                 WS-STRING-CHR(W-NUM) = "Ê" OR
                 WS-STRING-CHR(W-NUM) = "Ë"
              MOVE "E" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Í" OR
                 WS-STRING-CHR(W-NUM) = "Ì" OR
                 WS-STRING-CHR(W-NUM) = "Ï"
              MOVE "I" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ñ"
              MOVE "N" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ó" OR
                 WS-STRING-CHR(W-NUM) = "Ò" OR
                 WS-STRING-CHR(W-NUM) = "Õ" OR
                 WS-STRING-CHR(W-NUM) = "Ô" OR
                 WS-STRING-CHR(W-NUM) = "Ö"
              MOVE "O" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ú" OR
                 WS-STRING-CHR(W-NUM) = "Ù" OR
                 WS-STRING-CHR(W-NUM) = "Û" OR
                 WS-STRING-CHR(W-NUM) = "Ü"
              MOVE "U" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "\"
              MOVE "/" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "[" OR
                 WS-STRING-CHR(W-NUM) = "{"
              MOVE "(" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "]" OR
                 WS-STRING-CHR(W-NUM) = "}"
              MOVE ")" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = """" OR
                 WS-STRING-CHR(W-NUM) = "«"  OR
                 WS-STRING-CHR(W-NUM) = "»"
              MOVE "'" TO WS-STRING-CHR(W-NUM)

            WHEN OTHER
              MOVE " " TO WS-STRING-CHR(W-NUM)

           END-EVALUATE.

     š*---------------------------------------------------------------------
       P990-ERRO-FS.
     š*---------------------------------------------------------------------
           EVALUATE TRUE
           WHEN GRS-READ-DUP-KEY
                  STRING "ERRO DE DADOS: CHAVE DUPLICADA NO FICHEIRO <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-FS
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ ERRO    <"
                      W-EDTFILC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-FS
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           SET  COM-ERRO        TO TRUE
           MOVE  1              TO ERRRTC   OF  KYCT900A
           MOVE W-FILE-CODE     TO ERRCOD   OF  KYCT900A
           MOVE MSG-ERRO-FS     TO ERRMSG   OF  KYCT900A
           MOVE "BPCPPRINX"       TO ERRPRG   OF  KYCT900A
           MOVE "BPC"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE W-CMDEXEC       TO ERRCMD   OF  KYCT900A
           MOVE W-PARAGRAFO     TO ERRFLD   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           SET  COM-ERRO        TO TRUE
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BPCPPRINX"      TO  ERRPRG OF KYCT900A.
           MOVE   WS-IDCONT(1:7) TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "KYCT900A"       TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P999-FIMPGM.
     š*---------------------------------------------------------------------
           EXIT PROGRAM.
           STOP RUN.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BPCPPRINX
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

