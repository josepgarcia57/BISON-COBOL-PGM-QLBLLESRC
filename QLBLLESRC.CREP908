      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP908.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP908
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP908"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-IDREGSEL.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDDIALOG         PIC X(08).
               10  FILLER              PIC X(61).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVDIALOG-I   REDEFINES WS-PARMSLDV.
               10  WS-IDDIALOG         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVDIALOG-O   REDEFINES WS-PARMSLDV.
               10  WS-IDDIALOG         PIC X(08).
           05  WS-LVPRCAPL-I   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDTIPDLG         PIC XX.
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPRCAPL-O   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-SNPGNHZ1-DFLT        PIC X       VALUE "S".
           05  KV-SNPGNHZ2-DFLT        PIC X       VALUE "N".

           05  KV-IDTIPDLG-LS.
               10  KV-IDTIPDLG-MNTD    PIC XX      VALUE "MD".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção Directa".
               10  KV-IDTIPDLG-MNTI    PIC XX      VALUE "MI".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção c/ Apoio".
               10  KV-IDTIPDLG-MNTA    PIC XX      VALUE "MA".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção de Apoio".
               10  KV-IDTIPDLG-CNSD    PIC XX      VALUE "CD".
               10  FILLER              PIC X(19)   VALUE
                                               "Consulta Directa".
               10  KV-IDTIPDLG-CNSL    PIC XX      VALUE "CL".
               10  FILLER              PIC X(19)   VALUE
                                               "Consulta em Lista".
               10  KV-IDTIPDLG-PEDX    PIC XX      VALUE "PD".
               10  FILLER              PIC X(19)   VALUE
                                               "Pedido de Execução".
           05  KV-IDTIPDLG-LV  REDEFINES KV-IDTIPDLG-LS    OCCURS 6.
               10  KV-IDTIPDLG-ID      PIC XX.
               10  KV-IDTIPDLG-DS      PIC X(19).
           05  KV-IDTIPDLG-OCC         PIC 9       VALUE 6.

           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB958.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET902 OF CRET902.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET904 OF CRET904.
           COPY DDS-CRET905 OF CRET905.
           COPY DDS-CRET906 OF CRET906.
           COPY DDS-CRET907 OF CRET907.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-IDREGSEL     TO WS-IDREGSEL
           IF  CREB000-MSGDLG-P > 1
           OR  CREB000-IDCANALC = CREB950-IDCANALC-RM
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB958-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB958-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN(CREB000-INITDLG AND NOT CREB000-PROCLOV)
           OR   CREB000-PROCIDT
               PERFORM P2100-INICIALIZA
               IF  CREB000-PROCNLF
               AND(CREB000-PROC-OK OR  CREB000-MSGINFO)
               AND CREB000-IDREG-OK
                   PERFORM P2500-PROC-REGEXIST
               END-IF
           WHEN CREB000-PROCLOV
               PERFORM P2200-PROC-LISTVALS
           WHEN CREB000-IDREG-NV
               PERFORM P2300-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2400-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2500-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2600-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2700-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2800-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB958-MSGDIALG
           SET  CREB000-IDREG-NF TO TRUE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCIDT OR CREB000-PROCSEL
               PERFORM P2110-READ-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2120-INIT-NOVOREG
           END-EVALUATE
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2110-READ-REGEXIST         SECTION.

           MOVE WS-IDPRCAPL OF WS-IDREGSEL TO CREB958-IDPRCAPL-V
           PERFORM P9000-READ-CRET903

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-NF
               PERFORM P9000-READ-CRET904
           END-EVALUATE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2111-FRMT-MSGDIALG
           WHEN OTHER
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGINFO        TO TRUE
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
               AND NOT CREB950-FUNCAO-INTERRP
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           END-IF
           .
       P2110-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2111-FRMT-MSGDIALG         SECTION.

           MOVE IDPRCSGA OF CRET903             TO CREB958-IDPRCSGA-V
           MOVE DSPRCAPL OF CRET903             TO CREB958-DSPRCAPL-V
           MOVE IDAPLICA OF CRET903             TO CREB958-IDAPLICA-V
           MOVE IDPRGGST OF CRET903             TO CREB958-IDGSTPRC-V
           MOVE IDDIALOG OF CRET903             TO CREB958-IDDIALOG-V
           MOVE IDTIPDLG OF CRET903             TO CREB958-IDTIPDLG-V
           PERFORM P9000-READ-IDTIPDLG
           IF  WS-ARRYNDX1 <= KV-IDTIPDLG-OCC
               MOVE KV-IDTIPDLG-DS(WS-ARRYNDX1) TO CREB958-DSTIPDLG-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB958-DSTIPDLG-V
           END-IF
           MOVE SNPGNHZ1 OF CRET903             TO CREB958-SNPGNHZ1-V
           MOVE SNPGNHZ2 OF CRET903             TO CREB958-SNPGNHZ2-V
           IF  IDTIPDLG OF CRET903 NOT = KV-IDTIPDLG-MNTA
               MOVE NVAUTLER OF CRET903         TO CREB958-NVAUTLER-V
               MOVE NVAUTIMP OF CRET903         TO CREB958-NVAUTIMP-V
               MOVE NVAUTCRI OF CRET903         TO CREB958-NVAUTCRI-V
               MOVE NVAUTMDF OF CRET903         TO CREB958-NVAUTMDF-V
               MOVE NVAUTANL OF CRET903         TO CREB958-NVAUTANL-V
               MOVE NVAUTMIN OF CRET903         TO CREB958-NVAUTMIN-V
               MOVE IDPRCDEP OF CRET903         TO CREB958-IDPRCDEP-V
               MOVE DHINISIT OF CRET903         TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO       TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14            TO CREB958-DHINISIT-V
               MOVE CORR CREB950-DTHR-ISO       TO CREB950-DATA-X08
               EVALUATE TRUE
               WHEN CREB950-DATA-N08    <= CREB000-DTPRCAPL
                   MOVE MCSITEXE OF CRET903     TO CREB958-MCSITEXE-V
               WHEN MCSITEXE OF CRET903  = KV-MCSITEXE-ACTV
                   MOVE KV-MCSITEXE-INIB        TO CREB958-MCSITEXE-V
               WHEN OTHER
                   MOVE KV-MCSITEXE-ACTV        TO CREB958-MCSITEXE-V
               END-EVALUATE
               PERFORM P9000-READ-SITEXECU

               MOVE DHCRIREG OF CRET903         TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO       TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14            TO CREB958-DHCRIREG-V
               IF  DHMDFREG OF CRET903 NOT = CREB950-TMSTPMIN
                   MOVE DHMDFREG OF CRET903     TO CREB950-DTHR-ISO
                   MOVE CORR CREB950-DTHR-ISO   TO CREB950-DTHR-X14
                   MOVE CREB950-DTHR-N14        TO CREB958-DHMDFREG-V
               END-IF
           END-IF
           MOVE DHMDFREG OF CRET903             TO CREB000-DHMDFREG
           PERFORM P9000-READ-CRET901

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB958-IDPRCDEP-V = SPACES
               MOVE NMAPLICA OF CRET901         TO CREB958-NMAPLICA-V
           WHEN OTHER
               MOVE NMAPLICA OF CRET901         TO CREB958-NMAPLICA-V
               PERFORM P9000-READ-IDPRCDEP

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   MOVE DSPRCAPL OF CRET903     TO CREB958-NMPRCDEP-V
               END-IF
           END-EVALUATE
           .
       P2111-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2120-INIT-NOVOREG          SECTION.

           MOVE WS-IDAPLICA OF WS-IDREGSEL TO CREB958-IDAPLICA-V
           MOVE WS-IDPRCAPL OF WS-IDREGSEL TO CREB958-IDPRCAPL-V
           MOVE WS-IDDIALOG OF WS-IDREGSEL TO CREB958-IDDIALOG-V
           MOVE KV-SNPGNHZ1-DFLT           TO CREB958-SNPGNHZ1-V
           MOVE KV-SNPGNHZ2-DFLT           TO CREB958-SNPGNHZ2-V
           MOVE KV-MCSITEXE-ACTV           TO CREB958-MCSITEXE-V
           PERFORM P9000-READ-SITEXECU

           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PERMIT-DET
           PERFORM P9000-NDISPL-DET
           .
       P2120-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2210-FRMT-PARMSLDV
           ELSE
               PERFORM P2220-RECB-IDSELLDV
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDAPLICA"
               MOVE CREB958-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB958-IDAPLICA-PC TO TRUE
           WHEN "IDDIALOG"
               MOVE CREB958-IDDIALOG-V  TO WS-IDDIALOG OF WS-LVDIALOG-I
               MOVE CREB958-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVDIALOG-I
               SET  CREB958-IDDIALOG-PC TO TRUE
           WHEN "IDPRCDEP"
               MOVE CREB958-IDPRCDEP-V  TO WS-IDPRCAPL OF WS-LVPRCAPL-I
               MOVE CREB958-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVPRCAPL-I
               IF  CREB958-IDTIPDLG-V = KV-IDTIPDLG-MNTI
                   MOVE CREB950-MCDLGDEP TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               ELSE
                   MOVE CREB950-MCDLGPRN TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               END-IF
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPRCAPL-I
               SET  CREB958-IDPRCDEP-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB958-IDAPLICA-V
               SET  CREB958-IDAPLICA-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDDIALOG"
           AND  WS-IDDIALOG OF WS-LVDIALOG-O NOT = SPACES
               MOVE WS-IDDIALOG OF WS-LVDIALOG-O TO CREB958-IDDIALOG-V
               SET  CREB958-IDDIALOG-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDPRCDEP"
           AND  WS-IDPRCAPL OF WS-LVPRCAPL-O NOT = SPACES
               MOVE WS-IDPRCAPL OF WS-LVPRCAPL-O TO CREB958-IDPRCDEP-V
               SET  CREB958-IDPRCDEP-PC          TO TRUE
           END-EVALUATE
           .
       P2220-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB958-IDPRCAPL-V   TO CREB999-IDPRCAPL
                                        CREB999-IDPRCORI
           MOVE CREB958-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB958-IDDIALOG-V   TO CREB999-IDDIALOG
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO CREB000-PARMSINP
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB958-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           MOVE CREB958-IDPRCAPL-V TO WS-STRING80
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF CREB958-IDPRCAPL-V
             OR   (WS-CHRSTR80(WS-ARRYNDX1) = SPACE OR "*" OR "?")
               CONTINUE
           END-PERFORM

           PERFORM
           VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF CREB958-IDPRCAPL-V
             OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
               CONTINUE
           END-PERFORM

           EVALUATE TRUE
           WHEN CREB958-IDPRCAPL-V  = SPACES
               SET  CREB958-IDPRCAPL-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN WS-ARRYNDX1        <= LENGTH OF CREB958-IDPRCAPL-V
               SET  CREB958-IDPRCAPL-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               PERFORM P2510-VRIF-NIVEL01
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           PERFORM P9000-READ-CRET903

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-NF
               PERFORM P9000-READ-CRET904
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-OK
               SET  CREB958-IDPRCAPL-PC    TO TRUE
               SET  CREB950-REGIST-JEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
               SET  CREB000-IDREG-NF       TO TRUE
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2520-VRIF-NIVEL02
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-MSGDIALG         SECTION.

           PERFORM P2530-FRMT-MSGDIALG
           .
       P2430-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE CREB958-VARSOUT
               PERFORM P2510-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2520-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2530-FRMT-MSGDIALG
               END-IF
           ELSE
               PERFORM P2540-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2500-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2510-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB958-DSPRCAPL-V = SPACES
               SET  CREB958-DSPRCAPL-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB958-IDAPLICA-V = SPACES
               SET  CREB958-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB958-IDGSTPRC-V = SPACES
               SET  CREB958-IDGSTPRC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB958-IDDIALOG-V = SPACES
               SET  CREB958-IDDIALOG-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB958-IDTIPDLG-V = SPACES
               SET  CREB958-IDTIPDLG-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               PERFORM P9000-VLID-IDTIPDLG
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB958-SNPGNHZ1-V     = SPACES
               SET  CREB958-SNPGNHZ1-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-SNPGNHZ1-V NOT = "S" AND "N"
               SET  CREB958-SNPGNHZ1-PC        TO TRUE
               SET  CREB950-VCAMPO-INVALID     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-SNPGNHZ2-V     = SPACES
               SET  CREB958-SNPGNHZ2-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-SNPGNHZ2-V NOT = "S" AND "N"
               SET  CREB958-SNPGNHZ2-PC        TO TRUE
               SET  CREB950-VCAMPO-INVALID     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-SNPGNHZ1-V     = "N"
           AND  CREB958-SNPGNHZ2-V     = "N"
               SET  CREB958-SNPGNHZ1-PC        TO TRUE
               SET  CREB950-PGNHRZ-INFMINI     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB958-NVAUTMIN-V     = ZERO
           AND  CREB958-IDTIPDLG-V     = KV-IDTIPDLG-PEDX
               SET  CREB958-NVAUTMIN-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-IDTIPDLG-V     = KV-IDTIPDLG-MNTA
                OR                       KV-IDTIPDLG-PEDX
               INITIALIZE CREB958-NVAUTLER-V
                          CREB958-NVAUTIMP-V
                          CREB958-NVAUTCRI-V
                          CREB958-NVAUTMDF-V
                          CREB958-NVAUTANL-V
           WHEN CREB958-NVAUTLER-V     = ZERO
               SET  CREB958-NVAUTLER-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-IDTIPDLG-V     = KV-IDTIPDLG-CNSD
                OR                       KV-IDTIPDLG-CNSL
               INITIALIZE CREB958-NVAUTCRI-V
                          CREB958-NVAUTMDF-V
                          CREB958-NVAUTANL-V
           WHEN CREB958-NVAUTCRI-V     = ZERO
           AND  CREB958-NVAUTMDF-V     = ZERO
           AND  CREB958-NVAUTANL-V     = ZERO
           AND (CREB958-IDTIPDLG-V     = KV-IDTIPDLG-MNTD
                OR                       KV-IDTIPDLG-MNTI)
               SET  CREB958-NVAUTCRI-PC        TO TRUE
               SET  CREB950-1NVMNT-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-NVAUTIMP-V     > ZERO
           AND  CREB958-NVAUTIMP-V     < CREB958-NVAUTLER-V
               SET  CREB958-NVAUTIMP-PC        TO TRUE
               SET  CREB950-NVAUTZ-INFMINI     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-NVAUTCRI-V     > ZERO
           AND  CREB958-NVAUTCRI-V     < CREB958-NVAUTLER-V
               SET  CREB958-NVAUTCRI-PC        TO TRUE
               SET  CREB950-NVAUTZ-INFMINI     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-NVAUTMDF-V     > ZERO
           AND  CREB958-NVAUTMDF-V     < CREB958-NVAUTLER-V
               SET  CREB958-NVAUTMDF-PC        TO TRUE
               SET  CREB950-NVAUTZ-INFMINI     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-NVAUTANL-V     > ZERO
           AND  CREB958-NVAUTANL-V     < CREB958-NVAUTLER-V
               SET  CREB958-NVAUTANL-PC        TO TRUE
               SET  CREB950-NVAUTZ-INFMINI     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB958-IDTIPDLG-V     = KV-IDTIPDLG-MNTI
           AND  CREB958-IDPRCDEP-V     = SPACES
               SET  CREB958-IDPRCDEP-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-EVALUATE

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-PEDX
               MOVE CREB958-NVAUTLER-V         TO CREB958-NVAUTMIN-V
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTI
               AND                      KV-IDTIPDLG-CNSL)
               INITIALIZE CREB958-IDPRCDEP-V
           END-IF
           .
       P2510-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2520-VRIF-NIVEL02          SECTION.

           PERFORM P9000-VLID-IDAPLICA

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VLID-IDDIALOG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN SNPGNVRT OF CRET902    = "N"
           AND (CREB958-IDTIPDLG-V     = KV-IDTIPDLG-MNTI
                OR                       KV-IDTIPDLG-CNSL)
               SET  CREB958-IDTIPDLG-PC    TO TRUE
               SET  CREB950-VCAMPO-NCMPDLG TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN SNPGNVRT OF CRET902    = "S"
           AND (CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTI
                AND                      KV-IDTIPDLG-CNSL)
               SET  CREB958-IDTIPDLG-PC    TO TRUE
               SET  CREB950-VCAMPO-NCMPDLG TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB958-IDPRCDEP-V NOT = SPACES
               PERFORM P9000-VLID-IDPRCDEP
           END-EVALUATE
           .
       P2520-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2530-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2530-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2540-VRIF-RELDPEND         SECTION.

           IF  CREB958-IDTIPDLG-V = KV-IDTIPDLG-MNTA
               PERFORM P9000-VRIF-CRET903D
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VRIF-CRET907
           END-IF
           .
       P2540-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2600-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2710-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2720-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2730-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-EXEC-TRNCRIAR         SECTION.

           IF  CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTA
               PERFORM P9000-FRMT-CRET903-CHAV
               PERFORM P9000-FRMT-CRET903-ATRB
               PERFORM P9000-INSR-CRET903
           ELSE
               PERFORM P9000-FRMT-CRET904-CHAV
               PERFORM P9000-FRMT-CRET904-ATRB
               PERFORM P9000-INSR-CRET904
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE CREB958-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
               MOVE CREB958-IDPRCAPL-V TO WS-IDPRCAPL OF WS-IDREGSEL
               MOVE CREB958-IDDIALOG-V TO WS-IDDIALOG OF WS-IDREGSEL
           END-IF
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-EXEC-TRNMODIF         SECTION.

           IF  CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTA
               PERFORM P9000-FRMT-CRET903-CHAV
               PERFORM P9000-FRMT-CRET903-ATRB
               PERFORM P9000-UPDT-CRET903
           ELSE
               PERFORM P9000-FRMT-CRET904-CHAV
               PERFORM P9000-FRMT-CRET904-ATRB
               PERFORM P9000-UPDT-CRET904
           END-IF
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-EXEC-TRNANULA         SECTION.

           IF  CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTA
               PERFORM P9000-FRMT-CRET903-CHAV
               PERFORM P9000-DELT-CRET903
           ELSE
               PERFORM P9000-FRMT-CRET904-CHAV
               PERFORM P9000-DELT-CRET904
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DELT-CRET906
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DELT-CRET905
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DELT-CRET907
           END-IF
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-IDREG-NF
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
           WHEN CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-EVALUATE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
           OR   CREB000-IDCANALC = CREB950-IDCANALC-RM
               CONTINUE
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB958-MSGDIALG
           END-EVALUATE
           MOVE CREB958-MSGDIALG       TO CREB000-MSGDLG-V
                                         (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL   TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB958-IDPRCAPL-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB958-IDPRCAPL-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  CREB958-IDPRCSGA-UP TO TRUE
           SET  CREB958-DSPRCAPL-UP TO TRUE
           SET  CREB958-IDAPLICA-UP TO TRUE
           SET  CREB958-IDGSTPRC-UP TO TRUE
           SET  CREB958-IDDIALOG-UP TO TRUE
           SET  CREB958-SNPGNHZ1-UP TO TRUE
           SET  CREB958-SNPGNHZ2-UP TO TRUE

           EVALUATE TRUE
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               SET  CREB958-IDTIPDLG-UP TO TRUE
               SET  CREB958-NVAUTLER-UP TO TRUE
               SET  CREB958-NVAUTIMP-UP TO TRUE
               SET  CREB958-NVAUTCRI-UP TO TRUE
               SET  CREB958-NVAUTMDF-UP TO TRUE
               SET  CREB958-NVAUTANL-UP TO TRUE
               SET  CREB958-NVAUTMIN-UP TO TRUE
               SET  CREB958-IDPRCDEP-UP TO TRUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-MNTD
               SET  CREB958-IDTIPDLG-PR TO TRUE
               SET  CREB958-NVAUTLER-UP TO TRUE
               SET  CREB958-NVAUTIMP-UP TO TRUE
               SET  CREB958-NVAUTCRI-UP TO TRUE
               SET  CREB958-NVAUTMDF-UP TO TRUE
               SET  CREB958-NVAUTANL-UP TO TRUE
               SET  CREB958-NVAUTMIN-PR TO TRUE
               SET  CREB958-IDPRCDEP-PR TO TRUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-MNTI
               SET  CREB958-IDTIPDLG-PR TO TRUE
               SET  CREB958-NVAUTLER-UP TO TRUE
               SET  CREB958-NVAUTIMP-UP TO TRUE
               SET  CREB958-NVAUTCRI-UP TO TRUE
               SET  CREB958-NVAUTMDF-UP TO TRUE
               SET  CREB958-NVAUTANL-UP TO TRUE
               SET  CREB958-NVAUTMIN-PR TO TRUE
               SET  CREB958-IDPRCDEP-UP TO TRUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-MNTA
               SET  CREB958-IDTIPDLG-PR TO TRUE
               SET  CREB958-NVAUTLER-PR TO TRUE
               SET  CREB958-NVAUTIMP-PR TO TRUE
               SET  CREB958-NVAUTCRI-PR TO TRUE
               SET  CREB958-NVAUTMDF-PR TO TRUE
               SET  CREB958-NVAUTANL-PR TO TRUE
               SET  CREB958-NVAUTMIN-PR TO TRUE
               SET  CREB958-IDPRCDEP-PR TO TRUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-CNSD
               SET  CREB958-IDTIPDLG-PR TO TRUE
               SET  CREB958-NVAUTLER-UP TO TRUE
               SET  CREB958-NVAUTIMP-UP TO TRUE
               SET  CREB958-NVAUTCRI-PR TO TRUE
               SET  CREB958-NVAUTMDF-PR TO TRUE
               SET  CREB958-NVAUTANL-PR TO TRUE
               SET  CREB958-NVAUTMIN-PR TO TRUE
               SET  CREB958-IDPRCDEP-PR TO TRUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-CNSL
               SET  CREB958-IDTIPDLG-PR TO TRUE
               SET  CREB958-NVAUTLER-UP TO TRUE
               SET  CREB958-NVAUTIMP-UP TO TRUE
               SET  CREB958-NVAUTCRI-PR TO TRUE
               SET  CREB958-NVAUTMDF-PR TO TRUE
               SET  CREB958-NVAUTANL-PR TO TRUE
               SET  CREB958-NVAUTMIN-PR TO TRUE
               SET  CREB958-IDPRCDEP-UP TO TRUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-PEDX
               SET  CREB958-IDTIPDLG-PR TO TRUE
               SET  CREB958-NVAUTLER-PR TO TRUE
               SET  CREB958-NVAUTIMP-PR TO TRUE
               SET  CREB958-NVAUTCRI-PR TO TRUE
               SET  CREB958-NVAUTMDF-PR TO TRUE
               SET  CREB958-NVAUTANL-PR TO TRUE
               SET  CREB958-NVAUTMIN-UP TO TRUE
               SET  CREB958-IDPRCDEP-PR TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  CREB958-IDPRCSGA-PR TO TRUE
           SET  CREB958-DSPRCAPL-PR TO TRUE
           SET  CREB958-IDAPLICA-PR TO TRUE
           SET  CREB958-IDGSTPRC-PR TO TRUE
           SET  CREB958-IDDIALOG-PR TO TRUE
           SET  CREB958-IDTIPDLG-PR TO TRUE
           SET  CREB958-SNPGNHZ1-PR TO TRUE
           SET  CREB958-SNPGNHZ2-PR TO TRUE
           SET  CREB958-NVAUTLER-PR TO TRUE
           SET  CREB958-NVAUTIMP-PR TO TRUE
           SET  CREB958-NVAUTCRI-PR TO TRUE
           SET  CREB958-NVAUTMDF-PR TO TRUE
           SET  CREB958-NVAUTANL-PR TO TRUE
           SET  CREB958-NVAUTMIN-PR TO TRUE
           SET  CREB958-IDPRCDEP-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           IF  CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTA
               SET  CREB958-DSSITEXE-PR TO TRUE
               SET  CREB958-DHINISIT-PR TO TRUE
               SET  CREB958-DHCRIREG-PR TO TRUE
               SET  CREB958-DHMDFREG-PR TO TRUE
           ELSE
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           IF  CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTA
           AND(CREB000-PROCCRT
           OR (CREB000-PROCVRF AND CREB000-IDREG-NF))
               SET  CREB958-DSSITEXE-PR TO TRUE
           ELSE
               SET  CREB958-DSSITEXE-ND TO TRUE
           END-IF
           SET  CREB958-DHINISIT-ND     TO TRUE
           SET  CREB958-DHCRIREG-ND     TO TRUE
           SET  CREB958-DHMDFREG-ND     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903          SECTION.

           INITIALIZE CRET903
           MOVE CREB958-IDPRCAPL-V TO IDPRCAPL OF CRET903
           EXEC SQL SELECT IDPRCSGA
                         , DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDTIPDLG
                         , IDDIALOG
                         , SNPGNHZ1
                         , SNPGNHZ2
                         , NVAUTMIN
                         , NVAUTCRI
                         , NVAUTLER
                         , NVAUTIMP
                         , NVAUTMDF
                         , NVAUTANL
                         , IDPRCDEP
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , DHCRIREG
                      INTO :CRET903.IDPRCSGA
                         , :CRET903.DSPRCAPL
                         , :CRET903.IDAPLICA
                         , :CRET903.IDPRGGST
                         , :CRET903.IDTIPDLG
                         , :CRET903.IDDIALOG
                         , :CRET903.SNPGNHZ1
                         , :CRET903.SNPGNHZ2
                         , :CRET903.NVAUTMIN
                         , :CRET903.NVAUTCRI
                         , :CRET903.NVAUTLER
                         , :CRET903.NVAUTIMP
                         , :CRET903.NVAUTMDF
                         , :CRET903.NVAUTANL
                         , :CRET903.IDPRCDEP
                         , :CRET903.MCSITEXE
                         , :CRET903.DHINISIT
                         , :CRET903.DHMDFREG
                         , :CRET903.DHCRIREG
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET904          SECTION.

           MOVE CREB958-IDPRCAPL-V TO IDPRCAPL OF CRET904
           EXEC SQL SELECT DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDDIALOG
                         , SNPGNHZ1
                         , SNPGNHZ2
                         , DHMDFREG
                         , DHCRIREG
                      INTO :CRET903.DSPRCAPL
                         , :CRET903.IDAPLICA
                         , :CRET903.IDPRGGST
                         , :CRET903.IDDIALOG
                         , :CRET903.SNPGNHZ1
                         , :CRET903.SNPGNHZ2
                         , :CRET903.DHMDFREG
                         , :CRET903.DHCRIREG
                      FROM CRET904
                     WHERE IDPRCAPL = :CRET904.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
               MOVE KV-IDTIPDLG-MNTA  TO IDTIPDLG OF CRET903
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET904" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDTIPDLG         SECTION.

           PERFORM P9000-READ-IDTIPDLG

           IF  WS-ARRYNDX1 > KV-IDTIPDLG-OCC
               SET  CREB958-IDTIPDLG-PC         TO TRUE
               SET  CREB950-TIPDLG-INVALID      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           ELSE
               MOVE KV-IDTIPDLG-DS(WS-ARRYNDX1) TO CREB958-DSTIPDLG-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-IDTIPDLG         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-IDTIPDLG-OCC
             OR    KV-IDTIPDLG-ID(WS-ARRYNDX1) = CREB958-IDTIPDLG-V
               CONTINUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB958-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB958-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB958-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDAPLICA         SECTION.

           PERFORM P9000-READ-CRET901

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB958-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE NMAPLICA OF CRET901    TO CREB958-NMAPLICA-V
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDDIALOG         SECTION.

           MOVE CREB958-IDDIALOG-V         TO IDDIALOG OF CRET902
           EXEC SQL SELECT SNPGNVRT
                         , NRPGSHRZ
                      INTO :CRET902.SNPGNVRT
                         , :CRET902.NRPGSHRZ
                      FROM CRET902
                     WHERE IDDIALOG = :CRET902.IDDIALOG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB958-IDDIALOG-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.CRET902"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDPRCDEP         SECTION.

           PERFORM P9000-READ-IDPRCDEP

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB958-IDPRCDEP-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB958-IDTIPDLG-V      = KV-IDTIPDLG-CNSL
           AND (IDTIPDLG OF CRET903 NOT = KV-IDTIPDLG-MNTD
                AND                       KV-IDTIPDLG-CNSD
                AND                       KV-IDTIPDLG-PEDX)
               SET  CREB958-IDPRCDEP-PC    TO TRUE
               SET  CREB950-PRCDEP-NCOMPAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE DSPRCAPL OF CRET903    TO CREB958-NMPRCDEP-V
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET903D         SECTION.

           MOVE CREB000-IDAPLICA   TO IDAPLICA OF CRET901
           MOVE CREB958-IDPRCAPL-V TO IDPRCDEP OF CRET903
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET903
                     WHERE IDPRCDEP = :CRET903.IDPRCDEP
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET903"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.EXIST01"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET907          SECTION.

           MOVE CREB000-IDAPLICA   TO IDAPLICA OF CRET901
           MOVE CREB958-IDPRCAPL-V TO IDPRCDST OF CRET907
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET907
                     WHERE IDPRCDST = :CRET907.IDPRCDST
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET907"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.EXIST02"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           MOVE CREB958-IDAPLICA-V    TO IDAPLICA OF CRET901
           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO NMAPLICA OF CRET901
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET901" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-IDPRCDEP         SECTION.

           IF  CREB958-IDTIPDLG-V NOT = KV-IDTIPDLG-MNTI
               PERFORM P9000-READ-CRET903D
           ELSE
               PERFORM P9000-READ-CRET904D
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903D         SECTION.

           MOVE CREB958-IDPRCDEP-V    TO IDPRCAPL OF CRET903
           EXEC SQL SELECT DSPRCAPL
                         , IDTIPDLG
                      INTO :CRET903.DSPRCAPL
                         , :CRET903.IDTIPDLG
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSPRCAPL OF CRET903
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET904D         SECTION.

           MOVE CREB958-IDPRCDEP-V    TO IDPRCAPL OF CRET904
           EXEC SQL SELECT DSPRCAPL
                      INTO :CRET903.DSPRCAPL
                      FROM CRET904
                     WHERE IDPRCAPL = :CRET904.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE KV-IDTIPDLG-MNTA  TO IDTIPDLG OF CRET903
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSPRCAPL OF CRET903
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET904" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET903-CHAV     SECTION.

           MOVE CREB958-IDPRCAPL-V     TO IDPRCAPL OF CRET903
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET903
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET903-ATRB     SECTION.

           MOVE CREB958-IDPRCSGA-V        TO IDPRCSGA OF CRET903
           MOVE CREB958-DSPRCAPL-V        TO DSPRCAPL OF CRET903
           MOVE CREB958-IDAPLICA-V        TO IDAPLICA OF CRET903
           MOVE CREB958-IDGSTPRC-V        TO IDPRGGST OF CRET903
           MOVE CREB958-IDDIALOG-V        TO IDDIALOG OF CRET903
           MOVE CREB958-SNPGNHZ1-V        TO SNPGNHZ1 OF CRET903
           MOVE CREB958-SNPGNHZ2-V        TO SNPGNHZ2 OF CRET903
           MOVE CREB958-NVAUTMIN-V        TO NVAUTMIN OF CRET903
           MOVE CREB958-NVAUTCRI-V        TO NVAUTCRI OF CRET903
           MOVE CREB958-NVAUTLER-V        TO NVAUTLER OF CRET903
           MOVE CREB958-NVAUTIMP-V        TO NVAUTIMP OF CRET903
           MOVE CREB958-NVAUTIMP-V        TO NVAUTIMP OF CRET903
           MOVE CREB958-NVAUTMDF-V        TO NVAUTMDF OF CRET903
           MOVE CREB958-NVAUTANL-V        TO NVAUTANL OF CRET903
           MOVE CREB958-IDPRCDEP-V        TO IDPRCDEP OF CRET903
           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB958-IDTIPDLG-V    TO IDTIPDLG OF CRET903
               MOVE CREB958-MCSITEXE-V    TO MCSITEXE OF CRET903
               MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB000-DTPRCAPL      TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08 TO CREB950-DTHR-X14
               MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
               MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET903
               MOVE CREB000-IDUTILIZ      TO IDUTLCRI OF CRET903
               MOVE CREB950-TMSTPMIN      TO DHMDFREG OF CRET903
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET903
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET904-CHAV     SECTION.

           MOVE CREB958-IDPRCAPL-V     TO IDPRCAPL OF CRET904
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET904
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET904-ATRB     SECTION.

           MOVE CREB958-DSPRCAPL-V     TO DSPRCAPL OF CRET904
           MOVE CREB958-IDAPLICA-V     TO IDAPLICA OF CRET904
           MOVE CREB958-IDGSTPRC-V     TO IDPRGGST OF CRET904
           MOVE CREB958-IDDIALOG-V     TO IDDIALOG OF CRET904
           MOVE CREB958-SNPGNHZ1-V     TO SNPGNHZ1 OF CRET904
           MOVE CREB958-SNPGNHZ2-V     TO SNPGNHZ2 OF CRET904
           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB000-IDUTILIZ   TO IDUTLCRI OF CRET904
               MOVE CREB950-TMSTPMIN   TO DHMDFREG OF CRET904
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ   TO IDUTLMDF OF CRET904
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-CRET903          SECTION.

           EXEC SQL INSERT INTO CRET903
                         ( IDPRCAPL
                         , IDPRCSGA
                         , DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDTIPDLG
                         , IDDIALOG
                         , SNPGNHZ1
                         , SNPGNHZ2
                         , NVAUTMIN
                         , NVAUTCRI
                         , NVAUTLER
                         , NVAUTIMP
                         , NVAUTMDF
                         , NVAUTANL
                         , IDPRCDEP
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , IDUTLMDF
                         , DHCRIREG
                         , IDUTLCRI
                         )
                    VALUES
                         ( :CRET903.IDPRCAPL
                         , :CRET903.IDPRCSGA
                         , :CRET903.DSPRCAPL
                         , :CRET903.IDAPLICA
                         , :CRET903.IDPRGGST
                         , :CRET903.IDTIPDLG
                         , :CRET903.IDDIALOG
                         , :CRET903.SNPGNHZ1
                         , :CRET903.SNPGNHZ2
                         , :CRET903.NVAUTMIN
                         , :CRET903.NVAUTCRI
                         , :CRET903.NVAUTLER
                         , :CRET903.NVAUTIMP
                         , :CRET903.NVAUTMDF
                         , :CRET903.NVAUTANL
                         , :CRET903.IDPRCDEP
                         , :CRET903.MCSITEXE
                         , :CRET903.DHINISIT
                         , :CRET903.DHMDFREG
                         , :CRET903.IDUTLMDF
                         , CURRENT TIMESTAMP
                         , :CRET903.IDUTLCRI
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-CRET904          SECTION.

           EXEC SQL INSERT INTO CRET904
                         ( IDPRCAPL
                         , DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDDIALOG
                         , SNPGNHZ1
                         , SNPGNHZ2
                         , DHMDFREG
                         , IDUTLMDF
                         , DHCRIREG
                         , IDUTLCRI
                         )
                    VALUES
                         ( :CRET904.IDPRCAPL
                         , :CRET904.DSPRCAPL
                         , :CRET904.IDAPLICA
                         , :CRET904.IDPRGGST
                         , :CRET904.IDDIALOG
                         , :CRET904.SNPGNHZ1
                         , :CRET904.SNPGNHZ2
                         , :CRET904.DHMDFREG
                         , :CRET904.IDUTLMDF
                         , CURRENT TIMESTAMP
                         , :CRET904.IDUTLCRI
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET904" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET903          SECTION.

           EXEC SQL UPDATE CRET903
                       SET IDPRCSGA = :CRET903.IDPRCSGA
                         , DSPRCAPL = :CRET903.DSPRCAPL
                         , IDAPLICA = :CRET903.IDAPLICA
                         , IDPRGGST = :CRET903.IDPRGGST
                         , IDDIALOG = :CRET903.IDDIALOG
                         , SNPGNHZ1 = :CRET903.SNPGNHZ1
                         , SNPGNHZ2 = :CRET903.SNPGNHZ2
                         , NVAUTMIN = :CRET903.NVAUTMIN
                         , NVAUTCRI = :CRET903.NVAUTCRI
                         , NVAUTLER = :CRET903.NVAUTLER
                         , NVAUTIMP = :CRET903.NVAUTIMP
                         , NVAUTMDF = :CRET903.NVAUTMDF
                         , NVAUTANL = :CRET903.NVAUTANL
                         , IDPRCDEP = :CRET903.IDPRCDEP
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET903.IDUTLMDF
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL
                       AND DHMDFREG = :CRET903.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.CRET903"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET904          SECTION.

           EXEC SQL UPDATE CRET904
                       SET DSPRCAPL = :CRET904.DSPRCAPL
                         , IDAPLICA = :CRET904.IDAPLICA
                         , IDPRGGST = :CRET904.IDPRGGST
                         , IDDIALOG = :CRET904.IDDIALOG
                         , SNPGNHZ1 = :CRET904.SNPGNHZ1
                         , SNPGNHZ2 = :CRET904.SNPGNHZ2
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET904.IDUTLMDF
                     WHERE IDPRCAPL = :CRET904.IDPRCAPL
                       AND DHMDFREG = :CRET904.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.CRET904"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET903          SECTION.

           EXEC SQL DELETE FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL
                       AND DHMDFREG = :CRET903.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.CRET903"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET904          SECTION.

           EXEC SQL DELETE FROM CRET904
                     WHERE IDPRCAPL = :CRET904.IDPRCAPL
                       AND DHMDFREG = :CRET904.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP908.CRET904"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET906          SECTION.

           MOVE CREB958-IDPRCAPL-V    TO IDPRCAPL OF CRET906
           EXEC SQL DELETE FROM CRET906
                     WHERE IDPRCAPL = :CRET906.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET906" TO CREB000-NMOBJECT
               SET  CREB000-DELT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET905          SECTION.

           MOVE CREB958-IDPRCAPL-V    TO IDPRCAPL OF CRET905
           EXEC SQL DELETE FROM CRET905
                     WHERE IDPRCAPL = :CRET905.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET905" TO CREB000-NMOBJECT
               SET  CREB000-DELT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET907          SECTION.

           MOVE CREB958-IDPRCAPL-V    TO IDPRCORI OF CRET907
           EXEC SQL DELETE FROM CRET907
                     WHERE IDPRCORI = :CRET907.IDPRCORI        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP908.CRET907" TO CREB000-NMOBJECT
               SET  CREB000-DELT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP908 ------------------------------------*
