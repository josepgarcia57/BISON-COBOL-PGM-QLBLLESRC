       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BPCSCCIN   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BPC - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este Interface destina-se a Extrair Info   *
     š*                     Estática de Instrumentos e Contratos       *
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 EXECUTA-CURCTR             PIC 9(01).
          88 INICIO-CURCTR           VALUE 0.
          88 FIM-CURCTR              VALUE 1.
      *
       01 EXECUTA-CURMVT             PIC 9(01).
          88 INICIO-CURMVT           VALUE 0.
          88 FIM-CURMVT              VALUE 1.
      *
       01 EXISTE-INFO                PIC 9(01).
          88 SIM-EXISTE              VALUE 1.
          88 NAO-EXISTE              VALUE 0.
      *
       01 EXISTE-INFO-CTR            PIC 9(01).
          88 SIM-EXISTE-CTR          VALUE 1.
          88 NAO-EXISTE-CTR          VALUE 0.
      *
       01 ENVIADA-INFO               PIC 9(01).
          88 SIM-ENVIADA             VALUE 1.
          88 NAO-ENVIADA             VALUE 0.
      *
       01 EXISTE-ALTER               PIC 9(01).
          88 SIM-ALTER               VALUE 1.
          88 NAO-ALTER               VALUE 0.
      *
       01 F-ALTER                    PIC 9(01).
          88 SIM-FALTER              VALUE 1.
          88 NAO-FALTER              VALUE 0.
      *
       01 TIPO-TAXA                  PIC X(01).
          88 TAXA-FIXA               VALUE "F".
          88 TAXA-VAR                VALUE "V".
      *
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(8)    VALUE "BPCSCCIN".

     š* Indice para Pesquisa de String
       01 S-CHAR-L                   PIC S9(4)   COMP.
       01 S-CHAR-V                   PIC X(50).
       01 W-CHAR.
          03 W-NOMECHR               PIC X       OCCURS 50.

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
           COPY BPCBCINT.
           COPY BPCBCCIN3.
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".
       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA        PIC 9(04).
           03  FILLER              PIC X VALUE "-".
           03  WS-MESIN-DIA        PIC 9(02).
           03  FILLER              PIC X VALUE "-".
           03  WS-DIAIN-DIA        PIC 9(02).

       01 WS-TST-DATE              PIC X(10).
       01 WS-VAL-DATE              PIC X(10).
       01 WS-OUT-DATE              PIC X(10).

       01 WS-NURACI                PIC X(07).
       01 WS-MVDTA                 PIC S9(09).
       01 WS-MVDTAX                PIC S9(09).
       01 WS-BALCAO                PIC X(07).
       01 WS-BTGRE                 PIC X(03).
       01 WS-BTRUB                 PIC X(03).
       01 WS-BTMON                 PIC X(03).

     š* Definição de Contrato
     š* Se Não Houver Contrato CTID = '000' e CTCONT = Racine
       01 WS-CTR                   PIC X(32).
       01 WX-CTR    REDEFINES WS-CTR.
          05 WS-CTID-CTR           PIC X(03).
          05 WS-CTCONT-CTR         PIC X(07).

     š* Contrato
       01 WS-IDCONT                PIC X(32).
       01 WX-IDCONT REDEFINES WS-IDCONT.
          05 WS-CTR-CONT           PIC X(03).
          05 WS-FILLER1-CONT       PIC X(01).
          05 WS-SEQ-CONT           PIC X(07).

     š* Definição de Conta - Contrato No Fim se Houver
       01 WS-CONTA                 PIC X(32).
       01 WX-CONTA  REDEFINES WS-CONTA.
          05 WS-RACINE             PIC X(07).
          05 WS-GRE                PIC X(03).
          05 WS-RUB                PIC X(03).
          05 WS-MON                PIC X(03).
          05 WS-CTID               PIC X(03).
          05 WS-CTCONT             PIC X(07).

       01 WS-MON-ALFA              PIC X(03).
       01 WS-TBCODE                PIC X(03).

     š* Instrumento de Crédito
       01 WS-IDINST                PIC X(32).
       01 WX-IDINT  REDEFINES WS-IDINST.
          05 WS-RACINE-INST        PIC X(07).
          05 WS-FILLER1            PIC X(01).
          05 WS-GRE-INST           PIC X(03).
          05 WS-FILLER2            PIC X(01).
          05 WS-RUB-INST           PIC X(03).
          05 WS-FILLER3            PIC X(01).
          05 WS-MON-INST           PIC X(03).
          05 WS-FILLER4            PIC X(01).
          05 WS-CTID-INST          PIC X(03).
          05 WS-FILLER5            PIC X(01).
          05 WS-CTCONT-INST        PIC X(07).

       01 WS-TXJURD                PIC S9(3)V9(6) COMP-3.
       01 WS-SPRTXJURD             PIC S9(3)V9(6) COMP-3.
       01 WS-TXJURC                PIC S9(3)V9(6) COMP-3.
       01 WS-SPRTXJURC             PIC S9(3)V9(6) COMP-3.
       01 WS-DTINI-DESC            PIC S9(9) COMP-3.
       01 WW-DTINI-DESC            PIC X(9).
       01 WX-DTINI-DESC REDEFINES WW-DTINI-DESC.
          05 FILLER1               PIC X(1).
          05 DTANO-DESC            PIC X(4).
          05 DTMES-DESC            PIC X(2).
          05 DTDIA-DESC            PIC X(2).
       01 WS-TXDESC                PIC X(35).
       01 WS-CVTAB                 PIC X(03).
       01 WS-CVCOD                 PIC X(03).
       01 WS-CVMOED                PIC X(03).
       01 WS-CVPRZ                 PIC S9(02) COMP-3.
       01 WS-SALDO-BYVAL           PIC S9(13)V9(2).

     š* Copy : Tabela de Entidades
           EXEC SQL
               INCLUDE BPCHCENTN
           END-EXEC.

     š* Copy : Tabela de Instrumentos NOVA
           EXEC SQL
               INCLUDE BPCHCCINN
           END-EXEC.

     š* Copy : Tabela de Instrumentos ANTIGA
           EXEC SQL
               INCLUDE BPCHCCINO
           END-EXEC.

     š* Copy : Tabela de Clientes
       01  R-BPCT199.
           COPY DDS-ALL-FORMATS OF BPCT199.

     š* Copy : Tabela de Clientes
       01  R-FDBTAB.
           COPY DDS-ALL-FORMATS OF FDBTAB.

     š* Copy : Tabela de Taxas de Juros Contas Cash
       01  R-FDBBCT.
           COPY DDS-ALL-FORMATS OF FDBBCT.

     š* Copy : Tabela de Dados de Contrato Crédito
       01  R-FDBCTR.
           COPY DDS-ALL-FORMATS OF FDBCTR.

     š* Copy : Tabela de Saldos de Conta
       01  R-FDBCPT.
           COPY DDS-ALL-FORMATS OF FDBCPT.

     š* Copy : Tabela de Other Numbering
       01  R-FDBNUM.
           COPY DDS-ALL-FORMATS OF FDBNUM.

     š* Copy : Tabela de Movimentos Data Inversa
       01  R-FDBMVT.
           COPY DDS-ALL-FORMATS OF FDBMVT.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.

     š* Cursor de Contratos de um Produto
           EXEC SQL DECLARE CURCTR   CURSOR FOR
                SELECT  CTID
                     ,  CTCONT
                     ,  CTOPER
                     ,  CTNOPR
                     ,  CTDTOU
                     ,  CTDTPR
                     ,  CTDTLQ
                     ,  CTSTAT
                     ,  CTSAIP
                     ,  CTCNTP
                     ,  CTCENT
                     ,  CTRACI
                     ,  CTGRE
                     ,  CTRUB
                     ,  CTMON
                     ,  CTTYPE
                     ,  CTBACA
                     ,  CTDTEJ
                     ,  CTDTEM
                     ,  CTDTEA
                     ,  CTDJEX
                     ,  CTDMEX
                     ,  CTDAEX
                     ,  CTCRAC
                     ,  CTCGRE
                     ,  CTCRUB
                     ,  CTCMON
                     ,  CTDRAC
                     ,  CTDGRE
                     ,  CTDRUB
                     ,  CTDMON
                     ,  CTCAPI
                     ,  CTTAUX
                     ,  CTDTVJ
                     ,  CTDTVM
                     ,  CTDTVA
                     ,  CTECHJ
                     ,  CTECHM
                     ,  CTECHA
                     ,  CTFCHJ
                     ,  CTFCHM
                     ,  CTFCHA
                     ,  CTNBRJ
                     ,  CTSJOU
                     ,  CTINTE
                     ,  CTINML
                     ,  CTINTC
                     ,  CTCIMP
                     ,  CTIMPO
                     ,  CTCOMM
                     ,  CTTCOM
                     ,  CTCOMI
                     ,  CTFRAI
                     ,  CTOTAU
                     ,  CTOCAP
                     ,  CTODVJ
                     ,  CTODVM
                     ,  CTODVA
                     ,  CTODEJ
                     ,  CTODEM
                     ,  CTODEA
                     ,  CTTABA
                     ,  CTSIMA
                     ,  CTMARG
                     ,  CTRORI
                     ,  CTRGRP
                     ,  CTAMMT
                     ,  CTJPEN
                     ,  CTTPEN
                     ,  CTXCCA
                     ,  CTXCIN
                     ,  CTTAUV
                     ,  CTFRIN
                     ,  CTTYPT
                     ,  CTRFRQTAU

                FROM  FDBCTR A
                WHERE A.CTRACI = :WS-NURACI
                 AND  A.CTGRE  = :WS-BTGRE
                 AND  A.CTRUB  = :WS-BTRUB
                 AND  A.CTMON  = :WS-BTMON
           END-EXEC.

     š* Cursor de Contratos de um Produto
           EXEC SQL DECLARE CURMVT   CURSOR FOR
                SELECT  MVRACI
                     ,  MVGRE
                     ,  MVRUB
                     ,  MVMON
                     ,  MVDTVA
                     ,  MVSEQC
                     ,  MVMONT
                     ,  MVSOLD
                FROM FSVMVT
                WHERE MVDTVA <= :WS-MVDTA
                  AND MVRACI  = :WS-NURACI
                  AND MVGRE   = :WS-BTGRE
                  AND MVRUB   = :WS-BTRUB
                  AND MVMON   = :WS-BTMON
                 ORDER BY  MVDTVA  Desc, MVSEQC  Desc
           END-EXEC.

     š* Taxas de Juro de DOs para Descobertos
           EXEC SQL DECLARE CURTAXAS  CURSOR FOR
                 SELECT   CPRACI
                       ,  CPGRE
                       ,  CPRUB
                       ,  CPMON
                       ,  VALUE(BTDTVA , "0001-01-01" )
                       ,  VALUE(BTETAT , " " )
                       ,  VALUE(BTTYPE , " " )
                       ,  VALUE(BTTXMA , 0 )
                       ,  VALUE(BTSIGN , " " )
                       ,  CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "10" )
                              THEN VALUE(ICCDTD, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "10" )
                              THEN VALUE(ICCDTD * -1, 0 )
                            ELSE 0
                          END AS TXJURD
                       ,  CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "20")
                              THEN VALUE(ICCDTD, 0 )
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  = "30"
                              THEN VALUE(BTTXMA, 0 )

                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "20" )
                              THEN VALUE(ICCDTD * -1, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  = "30"
                              THEN VALUE(BTTXMA * -1, 0 )
                            ELSE 0
                          END AS SPRTXJURD
                       ,  CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "10")
                              THEN VALUE(ICCDTC, 0 )

                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "10")
                              THEN VALUE(ICCDTC * -1, 0 )
                            ELSE 0
                          END AS TXJURC
                       ,  CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "20" )
                              THEN VALUE(ICCDTC, 0 )
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  = "40"
                              THEN VALUE(BTTXMA, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "20" )
                              THEN VALUE(ICCDTC * -1, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  = "40"
                              THEN VALUE(BTTXMA * -1, 0 )
                            ELSE 0
                          END AS SPRTXJURC

                       ,  VALUE(TBDES1, " " )

                FROM FDBCPT B
                LEFT JOIN  FDBBCT A
                 ON (       A.BTRAC = B.CPRACI
                      AND   A.BTGRE = B.CPGRE
                      AND   A.BTRUB = B.CPRUB
                      AND   A.BTMON = B.CPMON
                      AND   A.BTDTVA = ( SELECT MAX(X.BTDTVA)
                                         FROM FDBBCT X
                                         WHERE A.BTRAC  =  X.BTRAC
                                          AND  A.BTGRE  =  X.BTGRE
                                          AND  A.BTRUB  =  X.BTRUB
                                          AND  A.BTMON  =  X.BTMON  ))

                LEFT JOIN FDBICF C
                 ON (     C.ICGRE  = a.btGRE
                      AND C.ICMON  = a.btMON
                      AND C.ICBASE = SUBSTR( DIGITS(A.BTTXMA) , 1, 3 )
                      AND C.ICDTVA = ( SELECT MAX(ICDTVA)
                                       FROM FDBICF Y
                                        WHERE Y.ICGRE  = C.ICGRE
                                         AND  Y.ICMON  = C.ICMON
                                         AND  Y.ICBASE = C.ICBASE ))

                LEFT JOIN FDBTAB D
                 ON (      D.TBID   = "080"
                       AND D.TBCODE = A.BTTXMA)
                WHERE
                      CPETAT =  " "
                  AND BTRAC  = :WS-NURACI
                  AND BTGRE  = :WS-BTGRE
                  AND BTRUB  = :WS-BTRUB
                  AND BTMON  = :WS-BTMON
           END-EXEC.
           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
           COPY BPCBCCIN.
      *================================================================*
       PROCEDURE DIVISION USING BPCBCCIN-LKPARMS.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P001-INICIO
     š*- Obtem Saldos Cash Accounts (Saldo Contab. Agregado Contrato)
           PERFORM P300-GET-FDBCPT.
           IF NAO-EXISTE
             PERFORM P999-FIMPGM
           ELSE
     š*- Obtem Taxas de Juro Se DO
           IF BPCBCCIN-CPGRE  =  "001" or "040"
             PERFORM P300-GET-TAXAS-JURO
           END-IF.
     š*- Havendo Contrato Desagrega Saldos Por Contrato ( Mais que um )
           PERFORM P100-OPEN-CURSOR-CURCTR
           PERFORM P200-FETCH-CURSOR-CURCTR
           PERFORM P300-PROCESSO-CURCTR
                   WITH TEST AFTER
                           UNTIL FIM-CURCTR
           PERFORM P400-CLOSE-CURSOR-CURCTR
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P001-INICIO.
     š*---------------------------------------------------------------------
           INITIALIZE  NEWCCIN-REC
                    ,  OLDCCIN-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           INITIALIZE  R-FDBCTR
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           MOVE  ZEROS            TO CTTAUX OF R-FDBCTR.
           MOVE "000"             TO WS-CTID-CTR.
           MOVE BPCBCCIN-RACINE   TO WS-CTCONT-CTR.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.
           MOVE ZERO TO EXISTE-ALTER.
     š*-
     š*----------------------------------------------------------------
       P300-GET-FDBCPT.
     š*----------------------------------------------------------------
     š*-
           INITIALIZE  R-FDBCPT
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
     š*- Obtem Informação Mestre de Saldos
           MOVE  BPCBCCIN-RACINE  TO WS-NURACI
           MOVE  BPCBCCIN-CPGRE   TO WS-BTGRE
           MOVE  BPCBCCIN-CPRUB   TO WS-BTRUB
           MOVE  BPCBCCIN-CPMON   TO WS-BTMON

           EXEC SQL
                SELECT  CPRACI
                     ,  CPGRE
                     ,  CPRUB
                     ,  CPMON
                     ,  CPETAT
                     ,  CPDTOU
                     ,  CPDTMU
                     ,  CPDTAN
                     ,  CPNBRM
                     ,  CPTRUB
                     ,  CPACPT
                     ,  CPNATU
                     ,  CPFRBO
                     ,  CPMLIM
                     ,  CPDVDU
                     ,  CPDVAU
                     ,  CPFRAM
                     ,  CPMTAM
                     ,  CPDTAM
                     ,  CPCPTI
                     ,  CPCPTC
                     ,  CPCPTF
                     ,  CPBCAL
                     ,  CPDVLT
                     ,  CPDCBA
                     ,  CPDTBA
                     ,  CPDSIG
                     ,  CPDMAR
                     ,  CPDEFF
                     ,  CPCCBA
                     ,  CPCTBA
                     ,  CPCSIG
                     ,  CPCMAR
                     ,  CPCEFF
                     ,  CPCCOM
                     ,  CPCBCL
                     ,  CPAVOF
                     ,  CPECHD
                     ,  CPPRIF
                     ,  CPCOFI
                     ,  CPCDOR
                     ,  CPDDOR
                     ,  CPFAVI
                     ,  CPFREL
                     ,  CPFEXT
                     ,  CPFANU
                     ,  CPFBCL
                     ,  CPFCOG
                     ,  CPFCOM
                     ,  CPSOLD
                     ,  CPSMLC

                INTO   :CPRACI
                     , :CPGRE
                     , :CPRUB
                     , :CPMON
                     , :CPETAT
                     , :CPDTOU
                     , :CPDTMU
                     , :CPDTAN
                     , :CPNBRM
                     , :CPTRUB
                     , :CPACPT
                     , :CPNATU
                     , :CPFRBO
                     , :CPMLIM
                     , :CPDVDU
                     , :CPDVAU
                     , :CPFRAM
                     , :CPMTAM
                     , :CPDTAM
                     , :CPCPTI
                     , :CPCPTC
                     , :CPCPTF
                     , :CPBCAL
                     , :CPDVLT
                     , :CPDCBA
                     , :CPDTBA
                     , :CPDSIG
                     , :CPDMAR
                     , :CPDEFF
                     , :CPCCBA
                     , :CPCTBA
                     , :CPCSIG
                     , :CPCMAR
                     , :CPCEFF
                     , :CPCCOM
                     , :CPCBCL
                     , :CPAVOF
                     , :CPECHD
                     , :CPPRIF
                     , :CPCOFI
                     , :CPCDOR
                     , :CPDDOR
                     , :CPFAVI
                     , :CPFREL
                     , :CPFEXT
                     , :CPFANU
                     , :CPFBCL
                     , :CPFCOG
                     , :CPFCOM
                     , :CPSOLD
                     , :CPSMLC
                FROM  FDBCPT A
                WHERE A.CPRACI = :WS-NURACI
                 AND  A.CPGRE  = :WS-BTGRE
                 AND  A.CPRUB  = :WS-BTRUB
                 AND  A.CPMON  = :WS-BTMON
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET SIM-EXISTE           TO TRUE
           WHEN SQLCODE-NOTFOUND
              SET NAO-EXISTE           TO TRUE
           WHEN OTHER
              MOVE "FDBCPT "           TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURCTR.
     š*---------------------------------------------------------------------
     š*-
           INITIALIZE  R-FDBCTR
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           MOVE  ZEROS            TO CTTAUX OF R-FDBCTR.
           MOVE "000"             TO WS-CTID-CTR.
           MOVE BPCBCCIN-RACINE   TO WS-CTCONT-CTR.

           EXEC SQL
                OPEN CURCTR
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-CURCTR       TO TRUE
           WHEN OTHER
              SET  FIM-CURCTR          TO TRUE
              MOVE "CURCTR"            TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*---------------------------------------------------------------------
       P200-FETCH-CURSOR-CURCTR.
     š*---------------------------------------------------------------------
     š*-
           EXEC SQL FETCH CURCTR
                INTO   :CTID
                     , :CTCONT
                     , :CTOPER
                     , :CTNOPR
                     , :CTDTOU
                     , :CTDTPR
                     , :CTDTLQ
                     , :CTSTAT
                     , :CTSAIP
                     , :CTCNTP
                     , :CTCENT
                     , :CTRACI
                     , :CTGRE
                     , :CTRUB
                     , :CTMON
                     , :CTTYPE
                     , :CTBACA
                     , :CTDTEJ
                     , :CTDTEM
                     , :CTDTEA
                     , :CTDJEX
                     , :CTDMEX
                     , :CTDAEX
                     , :CTCRAC
                     , :CTCGRE
                     , :CTCRUB
                     , :CTCMON
                     , :CTDRAC
                     , :CTDGRE
                     , :CTDRUB
                     , :CTDMON
                     , :CTCAPI
                     , :CTTAUX
                     , :CTDTVJ
                     , :CTDTVM
                     , :CTDTVA
                     , :CTECHJ
                     , :CTECHM
                     , :CTECHA
                     , :CTFCHJ
                     , :CTFCHM
                     , :CTFCHA
                     , :CTNBRJ
                     , :CTSJOU
                     , :CTINTE
                     , :CTINML
                     , :CTINTC
                     , :CTCIMP
                     , :CTIMPO
                     , :CTCOMM
                     , :CTTCOM
                     , :CTCOMI
                     , :CTFRAI
                     , :CTOTAU
                     , :CTOCAP
                     , :CTODVJ
                     , :CTODVM
                     , :CTODVA
                     , :CTODEJ
                     , :CTODEM
                     , :CTODEA
                     , :CTTABA
                     , :CTSIMA
                     , :CTMARG
                     , :CTRORI
                     , :CTRGRP
                     , :CTAMMT
                     , :CTJPEN
                     , :CTTPEN
                     , :CTXCCA
                     , :CTXCIN
                     , :CTTAUV
                     , :CTFRIN
                     , :CTTYPT
                     , :CTRFRQTAU
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
     š*- Formata Instrumento c/Contrato
             MOVE  SPACES TO WS-CONTA
             MOVE  BPCBCCIN-RACINE    TO WS-RACINE
             MOVE  BPCBCCIN-CPGRE     TO WS-GRE
             MOVE  BPCBCCIN-CPRUB     TO WS-RUB
             MOVE  BPCBCCIN-CPMON     TO WS-MON
     š*- CTR
             MOVE  CTCONT OF R-FDBCTR TO WS-CTCONT
                                         WS-CTCONT-CTR
             MOVE  CTID   OF R-FDBCTR TO WS-CTID
                                         WS-CTID-CTR
             SET  SIM-EXISTE-CTR      TO TRUE
           WHEN SQLCODE-NOTFOUND
             SET  FIM-CURCTR          TO TRUE
             SET  NAO-EXISTE-CTR      TO TRUE
     š*- Formata Contrato
             MOVE  SPACES TO WS-CONTA
     š*- Formata Instrumento s/Contrato
             MOVE  BPCBCCIN-RACINE    TO WS-RACINE
             MOVE  BPCBCCIN-CPGRE     TO WS-GRE
             MOVE  BPCBCCIN-CPRUB     TO WS-RUB
             MOVE  BPCBCCIN-CPMON     TO WS-MON
     š*- CTR a Espacos
             MOVE  SPACES             TO WS-CTR WS-CTID WS-CTCONT
             MOVE  "000"              TO WS-CTID-CTR
             MOVE  BPCBCCIN-RACINE    TO WS-CTCONT-CTR
           WHEN OTHER
             SET   FIM-CURCTR         TO TRUE
             MOVE "CURCTR"            TO W-OBJECT-NAME
             SET   CMD-FETCH-CURSOR   TO TRUE
             PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P300-PROCESSO-CURCTR.
     š*----------------------------------------------------------------
     š*-
           IF BPCBCCIN-PROC-OK
              PERFORM P400-MOVE-BPCJCCIN
              PERFORM P500-TESTA-BPCJCCIN
           END-IF

     š*-  Processo que vai ler os Dados da Tabela Externa
           IF BPCBCCIN-PROC-OK
             PERFORM P400-CALL-BPCSCCIN3
           END-IF

           IF BPCBCCIN-PROC-OK
             PERFORM P500-TRATA-BPCJCCIN
           END-IF.

           IF BPCBCCIN-PROC-OK
             PERFORM P500-TRATA-BPCJCINT
           END-IF.
     š*- Contrato Seguinte
           PERFORM P200-FETCH-CURSOR-CURCTR.
     š*-
     š*----------------------------------------------------------------
       P400-CLOSE-CURSOR-CURCTR.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
               CLOSE CURCTR
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET     FIM-CURCTR       TO TRUE
           WHEN OTHER
              SET     FIM-CURCTR       TO TRUE
              MOVE "CURCTR"            TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*---------------------------------------------------------------------
       P450-OPEN-CURSOR-CURMVT.
     š*---------------------------------------------------------------------
     š*-
           INITIALIZE  R-FDBMVT
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURMVT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-CURMVT       TO TRUE
           WHEN OTHER
              SET  FIM-CURMVT          TO TRUE
              MOVE "CURMVT"            TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*---------------------------------------------------------------------
       P450-FETCH-CURSOR-CURMVT.
     š*---------------------------------------------------------------------
     š*-
           EXEC SQL FETCH CURMVT
                INTO   :MVRACI
                     , :MVGRE
                     , :MVRUB
                     , :MVMON
                     , :MVDTVA
                     , :MVSEQC
                     , :MVMONT
                     , :MVSOLD
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             COMPUTE  WS-SALDO-BYVAL  = WS-SALDO-BYVAL - MVMONT
             IF WS-SALDO-BYVAL >= 0
               SET FIM-CURMVT         TO TRUE
             END-IF
           WHEN SQLCODE-NOTFOUND
             SET  FIM-CURMVT          TO TRUE
           WHEN OTHER
             SET  FIM-CURMVT          TO TRUE
             MOVE "CURMVT"            TO W-OBJECT-NAME
             SET  CMD-FETCH-CURSOR    TO TRUE
             PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P450-CLOSE-CURSOR-CURMVT.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
               CLOSE CURMVT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET     FIM-CURMVT       TO TRUE
           WHEN OTHER
              SET     FIM-CURMVT       TO TRUE
              MOVE "CURMVT"            TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P300-GET-TAXAS-JURO.
     š*----------------------------------------------------------------
     š*-
     š*- FDBBCT - Código da Taxa de Juro ( BTTXMA )
     š*-                                 ( BTTYPE = 10 Base / 20 Spread
     š*-                                            30 +Deb / 40 -Cred
     š*- FDBICF - Valor da Taxa e Spread por data   ICCDTD - Debito
     š*-                                            ICCDTC - Credito
     š*- FDBTAB = 80 - Descritivo Taxa Juro ( Cod Base )
     š*-
           MOVE  BPCBCCIN-RACINE  TO WS-NURACI
           MOVE  BPCBCCIN-CPGRE   TO WS-BTGRE
           MOVE  BPCBCCIN-CPRUB   TO WS-BTRUB
           MOVE  BPCBCCIN-CPMON   TO WS-BTMON

           EXEC SQL
                 SELECT   VALUE(SUM(CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "10" )
                              THEN VALUE(ICCDTD, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "10" )
                              THEN VALUE(ICCDTD * -1, 0 )
                            ELSE 0
                            END ) , 0 ) AS TXJURD
                       ,  VALUE(SUM(CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "20")
                              THEN VALUE(ICCDTD, 0 )
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  = "30"
                              THEN VALUE(BTTXMA, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "20" )
                              THEN VALUE(ICCDTD * -1, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  = "30"
                              THEN VALUE(BTTXMA * -1, 0 )
                            ELSE 0
                            END ) , 0 ) AS SPRTXJURD
                       ,  VALUE(SUM(CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "10")
                              THEN VALUE(ICCDTC, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "10")
                              THEN VALUE(ICCDTC * -1, 0 )
                            ELSE 0
                            END ) , 0 ) AS TXJURC
                       ,  VALUE(SUM(CASE
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  IN ( "20" )
                              THEN VALUE(ICCDTC, 0 )
                            WHEN (BTSIGN = ""
                              OR  BTSIGN ="+" )
                              AND BTTYPE  = "40"
                              THEN VALUE(BTTXMA, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  IN ( "20" )
                              THEN VALUE(ICCDTC * -1, 0 )
                            WHEN BTSIGN = "-"
                              AND BTTYPE  = "40"
                              THEN VALUE(BTTXMA * -1, 0 )
                            ELSE 0
                            END) , 0 ) AS SPRTXJURC
                       ,  VALUE(MAX(VALUE(TBDES1, " " )) , " " )
                          AS DESC
                INTO    :WS-TXJURD
                      , :WS-SPRTXJURD
                      , :WS-TXJURC
                      , :WS-SPRTXJURC
                      , :WS-TXDESC

                FROM FDBCPT B
                LEFT JOIN  FDBBCT A
                 ON (       A.BTRAC = B.CPRACI
                      AND   A.BTGRE = B.CPGRE
                      AND   A.BTRUB = B.CPRUB
                      AND   A.BTMON = B.CPMON
                      AND   A.BTDTVA = ( SELECT MAX(X.BTDTVA)
                                         FROM FDBBCT X
                                         WHERE A.BTRAC  =  X.BTRAC
                                          AND  A.BTGRE  =  X.BTGRE
                                          AND  A.BTRUB  =  X.BTRUB
                                          AND  A.BTMON  =  X.BTMON  ))

                INNER JOIN FDBICF C
                 ON (     C.ICGRE  = a.btGRE
                      AND C.ICMON  = a.btMON
                      AND C.ICBASE = SUBSTR( DIGITS(A.BTTXMA) , 1, 3 )
                      AND C.ICDTVA = ( SELECT MAX(ICDTVA)
                                       FROM FDBICF Y
                                        WHERE Y.ICGRE  = C.ICGRE
                                         AND  Y.ICMON  = C.ICMON
                                         AND  Y.ICBASE = C.ICBASE ))

                LEFT JOIN FDBTAB D
                 ON (      D.TBID   = "080"
                       AND D.TBCODE = A.BTTXMA)
                WHERE
                      CPETAT =  " "
                  AND BTRAC  = :WS-NURACI
                  AND BTGRE  = :WS-BTGRE
                  AND BTRUB  = :WS-BTRUB
                  AND BTMON  = :WS-BTMON
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET SIM-EXISTE           TO TRUE
           WHEN SQLCODE-NOTFOUND
              CONTINUE
           WHEN OTHER
              MOVE "FDBICF "           TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-MOVE-BPCJCCIN.
     š*----------------------------------------------------------------
     š*- INFTINST
     š*- BPENV - E- Para Enviar
           MOVE "E"              TO NEWCCIN-BPENV
     š*- DTREFENT - data de Referencia
           MOVE BPCBCCIN-DTPROC  TO NEWCCIN-DTREFINST
     š*- Audit Log
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP       END-EXEC.
     š*- IDCONT  CTI/000  + SEQ/RACINE
           MOVE WS-CTID-CTR       TO  WS-CTR-CONT
           MOVE WS-CTCONT-CTR     TO  WS-SEQ-CONT
           MOVE "-"               TO WS-FILLER1-CONT
           MOVE WS-IDCONT         TO NEWCCIN-IDCONT
     š*- IDINST   CTRACI + CTGRE + CTRUB + CTMON
           MOVE WS-RACINE         TO WS-RACINE-INST
           MOVE WS-GRE            TO WS-GRE-INST
           MOVE WS-RUB            TO WS-RUB-INST
           MOVE WS-MON            TO WS-MON-INST
           MOVE WS-CTID           TO WS-CTID-INST
           MOVE WS-CTCONT         TO WS-CTCONT-INST
           MOVE "-"               TO WS-FILLER1
           MOVE "-"               TO WS-FILLER2
           MOVE "-"               TO WS-FILLER3
           IF WS-CTID NOT EQUAL SPACES
             MOVE "-"             TO WS-FILLER4
           END-IF
           IF WS-CTCONT NOT EQUAL SPACES
             MOVE "-"             TO WS-FILLER5
           END-IF
           MOVE WS-IDINST         TO NEWCCIN-IDINST
     š*- BALCAO
           MOVE CTCENT OF R-FDBCTR TO WS-BALCAO
           IF  WS-BALCAO      = SPACES  OR
                              = "0000001"
               MOVE "1"           TO WS-BALCAO
           END-IF
           MOVE WS-BALCAO         TO NEWCCIN-BALCAO
     š*- PROJFINAN
           MOVE "000"             TO NEWCCIN-PROJFINAN
     š*- IDCONTSIND
     š*- LITIGJUD  ( VERIFICAR FDBCLI.CLGERA  = "0001890" )
           MOVE "000"             TO NEWCCIN-LITIGJUD
     š*- IEB ( EUROSISTEMA )
     š*- PAISLEGIS
           MOVE "PRT"             TO NEWCCIN-PAISLEGIS
     š*- CANALCOMER
           MOVE "002"             TO NEWCCIN-CANALCOMER
     š*- CLAUSRENUN
           MOVE "000"             TO NEWCCIN-CLAUSRENUN
     š*- SUBVPROTOCOLO
           MOVE "000"             TO NEWCCIN-SUBVPROTOCOLO
     š*- REFEXTINST
           MOVE WS-IDINST         TO NEWCCIN-REFEXTINST
     š*- TPINST ( 0180 - OUTROS CREDITOS )
           EVALUATE WS-BTGRE
           WHEN "001"
             MOVE "0030"          TO NEWCCIN-TPINST
           WHEN "040"
             MOVE "0010"          TO NEWCCIN-TPINST
           WHEN OTHER
             MOVE "0050"          TO NEWCCIN-TPINST
           END-EVALUATE
     š*- MOEDA CONVERTE EM ALFA POR FDBTAB = 40
           PERFORM P300-MOEDA-ALFA
           MOVE WS-MON-ALFA          TO NEWCCIN-MOEDA
     š*- DTUTILFUND ( AAAA-MM-DD CTDAEX / CTDMEX /  CTDJEX )
           MOVE CTDAEX               TO WS-ANOIN  OF WX-DATA-CONVER
           MOVE CTDMEX               TO WS-MESIN  OF WX-DATA-CONVER
           MOVE CTDJEX               TO WS-DIAIN  OF WX-DATA-CONVER
           MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
           MOVE WS-DATA-CONVER       TO WS-TST-DATE
           PERFORM P450-VALIDA-DATE
           MOVE WS-OUT-DATE          TO NEWCCIN-DTUTILFUND
     š*- Sendo Descoberto Terá de se determinar a Data Início
     š*- DTINIINST (  AAAA-MM-DD CTDTEA / CTDTEM /  CTDTEJ )
           IF NAO-EXISTE-CTR
      *      PERFORM P400-DTINIC-DESC
             IF CPSOLD OF R-FDBCPT NOT EQUAL ZERO
               PERFORM P400-DTINIC-DESC-BYVAL
               MOVE WS-DTINI-DESC        TO WW-DTINI-DESC
               MOVE DTANO-DESC           TO WS-ANOIN  OF WX-DATA-CONVER
               MOVE DTMES-DESC           TO WS-MESIN  OF WX-DATA-CONVER
               MOVE DTDIA-DESC           TO WS-DIAIN  OF WX-DATA-CONVER
               MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
               MOVE WS-DATA-CONVER       TO WS-TST-DATE
               PERFORM P450-VALIDA-DATE
               MOVE WS-OUT-DATE          TO NEWCCIN-DTINIINST
             ELSE
               PERFORM P400-DTINIC-DESC-BYVAL1
               MOVE WS-DTINI-DESC        TO WW-DTINI-DESC
               MOVE DTANO-DESC           TO WS-ANOIN  OF WX-DATA-CONVER
               MOVE DTMES-DESC           TO WS-MESIN  OF WX-DATA-CONVER
               MOVE DTDIA-DESC           TO WS-DIAIN  OF WX-DATA-CONVER
               MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
               MOVE WS-DATA-CONVER       TO WS-TST-DATE
               PERFORM P450-VALIDA-DATE
               MOVE WS-OUT-DATE          TO NEWCCIN-DTINIINST
             END-IF
           ELSE
             MOVE CTDTOU OF R-FDBCTR(1:2) TO WS-DIAIN  OF WX-DATA-CONVER
             MOVE CTDTOU OF R-FDBCTR(3:2) TO WS-MESIN  OF WX-DATA-CONVER
             MOVE CTDTOU OF R-FDBCTR(5:2) TO WS-ANO-34
             MOVE "20"                    TO WS-ANO-12
             MOVE WX-ANO-CONVER           TO WS-ANOIN  OF WX-DATA-CONVER

             MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
             MOVE WS-DATA-CONVER       TO WS-TST-DATE
             PERFORM P450-VALIDA-DATE
             MOVE WS-OUT-DATE          TO NEWCCIN-DTINIINST
           END-IF
     š*- Sendo Descoberto Terá data de Maturidade Indefinida 999-12-31
     š*- DTORIMAT  - Data de Maturidade Original ( Echeance )
           IF NAO-EXISTE-CTR
             MOVE "9999-12-31"         TO NEWCCIN-DTORIMAT
           ELSE
             MOVE CTECHA               TO WS-ANOIN  OF WX-DATA-CONVER
             MOVE CTECHM               TO WS-MESIN  OF WX-DATA-CONVER
             MOVE CTECHJ               TO WS-DIAIN  OF WX-DATA-CONVER
             MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
             MOVE WS-DATA-CONVER       TO WS-TST-DATE
             PERFORM P450-VALIDA-DATE
             MOVE WS-OUT-DATE          TO NEWCCIN-DTORIMAT
           END-IF
     š*- Sendo Descoberto Terá data de Maturidade Indefinida 9999-12-31
     š*- DTMAT     - Data de Maturidade Actual
           IF NAO-EXISTE-CTR
             MOVE "9999-12-31"         TO NEWCCIN-DTMAT
           ELSE
             IF CTECHA NOT EQUAL "0000"
               MOVE CTECHA               TO WS-ANOIN  OF WX-DATA-CONVER
               MOVE CTECHM               TO WS-MESIN  OF WX-DATA-CONVER
               MOVE CTECHJ               TO WS-DIAIN  OF WX-DATA-CONVER
               MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
               MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
               MOVE WS-DATA-CONVER       TO WS-TST-DATE
               PERFORM P450-VALIDA-DATE
               MOVE WS-OUT-DATE          TO NEWCCIN-DTMAT
             ELSE
               MOVE CTFCHA               TO WS-ANOIN  OF WX-DATA-CONVER
               MOVE CTFCHM               TO WS-MESIN  OF WX-DATA-CONVER
               MOVE CTFCHJ               TO WS-DIAIN  OF WX-DATA-CONVER
               MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
               MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
               MOVE WS-DATA-CONVER       TO WS-TST-DATE
               PERFORM P450-VALIDA-DATE
               MOVE WS-OUT-DATE          TO NEWCCIN-DTMAT
             END-IF
           END-IF
     š*- Caso Raro em que dtiniinst = dtmat
           IF SIM-EXISTE-CTR AND
              NEWCCIN-DTMAT = NEWCCIN-DTINIINST
              PERFORM P4000-GET-DTINIINST-BYMOV
              MOVE WS-DTINI-DESC        TO WW-DTINI-DESC
              MOVE DTANO-DESC           TO WS-ANOIN  OF WX-DATA-CONVER
              MOVE DTMES-DESC           TO WS-MESIN  OF WX-DATA-CONVER
              MOVE DTDIA-DESC           TO WS-DIAIN  OF WX-DATA-CONVER
              MOVE CORR  WX-DATA-CONVER TO WS-DATA-CONVER
              MOVE WS-DATA-CONVER       TO WS-TST-DATE
              PERFORM P450-VALIDA-DATE
              IF WS-OUT-DATE < NEWCCIN-DTINIINST AND
                 WS-OUT-DATE NOT EQUAL "0001-01-01"
                 MOVE WS-OUT-DATE       TO NEWCCIN-DTINIINST
              END-IF
           END-IF
     š*- DTINICARJUR
     š*- DTFIMCARJUR
     š*- DTINICARCAP
     š*- DTFIMCARCAP
     š*- DIRREEMBIME - So para as colectivas
           MOVE SPACES               TO NEWCCIN-DIRREEMBLME
           PERFORM P500-GET-BPCJCENT
           IF NEWCENT-TPENT = "001"
             MOVE "001"                TO NEWCCIN-DIRREEMBLME
           END-IF
     š*- RECURSO
           MOVE "000"                 TO NEWCCIN-RECURSO
     š*- TXREF indexante Necessita Conversão
           IF NAO-EXISTE-CTR
             PERFORM P400-INDEXANTE-DO
             IF NOT TAXA-FIXA AND
                BTTXMA NOT EQUAL ZERO
               MOVE "TXR"             TO WS-CVTAB
               MOVE BTTXMA            TO WS-CVCOD
               MOVE BPCBCCIN-CPMON    TO WS-CVMOED
               MOVE ZERO              TO WS-CVPRZ
               PERFORM P400-CONVERTE-BP
               IF  CODBP NOT EQUAL SPACES
                 MOVE CODBP           TO NEWCCIN-TXREF
                 SET TAXA-VAR         TO TRUE
               ELSE
                 MOVE " "            TO NEWCCIN-TXREF
                 SET TAXA-FIXA        TO TRUE
               END-IF
             ELSE
               SET TAXA-FIXA          TO TRUE
               MOVE " "               TO NEWCCIN-FREQATUALIZTX
             END-IF
           END-IF
     š*- TXREF indexante Necessita Conversão
           IF SIM-EXISTE-CTR
             IF (CTTAUV =  "Y") AND ( CTTYPT NOT EQUAL " " )
               MOVE "TXR"             TO WS-CVTAB
               MOVE CTTYPT            TO WS-CVCOD
               EVALUATE TRUE
                 WHEN  WS-CVCOD(1:1) = "L"
                  MOVE "LIB"   TO  WS-CVCOD
                 WHEN  WS-CVCOD(1:1) = "E"
                  MOVE "EUR"   TO  WS-CVCOD
                 WHEN OTHER
                  MOVE "EUR"   TO  WS-CVCOD
               END-EVALUATE
               MOVE BPCBCCIN-CPMON    TO WS-CVMOED
               MOVE CTRFRQTAU         TO WS-CVPRZ
               PERFORM P400-CONVERTE-BP
               IF  CODBP EQUAL SPACES
                 SET TAXA-FIXA          TO TRUE
                 MOVE " "               TO NEWCCIN-TXREF
               ELSE
                 MOVE  CODBP            TO NEWCCIN-TXREF
                 SET TAXA-VAR           TO TRUE
               END-IF
             ELSE
               SET TAXA-FIXA          TO TRUE
               MOVE " "               TO NEWCCIN-TXREF
             END-IF
           END-IF

     š*- TPTXJUR  001 - Variável / 002 - Fixa / 003 - Mista / 004 - Out
     š*- Descoberto a Taxa pode ser Fixa ou Não ( BTTYPE de  FDBBCT )
           IF TAXA-FIXA
             MOVE  "002"            TO NEWCCIN-TPTXJUR
             MOVE  "000"            TO NEWCCIN-FREQATUALIZTX
           ELSE
     š*- FREQATUALIZTX Rel.com a CTRFRQTAU
             IF SIM-EXISTE-CTR AND NOT TAXA-FIXA
               EVALUATE CTRFRQTAU
                 WHEN 60 MOVE "001"    TO NEWCCIN-FREQATUALIZTX
                 WHEN 12 MOVE "002"    TO NEWCCIN-FREQATUALIZTX
                 WHEN  4 MOVE "003"    TO NEWCCIN-FREQATUALIZTX
                 WHEN  2 MOVE "004"    TO NEWCCIN-FREQATUALIZTX
                 WHEN  1 MOVE "005"    TO NEWCCIN-FREQATUALIZTX
                 WHEN  OTHER
                       MOVE "007"    TO NEWCCIN-FREQATUALIZTX
               END-EVALUATE
             ELSE
               IF TAXA-FIXA
                 MOVE "000"    TO NEWCCIN-FREQATUALIZTX
               ELSE
                 MOVE "001"    TO NEWCCIN-FREQATUALIZTX
               END-IF
             END-IF
           END-IF
     š*- SPREAD
           IF NAO-EXISTE-CTR
              MOVE WS-SPRTXJURD     TO NEWCCIN-SPREAD
           ELSE
              MOVE CTMARG           TO NEWCCIN-SPREAD
           END-IF
     š*- TXMAX
     š*- TXMIN
     š*- TAEG
           IF NAO-EXISTE-CTR
             MOVE WS-TXJURD         TO NEWCCIN-TAEG
           ELSE
             MOVE CTTAUX            TO NEWCCIN-TAEG
           END-IF
     š*- TAE
           IF NAO-EXISTE-CTR
             MOVE WS-TXJURD         TO NEWCCIN-TAE
           ELSE
             MOVE CTTAUX            TO NEWCCIN-TAE
           END-IF
     š*- PERFIXTX - Dias para Actualização da Taxa ( Ver Ind. e Inst)
           IF NEWCCIN-TPTXJUR = "001"
             MOVE 30                TO NEWCCIN-PERFIXTX
           ELSE
             MOVE  999999           TO NEWCCIN-PERFIXTX
           END-IF
     š*- DURPLANOFIN
     š*- FINALIDADE
           MOVE "0000"              TO NEWCCIN-FINALIDADE
     š*- TPAMORT  ( para Crédito Habitação )
     š*- DIVSUBOR
     š*- INSTFIDUC
           MOVE "006"             TO NEWCCIN-TPAMORT
           MOVE "000"             TO NEWCCIN-DIVSUBOR
           MOVE "000"             TO NEWCCIN-INSTFIDUC
     š*- FREQPAGAM
           IF NAO-EXISTE-CTR
             MOVE  "007"            TO NEWCCIN-FREQPAGAM
           ELSE
             EVALUATE TRUE
     š*- Anual
             WHEN CTFRIN ="01"
               MOVE  "004"          TO  NEWCCIN-FREQPAGAM
     š*- Semestral
             WHEN CTFRIN ="02"
               MOVE  "003"          TO  NEWCCIN-FREQPAGAM
     š*- Trimestral
             WHEN CTFRIN ="04"
               MOVE  "002"          TO  NEWCCIN-FREQPAGAM
     š*- Mensal
             WHEN CTFRIN ="12"
               MOVE  "001"          TO  NEWCCIN-FREQPAGAM
     š*- Irregular
             WHEN OTHER
               MOVE  "007"          TO  NEWCCIN-FREQPAGAM
             END-EVALUATE
           END-IF.
     š*- MONTINI
           IF SIM-EXISTE-CTR
             MOVE CTCAPI            TO NEWCCIN-MONTINI
             MOVE WS-IDCONT         TO NEWCCIN-IDCONT
           END-IF
     š*- VARFV
     š*- DTRENEG
     š*- TPNEG 001 - Totalmente Nova
           MOVE "001"               TO NEWCCIN-TPNEG
     š*- PERCDIFCAP
     š*- TPTITULARIZ
           MOVE "000"             TO NEWCCIN-TPTITULARIZ.
     š*- SEGUROS
           MOVE S-SYSTMST         TO NEWCCIN-TMSCRIA.
           MOVE "BPCSCCIN"        TO NEWCCIN-USERCRIA.
     š*- "002" - FIXA
           IF NEWCCIN-TXREF   = " "   AND
              NEWCCIN-TPTXJUR = "001"
             MOVE  "000"            TO NEWCCIN-FREQATUALIZTX
             MOVE  "002"            TO NEWCCIN-TPTXJUR
           END-IF.
           IF NEWCCIN-TXREF NOT EQUAL SPACES  AND
              NEWCCIN-TPTXJUR = " "
             MOVE  "001"            TO NEWCCIN-TPTXJUR
           END-IF.
           IF NEWCCIN-TXREF NOT EQUAL SPACES  AND
              NEWCCIN-TPTXJUR  = "002"
             MOVE  "001"            TO NEWCCIN-TPTXJUR
           END-IF.
           IF NEWCCIN-TXREF EQUAL SPACES
             MOVE  "000"            TO NEWCCIN-FREQATUALIZTX
             MOVE  "002"            TO NEWCCIN-TPTXJUR
           END-IF.
           IF NEWCCIN-FREQPAGAM = " "  AND SIM-EXISTE-CTR
             MOVE "007"    TO NEWCCIN-FREQPAGAM
           END-IF.
           IF NEWCCIN-DTUTILFUND  = "0001-01-01"
             MOVE NEWCCIN-DTINIINST    TO NEWCCIN-DTUTILFUND
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P500-GET-BPCJCENT.
     š*----------------------------------------------------------------
     š*-
           MOVE WS-RACINE   TO NEWCENT-REFEXTENT

           EXEC SQL
             SELECT  IDENT
                   , TPENT
                   , REFEXTENT
             INTO
                    :NEWCENT-IDENT
                   ,:NEWCENT-TPENT
                   ,:NEWCENT-REFEXTENT
             FROM  BPCJCENT
               WHERE REFEXTENT= :NEWCENT-REFEXTENT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN SQLCODE-NOTFOUND
              CONTINUE
           WHEN OTHER
              MOVE "BPCJCENT"          TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-TRATA-BPCJCCIN.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe
     š*-
           IF SIM-ENVIADA
             IF SIM-ALTER OR SIM-ALTER-1
                MOVE "CIU"  TO NEWCCIN-INFTINST
                PERFORM P500-INSERT-HIST-BPCJCCIN
                PERFORM P500-UPDATE-BPCJCCIN
             ELSE
                CONTINUE
             END-IF
           ELSE
             MOVE "CII"    TO NEWCCIN-INFTINST
               PERFORM P500-INSERT-BPCJCCIN
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P500-TRATA-BPCJCINT.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe
           INITIALIZE                 BPCBCINT-LKPARMS.
           MOVE WS-IDCONT         TO  BPCBCINT-IDCONT
           MOVE WS-IDINST         TO  BPCBCINT-IDINST
           MOVE WS-NURACI         TO  BPCBCINT-RACINE
           MOVE WS-BTGRE          TO  BPCBCINT-CPGRE
           MOVE WS-BTRUB          TO  BPCBCINT-CPRUB
           MOVE WS-BTMON          TO  BPCBCINT-CPMON
           MOVE WS-BTGRE          TO  BPCBCINT-CTID
           MOVE WS-CTCONT         TO  BPCBCINT-CTCONT
           MOVE WS-DATA-DIA       TO  BPCBCINT-DTPROC

           CALL "BPCSCINT" USING  BPCBCINT-LKPARMS
           IF BPCBCCIN-PROC-OK
              CONTINUE
           ELSE
             INITIALIZE   KYCT900A
             MOVE WS-RACINE         TO RACINE   OF  KYCT900A
             MOVE BPCBCINT-DESCERR  TO ERRMSG   OF  KYCT900A
             MOVE BPCBCINT-CODERR   TO ERRCOD   OF  KYCT900A
             MOVE "BPCSCINT"        TO ERRPRG   OF  KYCT900A
             MOVE "BPC"             TO ERRAPL   OF  KYCT900A
             MOVE BPCBCINT-IDMSGEXE TO ERROBJ   OF  KYCT900A
             MOVE  1                TO ERRRTC   OF  KYCT900A
             PERFORM P995-INSERT-ERROS
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P500-TESTA-BPCJCCIN.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe

           MOVE WS-TMS-NULA  TO NEWCCIN-TMSALT
           MOVE "E"          TO NEWCCIN-BPENV

           EXEC SQL
             SELECT  DTREF
                   , BPENV
                   , INFTINST
                   , IDCONT
                   , IDINST
                   , BALCAO
                   , PROJFINAN
                   , IDCONTSIND
                   , LITIGJUD
                   , IEB
                   , PAISLEGIS
                   , CANALCOMER
                   , CLAUSRENUN
                   , SUBVPROT
                   , REFEXTINST
                   , TPINST
                   , MOEDA
                   , DTUTILFUND
                   , DTINIINST
                   , DTORIMAT
                   , DTMAT
                   , DTINICRJUR
                   , DTFIMCRJUR
                   , DTINICRCAP
                   , DTFIMCRCAP
                   , DIRREMBIM
                   , RECURSO
                   , TPTXJUR
                   , FREQACTTX
                   , TXREF
                   , SPREAD
                   , TXMAX
                   , TXMIN
                   , TAEG
                   , TAE
                   , PERFIXTX
                   , DURPLNFIN
                   , FINALIDADE
                   , TPAMORT
                   , FREQPAGAM
                   , DIVSUBOR
                   , INSTFIDUC
                   , MONTINI
                   , VARFV
                   , DTRENEG
                   , TPNEG
                   , PERCDIFCAP
                   , TPTITUL
                   , SEGUROS
                   , TMSCRIA
                   , USERCRIA
                   , TMSALT
                   , USERALT
             INTO   :OLDCCIN-DTREFINST
                   ,:OLDCCIN-BPENV
                   ,:OLDCCIN-INFTINST
                   ,:OLDCCIN-IDCONT
                   ,:OLDCCIN-IDINST
                   ,:OLDCCIN-BALCAO
                   ,:OLDCCIN-PROJFINAN
                   ,:OLDCCIN-IDCONTSIND
                   ,:OLDCCIN-LITIGJUD
                   ,:OLDCCIN-IEB
                   ,:OLDCCIN-PAISLEGIS
                   ,:OLDCCIN-CANALCOMER
                   ,:OLDCCIN-CLAUSRENUN
                   ,:OLDCCIN-SUBVPROTOCOLO
                   ,:OLDCCIN-REFEXTINST
                   ,:OLDCCIN-TPINST
                   ,:OLDCCIN-MOEDA
                   ,:OLDCCIN-DTUTILFUND
                   ,:OLDCCIN-DTINIINST
                   ,:OLDCCIN-DTORIMAT
                   ,:OLDCCIN-DTMAT
                   ,:OLDCCIN-DTINICARJUR
                   ,:OLDCCIN-DTFIMCARJUR
                   ,:OLDCCIN-DTINICARCAP
                   ,:OLDCCIN-DTFIMCARCAP
                   ,:OLDCCIN-DIRREEMBLME
                   ,:OLDCCIN-RECURSO
                   ,:OLDCCIN-TPTXJUR
                   ,:OLDCCIN-FREQATUALIZTX
                   ,:OLDCCIN-TXREF
                   ,:OLDCCIN-SPREAD
                   ,:OLDCCIN-TXMAX
                   ,:OLDCCIN-TXMIN
                   ,:OLDCCIN-TAEG
                   ,:OLDCCIN-TAE
                   ,:OLDCCIN-PERFIXTX
                   ,:OLDCCIN-DURPLANOFIN
                   ,:OLDCCIN-FINALIDADE
                   ,:OLDCCIN-TPAMORT
                   ,:OLDCCIN-FREQPAGAM
                   ,:OLDCCIN-DIVSUBOR
                   ,:OLDCCIN-INSTFIDUC
                   ,:OLDCCIN-MONTINI
                   ,:OLDCCIN-VARFV
                   ,:OLDCCIN-DTRENEG
                   ,:OLDCCIN-TPNEG
                   ,:OLDCCIN-PERCDIFCAP
                   ,:OLDCCIN-TPTITULARIZ
                   ,:OLDCCIN-SEGUROS
                   ,:OLDCCIN-TMSCRIA
                   ,:OLDCCIN-USERCRIA
                   ,:OLDCCIN-TMSALT
                   ,:OLDCCIN-USERALT
             FROM  BPCJCCIN
               WHERE IDCONT   = :NEWCCIN-IDCONT
                AND  IDINST   = :NEWCCIN-IDINST

           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
     š*-Igual Campos para comparação Global
              SET   SIM-ENVIADA       TO TRUE
              MOVE  NEWCCIN-INFTINST  TO OLDCCIN-INFTINST
              MOVE  NEWCCIN-DTREFINST TO OLDCCIN-DTREFINST
              MOVE  NEWCCIN-BPENV     TO OLDCCIN-BPENV
              MOVE  NEWCCIN-TMSCRIA   TO OLDCCIN-TMSCRIA
              MOVE  NEWCCIN-USERCRIA  TO OLDCCIN-USERCRIA
              MOVE  NEWCCIN-TMSALT    TO OLDCCIN-TMSALT
              MOVE  NEWCCIN-USERALT   TO OLDCCIN-USERALT
              IF    NEWCCIN-REC    = OLDCCIN-REC
               SET NAO-ALTER TO TRUE
              ELSE
               SET SIM-ALTER TO TRUE
              END-IF
           WHEN SQLCODE-NOTFOUND
              SET   NAO-ENVIADA TO TRUE
              SET   NAO-ALTER   TO TRUE
           WHEN OTHER
              MOVE "BPCJCCIN"        TO W-OBJECT-NAME
              SET CMD-SELECT         TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-CALL-BPCSCCIN3.
     š*----------------------------------------------------------------
           INITIALIZE                 BPCBCCIN3-LKPARMS.
           MOVE WS-IDCONT         TO  BPCBCCIN3-IDCONT
           MOVE WS-IDINST         TO  BPCBCCIN3-IDINST
           MOVE WS-NURACI         TO  BPCBCCIN3-RACINE
           MOVE WS-BTGRE          TO  BPCBCCIN3-CPGRE
           MOVE WS-BTRUB          TO  BPCBCCIN3-CPRUB
           MOVE WS-BTMON          TO  BPCBCCIN3-CPMON
           MOVE WS-BTGRE          TO  BPCBCCIN3-CTID
           MOVE WS-CTCONT         TO  BPCBCCIN3-CTCONT
           MOVE WS-DATA-DIA       TO  BPCBCCIN3-DTPROC

           CALL "BPCSCCIN3" USING     BPCBCCIN3-LKPARMS
           IF BPCBCCIN3-PROC-OK
              CONTINUE
           ELSE
             INITIALIZE   KYCT900A
             MOVE BPCBCCIN3-DESCERR   TO ERRMSG   OF  KYCT900A
             MOVE BPCBCCIN3-CODERR    TO ERRCOD   OF  KYCT900A
             MOVE "BPCSCCIN3"         TO ERRPRG   OF  KYCT900A
             MOVE "BPC"               TO ERRAPL   OF  KYCT900A
             MOVE BPCBCCIN3-IDMSGEXE  TO ERROBJ   OF  KYCT900A
             MOVE  1                  TO ERRRTC   OF  KYCT900A
             PERFORM P995-INSERT-ERROS
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P500-INSERT-BPCJCCIN.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe
     š*-
           MOVE WS-TMS-NULA  TO NEWCCIN-TMSALT
           MOVE "E"          TO NEWCCIN-BPENV
           EXEC SQL
                 INSERT INTO BPCJCCIN
                 VALUES ( :NEWCCIN-DTREFINST
                        , :NEWCCIN-BPENV
                        , :NEWCCIN-INFTINST
                        , :NEWCCIN-IDCONT
                        , :NEWCCIN-IDINST
                        , :NEWCCIN-BALCAO
                        , :NEWCCIN-PROJFINAN
                        , :NEWCCIN-IDCONTSIND
                        , :NEWCCIN-LITIGJUD
                        , :NEWCCIN-IEB
                        , :NEWCCIN-PAISLEGIS
                        , :NEWCCIN-CANALCOMER
                        , :NEWCCIN-CLAUSRENUN
                        , :NEWCCIN-SUBVPROTOCOLO
                        , :NEWCCIN-REFEXTINST
                        , :NEWCCIN-TPINST
                        , :NEWCCIN-MOEDA
                        , :NEWCCIN-DTUTILFUND
                        , :NEWCCIN-DTINIINST
                        , :NEWCCIN-DTORIMAT
                        , :NEWCCIN-DTMAT
                        , :NEWCCIN-DTINICARJUR
                        , :NEWCCIN-DTFIMCARJUR
                        , :NEWCCIN-DTINICARCAP
                        , :NEWCCIN-DTFIMCARCAP
                        , :NEWCCIN-DIRREEMBLME
                        , :NEWCCIN-RECURSO
                        , :NEWCCIN-TPTXJUR
                        , :NEWCCIN-FREQATUALIZTX
                        , :NEWCCIN-TXREF
                        , :NEWCCIN-SPREAD
                        , :NEWCCIN-TXMAX
                        , :NEWCCIN-TXMIN
                        , :NEWCCIN-TAEG
                        , :NEWCCIN-TAE
                        , :NEWCCIN-PERFIXTX
                        , :NEWCCIN-DURPLANOFIN
                        , :NEWCCIN-FINALIDADE
                        , :NEWCCIN-TPAMORT
                        , :NEWCCIN-FREQPAGAM
                        , :NEWCCIN-DIVSUBOR
                        , :NEWCCIN-INSTFIDUC
                        , :NEWCCIN-MONTINI
                        , :NEWCCIN-VARFV
                        , :NEWCCIN-DTRENEG
                        , :NEWCCIN-TPNEG
                        , :NEWCCIN-PERCDIFCAP
                        , :NEWCCIN-TPTITULARIZ
                        , :NEWCCIN-SEGUROS
                        , :NEWCCIN-TMSCRIA
                        , :NEWCCIN-USERCRIA
                        , :NEWCCIN-TMSALT
                        , :NEWCCIN-USERALT )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCCIN"          TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-INSERT-HIST-BPCJCCIN.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação
     š*-
           MOVE WS-TMS-NULA  TO NEWCCIN-TMSALT
           MOVE "E"          TO NEWCCIN-BPENV
           EXEC SQL
                 INSERT INTO BPCJHCCIN
                 VALUES ( :OLDCCIN-DTREFINST
                        , :OLDCCIN-BPENV
                        , :OLDCCIN-INFTINST
                        , :OLDCCIN-IDCONT
                        , :OLDCCIN-IDINST
                        , :OLDCCIN-BALCAO
                        , :OLDCCIN-PROJFINAN
                        , :OLDCCIN-IDCONTSIND
                        , :OLDCCIN-LITIGJUD
                        , :OLDCCIN-IEB
                        , :OLDCCIN-PAISLEGIS
                        , :OLDCCIN-CANALCOMER
                        , :OLDCCIN-CLAUSRENUN
                        , :OLDCCIN-SUBVPROTOCOLO
                        , :OLDCCIN-REFEXTINST
                        , :OLDCCIN-TPINST
                        , :OLDCCIN-MOEDA
                        , :OLDCCIN-DTUTILFUND
                        , :OLDCCIN-DTINIINST
                        , :OLDCCIN-DTORIMAT
                        , :OLDCCIN-DTMAT
                        , :OLDCCIN-DTINICARJUR
                        , :OLDCCIN-DTFIMCARJUR
                        , :OLDCCIN-DTINICARCAP
                        , :OLDCCIN-DTFIMCARCAP
                        , :OLDCCIN-DIRREEMBLME
                        , :OLDCCIN-RECURSO
                        , :OLDCCIN-TPTXJUR
                        , :OLDCCIN-FREQATUALIZTX
                        , :OLDCCIN-TXREF
                        , :OLDCCIN-SPREAD
                        , :OLDCCIN-TXMAX
                        , :OLDCCIN-TXMIN
                        , :OLDCCIN-TAEG
                        , :OLDCCIN-TAE
                        , :OLDCCIN-PERFIXTX
                        , :OLDCCIN-DURPLANOFIN
                        , :OLDCCIN-FINALIDADE
                        , :OLDCCIN-TPAMORT
                        , :OLDCCIN-FREQPAGAM
                        , :OLDCCIN-DIVSUBOR
                        , :OLDCCIN-INSTFIDUC
                        , :OLDCCIN-MONTINI
                        , :OLDCCIN-VARFV
                        , :OLDCCIN-DTRENEG
                        , :OLDCCIN-TPNEG
                        , :OLDCCIN-PERCDIFCAP
                        , :OLDCCIN-TPTITULARIZ
                        , :OLDCCIN-SEGUROS
                        , :OLDCCIN-TMSCRIA
                        , :OLDCCIN-USERCRIA
                        , :OLDCCIN-TMSALT
                        , :OLDCCIN-USERALT
                        ,  CURRENT TIMESTAMP )
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCCIN"          TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P500-UPDATE-BPCJCCIN.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe
     š*-
           MOVE "CIU"         TO NEWCCIN-INFTINST
           MOVE "E"           TO NEWCCIN-BPENV
           MOVE "BPCCCIN"     TO NEWCCIN-USERALT

           EXEC SQL
                 UPDATE BPCJCCIN
                 SET     INFTINST      = :NEWCCIN-INFTINST
                      ,  BPENV         = :NEWCCIN-BPENV
                      ,  DTREF         = :NEWCCIN-DTREFINST
                      ,  BALCAO        = :NEWCCIN-BALCAO
                      ,  PROJFINAN     = :NEWCCIN-PROJFINAN
                      ,  IDCONTSIND    = :NEWCCIN-IDCONTSIND
                      ,  LITIGJUD      = :NEWCCIN-LITIGJUD
                      ,  IEB           = :NEWCCIN-IEB
                      ,  PAISLEGIS     = :NEWCCIN-PAISLEGIS
                      ,  CANALCOMER    = :NEWCCIN-CANALCOMER
                      ,  CLAUSRENUN    = :NEWCCIN-CLAUSRENUN
                      ,  SUBVPROT      = :NEWCCIN-SUBVPROTOCOLO
                      ,  REFEXTINST    = :NEWCCIN-REFEXTINST
                      ,  TPINST        = :NEWCCIN-TPINST
                      ,  MOEDA         = :NEWCCIN-MOEDA
                      ,  DTUTILFUND    = :NEWCCIN-DTUTILFUND
                      ,  DTINIINST     = :NEWCCIN-DTINIINST
                      ,  DTORIMAT      = :NEWCCIN-DTORIMAT
                      ,  DTMAT         = :NEWCCIN-DTMAT
                      ,  DTINICRJUR    = :NEWCCIN-DTINICARJUR
                      ,  DTFIMCRJUR    = :NEWCCIN-DTFIMCARJUR
                      ,  DTINICRCAP    = :NEWCCIN-DTINICARCAP
                      ,  DTFIMCRCAP    = :NEWCCIN-DTFIMCARCAP
                      ,  DIRREMBIM     = :NEWCCIN-DIRREEMBLME
                      ,  RECURSO       = :NEWCCIN-RECURSO
                      ,  TPTXJUR       = :NEWCCIN-TPTXJUR
                      ,  FREQACTTX     = :NEWCCIN-FREQATUALIZTX
                      ,  TXREF         = :NEWCCIN-TXREF
                      ,  SPREAD        = :NEWCCIN-SPREAD
                      ,  TXMAX         = :NEWCCIN-TXMAX
                      ,  TXMIN         = :NEWCCIN-TXMIN
                      ,  TAEG          = :NEWCCIN-TAEG
                      ,  TAE           = :NEWCCIN-TAE
                      ,  PERFIXTX      = :NEWCCIN-PERFIXTX
                      ,  DURPLNFIN     = :NEWCCIN-DURPLANOFIN
                      ,  FINALIDADE    = :NEWCCIN-FINALIDADE
                      ,  TPAMORT       = :NEWCCIN-TPAMORT
                      ,  FREQPAGAM     = :NEWCCIN-FREQPAGAM
                      ,  DIVSUBOR      = :NEWCCIN-DIVSUBOR
                      ,  INSTFIDUC     = :NEWCCIN-INSTFIDUC
                      ,  MONTINI       = :NEWCCIN-MONTINI
                      ,  VARFV         = :NEWCCIN-VARFV
                      ,  DTRENEG       = :NEWCCIN-DTRENEG
                      ,  TPNEG         = :NEWCCIN-TPNEG
                      ,  PERCDIFCAP    = :NEWCCIN-PERCDIFCAP
                      ,  TPTITUL       = :NEWCCIN-TPTITULARIZ
                      ,  SEGUROS       = :NEWCCIN-SEGUROS
                      ,  TMSALT        = CURRENT TIMESTAMP
                      ,  USERALT       = :NEWCCIN-USERALT

                  WHERE IDCONT   = :NEWCCIN-IDCONT
                   AND  IDINST   = :NEWCCIN-IDINST
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCCIN"          TO W-OBJECT-NAME
              SET CMD-UPDATE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P300-MOEDA-ALFA.
     š*----------------------------------------------------------------
     š*- Descodifica MON  pela Tabela 040
     š*-
           INITIALIZE  R-FDBTAB
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           MOVE WS-MON  TO WS-TBCODE
           MOVE WS-MON  TO WS-MON-ALFA

           EXEC SQL
                 SELECT SUBSTR(A.TBCOMP, 1, 3 )
                  INTO :WS-MON-ALFA
                  FROM  FDBTAB A
                  WHERE A.TBID   = "040"
                   AND  A.TBCODE = :WS-TBCODE
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "FDBTAB"            TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-INDEXANTE-DO.
     š*----------------------------------------------------------------
     š*- Descodifica TXREF PELA pela Tabela  BPCT199
     š*- BTTYPE = "10" significa Indexante
     š*-
           INITIALIZE  R-FDBBCT
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
            SELECT      BTTYPE
                      , BTTXMA
                      , BTSIGN
                 INTO  :BTTYPE
                     , :BTTXMA
                     , :BTSIGN
                 FROM   FDBBCT A
                 WHERE  A.BTRAC =  :WS-NURACI
                  AND   A.BTGRE =  :WS-BTGRE
                  AND   A.BTRUB =  :WS-BTRUB
                  AND   A.BTMON =  :WS-BTMON
                  AND   A.BTTYPE = "10"
                  AND   A.BTDTVA = ( SELECT MAX(X.BTDTVA)
                                     FROM FDBBCT X
                                     WHERE A.BTRAC  =  X.BTRAC
                                      AND  A.BTGRE  =  X.BTGRE
                                      AND  A.BTRUB  =  X.BTRUB
                                      AND  A.BTMON  =  X.BTMON  )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             MOVE ZEROS                TO BTTXMA
             SET TAXA-FIXA             TO TRUE
           WHEN OTHER
              MOVE "FDBBCT"            TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*----------------------------------------------------------------
       P400-CONVERTE-BP.
     š*----------------------------------------------------------------
     š*- Descodifica TXREF PELA pela Tabela BPCT199
     š*-
           INITIALIZE  R-BPCT199
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
            SELECT      CTAB
                     ,  CODORG
                     ,  MOEDA
                     ,  MOEDAX
                     ,  PRZORG
                     ,  CODBP
                     ,  DESC
                 INTO  :CTAB
                     , :CODORG
                     , :MOEDA
                     , :MOEDAX
                     , :PRZORG
                     , :CODBP
                     , :DESC
                 FROM   BPCT199 A
                 WHERE  A.CTAB   =  :WS-CVTAB
                  AND   A.CODORG =  :WS-CVCOD
                  AND   A.MOEDA  =  :WS-CVMOED
                  AND   A.PRZORG =  :WS-CVPRZ
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-DUPROWS
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "FDBBCT"            TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*----------------------------------------------------------------
       P400-DTINIC-DESC.
     š*----------------------------------------------------------------
     š*- Por Data de Sistema
           INITIALIZE  WS-DTINI-DESC

           EXEC SQL
            SELECT MAX(A.MVDTVA)
             INTO :WS-DTINI-DESC
                 FROM FDBMVT A
                 WHERE
                       A.MVRACI  = :WS-NURACI
                   AND A.MVGRE   = :WS-BTGRE
                   AND A.MVRUB   = :WS-BTRUB
                   AND A.MVMON   = :WS-BTMON
                   AND A.MVSOLD >=  0
           END-EXEC

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN SQLCODE-NOTFOUND
             CONTINUE
           WHEN OTHER
              MOVE "FDBMVT"            TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*----------------------------------------------------------------
       P400-DTINIC-DESC-BYVAL.
     š*----------------------------------------------------------------
     š*- Acede FDBMVT
     š*- Chave FSVMVT MVRACI,MVGRE,MVRUB,MVMON,MVDTVA Desc,MVSEQC Desc
           MOVE CPSOLD OF R-FDBCPT    TO WS-SALDO-BYVAL
           MOVE WS-DATA-DIA           TO WS-MVDTA
           COMPUTE WS-MVDTA = WS-ANOIN-DIA * 10000 + WS-MESIN-DIA * 100
                            + WS-DIAIN-DIA

           PERFORM P450-OPEN-CURSOR-CURMVT

           PERFORM P450-FETCH-CURSOR-CURMVT
                UNTIL  WS-SALDO-BYVAL >= 0
                  OR   FIM-CURMVT.

           PERFORM P450-CLOSE-CURSOR-CURMVT.

           IF MVDTVA   IS NOT NUMERIC
              MOVE ZEROS TO MVDTVA
           END-IF
           MOVE MVDTVA  TO WS-DTINI-DESC.

     š*----------------------------------------------------------------
       P400-DTINIC-DESC-BYVAL1.
     š*----------------------------------------------------------------
     š*- Acede FDBMVT
     š*- Chave FSVMVT MVRACI,MVGRE,MVRUB,MVMON,MVDTVA Desc,MVSEQC Desc
           MOVE WS-DATA-DIA           TO WS-MVDTA
           COMPUTE WS-MVDTA = WS-ANOIN-DIA * 10000 + WS-MESIN-DIA * 100
                            + WS-DIAIN-DIA
           EXEC SQL
                SELECT  MAX(MVDTVA)
                INTO   :WS-MVDTAX
                FROM FSVMVT
                       WHERE MVDTVA <= :WS-MVDTA
                         AND MVRACI  = :WS-NURACI
                         AND MVGRE   = :WS-BTGRE
                         AND MVRUB   = :WS-BTRUB
                         AND MVMON   = :WS-BTMON
                  END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN OTHER
              MOVE ZEROS TO WS-MVDTAX
           END-EVALUATE.

           IF WS-MVDTAX IS NOT NUMERIC
              MOVE ZEROS TO WS-MVDTAX
           END-IF
           MOVE WS-MVDTAX TO WS-DTINI-DESC.

     š*----------------------------------------------------------------
       P4000-GET-DTINIINST-BYMOV.
     š*----------------------------------------------------------------
     š*- Acede FDBMVT

           EXEC SQL
                SELECT  MAX(MVDTVA)
                INTO   :WS-MVDTAX
                FROM FSVMVT
                       WHERE MVRACI  = :WS-NURACI
                         AND MVGRE   = :WS-BTGRE
                         AND MVRUB   = :WS-BTRUB
                         AND MVMON   = :WS-BTMON
                         AND MVNORS  = :WS-CTCONT
                  END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              CONTINUE
           WHEN OTHER
              MOVE ZEROS TO WS-MVDTAX
           END-EVALUATE.

           IF WS-MVDTAX IS NOT NUMERIC
              MOVE ZEROS TO WS-MVDTAX
           END-IF
           MOVE WS-MVDTAX TO WS-DTINI-DESC.

     š*----------------------------------------------------------------
       P450-VALIDA-DATE.
     š*----------------------------------------------------------------
     š*-
           EXEC SQL
               SET :WS-VAL-DATE  = DATE(:WS-TST-DATE)
           END-EXEC
           EVALUATE SQLCODE
              WHEN +0
                  MOVE  WS-TST-DATE TO WS-OUT-DATE
              WHEN -180
                  MOVE "0001-01-01" TO WS-OUT-DATE
           END-EVALUATE.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A
           SET BPCBCCIN-MSGSGBD  TO TRUE
           MOVE W-OBJECT-NAME   TO BPCBCCIN-IDPARERR
           MOVE W-SQLCODE       TO BPCBCCIN-CODERR
           MOVE MSG-ERRO-SQL    TO BPCBCCIN-DESCERR

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BPCSCCIN"     TO  ERRPRG OF KYCT900A.
           MOVE     WS-NURACI    TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "INSERT-T900A"   TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.


     š*****************************************************************
       P999-FIMPGM.
     š*****************************************************************
           GOBACK.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BPCSCCIN
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
