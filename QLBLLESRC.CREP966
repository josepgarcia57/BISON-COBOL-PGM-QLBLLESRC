      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP966.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP966
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE966"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE966
                  ASSIGN        TO WORKSTATION-CREE966-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE966
                  CONTROL-AREA  IS CA-CREE966.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE966
           LABEL RECORD OMITTED.
       01  CREE966-HDR.
           COPY DDS-CREE966HDR OF CREE966.
       01  CREE966-D01.
           COPY DDS-CREE966D01 OF CREE966.
       01  CREE966-FTR.
           COPY DDS-CREE966FTR OF CREE966.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE966                  PIC 99.
           88  CREE966-OK                          VALUE ZERO.

       01  CA-CREE966.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.

       01  WS-EDITVARS.
           05  ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDAPLICA            PIC X(03).
               10  NMAPLICA            PIC X(34).
               10  NRMNUPRC            PIC 99.
               10  IDPRCORI            PIC X(08).
               10  NMPRCORI            PIC X(32).
               10  IDPRCDST            PIC X(08).
               10  NMPRCDST            PIC X(32).
               10  REGLISTA                                OCCURS 9.
                   15  NROPCLIN        PIC 9.
                   15  IDAPLLIN        PIC X(03).
                   15  NRMNULIN        PIC 99.
                   15  IDORILIN        PIC X(08).
                   15  OPCMNLIN        PIC 99.
                   15  IDDSTLIN        PIC X(08).
                   15  NMDSTLIN        PIC X(33).
           05  ED-CREEVARS-I   REDEFINES ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDAPLICA            PIC X(03).
               10  NMAPLICA            PIC X(34).
               10  NRMNUPRC            PIC 99.
               10  IDPRCORI            PIC X(08).
               10  IDPRCDST            PIC X(08).
               10  REGLISTA                                OCCURS 9.
                   15  NROPCLIN        PIC 9.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE966".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE966HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE966D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE966FTR".
           05  KV-IDAPLICA-HI          PIC 99      VALUE 25.
           05  KV-IDAPLICA-PC          PIC 99      VALUE 26.
           05  KV-IDAPLICA-PR          PIC 99      VALUE 27.
           05  KV-NRMNUPRC-HI          PIC 99      VALUE 28.
           05  KV-NRMNUPRC-PC          PIC 99      VALUE 29.
           05  KV-NRMNUPRC-PR          PIC 99      VALUE 30.
           05  KV-IDPRCORI-HI          PIC 99      VALUE 31.
           05  KV-IDPRCORI-PC          PIC 99      VALUE 32.
           05  KV-IDPRCORI-PR          PIC 99      VALUE 33.
           05  KV-IDPRCDST-HI          PIC 99      VALUE 34.
           05  KV-IDPRCDST-PC          PIC 99      VALUE 35.
           05  KV-IDPRCDST-PR          PIC 99      VALUE 36.
           05  KV-SUBSET01.
               10  KV-NROPCL01-PC      PIC 99      VALUE 37.
               10  KV-NROPCL01-PR      PIC 99      VALUE 38.
               10  KV-NROPCL02-PC      PIC 99      VALUE 39.
               10  KV-NROPCL02-PR      PIC 99      VALUE 40.
               10  KV-NROPCL03-PC      PIC 99      VALUE 41.
               10  KV-NROPCL03-PR      PIC 99      VALUE 42.
               10  KV-NROPCL04-PC      PIC 99      VALUE 43.
               10  KV-NROPCL04-PR      PIC 99      VALUE 44.
               10  KV-NROPCL05-PC      PIC 99      VALUE 45.
               10  KV-NROPCL05-PR      PIC 99      VALUE 46.
               10  KV-NROPCL06-PC      PIC 99      VALUE 47.
               10  KV-NROPCL06-PR      PIC 99      VALUE 48.
               10  KV-NROPCL07-PC      PIC 99      VALUE 49.
               10  KV-NROPCL07-PR      PIC 99      VALUE 50.
               10  KV-NROPCL08-PC      PIC 99      VALUE 51.
               10  KV-NROPCL08-PR      PIC 99      VALUE 52.
               10  KV-NROPCL09-PC      PIC 99      VALUE 53.
               10  KV-NROPCL09-PR      PIC 99      VALUE 54.
           05  FILLER      REDEFINES KV-SUBSET01           OCCURS 9.
               10  KV-NROPCLIN-PC      PIC 99.
               10  KV-NROPCLIN-PR      PIC 99.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB966.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE966, CA-CREE966
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE966-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE966

           IF  NOT CREE966-OK
               MOVE FS-CREE966      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB966-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE966-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE966-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE966-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB966-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB966-IDAPLICA-PR
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PR)     TO TRUE
           END-IF
      *
           IF  CREB966-NRMNUPRC-PR
               SET  CREB000-INDICDLG-ON (KV-NRMNUPRC-PR)     TO TRUE
           END-IF
      *
           IF  CREB966-IDPRCORI-PR
               SET  CREB000-INDICDLG-ON (KV-IDPRCORI-PR)     TO TRUE
           END-IF
      *
           IF  CREB966-IDPRCDST-PR
               SET  CREB000-INDICDLG-ON (KV-IDPRCDST-PR)     TO TRUE
           END-IF
      *
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
               IF  CREB966-NROPCLIN-PR(WS-ARRYNDX1)
                   MOVE KV-NROPCLIN-PR(WS-ARRYNDX1) TO WS-ARRYNDX2
                   SET  CREB000-INDICDLG-ON (WS-ARRYNDX2)    TO TRUE
               END-IF
           END-PERFORM
      *
           EVALUATE TRUE
           WHEN CREB966-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC)     TO TRUE
           WHEN CREB966-NRMNUPRC-PC
               SET  CREB000-INDICDLG-ON (KV-NRMNUPRC-PC)     TO TRUE
           WHEN CREB966-IDPRCORI-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCORI-PC)     TO TRUE
           WHEN CREB966-IDPRCDST-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCDST-PC)     TO TRUE
           WHEN CREB966-PCURSOR > CREB966-NRPARAMS-INP
               COMPUTE WS-ARRYNDX1 = 24 + (CREB966-NRPARAMS-INP * 3)
                                   +(CREB966-PCURSOR
                                   - CREB966-NRPARAMS-INP) * 2 - 1
               SET  CREB000-INDICDLG-ON (WS-ARRYNDX1)        TO TRUE
           WHEN CREB966-IDAPLICA-UP
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC)     TO TRUE
           WHEN CREB966-NROPCLIN-UP(1)
               SET  CREB000-INDICDLG-ON (KV-NROPCL01-PC)     TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB966-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-HI)     TO TRUE
           WHEN CREB966-NRMNUPRC-PC
               SET  CREB000-INDICDLG-ON (KV-NRMNUPRC-HI)     TO TRUE
           WHEN CREB966-IDPRCORI-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCORI-HI)     TO TRUE
           WHEN CREB966-IDPRCDST-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCDST-HI)     TO TRUE
           END-EVALUATE
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE966-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE966HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE966HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE966HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE966HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE966HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE966HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE966HDR-O
           MOVE CREB000-NRENTLST    TO NRENTLST OF CREE966HDR-O
           MOVE CREB000-DSOPCSEL    TO DSOPCSEL OF CREE966HDR-O
           WRITE CREE966-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE966-OK
               MOVE FS-CREE966      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE966-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE966FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE966FTR-O
           WRITE CREE966-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE966-OK
               MOVE FS-CREE966      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO ED-CREEVARS-O
           MOVE CREB966-IDAPLICA-V  TO IDAPLICA OF ED-CREEVARS-O
           MOVE CREB966-NMAPLICA-V  TO NMAPLICA OF ED-CREEVARS-O
           MOVE CREB966-NRMNUPRC-V  TO NRMNUPRC OF ED-CREEVARS-O
           MOVE CREB966-IDPRCORI-V  TO IDPRCORI OF ED-CREEVARS-O
           MOVE CREB966-NMPRCORI-V  TO NMPRCORI OF ED-CREEVARS-O
           MOVE CREB966-IDPRCDST-V  TO IDPRCDST OF ED-CREEVARS-O
           MOVE CREB966-NMPRCDST-V  TO NMPRCDST OF ED-CREEVARS-O
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
               MOVE CREB966-NROPCLIN-V       (WS-ARRYNDX1)
                 TO NROPCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB966-IDAPLLIN-V       (WS-ARRYNDX1)
                 TO IDAPLLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB966-NRMNULIN-V       (WS-ARRYNDX1)
                 TO NRMNULIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB966-IDORILIN-V       (WS-ARRYNDX1)
                 TO IDORILIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB966-OPCMNLIN-V       (WS-ARRYNDX1)
                 TO OPCMNLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB966-IDDSTLIN-V       (WS-ARRYNDX1)
                 TO IDDSTLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB966-NMDSTLIN-V       (WS-ARRYNDX1)
                 TO NMDSTLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
           END-PERFORM
           MOVE ED-CREEVARS-O       TO CREE966D01-O
           WRITE CREE966-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE966-OK
               MOVE FS-CREE966      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE966
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE966-OK
               MOVE CREE966D01-I              TO ED-CREEVARS-I
               MOVE IDAPLICA OF ED-CREEVARS-I TO CREB966-IDAPLICA-V
               MOVE NRMNUPRC OF ED-CREEVARS-I TO CREB966-NRMNUPRC-V
               MOVE IDPRCORI OF ED-CREEVARS-I TO CREB966-IDPRCORI-V
               MOVE IDPRCDST OF ED-CREEVARS-I TO CREB966-IDPRCDST-V
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB966-NRSELLIN-MAX
                   MOVE NROPCLIN OF ED-CREEVARS-I(WS-ARRYNDX1)
                     TO CREB966-NROPCLIN-V       (WS-ARRYNDX1)
               END-PERFORM
               MOVE IDVARCSR OF ED-CREEVARS-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE966    TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE966                TO CREB000-CDRETORN
               MOVE KV-IDDIALOG               TO CREB000-NMOBJECT
               SET  CREB000-READ              TO TRUE
               SET  CREB000-MSGDLOG           TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE966

           IF  NOT CREE966-OK
               MOVE FS-CREE966      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP966 ------------------------------------*
