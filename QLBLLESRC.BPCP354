      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.     BPCP354.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE CRC
      * PROGRAMA .: BPCP354
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "BPCE354"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT BPCE354
                  ASSIGN        TO WORKSTATION-BPCE354-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-BPCE354
                  CONTROL-AREA  IS CA-BPCE354.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  BPCE354
           LABEL RECORD OMITTED.
       01  BPCE354-HDR.
           COPY DDS-BPCE354HDR OF BPCE354.
       01  BPCE354-D01.
           COPY DDS-BPCE354D01 OF BPCE354.
       01  BPCE354-FTR.
           COPY DDS-BPCE354FTR OF BPCE354.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-BPCE354                  PIC 99.
           88  BPCE354-OK                          VALUE ZERO.

       01  CA-BPCE354.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "BPCE354".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "BPCE354HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "BPCE354D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "BPCE354FTR".
           05  KV-IDINST-HI            PIC 99      VALUE 25.
           05  KV-IDINST-PC            PIC 99      VALUE 26.
           05  KV-IDINST-PR            PIC 99      VALUE 27.
           05  KV-IDPROT-HI            PIC 99      VALUE 28.
           05  KV-IDPROT-PC            PIC 99      VALUE 29.
           05  KV-IDPROT-PR            PIC 99      VALUE 30.
           05  KV-VALPROT-HI           PIC 99      VALUE 31.
           05  KV-VALPROT-PC           PIC 99      VALUE 32.
           05  KV-VALPROT-PR           PIC 99      VALUE 33.
           05  KV-CREDPRIOR-HI         PIC 99      VALUE 34.
           05  KV-CREDPRIOR-PC         PIC 99      VALUE 35.
           05  KV-CREDPRIOR-PR         PIC 99      VALUE 36.
           05  KV-INDEXECPRT-HI        PIC 99      VALUE 37.
           05  KV-INDEXECPRT-PC        PIC 99      VALUE 38.
           05  KV-INDEXECPRT-PR        PIC 99      VALUE 39.
           05  KV-VALEXECPRT-HI        PIC 99      VALUE 40.
           05  KV-VALEXECPRT-PC        PIC 99      VALUE 41.
           05  KV-VALEXECPRT-PR        PIC 99      VALUE 42.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 60.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 60.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY BPCB354.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-BPCE354, CA-BPCE354
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT BPCE354-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O BPCE354

           IF  NOT BPCE354-OK
               MOVE FS-BPCE354      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO BPCB354-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  BPCE354-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  BPCE354-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  BPCE354-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE BPCB354-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  BPCB354-IDINST-PR
               SET  CREB000-INDICDLG-ON (KV-IDINST-PR)   TO TRUE
           END-IF
      *
           IF  BPCB354-IDPROT-PR
               SET  CREB000-INDICDLG-ON (KV-IDPROT-PR) TO TRUE
           END-IF
      *
           IF  BPCB354-VALPROT-PR
               SET  CREB000-INDICDLG-ON (KV-VALPROT-PR)  TO TRUE
           END-IF
      *
           IF  BPCB354-CREDPRIOR-PR
               SET  CREB000-INDICDLG-ON (KV-CREDPRIOR-PR) TO TRUE
           END-IF
      *
           IF  BPCB354-CREDPRIOR-PR
               SET  CREB000-INDICDLG-ON (KV-CREDPRIOR-PR) TO TRUE
           END-IF
      *
           IF  BPCB354-INDEXECPRT-PR
               SET  CREB000-INDICDLG-ON (KV-INDEXECPRT-PR) TO TRUE
           END-IF
      *
           IF  BPCB354-VALEXECPRT-PR
               SET  CREB000-INDICDLG-ON (KV-VALEXECPRT-PR) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN BPCB354-IDINST-UP
               SET  CREB000-INDICDLG-ON   (KV-IDINST-PC)    TO TRUE
           WHEN BPCB354-IDPROT-PC
               SET  CREB000-INDICDLG-ON   (KV-IDPROT-PC)     TO TRUE
           WHEN BPCB354-VALPROT-PC
               SET  CREB000-INDICDLG-ON   (KV-VALPROT-PC)    TO TRUE
           WHEN BPCB354-CREDPRIOR-PC
               SET  CREB000-INDICDLG-ON   (KV-CREDPRIOR-PC)  TO TRUE
           WHEN BPCB354-INDEXECPRT-PC
               SET  CREB000-INDICDLG-ON   (KV-INDEXECPRT-PC) TO TRUE
           WHEN BPCB354-VALEXECPRT-PC
               SET  CREB000-INDICDLG-ON   (KV-VALEXECPRT-PC) TO TRUE
           WHEN BPCB354-IDINST-PC
               SET  CREB000-INDICDLG-ON   (KV-IDINST-PC)    TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN BPCB354-IDINST-PC
               SET  CREB000-INDICDLG-ON  (KV-IDINST-HI)     TO TRUE
           WHEN BPCB354-IDPROT-PC
               SET  CREB000-INDICDLG-ON  (KV-IDPROT-HI)     TO TRUE
           WHEN BPCB354-VALPROT-PC
               SET  CREB000-INDICDLG-ON  (KV-VALPROT-HI)    TO TRUE
           WHEN BPCB354-CREDPRIOR-PC
               SET  CREB000-INDICDLG-ON  (KV-CREDPRIOR-HI)  TO TRUE
           WHEN BPCB354-INDEXECPRT-PC
               SET  CREB000-INDICDLG-ON  (KV-INDEXECPRT-HI) TO TRUE
           WHEN BPCB354-VALEXECPRT-PC
               SET  CREB000-INDICDLG-ON  (KV-VALEXECPRT-HI) TO TRUE
           END-EVALUATE
      *
           IF  BPCB354-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  BPCB354-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO BPCE354-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF BPCE354HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF BPCE354HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF BPCE354HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF BPCE354HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF BPCE354HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF BPCE354HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF BPCE354HDR-O
           WRITE BPCE354-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT BPCE354-OK
               MOVE FS-BPCE354      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO BPCE354-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF BPCE354FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF BPCE354FTR-O
           WRITE BPCE354-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT BPCE354-OK
               MOVE FS-BPCE354      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES               TO BPCE354-D01
           MOVE BPCB354-IDINST-V     TO IDINST     OF BPCE354D01-O
           MOVE BPCB354-NMRACINE-V   TO NMRACINE   OF BPCE354D01-O
           MOVE BPCB354-NMINDEXEC-V  TO NMINDEXEC  OF BPCE354D01-O
           MOVE BPCB354-IDPROT-V     TO IDPROT     OF BPCE354D01-O
           MOVE BPCB354-VALPROT-V    TO VALPROT    OF BPCE354D01-O
           MOVE BPCB354-CREDPRIOR-V  TO CREDPRIOR  OF BPCE354D01-O
           MOVE BPCB354-INDEXECPRT-V TO INDEXECPRT OF BPCE354D01-O
           MOVE BPCB354-VALEXECPRT-V TO VALEXECPRT OF BPCE354D01-O
           MOVE BPCB354-DHCRIREG-V   TO DHCRIREG   OF BPCE354D01-O
           MOVE BPCB354-DHMDFREG-V   TO DHMDFREG   OF BPCE354D01-O
           WRITE BPCE354-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT BPCE354-OK
               MOVE FS-BPCE354      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ BPCE354
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  BPCE354-OK
               MOVE IDINST      OF BPCE354D01-I TO BPCB354-IDINST-V
               MOVE IDPROT      OF BPCE354D01-I TO BPCB354-IDPROT-V
               MOVE VALPROT     OF BPCE354D01-I TO BPCB354-VALPROT-V
               MOVE CREDPRIOR   OF BPCE354D01-I TO BPCB354-CREDPRIOR-V
               MOVE INDEXECPRT  OF BPCE354D01-I TO BPCB354-INDEXECPRT-V
               MOVE VALEXECPRT  OF BPCE354D01-I TO BPCB354-VALEXECPRT-V
               MOVE IDVARCSR    OF BPCE354D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF    OF CA-BPCE354   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-BPCE354               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE BPCE354

           IF  NOT BPCE354-OK
               MOVE FS-BPCE354      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA BPCP354 ------------------------------------*
