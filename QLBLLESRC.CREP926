      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP926.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP926
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP926"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDUTILIZ         PIC X(08).
               10  WS-NMUTILIZ         PIC X(30).
               10  WS-IDEMPRSA         PIC X(03).
               10  WS-IDBALCAO         PIC X(03).
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.
               10  FILLER              PIC X(27).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).
           05  WS-IDREGSEL.
               10  WS-IDUTILIZ         PIC X(08).
               10  FILLER              PIC X(72).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVUTILIZ-I   REDEFINES WS-PARMSLDV.
               10  WS-IDUTILIZ         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVUTILIZ-O   REDEFINES WS-PARMSLDV.
               10  WS-IDUTILIZ         PIC X(08).
           05  WS-LVPERFFN-I   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPERFFN-O   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.
           05  WS-COUNTER1             PIC S9(4)       BINARY.

           05  CS-QUTILIZ              PIC X.
               88  QUTILIZ-CLSE                    VALUE "C".
               88  QUTILIZ-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-SITEXECU-LS.
               10  FILLER              PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  FILLER              PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB976.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET908 OF CRET908.
           COPY DDS-CRET910 OF CRET910.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDUTILIZ.
                   49  DB-IDUTILIZ-L   PIC S9(4)       BINARY.
                   49  DB-IDUTILIZ-V   PIC X(08).
               10  DB-NMUTILIZ.
                   49  DB-NMUTILIZ-L   PIC S9(4)       BINARY.
                   49  DB-NMUTILIZ-V   PIC X(30).
               10  DB-IDEMPRSA         PIC X(03).
               10  DB-IDBALCAO         PIC X(03).
               10  DB-IDGRFUNC.
                   49  DB-IDGRFUNC-L   PIC S9(4)       BINARY.
                   49  DB-IDGRFUNC-V   PIC X(08).
               10  DB-MCSITEXE         PIC X.
           05  DB-SNPARAMS.
               10  DB-EMPRSFLT         PIC X.
                   88  EMPRSFLT-ON                 VALUE "S".
                   88  EMPRSFLT-OFF                VALUE "N".
               10  DB-BALCAFLT         PIC X.
                   88  BALCAFLT-ON                 VALUE "S".
                   88  BALCAFLT-OFF                VALUE "N".
               10  DB-STEXEFLT         PIC X.
                   88  STEXEFLT-ON                 VALUE "S".
                   88  STEXEFLT-OFF                VALUE "N".
           05  DB-IDREGLST.
               10  DB-IDUTILIZ         PIC X(08).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QUTILIZ CURSOR FOR
                    SELECT IDUTILIZ
                         , NMUTILIZ
                         , IDEMPRSA
                         , IDBALCAO
                         , IDGRFUNC
                         , MCSITEXE
                      FROM CRET910
                     WHERE IDUTILIZ LIKE :DB-PARMSINP.DB-IDUTILIZ
                       AND NMUTILIZ LIKE :DB-PARMSINP.DB-NMUTILIZ
                       AND(IDEMPRSA    = :DB-PARMSINP.DB-IDEMPRSA
                        OR "N"         = :DB-EMPRSFLT)
                       AND(IDBALCAO    = :DB-PARMSINP.DB-IDBALCAO
                        OR "N"         = :DB-BALCAFLT)
                       AND IDGRFUNC LIKE :DB-PARMSINP.DB-IDGRFUNC
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                       AND IDUTILIZ    > :DB-IDREGLST.DB-IDUTILIZ
                     ORDER BY 1
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-PARMSINP     TO WS-PARMSINP
           IF  CREB000-MSGDLG-P > ZERO
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB976-MSGDIALG
           END-IF
           IF  CREB000-IDREGLST-MIN
               INITIALIZE DB-IDREGLST
           ELSE
               MOVE CREB000-IDREGLST TO DB-IDREGLST
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB976-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCSEL
               PERFORM P2600-PREP-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2700-PROC-IMPRESSO
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB976-MSGDIALG, WS-PARMSINP
           SET  CREB000-IDREG-NF TO TRUE
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB976-MSGDIALG
           MOVE WS-IDUTILIZ OF WS-PARMSINP TO CREB976-IDUTILIZ-V
           MOVE WS-NMUTILIZ OF WS-PARMSINP TO CREB976-NMUTILIZ-V
           MOVE WS-IDEMPRSA OF WS-PARMSINP TO CREB976-IDEMPRSA-V
           MOVE WS-IDBALCAO OF WS-PARMSINP TO CREB976-IDBALCAO-V
           MOVE WS-IDGRFUNC OF WS-PARMSINP TO CREB976-IDGRFUNC-V
           MOVE WS-MCSITEXE OF WS-PARMSINP TO CREB976-MCSITEXE-V
           SET  CREB000-IDREG-NF TO TRUE
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDUTILIZ"
               MOVE CREB976-IDUTILIZ-V  TO WS-IDUTILIZ OF WS-LVUTILIZ-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVUTILIZ-I
               SET  CREB976-IDUTILIZ-PC TO TRUE
           WHEN "IDGRFUNC"
               MOVE CREB976-IDGRFUNC-V  TO WS-IDGRFUNC OF WS-LVPERFFN-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPERFFN-I
               SET  CREB976-IDGRFUNC-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDUTILIZ"
           AND  WS-IDUTILIZ OF WS-LVUTILIZ-O NOT = SPACES
               MOVE WS-IDUTILIZ OF WS-LVUTILIZ-O TO CREB976-IDUTILIZ-V
               SET  CREB976-IDUTILIZ-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDGRFUNC"
           AND  WS-IDGRFUNC OF WS-LVPERFFN-O NOT = SPACES
               MOVE WS-IDGRFUNC OF WS-LVPERFFN-O TO CREB976-IDGRFUNC-V
               SET  CREB976-IDGRFUNC-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB976-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB976-IDUTILIZ-V     = SPACES
           AND  CREB976-NMUTILIZ-V     = SPACES
           AND  CREB976-IDEMPRSA-V     = SPACES
           AND  CREB976-IDBALCAO-V     = SPACES
           AND  CREB976-IDGRFUNC-V     = SPACES
           AND  CREB976-MCSITEXE-V     = SPACES
               SET  CREB976-IDUTILIZ-PC    TO TRUE
               SET  CREB950-PARAMS-NINFORM TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN(CREB976-IDEMPRSA-V     = SPACES OR CREB950-MCPESQGN)
           AND  CREB976-IDBALCAO-V NOT = SPACES
               SET  CREB976-IDEMPRSA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB976-IDUTILIZ-V NOT = SPACES AND CREB950-MCPESQGN
               MOVE CREB976-IDUTILIZ-V TO WS-STRING80
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB976-IDUTILIZ-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) = SPACE
                   CONTINUE
               END-PERFORM

               PERFORM
               VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB976-IDUTILIZ-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
                   CONTINUE
               END-PERFORM

               IF  WS-ARRYNDX1 <= LENGTH OF CREB976-IDUTILIZ-V
                   SET  CREB976-IDUTILIZ-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-IF
           END-EVALUATE

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB976-IDGRFUNC-V NOT = SPACES AND CREB950-MCPESQGN)
               MOVE CREB976-IDGRFUNC-V TO WS-STRING80
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB976-IDGRFUNC-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) = SPACE
                   CONTINUE
               END-PERFORM

               PERFORM
               VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB976-IDGRFUNC-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
                   CONTINUE
               END-PERFORM

               IF  WS-ARRYNDX1 <= LENGTH OF CREB976-IDGRFUNC-V
                   SET  CREB976-IDGRFUNC-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-IF
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           IF  CREB976-IDGRFUNC-V NOT = SPACES AND CREB950-MCPESQGN
               PERFORM P9000-VRIF-IDGRFUNC
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB976-MCSITEXE-V NOT = SPACE  AND CREB950-MCPESQGN)
               PERFORM P9000-VLID-MCSITEXE
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB976-IDUTILIZ-V TO WS-IDUTILIZ OF WS-PARMSINP
           MOVE CREB976-NMUTILIZ-V TO WS-NMUTILIZ OF WS-PARMSINP
           MOVE CREB976-IDEMPRSA-V TO WS-IDEMPRSA OF WS-PARMSINP
           MOVE CREB976-IDBALCAO-V TO WS-IDBALCAO OF WS-PARMSINP
           MOVE CREB976-IDGRFUNC-V TO WS-IDGRFUNC OF WS-PARMSINP
           MOVE CREB976-MCSITEXE-V TO WS-MCSITEXE OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           INITIALIZE DB-PARMSINP, CRET910, CREB976-VARSDET
           IF  CREB976-IDUTILIZ-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-IDUTILIZ-V
               MOVE  1                 TO DB-IDUTILIZ-L
           ELSE
               MOVE CREB976-IDUTILIZ-V TO DB-IDUTILIZ-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-IDUTILIZ-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-IDUTILIZ-L
                      FROM LENGTH OF DB-IDUTILIZ-V BY -1
                     UNTIL DB-IDUTILIZ-L = ZERO
                     OR    WS-CHRSTR80(DB-IDUTILIZ-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-IDUTILIZ-V TO DB-IDUTILIZ-L
               END-IF
           END-IF

           IF  CREB976-NMUTILIZ-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-NMUTILIZ-V
               MOVE  1                 TO DB-NMUTILIZ-L
           ELSE
               MOVE CREB976-NMUTILIZ-V TO DB-NMUTILIZ-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-NMUTILIZ-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-NMUTILIZ-L
                      FROM LENGTH OF DB-NMUTILIZ-V BY -1
                     UNTIL DB-NMUTILIZ-L = ZERO
                     OR    WS-CHRSTR80(DB-NMUTILIZ-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-NMUTILIZ-V TO DB-NMUTILIZ-L
               END-IF
           END-IF

           IF  CREB976-IDEMPRSA-V = SPACES OR CREB950-MCPESQGN
               SET  EMPRSFLT-OFF       TO TRUE
           ELSE
               MOVE CREB976-IDEMPRSA-V TO DB-IDEMPRSA OF DB-PARMSINP
               SET  EMPRSFLT-ON        TO TRUE
           END-IF

           IF  CREB976-IDBALCAO-V = SPACES OR CREB950-MCPESQGN
               SET  BALCAFLT-OFF       TO TRUE
           ELSE
               MOVE CREB976-IDBALCAO-V TO DB-IDBALCAO OF DB-PARMSINP
               SET  BALCAFLT-ON        TO TRUE
           END-IF

           IF  CREB976-IDGRFUNC-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-IDGRFUNC-V
               MOVE  1                 TO DB-IDGRFUNC-L
           ELSE
               MOVE CREB976-IDGRFUNC-V TO DB-IDGRFUNC-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-IDGRFUNC-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-IDGRFUNC-L
                      FROM LENGTH OF DB-IDGRFUNC-V BY -1
                     UNTIL DB-IDGRFUNC-L = ZERO
                     OR    WS-CHRSTR80(DB-IDGRFUNC-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-IDGRFUNC-V TO DB-IDGRFUNC-L
               END-IF
           END-IF

           IF  CREB976-MCSITEXE-V = SPACE  OR CREB950-MCPESQGN
               SET  STEXEFLT-OFF       TO TRUE
           ELSE
               MOVE CREB976-MCSITEXE-V TO DB-MCSITEXE OF DB-PARMSINP
               SET  STEXEFLT-ON        TO TRUE
           END-IF
           SET  QUTILIZ-CLSE           TO TRUE
           PERFORM P2441-CONT-QUTILIZ

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-NRENTLST = ZERO
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               PERFORM P2442-OPEN-QUTILIZ
           END-EVALUATE

           IF  QUTILIZ-OPEN
               PERFORM P2443-FTCH-QUTILIZ

               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB976-NRSELLIN-MAX
                 OR    CREB000-IDREGLST-MAX
                 OR    CREB000-PROC-ERR
                   PERFORM P2444-LOAD-LSTSEL

                   IF  CREB000-PROC-OK OR CREB000-MSGINFO
                       PERFORM P2443-FTCH-QUTILIZ
                   END-IF
               END-PERFORM

               PERFORM P2445-CLSE-QUTILIZ
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND NOT CREB000-IDREGLST-MAX
               MOVE DB-IDREGLST TO CREB000-IDREGLST
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QUTILIZ          SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET910
                     WHERE IDUTILIZ LIKE :DB-PARMSINP.DB-IDUTILIZ
                       AND NMUTILIZ LIKE :DB-PARMSINP.DB-NMUTILIZ
                       AND(IDEMPRSA    = :DB-PARMSINP.DB-IDEMPRSA
                        OR "N"         = :DB-EMPRSFLT)
                       AND(IDBALCAO    = :DB-PARMSINP.DB-IDBALCAO
                        OR "N"         = :DB-BALCAFLT)
                       AND IDGRFUNC LIKE :DB-PARMSINP.DB-IDGRFUNC
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               MOVE DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP903.QMSGAPL" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2442-OPEN-QUTILIZ          SECTION.

           EXEC SQL OPEN  QUTILIZ                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QUTILIZ-OPEN      TO TRUE
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP926.QUTILIZ" TO CREB000-NMOBJECT
               SET  CREB000-OPEN-CS   TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2442-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2443-FTCH-QUTILIZ          SECTION.

           EXEC SQL FETCH QUTILIZ
                     INTO :CRET910.IDUTILIZ
                        , :CRET910.NMUTILIZ
                        , :CRET910.IDEMPRSA
                        , :CRET910.IDBALCAO
                        , :CRET910.IDGRFUNC
                        , :CRET910.MCSITEXE                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK     TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE      TO CREB000-CDRETORN
               MOVE "CREP926.QUTILIZ"    TO CREB000-NMOBJECT
               SET  CREB000-FTCH-CS      TO TRUE
               SET  CREB000-MSGSGBD      TO TRUE
           END-EVALUATE
           .
       P2443-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2444-LOAD-LSTSEL           SECTION.

           MOVE IDUTILIZ OF CRET910 TO CREB976-IDUTILIN-V(WS-ARRYNDX1)
                                       DB-IDUTILIZ OF DB-IDREGLST
           MOVE NMUTILIZ OF CRET910 TO CREB976-NMUTILIN-V(WS-ARRYNDX1)
           MOVE IDEMPRSA OF CRET910 TO CREB976-IDEMPLIN-V(WS-ARRYNDX1)
           MOVE IDBALCAO OF CRET910 TO CREB976-IDBALLIN-V(WS-ARRYNDX1)
           MOVE IDGRFUNC OF CRET910 TO CREB976-IDGRFLIN-V(WS-ARRYNDX1)
           MOVE MCSITEXE OF CRET910 TO CREB976-STEXELIN-V(WS-ARRYNDX1)
           .
       P2444-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2445-CLSE-QUTILIZ          SECTION.

           EXEC SQL CLOSE QUTILIZ                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP926.QUTILIZ"      TO CREB000-NMOBJECT
               SET  CREB000-CLSE-CS        TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P2445-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB976-IDUTILIZ-V   TO CREB999-IDUTILIZ
           MOVE CREB976-IDEMPRSA-V   TO CREB999-IDEMPRSA
           MOVE CREB976-IDBALCAO-V   TO CREB999-IDBALCAO
           MOVE CREB976-IDGRFUNC-V   TO CREB999-IDGRFUNC
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PREP-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-IDREGSEL = SPACES
               PERFORM P2610-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2620-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2630-FRMT-IDREGSEL
               END-IF
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2640-FRMT-IDPRCDST
           END-IF
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           INITIALIZE CREB000-NRLINSMC
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB976-NRSELLIN-MAX
             OR    CREB000-PROC-ERR
               MOVE CREB976-NROPCLIN-V(WS-ARRYNDX1) TO WS-ARRYNDX2
               EVALUATE TRUE
               WHEN WS-ARRYNDX2 = ZERO
                   CONTINUE
               WHEN CREB000-INDICOPC-ON(WS-ARRYNDX2)
                   ADD  1 TO CREB000-NRLINSMC
               WHEN OTHER
                   COMPUTE CREB976-PCURSOR = CREB976-NRPARAMS-INP
                                           + WS-ARRYNDX1
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-PERFORM

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-NRLINSMC = ZERO
               SET  CREB976-NROPCL01-PC        TO TRUE
               SET  CREB950-SELECC-NINFORM     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL CREB976-NROPCLIN-V(WS-ARRYNDX1) > ZERO
               CONTINUE
           END-PERFORM
           MOVE CREB976-NROPCLIN-V(WS-ARRYNDX1) TO CREB000-IDOPCSEL
           COMPUTE CREB976-PCURSOR = CREB976-NRPARAMS-INP + WS-ARRYNDX1
           INITIALIZE CREB976-NROPCLIN-V(WS-ARRYNDX1)
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL
           MOVE CREB976-IDUTILIN-V(WS-ARRYNDX1)
                                   TO WS-IDUTILIZ OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2630-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2640-FRMT-IDPRCDST         SECTION.

           CONTINUE
           .
       P2640-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2700-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN OTHER
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB976-MSGDIALG TO CREB000-MSGDLG-L
           END-EVALUATE
           MOVE CREB976-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)

           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB976-IDUTILIZ-UP TO TRUE
           SET  CREB976-NMUTILIZ-UP TO TRUE
           SET  CREB976-IDEMPRSA-UP TO TRUE
           SET  CREB976-IDBALCAO-UP TO TRUE
           SET  CREB976-IDGRFUNC-UP TO TRUE
           SET  CREB976-MCSITEXE-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB976-IDUTILIZ-PR TO TRUE
           SET  CREB976-NMUTILIZ-PR TO TRUE
           SET  CREB976-IDEMPRSA-PR TO TRUE
           SET  CREB976-IDBALCAO-PR TO TRUE
           SET  CREB976-IDGRFUNC-PR TO TRUE
           SET  CREB976-MCSITEXE-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB976-NRSELLIN-MAX
             OR    CREB976-IDUTILIN-V(WS-ARRYNDX1) = SPACES
               SET  CREB976-NROPCLIN-UP(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB976-NRSELLIN-MAX
               SET  CREB976-NROPCLIN-PR(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-IDGRFUNC         SECTION.

           INITIALIZE WS-COUNTER1
           INSPECT CREB976-IDGRFUNC-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               PERFORM P9000-VLID-IDGRFUNC
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDGRFUNC         SECTION.

           MOVE CREB976-IDGRFUNC-V TO IDGRFUNC OF CRET908
           PERFORM P9000-READ-CRET908

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSGRFUNC OF CRET908    TO CREB976-DSGRFUNC-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB976-IDGRFUNC-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET908          SECTION.

           EXEC SQL SELECT DSGRFUNC
                      INTO :CRET908.DSGRFUNC
                      FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF       TO DSGRFUNC OF CRET908
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP926.CRET908"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-MCSITEXE         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB976-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB976-DSSITEXE-V
           ELSE
               SET  CREB976-MCSITEXE-PC         TO TRUE
               SET  CREB950-SITEXE-INVALID      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP926 ------------------------------------*
