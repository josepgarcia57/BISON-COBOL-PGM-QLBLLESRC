      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.     BPCP302.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: BPCP302
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "BPCP302"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY BPCB002.
           COPY CREB950.

       01 WS-POS-NOMI                PIC S9(4)   COMP   VALUE 0.
       01 WS-NOM-L                   PIC S9(4)   COMP   VALUE 0.
       01 WS-NOM.
          03 W-NOM                   PIC X       OCCURS 35.
       01 WS-POS-PRNMI               PIC S9(4)   COMP   VALUE 0.
       01 WS-PRNM-L                  PIC S9(4)   COMP   VALUE 0.
       01 WS-PRNM.
          03 W-PRNM                  PIC X       OCCURS 35.
       01 WS-NOM-COMPLETO            PIC X(80).
       01 RUB-PROT                   PIC X(03).

       01  WS-VARSTRAB.
           05  WS-IDREGSEL.
               10  WS-IDDIALOG         PIC X(32).
               10  FILLER              PIC X(48).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVCTAB-I
               REDEFINES WS-PARMSLDV.
               10  WS-CTAB             PIC X(03).
               10  FILLER              PIC X(77).
           05  WS-LVCCOD-O
               REDEFINES WS-PARMSLDV.
               10  WS-COD              PIC X(20).
               10  FILLER              PIC X(60).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-NRPGSHRZ-DFLT        PIC 9       VALUE 1.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY BPCB352.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-BPCTCPRT OF BPCTCPRT.
       01  R-FDBCLI.
           COPY DDS-ALL-FORMATS OF FDBCLI.

       01  WS-NURACI                    PIC X(07).
       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-IDREGSEL     TO WS-IDREGSEL
           IF  CREB000-MSGDLG-P > 1
           OR  CREB000-IDCANALC = CREB950-IDCANALC-RM
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO BPCB352-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, BPCB352-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN(CREB000-INITDLG AND NOT CREB000-PROCLOV)
           OR   CREB000-PROCIDT
               PERFORM P2100-INICIALIZA
               IF  CREB000-PROCNLF
               AND(CREB000-PROC-OK OR  CREB000-MSGINFO)
               AND CREB000-IDREG-OK
                   PERFORM P2500-PROC-REGEXIST
               END-IF
           WHEN CREB000-PROCLOV
               PERFORM P2200-PROC-LISTVALS
           WHEN CREB000-IDREG-NV
               PERFORM P2300-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2400-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2500-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2600-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2700-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2800-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE BPCB352-MSGDIALG
           SET  CREB000-IDREG-NF TO TRUE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCIDT OR CREB000-PROCSEL
               PERFORM P2110-READ-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2120-INIT-NOVOREG
           END-EVALUATE
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2110-READ-REGEXIST         SECTION.

           MOVE WS-IDDIALOG OF WS-IDREGSEL TO BPCB352-IDPROT-V
           PERFORM P9000-READ-BPCTCPRT

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2111-FRMT-MSGDIALG
           WHEN OTHER
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
               AND NOT CREB950-FUNCAO-INTERRP
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           END-IF
           .
       P2110-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2111-FRMT-MSGDIALG         SECTION.

           MOVE IDPROT     OF BPCTCPRT     TO BPCB352-IDPROT-V
           MOVE TPPROT     OF BPCTCPRT     TO BPCB352-TPPROT-V
           MOVE IDENT      OF BPCTCPRT     TO BPCB352-IDENTPROT-V
           MOVE TYPER      OF BPCTCPRT     TO BPCB352-TYPER-V
           MOVE REFEXTPROT OF BPCTCPRT     TO BPCB352-REFEXTPROT-V
           MOVE PAISLPROT  OF BPCTCPRT     TO BPCB352-PAISLPROT-V
           MOVE REGLOCPROT OF BPCTCPRT     TO BPCB352-REGLOCPROT-V
           MOVE DTMATPROT  OF BPCTCPRT     TO BPCB352-DTMATPROT-V
           MOVE VALPROT    OF BPCTCPRT     TO BPCB352-VALPROT-V
           MOVE TPVALPROT  OF BPCTCPRT     TO BPCB352-TPVALPROT-V
           MOVE DTMATPROT  OF BPCTCPRT     TO BPCB352-DTMATPROT-V
           MOVE NUMREGPROT OF BPCTCPRT     TO BPCB352-NUMREGPROT-V
           MOVE HIERQPROT  OF BPCTCPRT     TO BPCB352-HIERQPROT-V
           MOVE VALORIPROT OF BPCTCPRT     TO BPCB352-VALORIPROT-V
           MOVE DTVALPRTO  OF BPCTCPRT     TO BPCB352-DTVALPRTO-V
           MOVE VALPROT    OF BPCTCPRT     TO BPCB352-VALPROT-v
           MOVE DTULTAVAL  OF BPCTCPRT     TO BPCB352-DTULTAVAL-V
           MOVE TPVALPROT  OF BPCTCPRT     TO BPCB352-TPVALPROT-V
           MOVE TPAVAL     OF BPCTCPRT     TO BPCB352-TPAVAL-V
           MOVE DTEXECPROT OF BPCTCPRT     TO BPCB352-DTEXECPROT-V
           MOVE VALACEXEC  OF BPCTCPRT     TO BPCB352-VALACEXEC-V
           MOVE STATEXPROT OF BPCTCPRT     TO BPCB352-STATEXPROT-V
           MOVE PRECOAQUIS OF BPCTCPRT     TO BPCB352-PRECOAQUIS-V
           MOVE TMSCRIA    OF BPCTCPRT     TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO      TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14           TO BPCB352-DHCRIREG-V
           IF  TMSALT   OF BPCTCPRT NOT = CREB950-TMSTPMIN
               MOVE TMSALT   OF BPCTCPRT  TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14      TO BPCB352-DHMDFREG-V
           END-IF
           MOVE TMSALT   OF BPCTCPRT      TO CREB000-DHMDFREG

      *- Carrega Descritivos na Consulta                               *
           PERFORM P9000-DESC-RUB
           PERFORM P9000-DESC-TPPROT
           PERFORM P9000-DESC-TPAVAL
           PERFORM P9000-DESC-TPVALPROT
           PERFORM P9000-DESC-TYPER
           PERFORM P9000-DESC-STATEXPROT
           PERFORM P9000-DESC-IDENTPROT
           .
       P2111-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2120-INIT-NOVOREG          SECTION.

           MOVE WS-IDDIALOG OF WS-IDREGSEL TO BPCB352-IDPROT-V
      *    MOVE KV-NRPGSHRZ-DFLT           TO BPCB352-NRPGSHRZ-V

           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PERMIT-DET
           .
       P2120-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2210-FRMT-PARMSLDV
           ELSE
               PERFORM P2220-RECB-IDSELLDV
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *-------------------------------                                 *
      *- Obtenção da Lista de Códigos                                  *
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "TPPROT"
               MOVE CREB950-TBP-TGAR    TO WS-CTAB
      *        MOVE BPCB352-TIPOPD-V    TO WS-IDAPLICA OF WS-LVAPLICA-I
      *        MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  BPCB352-TPPROT-PC   TO TRUE
           WHEN "TPVALPROT"
               MOVE CREB950-TBP-TVPROT  TO WS-CTAB
               SET  BPCB352-TPVALPROT-PC TO TRUE
           WHEN "STATEXPROT"
               MOVE CREB950-TBP-EEXECG    TO WS-CTAB
               SET  BPCB352-STATEXPROT-PC TO TRUE
           WHEN "TPAVAL"
               MOVE CREB950-TBP-AVCOLAT   TO WS-CTAB
               SET  BPCB352-TPAVAL-PC     TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2210-EXIT.                 EXIT.
      *------------------------------------------------                *
      *- Recebe Valor Escolhido na Lista de Valores                    *
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "TPPROT"
           AND  WS-COD  OF WS-LVCCOD-O   NOT = SPACES
               MOVE WS-COD  OF WS-LVCCOD-O TO BPCB352-TPPROT-V
               SET  BPCB352-TPPROT-PC            TO TRUE
           WHEN CREB000-IDVARCSR = "STATEXPROT"
           AND  WS-COD  OF WS-LVCCOD-O   NOT = SPACES
               MOVE WS-COD  OF WS-LVCCOD-O TO BPCB352-STATEXPROT-V
               SET  BPCB352-STATEXPROT-PC        TO TRUE
           WHEN CREB000-IDVARCSR = "TPAVAL"
           AND  WS-COD  OF WS-LVCCOD-O   NOT = SPACES
               MOVE WS-COD  OF WS-LVCCOD-O TO BPCB352-TPAVAL-V
               SET  BPCB352-TPAVAL-PC           TO TRUE
           END-EVALUATE
           .
       P2220-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE BPCB352-IDPROT-V     TO CREB999-IDDIALOG
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO CREB000-PARMSINP
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE BPCB352-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           MOVE BPCB352-IDPROT-V   TO WS-STRING80
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF BPCB352-IDPROT-V
             OR   (WS-CHRSTR80(WS-ARRYNDX1) = SPACE OR "*" OR "?")
               CONTINUE
           END-PERFORM

           PERFORM
           VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF BPCB352-IDPROT-V
             OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
               CONTINUE
           END-PERFORM

           EVALUATE TRUE
           WHEN BPCB352-IDPROT-V  = SPACES
               SET  BPCB352-IDPROT-PC      TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN WS-ARRYNDX1        <= LENGTH OF BPCB352-IDPROT-V
               SET  BPCB352-IDPROT-PC      TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               PERFORM P2510-VRIF-NIVEL01
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           PERFORM P9000-READ-BPCTCPRT

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-OK
               SET  BPCB352-IDPROT-PC      TO TRUE
               SET  CREB950-REGIST-JEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
               SET  CREB000-IDREG-NF       TO TRUE
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2520-VRIF-NIVEL02
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-MSGDIALG         SECTION.
      *- Coloca Aqui os Descritivos de Campos no Ecran                 *

           CONTINUE
           .
       P2430-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE BPCB352-VARSOUT
               PERFORM P2510-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2520-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2530-FRMT-MSGDIALG
               END-IF
           ELSE
               CONTINUE
      *        PERFORM P2540-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2500-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2510-VRIF-NIVEL01          SECTION.
      *- Verifica Campos Obrigatórios                                  *

           EVALUATE TRUE
           WHEN BPCB352-IDENTPROT-V = SPACES
               SET  BPCB352-IDENTPROT-PC   TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN BPCB352-TYPER-V = SPACES
               SET  BPCB352-TYPER-PC       TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN BPCB352-TYPER-V NOT EQUAL "i1" AND NOT EQUAL "i2"
               SET  BPCB352-TYPER-PC       TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2510-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2520-VRIF-NIVEL02          SECTION.

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-RUB
           END-IF.
           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-TPPROT
           END-IF.
           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-TPAVAL
           END-IF.
           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-TPVALPROT
           END-IF.
           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-TYPER
           END-IF.
           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-STATEXPROT
           END-IF.
           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DESC-IDENTPROT
           END-IF.

       P2520-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2530-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2530-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2600-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2710-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2720-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2730-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-EXEC-TRNCRIAR         SECTION.

           PERFORM P9000-FRMT-BPCTCPRT-CHAV
           PERFORM P9000-FRMT-BPCTCPRT-ATRB
           PERFORM P9000-INSR-BPCTCPRT

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE BPCB352-IDPROT-V   TO WS-IDDIALOG OF WS-IDREGSEL
           END-IF
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-BPCTCPRT-CHAV
           PERFORM P9000-FRMT-BPCTCPRT-ATRB
           PERFORM P9000-UPDT-BPCTCPRT
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-EXEC-TRNANULA         SECTION.

           PERFORM P9000-FRMT-BPCTCPRT-CHAV
           PERFORM P9000-DELT-BPCTCPRT
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-IDREG-NF
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
           WHEN CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-EVALUATE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
           OR   CREB000-IDCANALC = CREB950-IDCANALC-RM
               CONTINUE
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF BPCB352-MSGDIALG
           END-EVALUATE
           MOVE BPCB352-MSGDIALG       TO CREB000-MSGDLG-V
                                         (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL   TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  BPCB352-IDPROT-UP   TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  BPCB352-IDPROT-PR   TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  BPCB352-TPPROT-UP     TO TRUE
           SET  BPCB352-IDENTPROT-UP  TO TRUE
           SET  BPCB352-TYPER-UP      TO TRUE
           SET  BPCB352-REFEXTPROT-UP TO TRUE
           SET  BPCB352-PAISLPROT-UP  TO TRUE
           SET  BPCB352-REGLOCPROT-UP TO TRUE
           SET  BPCB352-DTMATPROT-UP  TO TRUE
           SET  BPCB352-HIERQPROT-UP  TO TRUE
           SET  BPCB352-VALORIPROT-UP TO TRUE
           SET  BPCB352-DTVALPRTO-UP  TO TRUE
           SET  BPCB352-VALPROT-UP    TO TRUE
           SET  BPCB352-DTULTAVAL-UP  TO TRUE
           SET  BPCB352-TPVALPROT-UP  TO TRUE
           SET  BPCB352-DTEXECPROT-UP TO TRUE
           SET  BPCB352-VALACEXEC-UP  TO TRUE
           SET  BPCB352-STATEXPROT-UP TO TRUE
           SET  BPCB352-PRECOAQUIS-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  BPCB352-TPPROT-PR     TO TRUE
           SET  BPCB352-IDENTPROT-PR  TO TRUE
           SET  BPCB352-TYPER-PR      TO TRUE
           SET  BPCB352-REFEXTPROT-PR TO TRUE
           SET  BPCB352-PAISLPROT-PR  TO TRUE
           SET  BPCB352-REGLOCPROT-PR TO TRUE
           SET  BPCB352-DTMATPROT-PR  TO TRUE
           SET  BPCB352-HIERQPROT-PR  TO TRUE
           SET  BPCB352-VALORIPROT-PR TO TRUE
           SET  BPCB352-DTVALPRTO-PR  TO TRUE
           SET  BPCB352-VALPROT-PR    TO TRUE
           SET  BPCB352-DTULTAVAL-PR  TO TRUE
           SET  BPCB352-TPVALPROT-PR  TO TRUE
           SET  BPCB352-DTEXECPROT-PR TO TRUE
           SET  BPCB352-VALACEXEC-PR  TO TRUE
           SET  BPCB352-STATEXPROT-PR TO TRUE
           SET  BPCB352-PRECOAQUIS-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  BPCB352-DHCRIREG-PR TO TRUE
           SET  BPCB352-DHMDFREG-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           SET  BPCB352-DHCRIREG-ND TO TRUE
           SET  BPCB352-DHMDFREG-ND TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-BPCTCPRT          SECTION.

           MOVE BPCB352-IDPROT-V      TO IDPROT   OF BPCTCPRT
           EXEC SQL SELECT
                           IDPROT
                         , IDENT
                         , TYPER
                         , TPPROT
                         , REFEXTPROT
                         , IDINST
                         , VALPROT
                         , TPVALPROT
                         , DTMATPROT
                         , PAISLPROT
                         , TPVALPROT
                         , DTMATPROT
                         , PAISLPROT
                         , REGLOCPROT
                         , DTULTAVAL
                         , TPAVAL
                         , VALORIPROT
                         , DTVALPRTO
                         , HIERQPROT
                         , PRECOAQUIS
                         , NUMREGPROT
                         , STATEXPROT
                         , DTEXECPROT
                         , TMSCRIA
                         , USERCRIA
                         , TMSALT
                         , USERALT
                      INTO :BPCTCPRT.IDPROT
                         , :BPCTCPRT.IDENT
                         , :BPCTCPRT.TYPER
                         , :BPCTCPRT.TPPROT
                         , :BPCTCPRT.REFEXTPROT
                         , :BPCTCPRT.IDINST
                         , :BPCTCPRT.VALPROT
                         , :BPCTCPRT.TPVALPROT
                         , :BPCTCPRT.DTMATPROT
                         , :BPCTCPRT.PAISLPROT
                         , :BPCTCPRT.TPVALPROT
                         , :BPCTCPRT.DTMATPROT
                         , :BPCTCPRT.PAISLPROT
                         , :BPCTCPRT.REGLOCPROT
                         , :BPCTCPRT.DTULTAVAL
                         , :BPCTCPRT.TPAVAL
                         , :BPCTCPRT.VALORIPROT
                         , :BPCTCPRT.DTVALPRTO
                         , :BPCTCPRT.HIERQPROT
                         , :BPCTCPRT.PRECOAQUIS
                         , :BPCTCPRT.NUMREGPROT
                         , :BPCTCPRT.STATEXPROT
                         , :BPCTCPRT.DTEXECPROT
                         , :BPCTCPRT.TMSCRIA
                         , :BPCTCPRT.USERCRIA
                         , :BPCTCPRT.TMSALT
                         , :BPCTCPRT.USERALT
                      FROM BPCTCPRT
                     WHERE IDPROT   = :BPCTCPRT.IDPROT       END-EXEC.

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "BPCP302.BPCTCPRT" TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-RUB              SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           MOVE  BPCB352-IDPROT-V(9:3) TO RUB-PROT

           EVALUATE RUB-PROT
           WHEN "560"
             MOVE "560 - Garantia Recebida"
                   TO BPCB352-NMPROT-V
           WHEN "561"
             MOVE "561 - Garantia Recebida Outros Bancos"
                   TO BPCB352-NMPROT-V
           WHEN "562"
             MOVE "562 - Garantia Recebida Imóveis"
                   TO BPCB352-NMPROT-V
           WHEN "563"
             MOVE "563 - Garantia Recebida Penhor Equipamento"
                   TO BPCB352-NMPROT-V
           WHEN "564"
             MOVE "564 - Garantia Recebida Carteira no BBI"
                   TO BPCB352-NMPROT-V
           WHEN "565"
             MOVE "565 - Garantia Recebida Avales"
                   TO BPCB352-NMPROT-V
           WHEN "566"
             MOVE "565 - Garantia Recebida do Estado"
                   TO BPCB352-NMPROT-V
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-TPPROT           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           INITIALIZE BPCB002-LKPARMS

           MOVE CREB950-TBP-TGAR    TO BPCB002-CTAB
           MOVE BPCB352-TPPROT-V    TO BPCB002-CACC

           CALL "BPCS002" USING BPCB002-LKPARMS

           IF BPCB002-PROC-OK
              MOVE BPCB002-CAMPO1          TO BPCB352-NMTPPROT-V
              IF BPCB002-CAMPO1 = SPACES
                 MOVE BPCB002-DESC         TO BPCB352-NMTPPROT-V
              END-IF
           ELSE
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB352-TPPROT-PC       TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-TPAVAL           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           INITIALIZE BPCB002-LKPARMS

           MOVE CREB950-TBP-AVCOLAT TO BPCB002-CTAB
           MOVE BPCB352-TPAVAL-V    TO BPCB002-CACC

           CALL "BPCS002" USING BPCB002-LKPARMS

           IF BPCB002-PROC-OK
              MOVE BPCB002-CAMPO1          TO BPCB352-NMTPAVAL-V
              IF BPCB002-CAMPO1 = SPACES
                 MOVE BPCB002-DESC         TO BPCB352-NMTPAVAL-V
              END-IF
           ELSE
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB352-TPAVAL-PC       TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-TPVALPROT        SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           INITIALIZE BPCB002-LKPARMS

           MOVE CREB950-TBP-TVPROT   TO BPCB002-CTAB
           MOVE BPCB352-TPVALPROT-V  TO BPCB002-CACC

           CALL "BPCS002" USING BPCB002-LKPARMS

           IF BPCB002-PROC-OK
              MOVE BPCB002-CAMPO1          TO BPCB352-NMTPVAL-V
              IF BPCB002-CAMPO1 = SPACES
                 MOVE BPCB002-DESC         TO BPCB352-NMTPVAL-V
              END-IF
           ELSE
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB352-TPVALPROT-PC    TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-TYPER            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EVALUATE BPCB352-TYPER-V
           WHEN "i1"
             MOVE "NIF/NIPC"             TO BPCB352-NMTYPER-V
           WHEN "i2"
             MOVE "Racine Ident"         TO BPCB352-NMTYPER-V
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-STATEXPROT       SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           INITIALIZE BPCB002-LKPARMS

           MOVE CREB950-TBP-EEXECG    TO BPCB002-CTAB
           MOVE BPCB352-STATEXPROT-V  TO BPCB002-CACC

           CALL "BPCS002" USING BPCB002-LKPARMS

           IF BPCB002-PROC-OK
              MOVE BPCB002-CAMPO1          TO BPCB352-NMSTATEX-V
              IF BPCB002-CAMPO1 = SPACES
                 MOVE BPCB002-DESC         TO BPCB352-NMSTATEX-V
              END-IF
           ELSE
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB352-STATEXPROT-PC   TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DESC-IDENTPROT        SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           MOVE  BPCB352-IDPROT-V(1:7) TO WS-NURACI

           EXEC SQL
                SELECT  CLRACI
                     ,  CLNOM
                     ,  CLPRNM
                INTO   :CLRACI
                     , :CLNOM
                     , :CLPRNM
                FROM  FDBCLI A
                WHERE A.CLRACI = :WS-NURACI
           END-EXEC.

           MOVE SQLCODE TO CREB950-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-OK
              PERFORM P9000-NOME
              MOVE WS-NOM-COMPLETO      TO BPCB352-NMIDENT-V
           WHEN SQLCODE-NOTFOUND
              SET  CREB950-VCAMPO-INVALID  TO TRUE
              SET  BPCB352-IDENTPROT-PC    TO TRUE
              SET  CREB000-MSGFUNC         TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "CREP909.FDBCLI"    TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE.

       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NOME  SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           MOVE  CLNOM     OF R-FDBCLI   TO WS-NOM
           MOVE  CLPRNM    OF R-FDBCLI   TO WS-PRNM
           MOVE ZEROS TO WS-POS-NOMI WS-POS-PRNMI

           INSPECT FUNCTION REVERSE(WS-NOM)
                   TALLYING WS-POS-NOMI     FOR LEADING SPACE.
           COMPUTE  WS-NOM-L    = LENGTH OF WS-NOM  -  WS-POS-NOMI.

           INSPECT FUNCTION REVERSE(WS-PRNM)
                   TALLYING WS-POS-PRNMI    FOR LEADING SPACE.
           COMPUTE  WS-PRNM-L   = LENGTH OF WS-PRNM - WS-POS-PRNMI.

           MOVE SPACES                    TO WS-NOM-COMPLETO
           IF WS-PRNM-L > 0
             MOVE WS-PRNM(1 :WS-PRNM-L)   TO WS-NOM-COMPLETO
           END-IF
           IF WS-NOM-L  > 0
             ADD 1 TO WS-PRNM-L
             MOVE WS-NOM(1:WS-NOM-L)
                             TO WS-NOM-COMPLETO(WS-PRNM-L:WS-NOM-L)
           END-IF.
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-BPCTCPRT-CHAV     SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE BPCB352-IDPROT-V       TO IDPROT   OF BPCTCPRT
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO TMSALT   OF BPCTCPRT
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-BPCTCPRT-ATRB     SECTION.

           MOVE BPCB352-IDPROT-V      TO IDPROT     OF BPCTCPRT
           MOVE BPCB352-IDENTPROT-V   TO IDENT      OF BPCTCPRT
           MOVE BPCB352-TYPER-V       TO TYPER      OF BPCTCPRT
           MOVE BPCB352-TPPROT-V      TO TPPROT     OF BPCTCPRT
           MOVE BPCB352-REFEXTPROT-V  TO REFEXTPROT OF BPCTCPRT
           MOVE BPCB352-IDPROT-V      TO IDPROT     OF BPCTCPRT
           MOVE BPCB352-VALPROT-V     TO VALPROT    OF BPCTCPRT
           MOVE BPCB352-TPVALPROT-V   TO TPVALPROT  OF BPCTCPRT
           MOVE BPCB352-DTMATPROT-V   TO DTMATPROT  OF BPCTCPRT
           MOVE BPCB352-PAISLPROT-V   TO PAISLPROT  OF BPCTCPRT
           MOVE BPCB352-TPVALPROT-V   TO TPVALPROT  OF BPCTCPRT
           MOVE BPCB352-DTMATPROT-V   TO DTMATPROT  OF BPCTCPRT
           MOVE BPCB352-PAISLPROT-V   TO PAISLPROT  OF BPCTCPRT
           MOVE BPCB352-REGLOCPROT-V  TO REGLOCPROT OF BPCTCPRT
           MOVE BPCB352-DTULTAVAL-V   TO DTULTAVAL  OF BPCTCPRT
           MOVE BPCB352-TPVALPROT-V   TO TPAVAL     OF BPCTCPRT
           MOVE BPCB352-VALORIPROT-V  TO VALORIPROT OF BPCTCPRT
           MOVE BPCB352-DTVALPRTO-V   TO DTVALPRTO  OF BPCTCPRT
           MOVE BPCB352-HIERQPROT-V   TO HIERQPROT  OF BPCTCPRT
           MOVE BPCB352-PRECOAQUIS-V  TO PRECOAQUIS OF BPCTCPRT
           MOVE BPCB352-HIERQPROT-V   TO NUMREGPROT OF BPCTCPRT
           MOVE BPCB352-STATEXPROT-V  TO STATEXPROT OF BPCTCPRT
           MOVE BPCB352-DTEXECPROT-V  TO DTEXECPROT OF BPCTCPRT
           MOVE BPCB352-VALACEXEC-V   TO VALACEXEC  OF BPCTCPRT

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB000-IDUTILIZ   TO USERCRIA OF BPCTCPRT
               MOVE CREB950-TMSTPMIN   TO TMSCRIA  OF BPCTCPRT
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ   TO USERALT  OF BPCTCPRT
           END-EVALUATE
           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-BPCTCPRT          SECTION.

           EXEC SQL INSERT INTO BPCTCPRT
                   (       IDPROT
                         , IDENT
                         , TYPER
                         , TPPROT
                         , REFEXTPROT
                         , IDINST
                         , VALPROT
                         , TPVALPROT
                         , DTMATPROT
                         , PAISLPROT
                         , REGLOCPROT
                         , DTULTAVAL
                         , TPAVAL
                         , VALORIPROT
                         , DTVALPRTO
                         , HIERQPROT
                         , PRECOAQUIS
                         , NUMREGPROT
                         , STATEXPROT
                         , TMSCRIA
                         , USERCRIA
                         , TMSALT
                         , USERALT
                         )
                    VALUES
                         ( :BPCTCPRT.IDPROT
                         , :BPCTCPRT.IDENT
                         , :BPCTCPRT.TYPER
                         , :BPCTCPRT.TPPROT
                         , :BPCTCPRT.REFEXTPROT
                         , :BPCTCPRT.IDINST
                         , :BPCTCPRT.VALPROT
                         , :BPCTCPRT.TPVALPROT
                         , :BPCTCPRT.DTMATPROT
                         , :BPCTCPRT.PAISLPROT
                         , :BPCTCPRT.REGLOCPROT
                         , :BPCTCPRT.DTULTAVAL
                         , :BPCTCPRT.TPAVAL
                         , :BPCTCPRT.VALORIPROT
                         , :BPCTCPRT.DTVALPRTO
                         , :BPCTCPRT.HIERQPROT
                         , :BPCTCPRT.PRECOAQUIS
                         , :BPCTCPRT.NUMREGPROT
                         , :BPCTCPRT.STATEXPROT
                         , :BPCTCPRT.TMSCRIA
                         , :BPCTCPRT.USERCRIA
                         , :BPCTCPRT.TMSALT
                         , :BPCTCPRT.USERALT
                         )                                     END-EXEC
           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "BPCP302.BPCTCPRT" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-BPCTCPRT          SECTION.

           EXEC SQL UPDATE BPCTCPRT
                       SET IDPROT      =:BPCTCPRT.IDPROT
                         , IDENT       =:BPCTCPRT.IDENT
                         , TYPER       =:BPCTCPRT.TYPER
                         , TPPROT      =:BPCTCPRT.TPPROT
                         , REFEXTPROT  =:BPCTCPRT.REFEXTPROT
                         , VALPROT     =:BPCTCPRT.VALPROT
                         , TPVALPROT   =:BPCTCPRT.TPVALPROT
                         , DTMATPROT   =:BPCTCPRT.DTMATPROT
                         , PAISLPROT   =:BPCTCPRT.PAISLPROT
                         , REGLOCPROT  =:BPCTCPRT.REGLOCPROT
                         , DTULTAVAL   =:BPCTCPRT.DTULTAVAL
                         , TPAVAL      =:BPCTCPRT.TPAVAL
                         , VALORIPROT  =:BPCTCPRT.VALORIPROT
                         , DTVALPRTO   =:BPCTCPRT.DTVALPRTO
                         , HIERQPROT   =:BPCTCPRT.HIERQPROT
                         , PRECOAQUIS  =:BPCTCPRT.PRECOAQUIS
                         , NUMREGPROT  =:BPCTCPRT.NUMREGPROT
                         , STATEXPROT  =:BPCTCPRT.STATEXPROT
                         , TMSALT      = CURRENT TIMESTAMP
                         , USERALT     =:BPCTCPRT.USERALT
                     WHERE IDPROT   = :BPCTCPRT.IDPROT
                       AND TMSALT   = :BPCTCPRT.TMSALT       END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "BPCP302.BPCTCPRT"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-BPCTCPRT          SECTION.

           EXEC SQL DELETE FROM BPCTCPRT
                     WHERE IDINST   = :BPCTCPRT.IDINST
                       AND TMSALT   = :BPCTCPRT.TMSALT        END-EXEC.

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "BPCP302.BPCTCPRT"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA BPCP302 ------------------------------------*
