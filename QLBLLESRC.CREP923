      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP923.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP923
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP923"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  FILLER              PIC X(69).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVGRFUNC-I   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVGRFUNC-O   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).

           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTINISIT             PIC 9(08).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB972.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET908 OF CRET908.
           COPY DDS-CRET909 OF CRET909.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-TIMESTMP             PIC X(26).
           05  DB-DTINISIT             PIC X(10).
           05  DB-HRINISIT             PIC X(08).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-IDREGSEL = SPACES
               MOVE CREB000-PARMSINP TO WS-PARMSINP
           ELSE
               MOVE CREB000-IDREGSEL TO WS-PARMSINP
           END-IF

           IF (CREB000-IDREGSEL = SPACES AND CREB000-MSGDLG-P > ZERO)
           OR  CREB000-MSGDLG-P > 1
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB972-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB972-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC OR CREB000-PROCCAN
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG OR CREB000-NRLINSMC > ZERO
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-PROCCNF
               PERFORM P2500-EXEC-ALTERABD
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB972-MSGDIALG, WS-PARMSINP
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB972-MSGDIALG
           MOVE WS-IDGRFUNC OF WS-PARMSINP   TO CREB972-IDGRFUNC-V
           MOVE WS-IDAPLICA OF WS-PARMSINP   TO CREB972-IDAPLICA-V
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDGRFUNC"
               MOVE CREB972-IDGRFUNC-V  TO WS-IDGRFUNC OF WS-LVGRFUNC-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVGRFUNC-I
               SET  CREB972-IDGRFUNC-PC TO TRUE
           WHEN "IDAPLICA"
               MOVE CREB972-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB972-IDAPLICA-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDGRFUNC"
           AND  WS-IDGRFUNC OF WS-LVGRFUNC-O NOT = SPACES
           AND NOT CREB972-IDGRFUNC-PR
               MOVE WS-IDGRFUNC OF WS-LVGRFUNC-O TO CREB972-IDGRFUNC-V
               SET  CREB972-IDGRFUNC-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
           AND NOT CREB972-IDAPLICA-PR
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB972-IDAPLICA-V
               SET  CREB972-IDAPLICA-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB972-IDGRFUNC-V = SPACES
               SET  CREB972-IDGRFUNC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB972-IDAPLICA-V = SPACES
               SET  CREB972-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           MOVE CREB972-IDGRFUNC-V TO IDGRFUNC OF CRET909
           MOVE CREB972-IDAPLICA-V TO IDAPLICA OF CRET909
           PERFORM P9000-READ-CRET909

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-NF
               SET  CREB972-IDGRFUNC-PC    TO TRUE
               SET  CREB950-REGIST-NEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB972-IDGRFUNC-V TO WS-IDGRFUNC OF WS-PARMSINP
           MOVE CREB972-IDAPLICA-V TO WS-IDAPLICA OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           MOVE NRMNUPRC OF CRET909     TO CREB972-NRMNUPRC-V
           MOVE NVACESSO OF CRET909     TO CREB972-NVACESSO-V
           MOVE MCSITEXE OF CRET909     TO CREB972-MCSITEXE-V
           MOVE DHINISIT OF CRET909     TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO   TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14        TO CREB972-DHINISIT-V
           MOVE DB-TIMESTMP             TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO   TO CREB950-DTHR-X14
           MOVE CREB000-DTPRCAPL        TO CREB950-DATA-N08
           MOVE CORR CREB950-DATA-X08   TO CREB950-DTHR-X14
           EVALUATE TRUE
           WHEN CREB972-DHINISIT-V  > CREB950-DTHR-N14
               CONTINUE
           WHEN MCSITEXE OF CRET909 = KV-MCSITEXE-INIB
               MOVE KV-MCSITEXE-ACTV    TO CREB972-MCSITEXE-V
               MOVE CREB950-DTHR-N14    TO CREB972-DHINISIT-V
           WHEN OTHER
               MOVE KV-MCSITEXE-INIB    TO CREB972-MCSITEXE-V
               MOVE CREB950-DTHR-N14    TO CREB972-DHINISIT-V
           END-EVALUATE
           PERFORM P9000-READ-SITEXECU
           MOVE DHMDFREG OF CRET909     TO CREB000-DHMDFREG
           PERFORM P9000-READ-CRET908

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE DSGRFUNC OF CRET908 TO CREB972-DSGRFUNC-V
               PERFORM P9000-READ-CRET901
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE NMAPLICA OF CRET901 TO CREB972-NMAPLICA-V
           END-IF
           .
       P2440-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P9000-FRMT-CRET909-CHAV
           PERFORM P9000-FRMT-CRET909-ATRB
           PERFORM P9000-UPDT-CRET909

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN CREB000-IDREGSEL = SPACES
           OR   CREB000-IDREG-NV
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB972-MSGDIALG TO CREB000-MSGDLG-L
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB972-MSGDIALG
           END-EVALUATE
           MOVE CREB972-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB972-IDGRFUNC-UP TO TRUE
           SET  CREB972-IDAPLICA-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB972-IDGRFUNC-PR TO TRUE
           SET  CREB972-IDAPLICA-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  CREB972-DSSITEXE-PR TO TRUE
           SET  CREB972-DHINISIT-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           SET  CREB972-DSSITEXE-ND TO TRUE
           SET  CREB972-DHINISIT-ND TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET909          SECTION.

           EXEC SQL SELECT NRMNUPRC
                         , NVACESSO
                         , MCSITEXE
                         , DHINISIT
                         , DHINISIT
                         , DHMDFREG
                         , CURRENT TIMESTAMP
                      INTO :CRET909.NRMNUPRC
                         , :CRET909.NVACESSO
                         , :CRET909.MCSITEXE
                         , :CRET909.DHINISIT
                         , :CRET909.DHINISIT
                         , :CRET909.DHMDFREG
                         , :DB-TIMESTMP
                      FROM CRET909
                     WHERE IDGRFUNC = :CRET909.IDGRFUNC
                       AND IDAPLICA = :CRET909.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP923.CRET909" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB972-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB972-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB972-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET908          SECTION.

           MOVE CREB972-IDGRFUNC-V TO IDGRFUNC OF CRET908
           EXEC SQL SELECT DSGRFUNC
                      INTO :CRET908.DSGRFUNC
                      FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSGRFUNC OF CRET908
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP923.CRET908" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           MOVE CREB972-IDAPLICA-V TO IDAPLICA OF CRET901
           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO NMAPLICA OF CRET901
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP923.CRET901" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET909-CHAV     SECTION.

           MOVE CREB972-IDGRFUNC-V TO IDGRFUNC OF CRET909
           MOVE CREB972-IDAPLICA-V TO IDAPLICA OF CRET909
           MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET909
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET909-ATRB     SECTION.

           MOVE CREB972-MCSITEXE-V    TO MCSITEXE OF CRET909
           MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
           MOVE CREB972-DHINISIT-V    TO CREB950-DTHR-N14
           MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
           MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET909
           MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET909
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET909          SECTION.

           EXEC SQL UPDATE CRET909
                       SET MCSITEXE = :CRET909.MCSITEXE
                         , DHINISIT = :CRET909.DHINISIT
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET909.IDUTLMDF
                     WHERE IDGRFUNC = :CRET909.IDGRFUNC
                       AND IDAPLICA = :CRET909.IDAPLICA
                       AND DHMDFREG = :CRET909.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP923.CRET909"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP923 ------------------------------------*
