      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CRES901.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CRES901
      * OBJECTIVO : IMPLEMENTAR UMA FUNCAO UNICA DE CARREGAMENTO DE UMA
      *             LISTA DE VALORES SOBRE A TABELA "CRET902"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT CREE000B
                  ASSIGN        TO WORKSTATION-CREE000B-SI
                  ORGANIZATION  IS TRANSACTION
                  ACCESS MODE   IS DYNAMIC
                  RELATIVE KEY  IS RK-CREE000B
                  FILE STATUS   IS FS-CREE000B
                  CONTROL-AREA  IS CA-CREE000B.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE000B
           EXTERNAL
           LABEL RECORD OMITTED.
       01  CREE000-REG                 PIC X(100).
       01  CREE000-SFL.
           COPY DDS-CREE000SFL OF CREE000B.
       01  CREE000-LOV.
           COPY DDS-CREE000LOV OF CREE000B.
       01  CREE000-WIN.
           COPY DDS-CREE000WIN OF CREE000B.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE000B                 PIC 99.
           88  CREE000B-OK                         VALUE ZERO.

       01  RK-CREE000B                 PIC 9(04)   EXTERNAL.

       01  CA-CREE000B.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDDIALOG         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).

           05  WS-SFLRECRD.
               10  WS-IDENTLIN.
                   15  WS-IDDIALOG     PIC X(08).
               10  WS-ATRIBLIN.
                   15  FILLER          PIC X(11)   VALUE
                                       "Aplicação: ".
                   15  WS-IDAPLICA     PIC X(03).
                   15  FILLER          PIC X(17)   VALUE
                                       "; c/Pagn.Vert.?: ".
                   15  WS-SNPGNVRT     PIC X.
                   15  FILLER          PIC X(16)   VALUE
                                       "; #Págs.Horiz.: ".
                   15  WS-NRPGSHRZ     PIC 9.

           05  WS-STRNGVAR.
               10  WS-STRNGPOS         PIC X               OCCURS 10.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.

           05  CS-QDIALOG              PIC X.
               88  QDIALOG-CLSE                    VALUE "C".
               88  QDIALOG-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDFMTDLG-SFL         PIC X(10)   VALUE "CREE000SFL".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET902 OF CRET902.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDAPLICA.
                   49  DB-IDAPLICA-L   PIC S9(4)       BINARY.
                   49  DB-IDAPLICA-V   PIC X(03).
               10  DB-IDDIALOG.
                   49  DB-IDDIALOG-L   PIC S9(4)       BINARY.
                   49  DB-IDDIALOG-V   PIC X(08).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QDIALOG CURSOR FOR
                    SELECT IDDIALOG
                         , IDAPLICA
                         , SNPGNVRT
                         , NRPGSHRZ
                      FROM CRET902
                     WHERE IDAPLICA LIKE :DB-PARMSINP.DB-IDAPLICA
                       AND IDDIALOG LIKE :DB-PARMSINP.DB-IDDIALOG
                     ORDER BY 1
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB900.

      *================================================================*
       PROCEDURE DIVISION USING CREB900-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB900-PARMSINP     TO WS-PARMSINP
           INITIALIZE WS-DBGBVARS, WS-DBLCVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  WS-IDAPLICA OF WS-PARMSINP = SPACES OR CREB950-MCPESQGN
               MOVE "%"                        TO DB-IDAPLICA-V
               MOVE  1                         TO DB-IDAPLICA-L
           ELSE
               MOVE WS-IDAPLICA OF WS-PARMSINP TO DB-IDAPLICA-V
                                                  WS-STRNGVAR
               INITIALIZE WS-ARRYNDX1
               INSPECT DB-IDAPLICA-V
                       TALLYING WS-ARRYNDX1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
                                     "?" BY "_"
               IF  WS-ARRYNDX1 > ZERO
                   PERFORM
                   VARYING DB-IDAPLICA-L
                      FROM LENGTH OF DB-IDAPLICA-V BY -1
                     UNTIL DB-IDAPLICA-L = ZERO
                     OR    WS-STRNGPOS(DB-IDAPLICA-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-IDAPLICA-V TO DB-IDAPLICA-L
               END-IF
           END-IF

           IF  WS-IDDIALOG OF WS-PARMSINP = SPACES OR CREB950-MCPESQGN
               MOVE "%"                        TO DB-IDDIALOG-V
               MOVE  1                         TO DB-IDDIALOG-L
           ELSE
               MOVE WS-IDDIALOG OF WS-PARMSINP TO DB-IDDIALOG-V
                                                  WS-STRNGVAR
               INITIALIZE WS-ARRYNDX1
               INSPECT DB-IDDIALOG-V
                       TALLYING WS-ARRYNDX1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
                                     "?" BY "_"
               IF  WS-ARRYNDX1 > ZERO
                   PERFORM
                   VARYING DB-IDDIALOG-L
                      FROM LENGTH OF DB-IDDIALOG-V BY -1
                     UNTIL DB-IDDIALOG-L = ZERO
                     OR    WS-STRNGPOS(DB-IDDIALOG-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE "%"                     TO DB-IDDIALOG-V
                   MOVE  1                      TO DB-IDDIALOG-L
               END-IF
           END-IF
           SET  QDIALOG-CLSE  TO TRUE
           PERFORM P2100-OPEN-QDIALOG

           IF  QDIALOG-OPEN
               PERFORM P2200-FTCH-QDIALOG

               PERFORM
               VARYING RK-CREE000B FROM 1 BY 1
                 UNTIL SQLCODE-NOTFOUND
                 OR    NOT CREB900-PROC-OK
                   PERFORM P2300-LOAD-LSTVAL

                   IF  CREB900-PROC-OK
                       PERFORM P2200-FTCH-QDIALOG
                   END-IF
               END-PERFORM

               PERFORM P2400-CLSE-QDIALOG
           END-IF
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-QDIALOG          SECTION.

           EXEC SQL OPEN  QDIALOG                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QDIALOG-OPEN      TO TRUE
           ELSE
               SET  CREB900-OPEN-CS   TO TRUE
               MOVE "CRES901.QDIALOG" TO CREB900-NMOBJECT
           END-IF
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-FTCH-QDIALOG          SECTION.

           EXEC SQL FETCH QDIALOG
                     INTO :CRET902.IDDIALOG
                        , :CRET902.IDAPLICA
                        , :CRET902.SNPGNVRT
                        , :CRET902.NRPGSHRZ                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               SET  CREB900-FTCH-CS   TO TRUE
               MOVE "CRES901.QDIALOG" TO CREB900-NMOBJECT
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-LOAD-LSTVAL           SECTION.

           MOVE IDDIALOG OF CRET902 TO WS-IDDIALOG OF WS-SFLRECRD
           MOVE IDAPLICA OF CRET902 TO WS-IDAPLICA OF WS-SFLRECRD
           MOVE SNPGNVRT OF CRET902 TO WS-SNPGNVRT OF WS-SFLRECRD
           MOVE NRPGSHRZ OF CRET902 TO WS-NRPGSHRZ OF WS-SFLRECRD

           INITIALIZE CREE000SFL-O
           MOVE WS-IDENTLIN OF WS-SFLRECRD TO IDENTLIN OF CREE000SFL-O
           MOVE WS-ATRIBLIN OF WS-SFLRECRD TO ATRIBLIN OF CREE000SFL-O
           WRITE SUBFILE CREE000-SFL
                             FORMAT IS KV-IDFMTDLG-SFL
           INVALID KEY CONTINUE
           END-WRITE
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-CLSE-QDIALOG          SECTION.

           EXEC SQL CLOSE QDIALOG                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QDIALOG-CLSE      TO TRUE
           ELSE
               SET  CREB900-CLSE-CS   TO TRUE
               MOVE "CRES901.QDIALOG" TO CREB900-NMOBJECT
           END-IF
           .
       P2400-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           IF  CREB900-PROC-OK
               COMPUTE CREB900-QTLINHAS = RK-CREE000B - 1
           ELSE
               MOVE "CRES901" TO CREB900-NMOBJECT
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CRES901 -----------------------------------*
