      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP965.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP965
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE965"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE965
                  ASSIGN        TO WORKSTATION-CREE965-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE965
                  CONTROL-AREA  IS CA-CREE965.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE965
           LABEL RECORD OMITTED.
       01  CREE965-HDR.
           COPY DDS-CREE965HDR OF CREE965.
       01  CREE965-D01.
           COPY DDS-CREE965D01 OF CREE965.
       01  CREE965-FTR.
           COPY DDS-CREE965FTR OF CREE965.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE965                  PIC 99.
           88  CREE965-OK                          VALUE ZERO.

       01  CA-CREE965.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE965".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE965HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE965D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE965FTR".
           05  KV-NROPCMNU-HI          PIC 99      VALUE 25.
           05  KV-NROPCMNU-PC          PIC 99      VALUE 26.
           05  KV-NROPCMNU-PR          PIC 99      VALUE 27.
           05  KV-IDPRCAPL-HI          PIC 99      VALUE 28.
           05  KV-IDPRCAPL-PC          PIC 99      VALUE 29.
           05  KV-IDPRCAPL-PR          PIC 99      VALUE 30.
           05  KV-DSOPCMNU-HI          PIC 99      VALUE 31.
           05  KV-DSOPCMNU-PC          PIC 99      VALUE 32.
           05  KV-DSOPCMNU-PR          PIC 99      VALUE 33.
           05  KV-DSTIPDLG-ND          PIC 99      VALUE 34.
           05  KV-NVAUTLER-ND          PIC 99      VALUE 34.
           05  KV-DSSITEXE-ND          PIC 99      VALUE 34.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 35.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 35.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB965.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE965, CA-CREE965
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE965-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE965

           IF  NOT CREE965-OK
               MOVE FS-CREE965      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB965-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE965-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE965-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE965-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB965-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB965-NROPCMNU-PR
               SET  CREB000-INDICDLG-ON (KV-NROPCMNU-PR) TO TRUE
           END-IF
      *
           IF  CREB965-IDPRCAPL-PR
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PR) TO TRUE
           END-IF
      *
           IF  CREB965-DSOPCMNU-PR
               SET  CREB000-INDICDLG-ON (KV-DSOPCMNU-PR) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN CREB965-NROPCMNU-PC
               SET  CREB000-INDICDLG-ON (KV-NROPCMNU-PC) TO TRUE
           WHEN CREB965-IDPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PC) TO TRUE
           WHEN CREB965-DSOPCMNU-PC
               SET  CREB000-INDICDLG-ON (KV-DSOPCMNU-PC) TO TRUE
           WHEN CREB965-NROPCMNU-UP
               SET  CREB000-INDICDLG-ON (KV-NROPCMNU-PC) TO TRUE
           WHEN CREB965-IDPRCAPL-UP
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PC) TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB965-NROPCMNU-PC
               SET  CREB000-INDICDLG-ON (KV-NROPCMNU-HI) TO TRUE
           WHEN CREB965-IDPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-HI) TO TRUE
           WHEN CREB965-DSOPCMNU-PC
               SET  CREB000-INDICDLG-ON (KV-DSOPCMNU-HI) TO TRUE
           END-EVALUATE
      *
           IF  CREB965-DSTIPDLG-ND
               SET  CREB000-INDICDLG-ON (KV-DSTIPDLG-ND) TO TRUE
           END-IF
      *
           IF  CREB965-NVAUTLER-ND
               SET  CREB000-INDICDLG-ON (KV-NVAUTLER-ND) TO TRUE
           END-IF
      *
           IF  CREB965-DSSITEXE-ND
               SET  CREB000-INDICDLG-ON (KV-DSSITEXE-ND) TO TRUE
           END-IF
      *
           IF  CREB965-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  CREB965-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE965-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE965HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE965HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE965HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE965HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE965HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE965HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE965HDR-O
           WRITE CREE965-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE965-OK
               MOVE FS-CREE965      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE965-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE965FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE965FTR-O
           WRITE CREE965-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE965-OK
               MOVE FS-CREE965      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO CREE965-D01
           MOVE CREB965-NMAPLICA-V  TO NMAPLICA OF CREE965D01-O
           MOVE CREB965-NRMNUPRC-V  TO NRMNUPRC OF CREE965D01-O
           MOVE CREB965-NVSBMNU1-V  TO NVSBMNU1 OF CREE965D01-O
           MOVE CREB965-NVSBMNU2-V  TO NVSBMNU2 OF CREE965D01-O
           MOVE CREB965-NMSUBMNU-V  TO NMSUBMNU OF CREE965D01-O
           MOVE CREB965-NROPCMNU-V  TO NROPCMNU OF CREE965D01-O
           MOVE CREB965-IDPRCAPL-V  TO IDPRCAPL OF CREE965D01-O
           MOVE CREB965-DSOPCMNU-V  TO DSOPCMNU OF CREE965D01-O
           MOVE CREB965-IDDIALOG-V  TO IDDIALOG OF CREE965D01-O
           MOVE CREB965-DSTIPDLG-V  TO DSTIPDLG OF CREE965D01-O
           MOVE CREB965-NVAUTLER-V  TO NVAUTLER OF CREE965D01-O
           MOVE CREB965-NVAUTIMP-V  TO NVAUTIMP OF CREE965D01-O
           MOVE CREB965-NVAUTCRI-V  TO NVAUTCRI OF CREE965D01-O
           MOVE CREB965-NVAUTMDF-V  TO NVAUTMDF OF CREE965D01-O
           MOVE CREB965-NVAUTANL-V  TO NVAUTANL OF CREE965D01-O
           MOVE CREB965-NVAUTMIN-V  TO NVAUTMIN OF CREE965D01-O
           MOVE CREB965-DSSITEXE-V  TO DSSITEXE OF CREE965D01-O
           MOVE CREB965-DHINISIT-V  TO DHINISIT OF CREE965D01-O
           MOVE CREB965-DHCRIREG-V  TO DHCRIREG OF CREE965D01-O
           MOVE CREB965-DHMDFREG-V  TO DHMDFREG OF CREE965D01-O
           WRITE CREE965-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE965-OK
               MOVE FS-CREE965      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE965
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE965-OK
               MOVE NROPCMNU OF CREE965D01-I TO CREB965-NROPCMNU-V
               MOVE IDPRCAPL OF CREE965D01-I TO CREB965-IDPRCAPL-V
               MOVE DSOPCMNU OF CREE965D01-I TO CREB965-DSOPCMNU-V
               MOVE IDVARCSR OF CREE965D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE965   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE965               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE965

           IF  NOT CREE965-OK
               MOVE FS-CREE965      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP965 ------------------------------------*
