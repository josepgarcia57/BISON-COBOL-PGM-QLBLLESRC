       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BPCPCCINX   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BPC - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este programa destina-se a Gerar o XML     *
     š*                     a enviar ao Banco de Portugal             *
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *------------------------------------------------------------------------*
       FILE-CONTROL.
      *------------------------------------------------------------------------*
           SELECT BPCFCCIN
                  ASSIGN TO DATABASE-BPCFCCIN
                  ORGANIZATION IS SEQUENTIAL
                  ACCESS MODE  IS SEQUENTIAL
                  FILE STATUS  IS GRS-FILE-STATUS.
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
       FD  BPCFCCIN.
       01  REG-BPCFCCIN.
           COPY DDS-ALL-FORMATS OF BPCFCCIN.
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 GRS-FILE-STATUS            PIC XX.

       01 EXECUTA-P001               PIC 9(01).
          88 INICIO-P001             VALUE 0.
          88 FIM-P001                VALUE 1.

       01 EXECUTA-P002               PIC 9(01).
          88 INICIO-P002             VALUE 0.
          88 FIM-P002                VALUE 1.
      *
       01 CICLO-SEQ                  PIC 9(01).
          88 CICLO-1                 VALUE 0.
          88 CICLO-N                 VALUE 1.

       01 CICLO-DOC                  PIC 9(01).
          88 CICLO-DOC-1             VALUE 0.
          88 CICLO-DOC-N             VALUE 1.
      *
       01 TRATA-INFO                 PIC 9(01).
          88 SIM-TRATA               VALUE 1.
          88 NAO-TRATA               VALUE 0.

       01 PGM-STATUS                 PIC 9(01).
          88 SEM-ERRO                VALUE 0.
          88 COM-ERRO                VALUE 1.

       01 NR-ADM                     PIC 9(06) VALUE ZEROS.
       01 NR-READ                    PIC 9(06) VALUE ZEROS.
       01 NR-PROC                    PIC 9(06) VALUE ZEROS.
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(9)    VALUE "BPCPCCINX".

       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA         PIC 9(04).
           03  FILLER               PIC X VALUE "-".
           03  WS-MESIN-DIA         PIC 9(02).
           03  FILLER               PIC X VALUE "-".
           03  WS-DIAIN-DIA         PIC 9(02).
     š* ------------------------------------------------------
     š* Linha com XML gerado HardCode pelo programa
       01 XML-OUTPUT       PIC X(1000).

       01 Ws-dtCriacao.
          05 ws-Data         PIC X(10).
          05 ws-SepT         PIC X(1).
          05 ws-HH           PIC X(2).
          05 ws-SepH         PIC X(1).
          05 ws-MM           PIC X(2).
          05 ws-SepM         PIC X(1).
          05 ws-SS           PIC X(2).

     š* ------------------------------------------------------
       01 WS-STRING.
          05 WS-STRING-CHR         PIC X           OCCURS 300.
       01 WS-VAL-TAG-A             PIC X(80).
       01 WS-NOME-TAG              PIC X(80).
       01 WS-VAL-TAG-L             PIC S9(4)       COMP   VALUE 0.
       01 WS-NOM-TAG-L             PIC S9(4)       COMP   VALUE 0.
     š* ------------------------------------------------------
     š* Determina Comprimento de uma String
       01 WS-POS-I                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-S                 PIC S9(4)       COMP   VALUE 0.
       01 WS-POS-XML               PIC S9(4)       COMP   VALUE 0.
       01 WS-STRING-L              PIC S9(4)       COMP   VALUE 0.
       01 W-NUM                    PIC 9(3).
       01 Edit-Nbr-Tx PIC      -----9,99999.
       01 Edit-Nbr-Mn PIC      --------9,99.
       01 Edit-Nbr-In PIC      -----------9.

     š* Campos de Caracteres Invalidos
        01 CHAR-TESTE                PIC X.
           88 CARACTER-VALIDO      VALUE
      *                             - letras minusculas
                                          "a", "b", "c", "d", "e",
                                          "f", "g", "h", "i", "j",
                                          "k", "l", "m", "n", "o",
                                          "p", "q", "r", "s", "t",
                                          "u", "v", "w", "x", "y",
                                          "z",
      *                             - letras maiusculas
                                          "A", "B", "C", "D", "E",
                                          "F", "G", "H", "I", "J",
                                          "K", "L", "M", "N", "O",
                                          "P", "Q", "R", "S", "T",
                                          "U", "V", "W", "X", "Y",
                                          "Z",
      *                             - numeros
                                          "0", "1", "2", "3", "4",
                                          "5", "6", "7", "8", "9",
      *                             - outros
                                          " ", "/", "-", "?", ":",
                                          "(", ")", ".", ",",
                                          "+".

      * "'" passou a ser considerado invalido

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
     š* Criação de Ficheiro de Destino
       01 WS-BIBORIG               PIC X(10).
       01 WS-FICHORIG              PIC X(10).
       01 WS-BIBDEST               PIC X(10).
       01 WS-FICHDEST              PIC X(10).
       01 WS-ERRO                  PIC X(10).
     š* Área de Ligação para o FTP
       01 BPCL001M-FICH-ORIGEM    PIC  X(15).
       01 BPCL001M-FICH-DESTINO   PIC  X(50).
       01 BPCL001M-ERRO-BPCPFTP   PIC  X(01).
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".

     š* Copy : Tabela de Instrumentos
           EXEC SQL
               INCLUDE BPCHCCINN
           END-EXEC.

     š* Copy : Tabela de Entidades Sindicadas
           EXEC SQL
               INCLUDE BPCHCCIN1N
           END-EXEC.

     š* Copy : Tabela de Informação de Risco
           EXEC SQL
               INCLUDE BPCHCCIN3N
           END-EXEC.

     š* Copy : Tabela XMP BP - CENT - Comunicação de Entidades
       01  R-BPCFCCIN.
           COPY DDS-ALL-FORMATS OF BPCFCCIN.

     š* Copy : Tabela de Controlo
       01  R-JCNTL.
           COPY DDS-ALL-FORMATS OF BPCJCNTL.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.

       01 WS-NURACI                PIC X(07).

     š* Cursor Principal
           EXEC SQL DECLARE CURJCCIN CURSOR FOR
            SELECT    A.INFTINST
                   ,  A.BPENV
                   ,  A.DTREF
                   ,  A.IDCONT
                   ,  A.IDINST
                   ,  A.BALCAO
                   ,  A.PROJFINAN
                   ,  A.IDCONTSIND
                   ,  A.LITIGJUD
                   ,  A.IEB
                   ,  A.PAISLEGIS
                   ,  A.CANALCOMER
                   ,  A.CLAUSRENUN
                   ,  A.SUBVPROT
                   ,  A.REFEXTINST
                   ,  A.TPINST
                   ,  A.MOEDA
                   ,  A.DTUTILFUND
                   ,  A.DTINIINST
                   ,  A.DTORIMAT
                   ,  A.DTMAT
                   ,  A.DTINICRJUR
                   ,  A.DTFIMCRJUR
                   ,  A.DTINICRCAP
                   ,  A.DTFIMCRCAP
                   ,  A.DIRREMBIM
                   ,  A.RECURSO
                   ,  A.TPTXJUR
                   ,  A.FREQACTTX
                   ,  A.TXREF
                   ,  A.SPREAD
                   ,  A.TXMAX
                   ,  A.TXMIN
                   ,  A.TAEG
                   ,  A.TAE
                   ,  A.PERFIXTX
                   ,  A.DURPLNFIN
                   ,  A.FINALIDADE
                   ,  A.TPAMORT
                   ,  A.FREQPAGAM
                   ,  A.DIVSUBOR
                   ,  A.INSTFIDUC
                   ,  A.MONTINI
                   ,  A.VARFV
                   ,  VALUE(B.DTRENEG , " " )
                   ,  VALUE(B.TPRENEG , "001" )
                   ,  A.PERCDIFCAP
                   ,  A.TPTITUL
                   ,  A.SEGUROS
                   ,  A.TMSCRIA
                   ,  A.USERCRIA
                   ,  A.TMSALT
                   ,  A.USERALT
                FROM  BPCJCCIN A
                 LEFT JOIN BPCTRENEG B
                  ON ( A.IDCONT = B.IDCONT
                   AND A.IDINST = B.IDINST  )
                WHERE BPENV = "E"
           END-EXEC.

     š* Cursor lstEntSind
           EXEC SQL DECLARE CURJCCIN1 CURSOR FOR
            SELECT    IDCONTSIND
                   ,  IDENT
                   ,  TYPER
                   ,  RELENTSIND
                   ,  RACINE
                   ,  TMSCRIA
                   ,  USERCRIA
                   ,  TMSALT
                   ,  USERALT
            FROM  BPCJCCIN1 A
             WHERE   IDCONTSIND = :NEWCCIN-IDCONTSIND
           END-EXEC.

     š* Cursor lstLigInst
     š* Cursor lstInfRiscoInst
           EXEC SQL DECLARE CURJCCIN3 CURSOR FOR
            SELECT    IDCONT
                   ,  IDINST
                   ,  DTPDINST
                   ,  PDINST
                   ,  TIPOPDINST
                   ,  TPAVALR
                   ,  SISAVALR
                   ,  MODIRBINST
                   ,  NOTACINST
                   ,  LGDINST
                   ,  MODLGDINST
                   ,  TMSCRIA
                   ,  USERCRIA
                   ,  TMSALT
                   ,  USERALT
            FROM  BPCJCCIN3 A
             WHERE   IDCONT    = :NEWCCIN-IDCONT
              AND    IDINST    = :NEWCCIN-IDINST
              AND    TPAVALR  <> " "
           END-EXEC.

     š* Cursor lstCaracEsp

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       PROCEDURE DIVISION.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P002-INICIO.

           PERFORM P002-REG-CONTROLO.

           PERFORM P100-OPEN-CURSOR-CURJCCIN.
           PERFORM P200-FETCH-CURSOR-CURJCCIN.
           PERFORM P300-PROCESSO-GERAL
                           UNTIL FIM-P001
           PERFORM P400-CLOSE-CURSOR-CURJCCIN
           IF NR-PROC NOT EQUAL ZERO
             PERFORM P300-CLOSE-TAG-comCInst
             PERFORM P300-CLOSE-TAG-conteudo
             PERFORM P300-CLOSE-TAG-crc
             PERFORM P400-BPCFCCIN-CLOSE
             PERFORM P450-FTP-FILE
           END-IF
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P002-INICIO.
     š*---------------------------------------------------------------------
           SET  INICIO-P001         TO  TRUE.
           SET  CICLO-1             TO  TRUE.
           EXEC SQL
              DELETE FROM BPCFCCIN
           END-EXEC.

           MOVE 1 TO WS-POS-XML
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.

     š*---------------------------------------------------------------------
       P002-REG-CONTROLO.
     š*---------------------------------------------------------------------
           INITIALIZE  BPCJCNTL
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           PERFORM P300-GET-BPCJCNTL.
     š*---------------------------------------------------------------------
       P300-GET-BPCJCNTL.
     š*---------------------------------------------------------------------
     š*- Último Envio
           SET SIM-TRATA TO TRUE

           EXEC SQL
             SELECT     X.DTREF
                     ,  X.REPTYPE
                     ,  X.ESTADO
                     ,  X.ENTOBS
                     ,  X.ENTREPORT
                     ,  X.IDDEST
                     ,  X.DTCRIACAO
                     ,  X.NOMEFICH
                     ,  X.NREC
                     ,  X.NREJ
                     ,  X.NINT
                     ,  X.NALERTA
                     ,  X.TMSCRIA
                     ,  X.TMSRET
             INTO      :BPCJCNTL.DTREF
                     , :BPCJCNTL.REPTYPE
                     , :BPCJCNTL.ESTADO
                     , :BPCJCNTL.ENTOBS
                     , :BPCJCNTL.ENTREPORT
                     , :BPCJCNTL.IDDEST
                     , :BPCJCNTL.DTCRIACAO
                     , :BPCJCNTL.NOMEFICH
                     , :BPCJCNTL.NREC
                     , :BPCJCNTL.NREJ
                     , :BPCJCNTL.NINT
                     , :BPCJCNTL.NALERTA
                     , :BPCJCNTL.TMSCRIA
                     , :BPCJCNTL.TMSRET
             FROM  BPCJCNTL  X
                WHERE TMSCRIA = ( SELECT MAX(TMSCRIA)
                                  FROM BPCJCNTL Y
                                  WHERE REPTYPE = "CCIN" )
                AND   REPTYPE = "CCIN"
                AND   ESTADO  IN( "ENV" , "RET" )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
              IF ESTADO OF BPCJCNTL = "RET"
                 SET NAO-TRATA TO TRUE
              END-IF
              MOVE S-SYSTMST(1:10) TO WS-DATA-DIA
              MOVE DTCRIACAO OF BPCJCNTL TO Ws-dtCriacao
           WHEN OTHER
              MOVE "BPCCINTX"        TO W-OBJECT-NAME
              SET CMD-SELECT         TO TRUE
              PERFORM  P990-ERRO-DB2
              SET NAO-TRATA TO TRUE
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURJCCIN.
     š*---------------------------------------------------------------------
           INITIALIZE  NEWCCIN-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURJCCIN
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P001         TO TRUE
           WHEN OTHER
              SET  FIM-P001            TO TRUE
              MOVE "CURJCCIN"          TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P200-FETCH-CURSOR-CURJCCIN.
     š*---------------------------------------------------------------------

           EXEC SQL FETCH CURJCCIN
                     INTO  :NEWCCIN-INFTINST
                         , :NEWCCIN-BPENV
                         , :NEWCCIN-DTREFINST
                         , :NEWCCIN-IDCONT
                         , :NEWCCIN-IDINST
                         , :NEWCCIN-BALCAO
                         , :NEWCCIN-PROJFINAN
                         , :NEWCCIN-IDCONTSIND
                         , :NEWCCIN-LITIGJUD
                         , :NEWCCIN-IEB
                         , :NEWCCIN-PAISLEGIS
                         , :NEWCCIN-CANALCOMER
                         , :NEWCCIN-CLAUSRENUN
                         , :NEWCCIN-SUBVPROTOCOLO
                         , :NEWCCIN-REFEXTINST
                         , :NEWCCIN-TPINST
                         , :NEWCCIN-MOEDA
                         , :NEWCCIN-DTUTILFUND
                         , :NEWCCIN-DTINIINST
                         , :NEWCCIN-DTORIMAT
                         , :NEWCCIN-DTMAT
                         , :NEWCCIN-DTINICARJUR
                         , :NEWCCIN-DTFIMCARJUR
                         , :NEWCCIN-DTINICARCAP
                         , :NEWCCIN-DTFIMCARCAP
                         , :NEWCCIN-DIRREEMBLME
                         , :NEWCCIN-RECURSO
                         , :NEWCCIN-TPTXJUR
                         , :NEWCCIN-FREQATUALIZTX
                         , :NEWCCIN-TXREF
                         , :NEWCCIN-SPREAD
                         , :NEWCCIN-TXMAX
                         , :NEWCCIN-TXMIN
                         , :NEWCCIN-TAEG
                         , :NEWCCIN-TAE
                         , :NEWCCIN-PERFIXTX
                         , :NEWCCIN-DURPLANOFIN
                         , :NEWCCIN-FINALIDADE
                         , :NEWCCIN-TPAMORT
                         , :NEWCCIN-FREQPAGAM
                         , :NEWCCIN-DIVSUBOR
                         , :NEWCCIN-INSTFIDUC
                         , :NEWCCIN-MONTINI
                         , :NEWCCIN-VARFV
                         , :NEWCCIN-DTRENEG
                         , :NEWCCIN-TPNEG
                         , :NEWCCIN-PERCDIFCAP
                         , :NEWCCIN-TPTITULARIZ
                         , :NEWCCIN-SEGUROS
                         , :NEWCCIN-TMSCRIA
                         , :NEWCCIN-USERCRIA
                         , :NEWCCIN-TMSALT
                         , :NEWCCIN-USERALT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P001          TO TRUE
           WHEN OTHER
              SET   FIM-P001         TO TRUE
              MOVE "CURJCCIN"        TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-PROCESSO-GERAL.
     š*---------------------------------------------------------------------
           IF CICLO-1
              PERFORM P300-BPCFCCIN-OPEN
              IF SEM-ERRO
                INITIALIZE REG-BPCFCCIN  XML-OUTPUT
                PERFORM P300-OPEN-TAG-crc
                PERFORM P300-GERA-TAG-controlo
                PERFORM P300-OPEN-TAG-conteudo
                PERFORM P300-OPEN-TAG-comCInst
                SET CICLO-N TO TRUE
              END-IF
           END-IF
           IF SEM-ERRO
              PERFORM P300-OPEN-TAG-infInst
     š*- Gera lstEntSind
              PERFORM P500-PROCESSO-entSind
     š*- Gera lstLigInst
     š*- Gera lstCaracEsp
              PERFORM P310-GERA-TAG-caractEsp
     š*- Gera lstInfRiscoInst
              PERFORM P500-PROCESSO-InfRiscoInst
     š*- Novo Ciclo
              PERFORM P200-FETCH-CURSOR-CURJCCIN
           END-IF.
           PERFORM P300-CLOSE-TAG-infInst.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-crc.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT
     š*-XML Doc Declaration
           MOVE "<?xml version='1.0' encoding='ISO-8859-1'?>"
                       TO XML-OUTPUT.

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*-CRC Tag Declaration
           MOVE "<crc versao='1.0' >"    TO XML-OUTPUT

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-controlo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "controlo"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "entObserv"             TO  WS-NOME-TAG
           MOVE   ENTOBS     OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "entReport"             TO  WS-NOME-TAG
           MOVE  ENTREPORT   OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtCriacao"             TO  WS-NOME-TAG
           MOVE   DTCRIACAO  OF BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idDest"                TO  WS-NOME-TAG
           MOVE   IDDEST    OF  BPCJCNTL TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idFichRelac"           TO  WS-NOME-TAG
           MOVE   spaces                 TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           PERFORM P300-END-TAG

           MOVE  "controlo"              TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-conteudo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "conteudo"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-comCInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT.

           MOVE  "comCInst"            TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "xmlns:xsi"         TO  WS-NOME-TAG
           MOVE  "http://www.w3.org/2001/XMLSchema-instance"
                                     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "xsi:noNamespaceSchemaLocation"
                                     TO  WS-NOME-TAG
           MOVE  "file:/C:/crc_bdp_schema.xsd"
                                     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-infInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT
     š*- Type - EI-Insert/EU-Update/ED-Delete

           MOVE  "infInst"           TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE  "xsi:type"          TO  WS-NOME-TAG
           MOVE   NEWCCIN-INFTINST   TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idCont"                TO  WS-NOME-TAG
           MOVE   NEWCCIN-IDCONT         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idInst"                TO  WS-NOME-TAG
           MOVE   NEWCCIN-IDInst         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtRefInst"         TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTREFINST  TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "balcao"            TO  WS-NOME-TAG
           MOVE   NEWCCIN-BALCAO     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "projFinan"         TO  WS-NOME-TAG
           MOVE   NEWCCIN-PROJFINAN  TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "idContSind"        TO  WS-NOME-TAG
           MOVE   NEWCCIN-IDCONTSIND TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "litigJud"          TO  WS-NOME-TAG
           MOVE   NEWCCIN-LITIGJUD   TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "IEB"               TO  WS-NOME-TAG
           MOVE   NEWCCIN-IEB        TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "paisLegis"         TO  WS-NOME-TAG
           MOVE   NEWCCIN-PAISLEGIS  TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "canalComer"        TO  WS-NOME-TAG
           MOVE   NEWCCIN-CANALCOMER TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "clausRenun"        TO  WS-NOME-TAG
           MOVE   NEWCCIN-CLAUSRENUN TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "subvProtocolo"        TO  WS-NOME-TAG
           MOVE   NEWCCIN-SUBVPROTOCOLO TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "refExtInst"           TO  WS-NOME-TAG
           MOVE   NEWCCIN-REFEXTINST    TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "tpInst"               TO  WS-NOME-TAG
           MOVE   NEWCCIN-TPINST        TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "moeda"                TO  WS-NOME-TAG
           MOVE   NEWCCIN-MOEDA         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "dtUtilFund"           TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTUTILFUND    TO  WS-VAL-TAG-A
           IF NEWCCIN-DTUTILFUND  NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTUTILFUND  NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dtIniInst"            TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTINIINST     TO  WS-VAL-TAG-A
           IF NEWCCIN-DTINIINST   NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTINIINST   NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dtOriMat"             TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTORIMAT      TO  WS-VAL-TAG-A
           IF NEWCCIN-DTORIMAT    NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTORIMAT    NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dtMat"                TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTMAT         TO  WS-VAL-TAG-A
           IF NEWCCIN-DTMAT       NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTMAT       NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dtIniCarJur"          TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTINICARJUR   TO  WS-VAL-TAG-A
           IF NEWCCIN-DTINICARJUR NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTINICARJUR NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dtFimCarJur"          TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTFIMCARJUR   TO  WS-VAL-TAG-A
           IF NEWCCIN-DTFIMCARJUR NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTFIMCARJUR NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dtFimCarCap"          TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTFIMCARCAP   TO  WS-VAL-TAG-A
           IF NEWCCIN-DTFIMCARCAP NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTFIMCARCAP NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "dirReembIme"           TO  WS-NOME-TAG
           MOVE   NEWCCIN-DIRREEMBLME   TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "recurso"              TO  WS-NOME-TAG
           MOVE   NEWCCIN-RECURSO       TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "tpTxJuro"             TO  WS-NOME-TAG
           MOVE   NEWCCIN-TPTXJUR       TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "freqAtualizTx"         TO  WS-NOME-TAG
           MOVE   NEWCCIN-FREQATUALIZTX  TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "txRef"                 TO  WS-NOME-TAG
           MOVE   NEWCCIN-TXREF          TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           IF NEWCCIN-TPTXJUR NOT EQUAL "002"
             MOVE  "spread"                TO  WS-NOME-TAG
             MOVE  NEWCCIN-SPREAD          TO  Edit-Nbr-Tx
             MOVE  Edit-Nbr-Tx             TO  WS-VAL-TAG-A
             PERFORM P300-ATRIBUTE-N

             MOVE  "txMax"                 TO  WS-NOME-TAG
             MOVE   NEWCCIN-TXMAX          TO  Edit-Nbr-Tx
             MOVE  Edit-Nbr-Tx             TO  WS-VAL-TAG-A
             PERFORM P300-ATRIBUTE-N

             MOVE  "txMin"                 TO  WS-NOME-TAG
             MOVE   NEWCCIN-TXMin          TO  Edit-Nbr-Tx
             MOVE  Edit-Nbr-Tx             TO  WS-VAL-TAG-A
             PERFORM P300-ATRIBUTE-N
           END-IF

           MOVE  "TAEG"                  TO  WS-NOME-TAG
           MOVE   NEWCCIN-TAEG           TO  Edit-Nbr-Tx
           MOVE  Edit-Nbr-Tx             TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "TAE"                   TO  WS-NOME-TAG
           MOVE   NEWCCIN-TAE            TO  Edit-Nbr-Tx
           MOVE  Edit-Nbr-Tx             TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "perFixTx"              TO  WS-NOME-TAG
           MOVE   NEWCCIN-PERFIXTX       TO  Edit-Nbr-In
           MOVE   Edit-Nbr-In            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "durPlanoFin"           TO  WS-NOME-TAG
           MOVE   NEWCCIN-DURPLANOFIN    TO  Edit-Nbr-In
           MOVE   Edit-Nbr-In            TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "finalidade"            TO  WS-NOME-TAG
           MOVE   NEWCCIN-FINALIDADE     TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "tpAmort"               TO  WS-NOME-TAG
           MOVE   NEWCCIN-TPAMORT        TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "freqPagam"             TO  WS-NOME-TAG
           MOVE   NEWCCIN-FREQPAGAM      TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "divSubor"              TO  WS-NOME-TAG
           MOVE   NEWCCIN-DIVSUBOR       TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "instFiduc"             TO  WS-NOME-TAG
           MOVE   NEWCCIN-INSTFIDUC      TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "montIni"               TO  WS-NOME-TAG
           MOVE   NEWCCIN-MONTINI        TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn             TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "varFV"                 TO  WS-NOME-TAG
           MOVE   NEWCCIN-VARFV          TO  Edit-Nbr-Mn
           MOVE  Edit-Nbr-Mn             TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "dtReneg"               TO  WS-NOME-TAG
           MOVE   NEWCCIN-DTRENEG        TO  WS-VAL-TAG-A
           IF NEWCCIN-DTRENEG NOT EQUAL "0001-01-01" AND
              NEWCCIN-DTRENEG NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE  "tpNeg"                 TO  WS-NOME-TAG
           MOVE   NEWCCIN-TPNEG          TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "percDifCap"            TO  WS-NOME-TAG
           MOVE   NEWCCIN-PERCDIFCAP     TO  Edit-Nbr-Tx
           MOVE  Edit-Nbr-Tx             TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE-N.

           MOVE  "tpTitulariz"           TO  WS-NOME-TAG
           MOVE   NEWCCIN-TPTITULARIZ    TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "seguros"               TO  WS-NOME-TAG
           MOVE   NEWCCIN-SEGUROS        TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE  "infInst"          TO  WS-NOME-TAG
           MOVE SPACES TO WS-VAL-TAG-A
           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-infInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "infInst"                 TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-comCInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "comCInst"                TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-conteudo.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "conteudo"              TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-crc.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "crc"                   TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-BPCFCCIN-OPEN.
     š*---------------------------------------------------------------------
           OPEN OUTPUT BPCFCCIN
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-OPENED
               CONTINUE
           ELSE
               SET  FIM-P001             TO TRUE
               MOVE "BPCPCCINX"            TO W-PROGRAMA
               MOVE "P300-BPCFCCIN-OPEN" TO W-PARAGRAFO
               MOVE "BPCFCCIN"           TO W-OBJECT-NAME
               MOVE "OPEN"               TO W-CMDEXEC
               MOVE SPACES               TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P400-BPCFCCIN-CLOSE.
     š*---------------------------------------------------------------------
           CLOSE BPCFCCIN
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO                OR
               GRS-FILE-ALREADY-CLOSED
               CONTINUE
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "BPCPCCINX"             TO W-PROGRAMA
               MOVE "P300-BPCFCCIN-CLOSE" TO W-PARAGRAFO
               MOVE "BPCFCCIN"            TO W-OBJECT-NAME
               MOVE "CLOSE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P450-FTP-FILE.
     š*---------------------------------------------------------------------
           INITIALIZE BPCL001M-FICH-ORIGEM
                      BPCL001M-FICH-DESTINO
                      BPCL001M-ERRO-BPCPFTP
                      REPLACING ALPHANUMERIC BY SPACES
                                    NUMERIC BY ZEROS.

           MOVE  "BPCFCCIN"            TO BPCL001M-FICH-ORIGEM
           MOVE  NOMEFICH  OF BPCJCNTL TO BPCL001M-FICH-DESTINO

           CALL "BPCCFTP" USING BPCL001M-FICH-ORIGEM
                                BPCL001M-FICH-DESTINO
                                BPCL001M-ERRO-BPCPFTP

           IF BPCL001M-ERRO-BPCPFTP NOT EQUAL SPACES
             MOVE "001"                     TO W-FILE-CODE
             MOVE WS-ERRO                   TO MSG-ERRO-FS
             MOVE "BPCCFTP"                 TO W-OBJECT-NAME
             MOVE "FTP"                     TO W-CMDEXEC
             MOVE "P450-FTP-FILE"           TO W-PARAGRAFO
             PERFORM P990-ERRO-FS
           END-IF.
     š*---------------------------------------------------------------------
       P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
           WRITE REG-BPCFCCIN
           MOVE GRS-FILE-STATUS TO W-FILE-CODE W-EDTFILC
           IF  GRS-GOOD-IO       OR
               GRS-DUP-KEY
                INITIALIZE REG-BPCFCCIN
                ADD 1 TO NR-PROC
           ELSE
               SET  FIM-P001              TO TRUE
               MOVE "BPCPCCINX"             TO W-PROGRAMA
               MOVE "P300-BPCFCCIN-WRITE" TO W-PARAGRAFO
               MOVE "BPCFCCIN"            TO W-OBJECT-NAME
               MOVE "WRITE"               TO W-CMDEXEC
               MOVE SPACES                TO W-CHAVE
               PERFORM  P990-ERRO-FS
           END-IF.

           MOVE 1 TO WS-POS-XML.

     š*---------------------------------------------------------------------
       P400-CLOSE-CURSOR-CURJCCIN.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJCCIN
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P001            TO TRUE
           WHEN OTHER
              SET  FIM-P001            TO TRUE
              MOVE "CURJCCIN"          TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P500-PROCESSO-entSind.
     š*---------------------------------------------------------------------
     š*--CICLO DE LEITURA DOC DE IDENTIFICAÇÃO
           SET CICLO-DOC-1 TO TRUE
           PERFORM P110-OPEN-CURSOR-CURJCCIN1.
           PERFORM P210-FETCH-CURSOR-CURJCCIN1.
           PERFORM P310-GERA-TAG-entSind
                           UNTIL FIM-P002
           PERFORM P410-CLOSE-CURSOR-CURJCCIN1.
           IF CICLO-DOC-N
             PERFORM P300-CLOSE-TAG-entSind
           END-IF.
     š*---------------------------------------------------------------------
       P500-PROCESSO-InfRiscoInst.
     š*---------------------------------------------------------------------
     š*--CICLO DE LEITURA DOC DE IDENTIFICAÇÃO
           SET CICLO-DOC-1 TO TRUE
           PERFORM P110-OPEN-CURSOR-CURJCCIN3.
           PERFORM P210-FETCH-CURSOR-CURJCCIN3.
           PERFORM P310-GERA-TAG-InfRiscoInst
                           UNTIL FIM-P002
           PERFORM P410-CLOSE-CURSOR-CURJCCIN3.
           IF CICLO-DOC-N
             PERFORM P300-CLOSE-TAG-InfRiscoInst
           END-IF.
     š*---------------------------------------------------------------------
       P110-OPEN-CURSOR-CURJCCIN1.
     š*---------------------------------------------------------------------
           INITIALIZE  NEWCCIN1-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           EXEC SQL
                OPEN CURJCCIN1
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P002         TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJCCIN1"         TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P210-FETCH-CURSOR-CURJCCIN1.
     š*---------------------------------------------------------------------
           EXEC SQL FETCH CURJCCIN1
                     INTO  :NEWCCIN1-IDCONTSIND
                         , :NEWCCIN1-IDENT
                         , :NEWCCIN1-TYPER
                         , :NEWCCIN1-RELENTSIND
                         , :NEWCCIN1-RACINE
                         , :NEWCCIN1-TMSCRIA
                         , :NEWCCIN1-USERCRIA
                         , :NEWCCIN1-TMSALT
                         , :NEWCCIN1-USERALT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P002          TO TRUE
           WHEN OTHER
              SET   FIM-P002         TO TRUE
              MOVE "CURJCCIN"        TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P300-OPEN-TAG-lstentSind.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstEntSind"             TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P310-GERA-TAG-entSind.
     š*---------------------------------------------------------------------
           IF CICLO-DOC-1
              SET CICLO-DOC-N TO TRUE
              PERFORM P300-OPEN-TAG-lstentSind
           END-IF

           INITIALIZE XML-OUTPUT

           MOVE   "entSind"                   TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE   "relEntSind"                TO  WS-NOME-TAG
           MOVE   NEWCCIN1-RELENTSIND         TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE   "entSind"                   TO  WS-NOME-TAG
           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.

           INITIALIZE XML-OUTPUT

           PERFORM P300-GERA-TAG-idEnt

           INITIALIZE XML-OUTPUT

           MOVE   "entSind"                   TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
           PERFORM P210-FETCH-CURSOR-CURJCCIN1.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-entSind.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE   "lstEntSind"                  TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-GERA-TAG-idEnt.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

     š*-Residentes i1
           MOVE   "idEnt"                     TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE   "xsi:type"                  TO  WS-NOME-TAG
           MOVE   NEWCCIN1-TYPER               TO  WS-VAL-TAG-A
           PERFORM P300-ATRIBUTE

           MOVE   "idEnt"                     TO  WS-NOME-TAG
           MOVE   NEWCCIN1-IDENT               TO  WS-VAL-TAG-A

           PERFORM P300-END-TAG.

           MOVE   NEWCCIN1-IDENT               TO  WS-VAL-TAG-A
           PERFORM P300-DATA-TAG.

           MOVE  "idEnt"                TO  WS-NOME-TAG
           MOVE  SPACES                 TO  WS-VAL-TAG-A
           PERFORM P300-CLOSE-TAG.

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P310-GERA-TAG-caractEsp.
     š*---------------------------------------------------------------------
           IF NEWCCIN-IDCONTSIND NOT EQUAL SPACES
              PERFORM P300-OPEN-TAG-lstCaracEsp

              INITIALIZE XML-OUTPUT

              MOVE   "caractEsp"                 TO  WS-NOME-TAG
              PERFORM P300-OPEN-TAG

              MOVE   "tpCaractEsp"               TO  WS-NOME-TAG
              MOVE   "001"                       TO  WS-VAL-TAG-A
              PERFORM P300-ATRIBUTE

              PERFORM P300-END-TAG

              MOVE   "caractEsp"                 TO  WS-NOME-TAG
              PERFORM P300-CLOSE-TAG

              MOVE XML-OUTPUT TO REG-BPCFCCIN
              PERFORM P300-BPCFCCIN-WRITE

              MOVE   "caractEsp"                 TO  WS-NOME-TAG
              PERFORM P300-END-TAG

              PERFORM P300-CLOSE-TAG-lstCaracEsp

           END-IF.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG-lstCaracEsp.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstCaracEsp"             TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-lstCaracEsp.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE   "lstCaracEsp"                 TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P110-OPEN-CURSOR-CURJCCIN3.
     š*---------------------------------------------------------------------
           INITIALIZE  NEWCCIN3-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL
                OPEN CURJCCIN3
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P002         TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJCCIN3"         TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P210-FETCH-CURSOR-CURJCCIN3.
     š*---------------------------------------------------------------------

           EXEC SQL FETCH CURJCCIN3
                     INTO  :NEWCCIN3-IDCONT
                         , :NEWCCIN3-IDINST
                         , :NEWCCIN3-DTPDINST
                         , :NEWCCIN3-PDINST
                         , :NEWCCIN3-TIPOPDINST
                         , :NEWCCIN3-TPAVALR
                         , :NEWCCIN3-SISAVALR
                         , :NEWCCIN3-MODIRBINST
                         , :NEWCCIN3-NOTACINST
                         , :NEWCCIN3-LGDINST
                         , :NEWCCIN3-MODLGDINST
                         , :NEWCCIN3-TMSCRIA
                         , :NEWCCIN3-USERCRIA
                         , :NEWCCIN3-TMSALT
                         , :NEWCCIN3-USERALT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P002          TO TRUE
           WHEN OTHER
              SET   FIM-P002         TO TRUE
              MOVE "CURJCCIN3"       TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P300-OPEN-TAG-lstInfRiscoInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE  "lstInfRiscoInst"        TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           PERFORM P300-END-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P310-GERA-TAG-InfRiscoInst.
     š*---------------------------------------------------------------------
           IF CICLO-DOC-1
              SET CICLO-DOC-N TO TRUE
              PERFORM P300-OPEN-TAG-lstInfRiscoInst
           END-IF

           INITIALIZE XML-OUTPUT

           MOVE   "infRiscoInst"              TO  WS-NOME-TAG
           PERFORM P300-OPEN-TAG

           MOVE   "tipoPDInst"                TO  WS-NOME-TAG
           MOVE   NEWCCIN3-TIPOPDINST         TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL SPACES
             PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "notacaoInst"               TO  WS-NOME-TAG
           MOVE   NEWCCIN3-NOTACINST          TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL SPACES
              PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "PDInst"                    TO  WS-NOME-TAG
           MOVE   NEWCCIN3-PDINST             TO  Edit-Nbr-Tx
           IF NEWCCIN3-PDINST NOT EQUAL ZEROS
             MOVE   Edit-Nbr-Tx               TO  WS-VAL-TAG-A
             PERFORM P300-ATRIBUTE-N
           END-IF

           MOVE   "dtPDInst"                  TO  WS-NOME-TAG
           MOVE   NEWCCIN3-DTPDINST           TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL  SPACES AND
              WS-VAL-TAG-A  NOT EQUAL "0001-01-01"
             PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "tpAvalRiscoInst"           TO  WS-NOME-TAG
           MOVE   NEWCCIN3-TPAVALR            TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL  SPACES
             PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "sistAvalRiscoInst"         TO  WS-NOME-TAG
           MOVE   NEWCCIN3-SISAVALR           TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL  SPACES
             PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "modIRBInst"                TO  WS-NOME-TAG
           MOVE   NEWCCIN3-MODIRBINST         TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL  SPACES
             PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "LGDInst"                   TO  WS-NOME-TAG
           MOVE   NEWCCIN3-LGDINST            TO  Edit-Nbr-Tx
           MOVE   Edit-Nbr-Tx                 TO  WS-VAL-TAG-A
           IF NEWCCIN3-LGDINST NOT EQUAL ZEROS
             PERFORM P300-ATRIBUTE-N
           END-IF

           MOVE   "modLGDInst"                TO  WS-NOME-TAG
           MOVE   NEWCCIN3-MODLGDINST         TO  WS-VAL-TAG-A
           IF WS-VAL-TAG-A  NOT EQUAL  SPACES
             PERFORM P300-ATRIBUTE
           END-IF

           MOVE   "infRiscoInst"              TO  WS-NOME-TAG
           STRING "/>"
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.

           PERFORM P210-FETCH-CURSOR-CURJCCIN3.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG-InfRiscoInst.
     š*---------------------------------------------------------------------
           INITIALIZE XML-OUTPUT

           MOVE   "lstInfRiscoInst"             TO  WS-NOME-TAG
           PERFORM P300-CLOSE-TAG

           MOVE XML-OUTPUT TO REG-BPCFCCIN
           PERFORM P300-BPCFCCIN-WRITE.
     š*---------------------------------------------------------------------
       P300-OPEN-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-ATRIBUTE.
     š*---------------------------------------------------------------------
     š*-Trata Valor Atributo
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF WS-STRING  NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-GERA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L
             MOVE WS-STRING    TO WS-VAL-TAG-A

     š*-Trata Nome da TAG
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L
             MOVE WS-STRING    TO WS-NOME-TAG

             STRING "<"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
                   "</"
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   ">"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-DATA-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A(1: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-DATA-TAG-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

             STRING
                   WS-VAL-TAG-A( WS-POS-S: WS-VAL-TAG-L)
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML
           END-IF.
     š*---------------------------------------------------------------------
       P300-ATRIBUTE-N.
     š*---------------------------------------------------------------------
     š*-Trata Valor da TAG
           MOVE WS-VAL-TAG-A TO WS-STRING
           IF  WS-VAL-TAG-A NOT = SPACES
             PERFORM P300-RETIRA-CARAC-ESPECIAIS
             PERFORM P300-COMPRIMENTO
             PERFORM P300-PRIMEIRO-ESPACO
             INSPECT WS-STRING REPLACING
                      all "," by "."
             MOVE WS-STRING TO WS-VAL-TAG-A
             MOVE WS-STRING-L  TO WS-VAL-TAG-L

     š*-Trata Nome e Valor Atributo
             MOVE WS-NOME-TAG TO WS-STRING
             PERFORM P300-COMPRIMENTO
             MOVE WS-STRING-L  TO WS-NOM-TAG-L

             STRING  " "
                   WS-NOME-TAG(1: WS-NOM-TAG-L)
                   "='"
                   WS-VAL-TAG-A(WS-POS-S: WS-VAL-TAG-L)
                   "'"
             DELIMITED BY SIZE INTO XML-OUTPUT
             WITH POINTER WS-POS-XML

           END-IF.
     š*---------------------------------------------------------------------
       P300-CLOSE-TAG.
     š*---------------------------------------------------------------------
     š*-Trata Nome da TAG
           MOVE WS-NOME-TAG TO WS-STRING
           PERFORM P300-COMPRIMENTO
           MOVE WS-STRING-L  TO WS-NOM-TAG-L

           STRING
                "</"
                WS-NOME-TAG(1: WS-NOM-TAG-L)
                ">"

           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.

     š*---------------------------------------------------------------------
       P300-END-TAG.
     š*---------------------------------------------------------------------
           STRING ">"
           DELIMITED BY SIZE INTO XML-OUTPUT
           WITH POINTER WS-POS-XML.
     š*---------------------------------------------------------------------
       P410-CLOSE-CURSOR-CURJCCIN1.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJCCIN1
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P002            TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJCCIN1"         TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P410-CLOSE-CURSOR-CURJCCIN3.
     š*---------------------------------------------------------------------
           EXEC SQL
               CLOSE CURJCCIN3
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  FIM-P002            TO TRUE
           WHEN OTHER
              SET  FIM-P002            TO TRUE
              MOVE "CURJCCIN3"         TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*---------------------------------------------------------------------
       P300-COMPRIMENTO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING
     š*- WY -Comprimento Final
           MOVE ZEROES TO WS-POS-I.
           INSPECT FUNCTION REVERSE(WS-STRING)
                   TALLYING WS-POS-I      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = LENGTH OF WS-STRING  -  WS-POS-I.
     š*---------------------------------------------------------------------
       P300-PRIMEIRO-ESPACO.
     š*---------------------------------------------------------------------
     š*--Retira Espaços  WS-STRING(1:WS-STRING-L)
           MOVE ZEROES TO WS-POS-S.
           INSPECT WS-STRING(1:WS-STRING-L)
                   TALLYING WS-POS-S      FOR LEADING SPACE.
           COMPUTE  WS-STRING-L = WS-STRING-L -  WS-POS-S.
           ADD 1 TO WS-POS-S.
     š*---------------------------------------------------------------------
       P300-RETIRA-CARAC-ESPECIAIS.
     š*---------------------------------------------------------------------
     š*-Retira Caracters Invalidos de WS-STRING
           PERFORM P300-UTLS001-TESTA
                   VARYING W-NUM FROM 1 BY 1
                   UNTIL W-NUM > LENGTH OF WS-STRING.

     š*---------------------------------------------------------------------
       P300-UTLS001-TESTA.
     š*---------------------------------------------------------------------
           MOVE WS-STRING-CHR(W-NUM)  TO CHAR-TESTE.
           IF   NOT CARACTER-VALIDO
                PERFORM  P300-UTLS001-ALTERA
           END-IF.

     š*---------------------------------------------------------------------
       P300-UTLS001-ALTERA.
     š*---------------------------------------------------------------------
           EVALUATE  TRUE

            WHEN WS-STRING-CHR(W-NUM) = "à" OR
                 WS-STRING-CHR(W-NUM) = "á" OR
                 WS-STRING-CHR(W-NUM) = "â" OR
                 WS-STRING-CHR(W-NUM) = "ã" OR
                 WS-STRING-CHR(W-NUM) = "ä" OR
                 WS-STRING-CHR(W-NUM) = "ª"
              MOVE "a" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ç"
              MOVE "c" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "é" OR
                 WS-STRING-CHR(W-NUM) = "è" OR
                 WS-STRING-CHR(W-NUM) = "ê" OR
                 WS-STRING-CHR(W-NUM) = "ë"
              MOVE "e" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "í" OR
                 WS-STRING-CHR(W-NUM) = "ì" OR
                 WS-STRING-CHR(W-NUM) = "ï"
              MOVE "i" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ñ"
              MOVE "n" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ó" OR
                 WS-STRING-CHR(W-NUM) = "ò" OR
                 WS-STRING-CHR(W-NUM) = "õ" OR
                 WS-STRING-CHR(W-NUM) = "ö"
              MOVE "o" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "ú" OR
                 WS-STRING-CHR(W-NUM) = "ù" OR
                 WS-STRING-CHR(W-NUM) = "û" OR
                 WS-STRING-CHR(W-NUM) = "ü"
              MOVE "u" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Á" OR
                 WS-STRING-CHR(W-NUM) = "À" OR
                 WS-STRING-CHR(W-NUM) = "Ã" OR
                 WS-STRING-CHR(W-NUM) = "Â" OR
                 WS-STRING-CHR(W-NUM) = "Ä"
              MOVE "A" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ç"
              MOVE "C" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "É" OR
                 WS-STRING-CHR(W-NUM) = "È" OR
                 WS-STRING-CHR(W-NUM) = "Ê" OR
                 WS-STRING-CHR(W-NUM) = "Ë"
              MOVE "E" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Í" OR
                 WS-STRING-CHR(W-NUM) = "Ì" OR
                 WS-STRING-CHR(W-NUM) = "Ï"
              MOVE "I" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ñ"
              MOVE "N" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ó" OR
                 WS-STRING-CHR(W-NUM) = "Ò" OR
                 WS-STRING-CHR(W-NUM) = "Õ" OR
                 WS-STRING-CHR(W-NUM) = "Ô" OR
                 WS-STRING-CHR(W-NUM) = "Ö"
              MOVE "O" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "Ú" OR
                 WS-STRING-CHR(W-NUM) = "Ù" OR
                 WS-STRING-CHR(W-NUM) = "Û" OR
                 WS-STRING-CHR(W-NUM) = "Ü"
              MOVE "U" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "\"
              MOVE "/" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "[" OR
                 WS-STRING-CHR(W-NUM) = "{"
              MOVE "(" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = "]" OR
                 WS-STRING-CHR(W-NUM) = "}"
              MOVE ")" TO WS-STRING-CHR(W-NUM)

            WHEN WS-STRING-CHR(W-NUM) = """" OR
                 WS-STRING-CHR(W-NUM) = "«"  OR
                 WS-STRING-CHR(W-NUM) = "»"
              MOVE "'" TO WS-STRING-CHR(W-NUM)

            WHEN OTHER
              MOVE " " TO WS-STRING-CHR(W-NUM)

           END-EVALUATE.

     š*---------------------------------------------------------------------
       P990-ERRO-FS.
     š*---------------------------------------------------------------------
           EVALUATE TRUE
           WHEN GRS-READ-DUP-KEY
                  STRING "ERRO DE DADOS: CHAVE DUPLICADA NO FICHEIRO <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-FS
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ ERRO    <"
                      W-EDTFILC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-FS
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           SET  COM-ERRO        TO TRUE
           MOVE  1              TO ERRRTC   OF  KYCT900A
           MOVE W-FILE-CODE     TO ERRCOD   OF  KYCT900A
           MOVE MSG-ERRO-FS     TO ERRMSG   OF  KYCT900A
           MOVE "BPCPCCINX"       TO ERRPRG   OF  KYCT900A
           MOVE "BPC"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE W-CMDEXEC       TO ERRCMD   OF  KYCT900A
           MOVE W-PARAGRAFO     TO ERRFLD   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           SET  COM-ERRO        TO TRUE
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BPCPCCINX"      TO  ERRPRG OF KYCT900A.
           MOVE   WS-NURACI      TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "KYCT900A"       TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.

     š*---------------------------------------------------------------------
       P999-FIMPGM.
     š*---------------------------------------------------------------------
           EXIT PROGRAM.
           STOP RUN.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BPCPCCINX
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

