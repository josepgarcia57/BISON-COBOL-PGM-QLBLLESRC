      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP919.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP919
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP919"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-IDREGSEL.
               10  WS-IDGRFUNC         PIC X(08).
               10  FILLER              PIC X(72).

           05  WS-PARMSLDV             PIC X(80).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB969.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET908 OF CRET908.
           COPY DDS-CRET909 OF CRET909.
           COPY DDS-CRET910 OF CRET910.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-IDREGSEL     TO WS-IDREGSEL
           IF  CREB000-MSGDLG-P > 1
           OR  CREB000-IDCANALC = CREB950-IDCANALC-RM
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB969-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB969-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN(CREB000-INITDLG AND NOT CREB000-PROCLOV)
           OR   CREB000-PROCIDT
               PERFORM P2100-INICIALIZA
               IF  CREB000-PROCNLF
               AND(CREB000-PROC-OK OR  CREB000-MSGINFO)
               AND CREB000-IDREG-OK
                   PERFORM P2500-PROC-REGEXIST
               END-IF
           WHEN CREB000-PROCLOV
               PERFORM P2200-PROC-LISTVALS
           WHEN CREB000-IDREG-NV
               PERFORM P2300-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2400-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2500-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2600-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2700-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2800-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB969-MSGDIALG
           SET  CREB000-IDREG-NF TO TRUE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCIDT OR CREB000-PROCSEL
               PERFORM P2110-READ-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2120-INIT-NOVOREG
           END-EVALUATE
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2110-READ-REGEXIST         SECTION.

           MOVE WS-IDGRFUNC OF WS-IDREGSEL   TO CREB969-IDGRFUNC-V
           PERFORM P9000-READ-CRET908

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2111-FRMT-MSGDIALG
           WHEN OTHER
               SET  CREB950-FUNCAO-INTERRP   TO TRUE
               SET  CREB000-MSGFUNC          TO TRUE
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               IF  CREB969-IDGRFUNC-V = CREB000-IDGRFUNC
                   SET  CREB000-FUNCANUL-OFF TO TRUE
               END-IF
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
               AND NOT CREB950-FUNCAO-INTERRP
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           END-IF
           .
       P2110-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2111-FRMT-MSGDIALG         SECTION.

           MOVE DSGRFUNC OF CRET908       TO CREB969-DSGRFUNC-V
           MOVE DHINISIT OF CRET908       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB969-DHINISIT-V
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DATA-X08
           EVALUATE TRUE
           WHEN CREB950-DATA-N08    <= CREB000-DTPRCAPL
               MOVE MCSITEXE OF CRET908   TO CREB969-MCSITEXE-V
           WHEN MCSITEXE OF CRET908  = KV-MCSITEXE-ACTV
               MOVE KV-MCSITEXE-INIB      TO CREB969-MCSITEXE-V
           WHEN OTHER
               MOVE KV-MCSITEXE-ACTV      TO CREB969-MCSITEXE-V
           END-EVALUATE
           PERFORM P9000-READ-SITEXECU
           MOVE DHCRIREG OF CRET908       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB969-DHCRIREG-V
           IF  DHMDFREG OF CRET908 NOT = CREB950-TMSTPMIN
               MOVE DHMDFREG OF CRET908   TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14      TO CREB969-DHMDFREG-V
           END-IF
           MOVE DHMDFREG OF CRET908       TO CREB000-DHMDFREG
           .
       P2111-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2120-INIT-NOVOREG          SECTION.

           MOVE WS-IDGRFUNC OF WS-IDREGSEL TO CREB969-IDGRFUNC-V
           MOVE KV-MCSITEXE-INIB           TO CREB969-MCSITEXE-V
           PERFORM P9000-READ-SITEXECU

           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PERMIT-DET
           PERFORM P9000-NDISPL-DET
           .
       P2120-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2210-FRMT-PARMSLDV
           ELSE
               PERFORM P2220-RECB-IDSELLDV
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-FRMT-PARMSLDV         SECTION.

           CONTINUE
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-RECB-IDSELLDV         SECTION.

           CONTINUE
           .
       P2220-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB969-IDGRFUNC-V   TO CREB999-IDGRFUNC
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO CREB000-PARMSINP
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB969-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           MOVE CREB969-IDGRFUNC-V TO WS-STRING80
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF CREB969-IDGRFUNC-V
             OR   (WS-CHRSTR80(WS-ARRYNDX1) = SPACE OR "*" OR "?")
               CONTINUE
           END-PERFORM

           PERFORM
           VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
             UNTIL WS-ARRYNDX1 > LENGTH OF CREB969-IDGRFUNC-V
             OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
               CONTINUE
           END-PERFORM

           EVALUATE TRUE
           WHEN CREB969-IDGRFUNC-V       = SPACES
               SET  CREB969-IDGRFUNC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN WS-ARRYNDX1             <= LENGTH OF CREB969-IDGRFUNC-V
               SET  CREB969-IDGRFUNC-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               PERFORM P2510-VRIF-NIVEL01
           END-EVALUATE
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           PERFORM P9000-READ-CRET908

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-IDREG-OK
               SET  CREB969-IDGRFUNC-PC    TO TRUE
               SET  CREB950-REGIST-JEXISTE TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
               SET  CREB000-IDREG-NF       TO TRUE
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2520-VRIF-NIVEL02
           END-IF
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-MSGDIALG         SECTION.

           PERFORM P2530-FRMT-MSGDIALG
           .
       P2430-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE CREB969-VARSOUT
               PERFORM P2510-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2520-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2530-FRMT-MSGDIALG
               END-IF
           ELSE
               PERFORM P2540-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2500-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2510-VRIF-NIVEL01          SECTION.

           IF  CREB969-DSGRFUNC-V = SPACES
               SET  CREB969-DSGRFUNC-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2510-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2520-VRIF-NIVEL02          SECTION.

           CONTINUE
           .
       P2520-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2530-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2530-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2540-VRIF-RELDPEND         SECTION.

           PERFORM P9000-VRIF-CRET910
           .
       P2540-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2600-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2710-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2720-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2730-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-EXEC-TRNCRIAR         SECTION.

           PERFORM P9000-FRMT-CRET908-CHAV
           PERFORM P9000-FRMT-CRET908-ATRB
           PERFORM P9000-INSR-CRET908

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE CREB969-IDGRFUNC-V TO WS-IDGRFUNC OF WS-IDREGSEL
           END-IF
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-CRET908-CHAV
           PERFORM P9000-FRMT-CRET908-ATRB
           PERFORM P9000-UPDT-CRET908
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-EXEC-TRNANULA         SECTION.

           PERFORM P9000-FRMT-CRET908-CHAV
           PERFORM P9000-DELT-CRET908

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-DELT-CRET909
           END-IF
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-IDREG-NF
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PERMIT-DET
           WHEN CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-EVALUATE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
           OR   CREB000-IDCANALC = CREB950-IDCANALC-RM
               CONTINUE
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB969-MSGDIALG
           END-EVALUATE
           MOVE CREB969-MSGDIALG       TO CREB000-MSGDLG-V
                                         (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL   TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB969-IDGRFUNC-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB969-IDGRFUNC-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  CREB969-DSGRFUNC-UP     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  CREB969-DSGRFUNC-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  CREB969-DSSITEXE-PR TO TRUE
           SET  CREB969-DHINISIT-PR TO TRUE
           SET  CREB969-DHCRIREG-PR TO TRUE
           SET  CREB969-DHMDFREG-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           IF  CREB000-PROCCRT
           OR (CREB000-PROCVRF AND CREB000-IDREG-NF)
               SET  CREB969-DSSITEXE-PR TO TRUE
           ELSE
               SET  CREB969-DSSITEXE-ND TO TRUE
           END-IF
           SET  CREB969-DHINISIT-ND     TO TRUE
           SET  CREB969-DHCRIREG-ND     TO TRUE
           SET  CREB969-DHMDFREG-ND     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET908          SECTION.

           MOVE CREB969-IDGRFUNC-V TO IDGRFUNC OF CRET908
           EXEC SQL SELECT DSGRFUNC
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , DHCRIREG
                      INTO :CRET908.DSGRFUNC
                         , :CRET908.MCSITEXE
                         , :CRET908.DHINISIT
                         , :CRET908.DHMDFREG
                         , :CRET908.DHCRIREG
                      FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP919.CRET908" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB969-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB969-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB969-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET910          SECTION.

           MOVE CREB000-IDAPLICA   TO IDAPLICA OF CRET901
           MOVE CREB969-IDGRFUNC-V TO IDGRFUNC OF CRET910
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901 A
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET910
                     WHERE IDGRFUNC = :CRET910.IDGRFUNC
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET910"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP919.EXIST01"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET908-CHAV     SECTION.

           MOVE CREB969-IDGRFUNC-V     TO IDGRFUNC OF CRET908
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET908
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET908-ATRB     SECTION.

           MOVE CREB969-DSGRFUNC-V        TO DSGRFUNC OF CRET908
           MOVE CREB969-MCSITEXE-V        TO MCSITEXE OF CRET908
           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB969-MCSITEXE-V    TO MCSITEXE OF CRET908
               MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB000-DTPRCAPL      TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08 TO CREB950-DTHR-X14
               MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
               MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET908
               MOVE CREB000-IDUTILIZ      TO IDUTLCRI OF CRET908
               MOVE CREB950-TMSTPMIN      TO DHMDFREG OF CRET908
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET908
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-CRET908          SECTION.

           EXEC SQL INSERT INTO CRET908
                         ( IDGRFUNC
                         , DSGRFUNC
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , IDUTLMDF
                         , DHCRIREG
                         , IDUTLCRI
                         )
                    VALUES
                         ( :CRET908.IDGRFUNC
                         , :CRET908.DSGRFUNC
                         , :CRET908.MCSITEXE
                         , :CRET908.DHINISIT
                         , :CRET908.DHMDFREG
                         , :CRET908.IDUTLMDF
                         , CURRENT TIMESTAMP
                         , :CRET908.IDUTLCRI
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP919.CRET908" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET908          SECTION.

           EXEC SQL UPDATE CRET908
                       SET DSGRFUNC = :CRET908.DSGRFUNC
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET908.IDUTLMDF
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC
                       AND DHMDFREG = :CRET908.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP919.CRET908"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET908          SECTION.

           EXEC SQL DELETE FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC
                       AND DHMDFREG = :CRET908.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP919.CRET908"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET909          SECTION.

           MOVE IDGRFUNC OF CRET908   TO IDGRFUNC OF CRET909
           EXEC SQL DELETE FROM CRET909
                     WHERE IDGRFUNC = :CRET909.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP919.CRET906" TO CREB000-NMOBJECT
               SET  CREB000-DELT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP919 ------------------------------------*
