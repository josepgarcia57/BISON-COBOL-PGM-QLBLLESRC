      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP909.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP909
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP909"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDPRCAPL         PIC X(08).
               10  FILLER              PIC X(72).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVPRCAPL-I   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDTIPDLG         PIC XX.
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPRCAPL-O   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).

           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTINISIT             PIC 9(08).
           05  WS-HRINISIT             PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDTIPDLG-LS.
               10  KV-IDTIPDLG-MNTD    PIC XX      VALUE "MD".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção Directa".
               10  KV-IDTIPDLG-MNTI    PIC XX      VALUE "MI".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção c/ Apoio".
               10  KV-IDTIPDLG-MNTA    PIC XX      VALUE "MA".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção de Apoio".
               10  KV-IDTIPDLG-CNSD    PIC XX      VALUE "CD".
               10  FILLER              PIC X(19)   VALUE
                                               "Consulta Directa".
               10  KV-IDTIPDLG-CNSL    PIC XX      VALUE "CL".
               10  FILLER              PIC X(19)   VALUE
                                               "Consulta em Lista".
               10  KV-IDTIPDLG-PEDX    PIC XX      VALUE "PD".
               10  FILLER              PIC X(19)   VALUE
                                               "Pedido de Execução".
           05  KV-IDTIPDLG-LV  REDEFINES KV-IDTIPDLG-LS    OCCURS 6.
               10  KV-IDTIPDLG-ID      PIC XX.
               10  KV-IDTIPDLG-DS      PIC X(19).
           05  KV-IDTIPDLG-OCC         PIC 9       VALUE 6.

           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB958.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET904 OF CRET904.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-TIMESTMP             PIC X(26).
           05  DB-DTINISIT             PIC X(10).
           05  DB-HRINISIT             PIC X(08).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-IDREGSEL = SPACES
               MOVE CREB000-PARMSINP TO WS-PARMSINP
           ELSE
               MOVE CREB000-IDREGSEL TO WS-PARMSINP
           END-IF

           IF (CREB000-IDREGSEL = SPACES AND CREB000-MSGDLG-P > ZERO)
           OR  CREB000-MSGDLG-P > 1
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB958-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB958-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG OR CREB000-NRLINSMC > ZERO
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2600-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2700-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2900-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2A00-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB958-MSGDIALG, WS-PARMSINP
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB958-MSGDIALG
           MOVE WS-IDPRCAPL OF WS-PARMSINP TO CREB958-IDPRCAPL-V
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDPRCAPL"
               MOVE CREB958-IDPRCAPL-V  TO WS-IDPRCAPL OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-IDAPLICA OF WS-LVPRCAPL-I
               MOVE CREB950-MCDLGPRN    TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPRCAPL-I
               SET  CREB958-IDPRCAPL-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDPRCAPL"
           AND  WS-IDPRCAPL OF WS-LVPRCAPL-O NOT = SPACES
               MOVE WS-IDPRCAPL OF WS-LVPRCAPL-O TO CREB958-IDPRCAPL-V
               SET  CREB958-IDPRCAPL-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2440-FRMT-MSGDIALG
           WHEN CREB000-FUNCCRIA-ON
               PERFORM P2450-INIT-NOVOREG
           END-EVALUATE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           WHEN CREB000-FUNCCRIA-ON
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           IF  CREB958-IDPRCAPL-V = SPACES
               SET  CREB958-IDPRCAPL-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           MOVE CREB958-IDPRCAPL-V TO IDPRCAPL OF CRET903
           PERFORM P9000-READ-CRET903
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB958-IDPRCAPL-V TO WS-IDPRCAPL OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           MOVE IDPRCSGA OF CRET903   TO CREB958-IDPRCSGA-V
           MOVE DSPRCAPL OF CRET903   TO CREB958-DSPRCAPL-V
           MOVE IDAPLICA OF CRET903   TO CREB958-IDAPLICA-V
           MOVE IDPRGGST OF CRET903   TO CREB958-IDGSTPRC-V
           MOVE IDDIALOG OF CRET903   TO CREB958-IDDIALOG-V
           MOVE IDTIPDLG OF CRET903   TO CREB958-IDTIPDLG-V
           MOVE SNPGNHZ1 OF CRET903   TO CREB958-SNPGNHZ1-V
           MOVE SNPGNHZ2 OF CRET903   TO CREB958-SNPGNHZ2-V
           MOVE IDPRCDEP OF CRET903   TO CREB958-IDPRCDEP-V
           MOVE NVAUTLER OF CRET903   TO CREB958-NVAUTLER-V
           MOVE NVAUTIMP OF CRET903   TO CREB958-NVAUTIMP-V
           MOVE NVAUTCRI OF CRET903   TO CREB958-NVAUTCRI-V
           MOVE NVAUTMDF OF CRET903   TO CREB958-NVAUTMDF-V
           MOVE NVAUTANL OF CRET903   TO CREB958-NVAUTANL-V
           MOVE NVAUTMIN OF CRET903   TO CREB958-NVAUTMIN-V
           MOVE MCSITEXE OF CRET903   TO CREB958-MCSITEXE-V
           MOVE DHINISIT OF CRET903   TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14      TO CREB958-DHINISIT-V
           MOVE DB-TIMESTMP           TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
           MOVE CREB000-DTPRCAPL      TO CREB950-DATA-N08
           MOVE CORR CREB950-DATA-X08 TO CREB950-DTHR-X14
           MOVE DHMDFREG OF CRET903   TO CREB000-DHMDFREG
           EVALUATE TRUE
           WHEN CREB958-DHINISIT-V  > CREB950-DTHR-N14
               CONTINUE
           WHEN MCSITEXE OF CRET903 = KV-MCSITEXE-INIB
               MOVE KV-MCSITEXE-ACTV  TO CREB958-MCSITEXE-V
               MOVE CREB950-DTHR-N14  TO CREB958-DHINISIT-V
           WHEN OTHER
               MOVE KV-MCSITEXE-INIB  TO CREB958-MCSITEXE-V
               MOVE CREB950-DTHR-N14  TO CREB958-DHINISIT-V
           END-EVALUATE
           PERFORM P9000-READ-IDTIPDLG
           PERFORM P9000-READ-SITEXECU
           PERFORM P9000-READ-CRET901

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
           OR   CREB958-IDPRCDEP-V = SPACES
               CONTINUE
           WHEN CREB958-IDTIPDLG-V = KV-IDTIPDLG-MNTI
               PERFORM P9000-READ-CRET904
           WHEN OTHER
               PERFORM P9000-READ-CRET903D
           END-EVALUATE
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2450-INIT-NOVOREG          SECTION.

           CONTINUE
           .
       P2450-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB958-IDPRCAPL-V   TO CREB999-IDPRCAPL
                                        CREB999-IDPRCORI
           MOVE CREB958-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB958-IDDIALOG-V   TO CREB999-IDDIALOG
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB958-VARSOUT
           PERFORM P2610-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2620-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2630-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           CONTINUE
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           CONTINUE
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2630-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               PERFORM P2710-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2720-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2730-FRMT-MSGDIALG
               END-IF
           ELSE
               PERFORM P2740-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           MOVE CREB958-DHINISIT-V         TO CREB950-DTHR-N14
           MOVE CORR CREB950-DTHR-X14      TO CREB950-DATA-X08
           MOVE CORR CREB950-DTHR-X14      TO CREB950-HORA-X06
           MOVE CREB950-DATA-N08           TO WS-DTINISIT
           MOVE CREB950-HORA-N06           TO WS-HRINISIT
           PERFORM P9000-READ-DTHRCORR

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE DB-TIMESTMP            TO CREB950-DTHR-ISO
               MOVE CREB000-DTPRCAPL       TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08  TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO  TO CREB950-DTHR-X14
               MOVE CORR CREB950-DTHR-ISO  TO CREB950-HORA-X06
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB958-DHINISIT-V = ZERO
               MOVE CREB950-DTHR-N14       TO CREB958-DHINISIT-V
               MOVE CREB950-DATA-N08       TO WS-DTINISIT
               MOVE CREB950-HORA-N06       TO WS-HRINISIT
           WHEN WS-DTINISIT        = ZERO
               MOVE CREB958-DHINISIT-V     TO CREB950-DTHR-N14
               MOVE CORR CREB950-DATA-X08  TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14       TO CREB958-DHINISIT-V
               MOVE CREB950-DATA-N08       TO WS-DTINISIT
           WHEN WS-HRINISIT        = ZERO
               MOVE CREB958-DHINISIT-V     TO CREB950-DTHR-N14
               MOVE CORR CREB950-HORA-X06  TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14       TO CREB958-DHINISIT-V
               MOVE CREB950-HORA-N06       TO WS-HRINISIT
           END-EVALUATE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN WS-DTINISIT        < CREB950-DATA-N08
               SET  CREB958-DTINISIT-PC    TO TRUE
               SET  CREB950-VCAMPO-INFMINI TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN WS-DTINISIT        = CREB950-DATA-N08
           AND  WS-HRINISIT        < CREB950-HORA-N06
               SET  CREB958-HRINISIT-PC    TO TRUE
               SET  CREB950-VCAMPO-INFMINI TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           MOVE CREB950-TMSTPMIN        TO CREB950-DTHR-ISO
           MOVE CREB958-DHINISIT-V      TO CREB950-DTHR-N14
           MOVE CORR CREB950-DTHR-X14   TO CREB950-DTHR-ISO
           MOVE CREB950-DTHR-ISO(01:10) TO DB-DTINISIT
           MOVE CREB950-DTHR-ISO(12:08) TO DB-HRINISIT
           PERFORM P9000-VLID-DTINISIT

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VLID-HRINISIT
           END-IF
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2730-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2740-VRIF-RELDPEND         SECTION.

           CONTINUE
           .
       P2740-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2900-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2910-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2920-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2930-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2900-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2910-EXEC-TRNCRIAR         SECTION.

           CONTINUE
           .
       P2910-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2920-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-CRET903-CHAV
           PERFORM P9000-FRMT-CRET903-ATRB
           PERFORM P9000-UPDT-CRET903
           .
       P2920-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2930-EXEC-TRNANULA         SECTION.

           CONTINUE
           .
       P2930-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2A00-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCCRT OR CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-IF
           .
       P2A00-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN CREB000-IDREGSEL = SPACES
           OR   CREB000-IDREG-NV
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB958-MSGDIALG TO CREB000-MSGDLG-L
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB958-MSGDIALG
           END-EVALUATE
           MOVE CREB958-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB958-IDPRCAPL-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB958-IDPRCAPL-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  CREB958-DHINISIT-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  CREB958-DHINISIT-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  CREB958-DSSITEXE-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           IF (CREB000-PROCIDT AND CREB000-IDREG-NF
           AND CREB000-FUNCCRIA-ON)
           OR  CREB000-PROCCRT
           OR (CREB000-PROCVRF AND CREB000-IDREG-NF)
               SET  CREB958-DSSITEXE-PR TO TRUE
           ELSE
               SET  CREB958-DSSITEXE-ND TO TRUE
           END-IF
           SET  CREB958-DHINISIT-ND     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903          SECTION.

           EXEC SQL SELECT IDPRCSGA
                         , DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDTIPDLG
                         , IDDIALOG
                         , SNPGNHZ1
                         , SNPGNHZ2
                         , IDPRCDEP
                         , NVAUTMIN
                         , NVAUTCRI
                         , NVAUTLER
                         , NVAUTIMP
                         , NVAUTMDF
                         , NVAUTANL
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , CURRENT TIMESTAMP
                      INTO :CRET903.IDPRCSGA
                         , :CRET903.DSPRCAPL
                         , :CRET903.IDAPLICA
                         , :CRET903.IDPRGGST
                         , :CRET903.IDTIPDLG
                         , :CRET903.IDDIALOG
                         , :CRET903.SNPGNHZ1
                         , :CRET903.SNPGNHZ2
                         , :CRET903.IDPRCDEP
                         , :CRET903.NVAUTMIN
                         , :CRET903.NVAUTCRI
                         , :CRET903.NVAUTLER
                         , :CRET903.NVAUTIMP
                         , :CRET903.NVAUTMDF
                         , :CRET903.NVAUTANL
                         , :CRET903.MCSITEXE
                         , :CRET903.DHINISIT
                         , :CRET903.DHMDFREG
                         , :DB-TIMESTMP
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK  TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF  TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP909.CRET903" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-IDTIPDLG         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-IDTIPDLG-OCC
             OR    KV-IDTIPDLG-ID(WS-ARRYNDX1) = CREB958-IDTIPDLG-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-IDTIPDLG-OCC
               MOVE KV-IDTIPDLG-DS(WS-ARRYNDX1) TO CREB958-DSTIPDLG-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB958-DSTIPDLG-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB958-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB958-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB958-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           MOVE CREB958-IDAPLICA-V      TO IDAPLICA OF CRET901
           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE NMAPLICA OF CRET901 TO CREB958-NMAPLICA-V
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF    TO CREB958-NMAPLICA-V
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "CREP909.CRET901"   TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET904          SECTION.

           EXEC SQL SELECT DSPRCAPL
                      INTO :CRET904.DSPRCAPL
                      FROM CRET904
                     WHERE IDPRCAPL = :CRET903.IDPRCDEP        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSPRCAPL OF CRET904 TO CREB958-NMPRCDEP-V
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF    TO CREB958-NMPRCDEP-V
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "CREP909.CRET904"   TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET903D         SECTION.

           EXEC SQL SELECT DSPRCAPL
                      INTO :CRET903.DSPRCAPL
                      FROM CRET903
                     WHERE IDPRCAPL = :CRET903.IDPRCDEP        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSPRCAPL OF CRET903 TO CREB958-NMPRCDEP-V
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF    TO CREB958-NMPRCDEP-V
           WHEN OTHER
               MOVE CREB950-SQLCODE     TO CREB000-CDRETORN
               MOVE "CREP909.CRET903"   TO CREB000-NMOBJECT
               SET  CREB000-SLCT        TO TRUE
               SET  CREB000-MSGSGBD     TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-DTHRCORR         SECTION.

           MOVE CREB958-IDAPLICA-V    TO IDAPLICA OF CRET901
           EXEC SQL SELECT CURRENT TIMESTAMP
                      INTO :DB-TIMESTMP
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP909.QRYDTHR" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-DTINISIT         SECTION.

           MOVE CREB000-IDAPLICA           TO IDAPLICA OF CRET901
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA     = :CRET901.IDAPLICA
                       AND CURRENT DATE = :DB-DTINISIT         END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT (SQLCODE-OK OR SQLCODE-NOTFOUND)
               SET  CREB958-DTINISIT-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-HRINISIT         SECTION.

           MOVE CREB000-IDAPLICA           TO IDAPLICA OF CRET901
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA     = :CRET901.IDAPLICA
                       AND CURRENT TIME = :DB-HRINISIT         END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT (SQLCODE-OK OR SQLCODE-NOTFOUND)
               SET  CREB958-HRINISIT-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET903-CHAV     SECTION.

           MOVE CREB958-IDPRCAPL-V    TO IDPRCAPL OF CRET903
           MOVE CREB000-DHMDFREG      TO DHMDFREG OF CRET903
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET903-ATRB     SECTION.

           MOVE CREB958-MCSITEXE-V    TO MCSITEXE OF CRET903
           MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
           MOVE CREB958-DHINISIT-V    TO CREB950-DTHR-N14
           MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
           MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET903
           MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET903
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET903          SECTION.

           EXEC SQL UPDATE CRET903
                       SET MCSITEXE = :CRET903.MCSITEXE
                         , DHINISIT = :CRET903.DHINISIT
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET903.IDUTLMDF
                     WHERE IDPRCAPL = :CRET903.IDPRCAPL
                       AND DHMDFREG = :CRET903.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP909.CRET903"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP909 ------------------------------------*
