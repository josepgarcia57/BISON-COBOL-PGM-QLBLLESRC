      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP927.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP927
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP927"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDUTILIZ         PIC X(08).
               10  FILLER              PIC X(72).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVUTILIZ-I   REDEFINES WS-PARMSLDV.
               10  WS-IDUTILIZ         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVUTILIZ-O   REDEFINES WS-PARMSLDV.
               10  WS-IDUTILIZ         PIC X(08).
           05  WS-LVGRFUNC-I   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVGRFUNC-O   REDEFINES WS-PARMSLDV.
               10  WS-IDGRFUNC         PIC X(08).

           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB977.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET908 OF CRET908.
           COPY DDS-CRET910 OF CRET910.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-IDREGSEL = SPACES
               MOVE CREB000-PARMSINP TO WS-PARMSINP
           ELSE
               MOVE CREB000-IDREGSEL TO WS-PARMSINP
           END-IF

           IF (CREB000-IDREGSEL = SPACES AND CREB000-MSGDLG-P > ZERO)
           OR  CREB000-MSGDLG-P > 1
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB977-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB977-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG OR CREB000-NRLINSMC > ZERO
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2600-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2700-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2900-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2A00-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB977-MSGDIALG, WS-PARMSINP
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB977-MSGDIALG
           MOVE WS-IDUTILIZ OF WS-PARMSINP TO CREB977-IDUTILIZ-V
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDUTILIZ"
               MOVE CREB977-IDUTILIZ-V  TO WS-IDUTILIZ OF WS-LVUTILIZ-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVUTILIZ-I
               SET  CREB977-IDUTILIZ-PC TO TRUE
           WHEN "IDGRFUNC"
               MOVE CREB977-IDGRFUNC-V  TO WS-IDGRFUNC OF WS-LVGRFUNC-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVGRFUNC-I
               SET  CREB977-IDGRFUNC-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDUTILIZ"
           AND  WS-IDUTILIZ OF WS-LVUTILIZ-O NOT = SPACES
               MOVE WS-IDUTILIZ OF WS-LVUTILIZ-O TO CREB977-IDUTILIZ-V
               SET  CREB977-IDUTILIZ-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDGRFUNC"
           AND  WS-IDGRFUNC OF WS-LVGRFUNC-O NOT = SPACES
               MOVE WS-IDGRFUNC OF WS-LVGRFUNC-O TO CREB977-IDGRFUNC-V
               SET  CREB977-IDGRFUNC-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2440-FRMT-MSGDIALG
           WHEN CREB000-FUNCCRIA-ON
               PERFORM P2450-INIT-NOVOREG
           END-EVALUATE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               IF  CREB977-IDUTILIZ-V = CREB000-IDUTILIZ
                   SET  CREB000-FUNCANUL-OFF TO TRUE
               END-IF
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           WHEN CREB000-FUNCCRIA-ON
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           IF  CREB977-IDUTILIZ-V = SPACES
               SET  CREB977-IDUTILIZ-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           MOVE CREB977-IDUTILIZ-V     TO IDUTILIZ OF CRET910
           PERFORM P9000-READ-CRET910
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB977-IDUTILIZ-V TO WS-IDUTILIZ OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           MOVE NMUTILIZ OF CRET910       TO CREB977-NMUTILIZ-V
           MOVE IDEMPRSA OF CRET910       TO CREB977-IDEMPRSA-V
           MOVE IDBALCAO OF CRET910       TO CREB977-IDBALCAO-V
           MOVE IDGRFUNC OF CRET910       TO CREB977-IDGRFUNC-V
           MOVE DHINISIT OF CRET910       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB977-DHINISIT-V
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DATA-X08
           EVALUATE TRUE
           WHEN CREB950-DATA-N08    <= CREB000-DTPRCAPL
               MOVE MCSITEXE OF CRET910   TO CREB977-MCSITEXE-V
           WHEN MCSITEXE OF CRET910  = KV-MCSITEXE-ACTV
               MOVE KV-MCSITEXE-INIB      TO CREB977-MCSITEXE-V
           WHEN OTHER
               MOVE KV-MCSITEXE-ACTV      TO CREB977-MCSITEXE-V
           END-EVALUATE
           PERFORM P9000-READ-SITEXECU
           MOVE DHCRIREG OF CRET910       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB977-DHCRIREG-V
           IF  DHMDFREG OF CRET910 NOT = CREB950-TMSTPMIN
               MOVE DHMDFREG OF CRET910   TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14      TO CREB977-DHMDFREG-V
           END-IF
           MOVE DHMDFREG OF CRET910       TO CREB000-DHMDFREG
           PERFORM P9000-READ-CRET908

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE DSGRFUNC OF CRET908   TO CREB977-DSGRFUNC-V
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2450-INIT-NOVOREG          SECTION.

           MOVE KV-MCSITEXE-ACTV        TO CREB977-MCSITEXE-V
           PERFORM P9000-READ-SITEXECU
           .
       P2440-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB977-IDUTILIZ-V   TO CREB999-IDUTILIZ
           MOVE CREB977-IDEMPRSA-V   TO CREB999-IDEMPRSA
           MOVE CREB977-IDBALCAO-V   TO CREB999-IDBALCAO
           MOVE CREB977-IDGRFUNC-V   TO CREB999-IDGRFUNC
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB977-VARSOUT
           PERFORM P2610-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2620-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2630-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           PERFORM P2710-VRIF-NIVEL01
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM P2720-VRIF-NIVEL02
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2630-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE CREB977-VARSOUT
               PERFORM P2710-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2720-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2730-FRMT-MSGDIALG
               END-IF
           ELSE
               PERFORM P2740-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB977-NMUTILIZ-V = SPACES
               SET  CREB977-NMUTILIZ-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB977-IDEMPRSA-V = SPACES
               SET  CREB977-IDEMPRSA-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB977-IDBALCAO-V = SPACES
               SET  CREB977-IDBALCAO-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           WHEN CREB977-IDGRFUNC-V = SPACES
               SET  CREB977-IDGRFUNC-PC        TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-EVALUATE
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           PERFORM P9000-VLID-IDEMPRSA

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VLID-IDBALCAO
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VLID-IDGRFUNC
           END-IF
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2730-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2740-VRIF-RELDPEND         SECTION.

           CONTINUE
           .
       P2740-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2900-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2910-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2920-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2930-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2900-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2910-EXEC-TRNCRIAR         SECTION.

           PERFORM P9000-FRMT-CRET910-CHAV
           PERFORM P9000-FRMT-CRET910-ATRB
           PERFORM P9000-INSR-CRET910
           .
       P2910-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2920-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-CRET910-CHAV
           PERFORM P9000-FRMT-CRET910-ATRB
           PERFORM P9000-UPDT-CRET910
           .
       P2920-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2930-EXEC-TRNANULA         SECTION.

           PERFORM P9000-FRMT-CRET910-CHAV
           PERFORM P9000-DELT-CRET910
           .
       P2930-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2A00-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCCRT OR CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-IF
           .
       P2A00-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN CREB000-IDREGSEL = SPACES
           OR   CREB000-IDREG-NV
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB977-MSGDIALG TO CREB000-MSGDLG-L
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB977-MSGDIALG
           END-EVALUATE
           MOVE CREB977-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB977-IDUTILIZ-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB977-IDUTILIZ-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  CREB977-NMUTILIZ-UP TO TRUE
           SET  CREB977-IDEMPRSA-UP TO TRUE
           SET  CREB977-IDBALCAO-UP TO TRUE
           SET  CREB977-IDGRFUNC-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  CREB977-NMUTILIZ-PR TO TRUE
           SET  CREB977-IDEMPRSA-PR TO TRUE
           SET  CREB977-IDBALCAO-PR TO TRUE
           SET  CREB977-IDGRFUNC-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  CREB977-DSSITEXE-PR TO TRUE
           SET  CREB977-DHINISIT-PR TO TRUE
           SET  CREB977-DHCRIREG-PR TO TRUE
           SET  CREB977-DHMDFREG-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           IF (CREB000-PROCIDT AND CREB000-IDREG-NF
           AND CREB000-FUNCCRIA-ON)
           OR  CREB000-PROCCRT
           OR (CREB000-PROCVRF AND CREB000-IDREG-NF)
               SET  CREB977-DSSITEXE-PR TO TRUE
           ELSE
               SET  CREB977-DSSITEXE-ND TO TRUE
           END-IF
           SET  CREB977-DHINISIT-ND     TO TRUE
           SET  CREB977-DHCRIREG-ND     TO TRUE
           SET  CREB977-DHMDFREG-ND     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET910          SECTION.

           EXEC SQL SELECT NMUTILIZ
                         , IDGRFUNC
                         , IDEMPRSA
                         , IDBALCAO
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , DHCRIREG
                      INTO :CRET910.NMUTILIZ
                         , :CRET910.IDGRFUNC
                         , :CRET910.IDEMPRSA
                         , :CRET910.IDBALCAO
                         , :CRET910.MCSITEXE
                         , :CRET910.DHINISIT
                         , :CRET910.DHMDFREG
                         , :CRET910.DHCRIREG
                      FROM CRET910
                     WHERE IDUTILIZ = :CRET910.IDUTILIZ        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK       TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF       TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP927.CRET910"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB977-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB977-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB977-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDEMPRSA         SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDBALCAO         SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDGRFUNC         SECTION.

           PERFORM P9000-READ-CRET908

           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE DSGRFUNC OF CRET908    TO CREB977-DSGRFUNC-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB977-IDGRFUNC-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET908          SECTION.

           MOVE CREB977-IDGRFUNC-V    TO IDGRFUNC OF CRET908
           EXEC SQL SELECT DSGRFUNC
                      INTO :CRET908.DSGRFUNC
                      FROM CRET908
                     WHERE IDGRFUNC = :CRET908.IDGRFUNC        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               MOVE CREB950-DSABRVNF  TO DSGRFUNC OF CRET908
           WHEN OTHER
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP927.CRET908" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET910-CHAV     SECTION.

           MOVE CREB977-IDUTILIZ-V     TO IDUTILIZ OF CRET910
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET910
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET910-ATRB     SECTION.

           MOVE CREB977-NMUTILIZ-V        TO NMUTILIZ OF CRET910
           MOVE CREB977-IDGRFUNC-V        TO IDGRFUNC OF CRET910
           MOVE CREB977-IDEMPRSA-V        TO IDEMPRSA OF CRET910
           MOVE CREB977-IDBALCAO-V        TO IDBALCAO OF CRET910
           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB977-MCSITEXE-V    TO MCSITEXE OF CRET910
               MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB000-DTPRCAPL      TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08 TO CREB950-DTHR-X14
               MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
               MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET910
               MOVE CREB000-IDUTILIZ      TO IDUTLCRI OF CRET910
               MOVE CREB950-TMSTPMIN      TO DHMDFREG OF CRET910
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET910
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-CRET910          SECTION.

           EXEC SQL INSERT INTO CRET910
                         ( IDUTILIZ
                         , NMUTILIZ
                         , IDGRFUNC
                         , IDEMPRSA
                         , IDBALCAO
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , IDUTLMDF
                         , DHCRIREG
                         , IDUTLCRI
                         )
                    VALUES
                         ( :CRET910.IDUTILIZ
                         , :CRET910.NMUTILIZ
                         , :CRET910.IDGRFUNC
                         , :CRET910.IDEMPRSA
                         , :CRET910.IDBALCAO
                         , :CRET910.MCSITEXE
                         , :CRET910.DHINISIT
                         , :CRET910.DHMDFREG
                         , :CRET910.IDUTLMDF
                         , CURRENT TIMESTAMP
                         , :CRET910.IDUTLCRI
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP927.CRET910"      TO CREB000-NMOBJECT
               SET  CREB000-INSR           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET910          SECTION.

           EXEC SQL UPDATE CRET910
                       SET NMUTILIZ = :CRET910.NMUTILIZ
                         , IDGRFUNC = :CRET910.IDGRFUNC
                         , IDEMPRSA = :CRET910.IDEMPRSA
                         , IDBALCAO = :CRET910.IDBALCAO
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET910.IDUTLMDF
                     WHERE IDUTILIZ = :CRET910.IDUTILIZ
                       AND DHMDFREG = :CRET910.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP927.CRET910"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET910          SECTION.

           EXEC SQL DELETE FROM CRET910
                     WHERE IDUTILIZ = :CRET910.IDUTILIZ
                       AND DHMDFREG = :CRET910.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP927.CRET910"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP927 ------------------------------------*
