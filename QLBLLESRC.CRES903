      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CRES903.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CRES903
      * OBJECTIVO : IMPLEMENTAR UMA FUNCAO UNICA DE CARREGAMENTO DE UMA
      *             LISTA DE VALORES SOBRE A TABELA "CRET908"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT CREE000B
                  ASSIGN        TO WORKSTATION-CREE000B-SI
                  ORGANIZATION  IS TRANSACTION
                  ACCESS MODE   IS DYNAMIC
                  RELATIVE KEY  IS RK-CREE000B
                  FILE STATUS   IS FS-CREE000B
                  CONTROL-AREA  IS CA-CREE000B.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE000B
           EXTERNAL
           LABEL RECORD OMITTED.
       01  CREE000-REG                 PIC X(100).
       01  CREE000-SFL.
           COPY DDS-CREE000SFL OF CREE000B.
       01  CREE000-LOV.
           COPY DDS-CREE000LOV OF CREE000B.
       01  CREE000-WIN.
           COPY DDS-CREE000WIN OF CREE000B.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE000B                 PIC 99.
           88  CREE000B-OK                         VALUE ZERO.

       01  RK-CREE000B                 PIC 9(04)   EXTERNAL.

       01  CA-CREE000B.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDGRFUNC         PIC X(08).
               10  WS-MCSITEXE         PIC X.

           05  WS-SFLRECRD.
               10  WS-IDENTLIN.
                   15  WS-IDGRFUNC     PIC X(08).
               10  WS-ATRIBLIN.
                   15  WS-DSGRFUNC     PIC X(50).

           05  WS-STRNGVAR.
               10  WS-STRNGPOS         PIC X               OCCURS 10.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.

           05  CS-QPERFFN              PIC X.
               88  QPERFFN-CLSE                    VALUE "C".
               88  QPERFFN-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDFMTDLG-SFL         PIC X(10)   VALUE "CREE000SFL".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET908 OF CRET908.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDGRFUNC.
                   49  DB-IDGRFUNC-L   PIC S9(4)       BINARY.
                   49  DB-IDGRFUNC-V   PIC X(08).
               10  DB-MCSITEXE         PIC X.
           05  DB-SNPARAMS.
               10  DB-STEXEFLT         PIC X.
                   88  STEXEFLT-ON                 VALUE "S".
                   88  STEXEFLT-OFF                VALUE "N".
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QPERFFN CURSOR FOR
                    SELECT IDGRFUNC
                         , DSGRFUNC
                      FROM CRET908
                     WHERE IDGRFUNC LIKE :DB-PARMSINP.DB-IDGRFUNC
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                     ORDER BY 1
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB900.

      *================================================================*
       PROCEDURE DIVISION USING CREB900-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB900-PARMSINP     TO WS-PARMSINP
           INITIALIZE WS-DBGBVARS, WS-DBLCVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  WS-IDGRFUNC OF WS-PARMSINP = SPACES OR CREB950-MCPESQGN
               MOVE "%"                        TO DB-IDGRFUNC-V
               MOVE  1                         TO DB-IDGRFUNC-L
           ELSE
               MOVE WS-IDGRFUNC OF WS-PARMSINP TO DB-IDGRFUNC-V
                                                  WS-STRNGVAR
               INITIALIZE WS-ARRYNDX1
               INSPECT DB-IDGRFUNC-V
                       TALLYING WS-ARRYNDX1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
                                     "?" BY "_"
               IF  WS-ARRYNDX1 > ZERO
                   PERFORM
                   VARYING DB-IDGRFUNC-L
                      FROM LENGTH OF DB-IDGRFUNC-V BY -1
                     UNTIL DB-IDGRFUNC-L = ZERO
                     OR    WS-STRNGPOS(DB-IDGRFUNC-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE "%"                     TO DB-IDGRFUNC-V
                   MOVE  1                      TO DB-IDGRFUNC-L
               END-IF
           END-IF

           IF  WS-MCSITEXE OF WS-PARMSINP = SPACES OR CREB950-MCPESQGN
               SET  STEXEFLT-OFF               TO TRUE
           ELSE
               MOVE WS-MCSITEXE OF WS-PARMSINP TO DB-MCSITEXE
               SET  STEXEFLT-ON                TO TRUE
           END-IF
           SET  QPERFFN-CLSE  TO TRUE
           PERFORM P2100-OPEN-QPERFFN

           IF  QPERFFN-OPEN
               PERFORM P2200-FTCH-QPERFFN

               PERFORM
               VARYING RK-CREE000B FROM 1 BY 1
                 UNTIL SQLCODE-NOTFOUND
                 OR    NOT CREB900-PROC-OK
                   PERFORM P2300-LOAD-LSTVAL

                   IF  CREB900-PROC-OK
                       PERFORM P2200-FTCH-QPERFFN
                   END-IF
               END-PERFORM

               PERFORM P2400-CLSE-QPERFFN
           END-IF
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-QPERFFN          SECTION.

           EXEC SQL OPEN  QPERFFN                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QPERFFN-OPEN      TO TRUE
           ELSE
               SET  CREB900-OPEN-CS   TO TRUE
               MOVE "CRES903.QPERFFN" TO CREB900-NMOBJECT
           END-IF
           .
       P2100-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-FTCH-QPERFFN          SECTION.

           EXEC SQL FETCH QPERFFN
                     INTO :CRET908.IDGRFUNC
                        , :CRET908.DSGRFUNC                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-ERRORCOD
               SET  CREB900-FTCH-CS   TO TRUE
               MOVE "CRES903.QPERFFN" TO CREB900-NMOBJECT
           END-IF
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-LOAD-LSTVAL           SECTION.

           MOVE IDGRFUNC OF CRET908 TO WS-IDGRFUNC OF WS-SFLRECRD
           MOVE DSGRFUNC OF CRET908 TO WS-DSGRFUNC OF WS-SFLRECRD

           INITIALIZE CREE000SFL-O
           MOVE WS-IDENTLIN OF WS-SFLRECRD TO IDENTLIN OF CREE000SFL-O
           MOVE WS-ATRIBLIN OF WS-SFLRECRD TO ATRIBLIN OF CREE000SFL-O
           WRITE SUBFILE CREE000-SFL
                             FORMAT IS KV-IDFMTDLG-SFL
           INVALID KEY CONTINUE
           END-WRITE
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-CLSE-QPERFFN          SECTION.

           EXEC SQL CLOSE QPERFFN                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QPERFFN-CLSE      TO TRUE
           ELSE
               SET  CREB900-CLSE-CS   TO TRUE
               MOVE "CRES903.QPERFFN" TO CREB900-NMOBJECT
           END-IF
           .
       P2400-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           IF  CREB900-PROC-OK
               COMPUTE CREB900-QTLINHAS = RK-CREE000B - 1
           ELSE
               MOVE "CRES903" TO CREB900-NMOBJECT
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CRES903 -----------------------------------*
