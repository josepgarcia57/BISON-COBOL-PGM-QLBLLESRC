      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP969.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP969
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE969"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE969
                  ASSIGN        TO WORKSTATION-CREE969-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE969
                  CONTROL-AREA  IS CA-CREE969.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE969
           LABEL RECORD OMITTED.
       01  CREE969-HDR.
           COPY DDS-CREE969HDR OF CREE969.
       01  CREE969-D01.
           COPY DDS-CREE969D01 OF CREE969.
       01  CREE969-FTR.
           COPY DDS-CREE969FTR OF CREE969.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE969                  PIC 99.
           88  CREE969-OK                          VALUE ZERO.

       01  CA-CREE969.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE969".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE969HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE969D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE969FTR".
           05  KV-IDGRFUNC-HI          PIC 99      VALUE 25.
           05  KV-IDGRFUNC-PC          PIC 99      VALUE 26.
           05  KV-IDGRFUNC-PR          PIC 99      VALUE 27.
           05  KV-DSGRFUNC-HI          PIC 99      VALUE 28.
           05  KV-DSGRFUNC-PC          PIC 99      VALUE 29.
           05  KV-DSGRFUNC-PR          PIC 99      VALUE 30.
           05  KV-DSSITEXE-ND          PIC 99      VALUE 31.
           05  KV-DHINISIT-ND          PIC 99      VALUE 32.
           05  KV-DTINISIT-HI          PIC 99      VALUE 33.
           05  KV-DTINISIT-PC          PIC 99      VALUE 34.
           05  KV-DHINISIT-PR          PIC 99      VALUE 35.
           05  KV-HRINISIT-HI          PIC 99      VALUE 36.
           05  KV-HRINISIT-PC          PIC 99      VALUE 37.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 38.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 38.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB969.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE969, CA-CREE969
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE969-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE969

           IF  NOT CREE969-OK
               MOVE FS-CREE969      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB969-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE969-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE969-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE969-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB969-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB969-IDGRFUNC-PR
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-PR) TO TRUE
           END-IF
      *
           IF  CREB969-DSGRFUNC-PR
               SET  CREB000-INDICDLG-ON (KV-DSGRFUNC-PR) TO TRUE
           END-IF
      *
           IF  CREB969-DHINISIT-PR
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-PR) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN CREB969-IDGRFUNC-PC
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-PC) TO TRUE
           WHEN CREB969-DSGRFUNC-PC
               SET  CREB000-INDICDLG-ON (KV-DSGRFUNC-PC) TO TRUE
           WHEN CREB969-DTINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-PC) TO TRUE
           WHEN CREB969-HRINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-HRINISIT-PC) TO TRUE
           WHEN CREB969-IDGRFUNC-UP
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-PC) TO TRUE
           WHEN CREB969-DSGRFUNC-UP
               SET  CREB000-INDICDLG-ON (KV-DSGRFUNC-PC) TO TRUE
           WHEN CREB969-DHINISIT-UP
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-PC) TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB969-IDGRFUNC-PC
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-HI) TO TRUE
           WHEN CREB969-DSGRFUNC-PC
               SET  CREB000-INDICDLG-ON (KV-DSGRFUNC-HI) TO TRUE
           WHEN CREB969-DTINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-HI) TO TRUE
           WHEN CREB969-HRINISIT-PC
               SET  CREB000-INDICDLG-ON (KV-HRINISIT-HI) TO TRUE
           END-EVALUATE
      *
           IF  CREB969-DSSITEXE-ND
               SET  CREB000-INDICDLG-ON (KV-DSSITEXE-ND) TO TRUE
           END-IF
      *
           IF  CREB969-DHINISIT-ND
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-PR) TO TRUE
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-ND) TO TRUE
           END-IF
      *
           IF  CREB969-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  CREB969-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE969-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE969HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE969HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE969HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE969HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE969HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE969HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE969HDR-O
           WRITE CREE969-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE969-OK
               MOVE FS-CREE969      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE969-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE969FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE969FTR-O
           WRITE CREE969-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE969-OK
               MOVE FS-CREE969      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO CREE969-D01
           MOVE CREB969-IDGRFUNC-V  TO IDGRFUNC OF CREE969D01-O
           MOVE CREB969-DSGRFUNC-V  TO DSGRFUNC OF CREE969D01-O
           MOVE CREB969-DSSITEXE-V  TO DSSITEXE OF CREE969D01-O
           MOVE CREB969-DHINISIT-V  TO WS-DTHR-N14
           MOVE WS-DATA-N08         TO DTINISIT OF CREE969D01-O
           MOVE WS-HORA-N06         TO HRINISIT OF CREE969D01-O
           MOVE CREB969-DHCRIREG-V  TO DHCRIREG OF CREE969D01-O
           MOVE CREB969-DHMDFREG-V  TO DHMDFREG OF CREE969D01-O
           WRITE CREE969-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE969-OK
               MOVE FS-CREE969      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE969
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE969-OK
               MOVE IDGRFUNC OF CREE969D01-I TO CREB969-IDGRFUNC-V
               MOVE DSGRFUNC OF CREE969D01-I TO CREB969-DSGRFUNC-V
               MOVE DTINISIT OF CREE969D01-I TO WS-DATA-N08
               MOVE HRINISIT OF CREE969D01-I TO WS-HORA-N06
               MOVE WS-DTHR-N14              TO CREB969-DHINISIT-V
               MOVE IDVARCSR OF CREE969D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE969   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE969               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE969

           IF  NOT CREE969-OK
               MOVE FS-CREE969      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP969 ------------------------------------*
