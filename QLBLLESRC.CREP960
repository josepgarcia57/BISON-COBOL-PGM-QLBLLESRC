      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP960.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP960
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE960"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE960
                  ASSIGN        TO WORKSTATION-CREE960-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE960
                  CONTROL-AREA  IS CA-CREE960.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE960
           LABEL RECORD OMITTED.
       01  CREE960-HDR.
           COPY DDS-CREE960HDR OF CREE960.
       01  CREE960-D01.
           COPY DDS-CREE960D01 OF CREE960.
       01  CREE960-FTR.
           COPY DDS-CREE960FTR OF CREE960.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE960                  PIC 99.
           88  CREE960-OK                          VALUE ZERO.

       01  CA-CREE960.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.

       01  WS-EDITVARS.
           05  ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDPRCAPL            PIC X(08).
               10  DSPRCAPL            PIC X(32).
               10  NMVARDLG            PIC X(10).
               10  DSVALVAR            PIC X(30).
               10  REGLISTA                                OCCURS 11.
                   15  NROPCLIN        PIC 9.
                   15  IDPRCLIN        PIC X(08).
                   15  NMPRCLIN        PIC X(32).
                   15  TPDLGLIN        PIC XX.
                   15  IDDLGLIN        PIC X(08).
                   15  MCARALIN        PIC X.
                   15  NMVARLIN        PIC X(10).
           05  ED-CREEVARS-I   REDEFINES ED-CREEVARS-O.
               10  IDREGCSR            PIC X(10).
               10  IDVARCSR            PIC X(10).
               10  IDPRCAPL            PIC X(08).
               10  DSPRCAPL            PIC X(32).
               10  NMVARDLG            PIC X(10).
               10  DSVALVAR            PIC X(30).
               10  REGLISTA                                OCCURS 11.
                   15  NROPCLIN        PIC 9.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE960".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE960HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE960D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE960FTR".
           05  KV-IDPRCAPL-HI          PIC 99      VALUE 25.
           05  KV-IDPRCAPL-PC          PIC 99      VALUE 26.
           05  KV-IDPRCAPL-PR          PIC 99      VALUE 27.
           05  KV-NMVARDLG-HI          PIC 99      VALUE 28.
           05  KV-NMVARDLG-PC          PIC 99      VALUE 29.
           05  KV-NMVARDLG-PR          PIC 99      VALUE 30.
           05  KV-DSVALVAR-HI          PIC 99      VALUE 31.
           05  KV-DSVALVAR-PC          PIC 99      VALUE 32.
           05  KV-DSVALVAR-PR          PIC 99      VALUE 33.
           05  KV-SUBSET01.
               10  KV-NROPCL01-PC      PIC 99      VALUE 34.
               10  KV-NROPCL01-PR      PIC 99      VALUE 35.
               10  KV-NROPCL02-PC      PIC 99      VALUE 36.
               10  KV-NROPCL02-PR      PIC 99      VALUE 37.
               10  KV-NROPCL03-PC      PIC 99      VALUE 38.
               10  KV-NROPCL03-PR      PIC 99      VALUE 39.
               10  KV-NROPCL04-PC      PIC 99      VALUE 40.
               10  KV-NROPCL04-PR      PIC 99      VALUE 41.
               10  KV-NROPCL05-PC      PIC 99      VALUE 42.
               10  KV-NROPCL05-PR      PIC 99      VALUE 43.
               10  KV-NROPCL06-PC      PIC 99      VALUE 44.
               10  KV-NROPCL06-PR      PIC 99      VALUE 45.
               10  KV-NROPCL07-PC      PIC 99      VALUE 46.
               10  KV-NROPCL07-PR      PIC 99      VALUE 47.
               10  KV-NROPCL08-PC      PIC 99      VALUE 48.
               10  KV-NROPCL08-PR      PIC 99      VALUE 49.
               10  KV-NROPCL09-PC      PIC 99      VALUE 50.
               10  KV-NROPCL09-PR      PIC 99      VALUE 51.
               10  KV-NROPCL10-PC      PIC 99      VALUE 52.
               10  KV-NROPCL10-PR      PIC 99      VALUE 53.
               10  KV-NROPCL11-PC      PIC 99      VALUE 54.
               10  KV-NROPCL11-PR      PIC 99      VALUE 55.
           05  FILLER      REDEFINES KV-SUBSET01           OCCURS 11.
               10  KV-NROPCLIN-PC      PIC 99.
               10  KV-NROPCLIN-PR      PIC 99.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB960.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE960, CA-CREE960
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE960-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE960

           IF  NOT CREE960-OK
               MOVE FS-CREE960      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB960-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE960-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE960-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE960-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB960-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB960-IDPRCAPL-PR
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PR)     TO TRUE
           END-IF
      *
           IF  CREB960-NMVARDLG-PR
               SET  CREB000-INDICDLG-ON (KV-NMVARDLG-PR)     TO TRUE
           END-IF
      *
           IF  CREB960-DSVALVAR-PR
               SET  CREB000-INDICDLG-ON (KV-DSVALVAR-PR)     TO TRUE
           END-IF
      *
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
               IF  CREB960-NROPCLIN-PR(WS-ARRYNDX1)
                   MOVE KV-NROPCLIN-PR(WS-ARRYNDX1) TO WS-ARRYNDX2
                   SET  CREB000-INDICDLG-ON (WS-ARRYNDX2)    TO TRUE
               END-IF
           END-PERFORM
      *
           EVALUATE TRUE
           WHEN CREB960-IDPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PC)     TO TRUE
           WHEN CREB960-NMVARDLG-PC
               SET  CREB000-INDICDLG-ON (KV-NMVARDLG-PC)     TO TRUE
           WHEN CREB960-DSVALVAR-PC
               SET  CREB000-INDICDLG-ON (KV-DSVALVAR-PC)     TO TRUE
           WHEN CREB960-PCURSOR > CREB960-NRPARAMS-INP
               COMPUTE WS-ARRYNDX1 = 24 + (CREB960-NRPARAMS-INP * 3)
                                   +(CREB960-PCURSOR
                                   - CREB960-NRPARAMS-INP) * 2 - 1
               SET  CREB000-INDICDLG-ON (WS-ARRYNDX1)        TO TRUE
           WHEN CREB960-IDPRCAPL-UP
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-PC)     TO TRUE
           WHEN CREB960-NROPCLIN-UP(1)
               SET  CREB000-INDICDLG-ON (KV-NROPCL01-PC)     TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB960-IDPRCAPL-PC
               SET  CREB000-INDICDLG-ON (KV-IDPRCAPL-HI)     TO TRUE
           WHEN CREB960-NMVARDLG-PC
               SET  CREB000-INDICDLG-ON (KV-NMVARDLG-HI)     TO TRUE
           WHEN CREB960-DSVALVAR-PC
               SET  CREB000-INDICDLG-ON (KV-DSVALVAR-HI)     TO TRUE
           END-EVALUATE
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE960-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE960HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE960HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE960HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE960HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE960HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE960HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE960HDR-O
           MOVE CREB000-NRENTLST    TO NRENTLST OF CREE960HDR-O
           MOVE CREB000-DSOPCSEL    TO DSOPCSEL OF CREE960HDR-O
           WRITE CREE960-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE960-OK
               MOVE FS-CREE960      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE960-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE960FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE960FTR-O
           WRITE CREE960-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE960-OK
               MOVE FS-CREE960      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO ED-CREEVARS-O
           MOVE CREB960-IDPRCAPL-V  TO IDPRCAPL OF ED-CREEVARS-O
           MOVE CREB960-DSPRCAPL-V  TO DSPRCAPL OF ED-CREEVARS-O
           MOVE CREB960-NMVARDLG-V  TO NMVARDLG OF ED-CREEVARS-O
           MOVE CREB960-DSVALVAR-V  TO DSVALVAR OF ED-CREEVARS-O
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
               MOVE CREB960-NROPCLIN-V       (WS-ARRYNDX1)
                 TO NROPCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB960-IDPRCLIN-V       (WS-ARRYNDX1)
                 TO IDPRCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB960-NMPRCLIN-V       (WS-ARRYNDX1)
                 TO NMPRCLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB960-TPDLGLIN-V       (WS-ARRYNDX1)
                 TO TPDLGLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB960-IDDLGLIN-V       (WS-ARRYNDX1)
                 TO IDDLGLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB960-MCARALIN-V       (WS-ARRYNDX1)
                 TO MCARALIN OF ED-CREEVARS-O(WS-ARRYNDX1)
               MOVE CREB960-NMVARLIN-V       (WS-ARRYNDX1)
                 TO NMVARLIN OF ED-CREEVARS-O(WS-ARRYNDX1)
           END-PERFORM
           MOVE ED-CREEVARS-O       TO CREE960D01-O
           WRITE CREE960-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE960-OK
               MOVE FS-CREE960      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE960
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE960-OK
               MOVE CREE960D01-I              TO ED-CREEVARS-I
               MOVE IDPRCAPL OF ED-CREEVARS-I TO CREB960-IDPRCAPL-V
               MOVE NMVARDLG OF ED-CREEVARS-I TO CREB960-NMVARDLG-V
               MOVE DSVALVAR OF ED-CREEVARS-I TO CREB960-DSVALVAR-V
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB960-NRSELLIN-MAX
                   MOVE NROPCLIN OF ED-CREEVARS-I(WS-ARRYNDX1)
                     TO CREB960-NROPCLIN-V       (WS-ARRYNDX1)
               END-PERFORM
               MOVE IDVARCSR OF ED-CREEVARS-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE960    TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE960                TO CREB000-CDRETORN
               MOVE KV-IDDIALOG               TO CREB000-NMOBJECT
               SET  CREB000-READ              TO TRUE
               SET  CREB000-MSGDLOG           TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE960

           IF  NOT CREE960-OK
               MOVE FS-CREE960      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP960 ------------------------------------*
