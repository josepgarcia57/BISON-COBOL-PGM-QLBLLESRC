      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP907.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP907
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP907"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-DSPRCAPL         PIC X(32).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDDIALOG         PIC X(08).
               10  WS-IDTIPDLG         PIC XX.
               10  WS-MCSITEXE         PIC X.
               10  WS-IDPRCDEP         PIC X(08).
               10  FILLER              PIC X(18).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).
           05  WS-IDREGSEL.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDDIALOG         PIC X(08).
               10  FILLER              PIC X(61).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X.
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVDIALOG-I   REDEFINES WS-PARMSLDV.
               10  WS-IDDIALOG         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
           05  WS-LVDIALOG-O   REDEFINES WS-PARMSLDV.
               10  WS-IDDIALOG         PIC X(08).
           05  WS-LVPRCAPL-I   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).
               10  WS-IDAPLICA         PIC X(03).
               10  WS-IDTIPDLG         PIC XX.
               10  WS-MCSITEXE         PIC X.
           05  WS-LVPRCAPL-O   REDEFINES WS-PARMSLDV.
               10  WS-IDPRCAPL         PIC X(08).

           05  WS-STRING80.
               10  WS-CHRSTR80         PIC X               OCCURS 80.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-ARRYNDX2             PIC S9(4)       BINARY.
           05  WS-COUNTER1             PIC S9(4)       BINARY.

           05  CS-QPROCES              PIC X.
               88  QPROCES-CLSE                    VALUE "C".
               88  QPROCES-OPEN                    VALUE "O".
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDTIPDLG-LS.
               10  KV-IDTIPDLG-MNTD    PIC XX      VALUE "MD".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção Directa".
               10  KV-IDTIPDLG-MNTI    PIC XX      VALUE "MI".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção c/ Apoio".
               10  KV-IDTIPDLG-MNTA    PIC XX      VALUE "MA".
               10  FILLER              PIC X(19)   VALUE
                                               "Manutenção de Apoio".
               10  KV-IDTIPDLG-CNSD    PIC XX      VALUE "CD".
               10  FILLER              PIC X(19)   VALUE
                                               "Consulta Directa".
               10  KV-IDTIPDLG-CNSL    PIC XX      VALUE "CL".
               10  FILLER              PIC X(19)   VALUE
                                               "Consulta em Lista".
               10  KV-IDTIPDLG-PEDX    PIC XX      VALUE "PD".
               10  FILLER              PIC X(19)   VALUE
                                               "Pedido de Execução".
           05  KV-IDTIPDLG-LV  REDEFINES KV-IDTIPDLG-LS    OCCURS 6.
               10  KV-IDTIPDLG-ID      PIC XX.
               10  KV-IDTIPDLG-DS      PIC X(19).
           05  KV-IDTIPDLG-OCC         PIC 9       VALUE 6.

           05  KV-SITEXECU-LS.
               10  FILLER              PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activo ".
               10  FILLER              PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibido".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB957.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET902 OF CRET902.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET904 OF CRET904.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-PARMSINP.
               10  DB-IDDIALOG         PIC X(08).
               10  DB-IDAPLICA         PIC X(03).
               10  DB-IDPRCAPL.
                   49  DB-IDPRCAPL-L   PIC S9(4)       BINARY.
                   49  DB-IDPRCAPL-V   PIC X(08).
               10  DB-DSPRCAPL.
                   49  DB-DSPRCAPL-L   PIC S9(4)       BINARY.
                   49  DB-DSPRCAPL-V   PIC X(32).
               10  DB-IDTIPDLG         PIC XX.
               10  DB-MCSITEXE         PIC X.
               10  DB-IDPRCDEP         PIC X(08).
           05  DB-SNPARAMS.
               10  DB-IDAPLFLT         PIC X.
                   88  IDAPLFLT-ON                 VALUE "S".
                   88  IDAPLFLT-OFF                VALUE "N".
               10  DB-IDDLGFLT         PIC X.
                   88  IDDLGFLT-ON                 VALUE "S".
                   88  IDDLGFLT-OFF                VALUE "N".
               10  DB-TPDLGFLT         PIC X.
                   88  TPDLGFLT-ON                 VALUE "S".
                   88  TPDLGFLT-OFF                VALUE "N".
               10  DB-STEXEFLT         PIC X.
                   88  STEXEFLT-ON                 VALUE "S".
                   88  STEXEFLT-OFF                VALUE "N".
               10  DB-PRCDPFLT         PIC X.
                   88  PRCDPFLT-ON                 VALUE "S".
                   88  PRCDPFLT-OFF                VALUE "N".
           05  DB-IDREGLST.
               10  DB-IDPRCAPL         PIC X(08).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL DECLARE QPROCES CURSOR FOR
                    SELECT IDPRCAPL
                         , DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDDIALOG
                         , IDTIPDLG
                         , MCSITEXE
                      FROM CRET903
                     WHERE IDPRCAPL LIKE :DB-PARMSINP.DB-IDPRCAPL
                       AND DSPRCAPL LIKE :DB-PARMSINP.DB-DSPRCAPL
                       AND(IDAPLICA    = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"         = :DB-IDAPLFLT)
                       AND(IDDIALOG    = :DB-PARMSINP.DB-IDDIALOG
                        OR "N"         = :DB-IDDLGFLT)
                       AND(IDTIPDLG    = :DB-PARMSINP.DB-IDTIPDLG
                        OR "N"         = :DB-TPDLGFLT)
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                       AND(IDPRCDEP    = :DB-PARMSINP.DB-IDPRCDEP
                        OR "N"         = :DB-PRCDPFLT)
                       AND IDPRCAPL    > :DB-IDREGLST.DB-IDPRCAPL
                     UNION ALL
                    SELECT IDPRCAPL
                         , DSPRCAPL
                         , IDAPLICA
                         , IDPRGGST
                         , IDDIALOG
                         , "MA"
                         , " "
                      FROM CRET904
                     WHERE IDPRCAPL LIKE :DB-PARMSINP.DB-IDPRCAPL
                       AND DSPRCAPL LIKE :DB-PARMSINP.DB-DSPRCAPL
                       AND(IDAPLICA    = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"         = :DB-IDAPLFLT)
                       AND(IDDIALOG    = :DB-PARMSINP.DB-IDDIALOG
                        OR "N"         = :DB-IDDLGFLT)
                       AND("MA"        = :DB-PARMSINP.DB-IDTIPDLG
                        OR "N"         = :DB-TPDLGFLT)
                       AND(" "         = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                       AND(" "         = :DB-PARMSINP.DB-IDPRCDEP
                        OR "N"         = :DB-PRCDPFLT)
                       AND IDPRCAPL    > :DB-IDREGLST.DB-IDPRCAPL
                     ORDER BY 1
                    FOR READ ONLY                              END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           MOVE CREB000-PARMSINP     TO WS-PARMSINP
           IF  CREB000-MSGDLG-P > ZERO
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB957-MSGDIALG
           END-IF
           IF  CREB000-IDREGLST-MIN
               INITIALIZE DB-IDREGLST
           ELSE
               MOVE CREB000-IDREGLST TO DB-IDREGLST
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB957-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCSEL
               PERFORM P2600-PREP-REGEXIST
           WHEN CREB000-PROCCRT
               PERFORM P2700-PREP-NOVOREG
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB957-MSGDIALG, WS-PARMSINP
           SET  CREB000-IDREG-NF TO TRUE
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB957-MSGDIALG
           MOVE WS-IDAPLICA OF WS-PARMSINP TO CREB957-IDAPLICA-V
           MOVE WS-IDPRCAPL OF WS-PARMSINP TO CREB957-IDPRCAPL-V
           MOVE WS-DSPRCAPL OF WS-PARMSINP TO CREB957-DSPRCAPL-V
           MOVE WS-IDDIALOG OF WS-PARMSINP TO CREB957-IDDIALOG-V
           MOVE WS-IDTIPDLG OF WS-PARMSINP TO CREB957-IDTIPDLG-V
           MOVE WS-MCSITEXE OF WS-PARMSINP TO CREB957-MCSITEXE-V
           MOVE WS-IDPRCDEP OF WS-PARMSINP TO CREB957-IDPRCDEP-V
           SET  CREB000-IDREG-NF           TO TRUE
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDAPLICA"
               MOVE CREB957-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB957-IDAPLICA-PC TO TRUE
           WHEN "IDDIALOG"
               MOVE CREB957-IDDIALOG-V  TO WS-IDDIALOG OF WS-LVDIALOG-I
               MOVE CREB957-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVDIALOG-I
               SET  CREB957-IDDIALOG-PC TO TRUE
           WHEN "IDPRCAPL"
               MOVE CREB957-IDPRCAPL-V  TO WS-IDPRCAPL OF WS-LVPRCAPL-I
               MOVE CREB957-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-IDTIPDLG OF WS-LVPRCAPL-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVPRCAPL-I
               SET  CREB957-IDPRCAPL-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB957-IDAPLICA-V
               SET  CREB957-IDAPLICA-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDDIALOG"
           AND  WS-IDDIALOG OF WS-LVDIALOG-O NOT = SPACES
               MOVE WS-IDDIALOG OF WS-LVDIALOG-O TO CREB957-IDDIALOG-V
               SET  CREB957-IDDIALOG-PC          TO TRUE
           WHEN CREB000-IDVARCSR = "IDPRCAPL"
           AND  WS-IDPRCAPL OF WS-LVPRCAPL-O NOT = SPACES
               MOVE WS-IDPRCAPL OF WS-LVPRCAPL-O TO CREB957-IDPRCAPL-V
               SET  CREB957-IDPRCAPL-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB957-VARSOUT
           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2440-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           IF  CREB957-IDPRCAPL-V NOT = SPACES AND CREB950-MCPESQGN
               MOVE CREB957-IDPRCAPL-V TO WS-STRING80
               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB957-IDPRCAPL-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) = SPACE
                   CONTINUE
               END-PERFORM

               PERFORM
               VARYING WS-ARRYNDX1 FROM WS-ARRYNDX1 BY 1
                 UNTIL WS-ARRYNDX1 > LENGTH OF CREB957-IDPRCAPL-V
                 OR    WS-CHRSTR80(WS-ARRYNDX1) > SPACE
                   CONTINUE
               END-PERFORM

               IF  WS-ARRYNDX1 <= LENGTH OF CREB957-IDPRCAPL-V
                   SET  CREB957-IDPRCAPL-PC    TO TRUE
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-IF
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           IF  CREB957-IDAPLICA-V NOT = SPACES AND CREB950-MCPESQGN
               PERFORM P9000-VLID-IDAPLICA
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB957-IDDIALOG-V NOT = SPACES AND CREB950-MCPESQGN)
               PERFORM P9000-VLID-IDDIALOG
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB957-IDTIPDLG-V NOT = SPACES AND CREB950-MCPESQGN)
               PERFORM P9000-VLID-IDTIPDLG
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB957-MCSITEXE-V NOT = SPACE AND CREB950-MCPESQGN)
               PERFORM P9000-VLID-MCSITEXE
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND(CREB957-IDPRCDEP-V NOT = SPACE AND CREB950-MCPESQGN)
               INITIALIZE WS-COUNTER1
               INSPECT CREB957-IDPRCDEP-V
                       TALLYING WS-COUNTER1 FOR ALL "*"
               IF  WS-COUNTER1 > ZERO
                   SET  CREB957-IDPRCDEP-PC     TO TRUE
                   SET  CREB950-VCAMPO-INVALID  TO TRUE
                   SET  CREB000-MSGFUNC         TO TRUE
               END-IF
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB957-IDTIPDLG-V = KV-IDTIPDLG-MNTA
           AND(CREB957-MCSITEXE-V NOT = SPACE AND CREB950-MCPESQGN)
               SET  CREB957-MCSITEXE-PC         TO TRUE
               SET  CREB950-PCAMPO-NAPLICA      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           WHEN CREB957-IDTIPDLG-V = KV-IDTIPDLG-MNTA
           AND(CREB957-IDPRCDEP-V NOT = SPACE AND CREB950-MCPESQGN)
               SET  CREB957-IDPRCDEP-PC         TO TRUE
               SET  CREB950-PCAMPO-NAPLICA      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           END-EVALUATE
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB957-IDAPLICA-V     TO WS-IDAPLICA OF WS-PARMSINP
           MOVE CREB957-IDPRCAPL-V     TO WS-IDPRCAPL OF WS-PARMSINP
           MOVE CREB957-DSPRCAPL-V     TO WS-DSPRCAPL OF WS-PARMSINP
           MOVE CREB957-IDDIALOG-V     TO WS-IDDIALOG OF WS-PARMSINP
           MOVE CREB957-IDTIPDLG-V     TO WS-IDTIPDLG OF WS-PARMSINP
           MOVE CREB957-MCSITEXE-V     TO WS-MCSITEXE OF WS-PARMSINP
           MOVE CREB957-IDPRCDEP-V     TO WS-IDPRCDEP OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           INITIALIZE DB-PARMSINP, CRET903, CREB957-VARSDET
           IF  CREB957-IDAPLICA-V = SPACES OR CREB950-MCPESQGN
               SET  IDAPLFLT-OFF       TO TRUE
           ELSE
               MOVE CREB957-IDAPLICA-V TO DB-IDAPLICA OF DB-PARMSINP
               SET  IDAPLFLT-ON        TO TRUE
           END-IF

           IF  CREB957-IDPRCAPL-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-IDPRCAPL-V
               MOVE  1                 TO DB-IDPRCAPL-L
           ELSE
               MOVE CREB957-IDPRCAPL-V TO DB-IDPRCAPL-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-IDPRCAPL-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-IDPRCAPL-L
                     FROM LENGTH OF DB-IDPRCAPL-V BY -1
                    UNTIL DB-IDPRCAPL-L = ZERO
                    OR    WS-CHRSTR80(DB-IDPRCAPL-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-IDPRCAPL-V TO DB-IDPRCAPL-L
               END-IF
           END-IF

           IF  CREB957-DSPRCAPL-V = SPACES OR CREB950-MCPESQGN
               MOVE "%"                TO DB-DSPRCAPL-V
               MOVE  1                 TO DB-DSPRCAPL-L
           ELSE
               MOVE CREB957-DSPRCAPL-V TO DB-DSPRCAPL-V
                                          WS-STRING80
               INITIALIZE WS-COUNTER1
               INSPECT DB-DSPRCAPL-V TALLYING WS-COUNTER1 FOR ALL "*"
                       REPLACING ALL "*" BY "%"
               IF  WS-COUNTER1 > ZERO
                   PERFORM
                   VARYING DB-DSPRCAPL-L
                      FROM LENGTH OF DB-DSPRCAPL-V BY -1
                     UNTIL DB-DSPRCAPL-L = ZERO
                     OR    WS-CHRSTR80(DB-DSPRCAPL-L) > SPACE
                       CONTINUE
                   END-PERFORM
               ELSE
                   MOVE LENGTH OF DB-DSPRCAPL-V TO DB-DSPRCAPL-L
               END-IF
           END-IF

           IF  CREB957-IDDIALOG-V = SPACES OR CREB950-MCPESQGN
               SET  IDDLGFLT-OFF       TO TRUE
           ELSE
               MOVE CREB957-IDDIALOG-V TO DB-IDDIALOG OF DB-PARMSINP
               SET  IDDLGFLT-ON        TO TRUE
           END-IF

           IF  CREB957-IDTIPDLG-V = SPACES OR CREB950-MCPESQGN
               SET  TPDLGFLT-OFF       TO TRUE
           ELSE
               MOVE CREB957-IDTIPDLG-V TO DB-IDTIPDLG OF DB-PARMSINP
               SET  TPDLGFLT-ON        TO TRUE
           END-IF

           IF  CREB957-MCSITEXE-V = SPACES OR CREB950-MCPESQGN
               SET  STEXEFLT-OFF       TO TRUE
           ELSE
               MOVE CREB957-MCSITEXE-V TO DB-MCSITEXE OF DB-PARMSINP
               SET  STEXEFLT-ON        TO TRUE
           END-IF

           IF  CREB957-IDPRCDEP-V = SPACES OR CREB950-MCPESQGN
               SET  PRCDPFLT-OFF       TO TRUE
           ELSE
               MOVE CREB957-IDPRCDEP-V TO DB-IDPRCDEP OF DB-PARMSINP
               SET  PRCDPFLT-ON        TO TRUE
           END-IF
           SET  QPROCES-CLSE           TO TRUE
           PERFORM P2441-CONT-QPROCES-P

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2441-CONT-QPROCES-D
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-NRENTLST = ZERO
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               PERFORM P2442-OPEN-QPROCES
           END-EVALUATE

           IF  QPROCES-OPEN
               PERFORM P2443-FTCH-QPROCES

               PERFORM
               VARYING WS-ARRYNDX1 FROM 1 BY 1
                 UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
                 OR    CREB000-IDREGLST-MAX
                 OR    CREB000-PROC-ERR
                   PERFORM P2444-LOAD-LSTSEL

                   IF  CREB000-PROC-OK OR CREB000-MSGINFO
                       PERFORM P2443-FTCH-QPROCES
                   END-IF
               END-PERFORM

               PERFORM P2445-CLSE-QPROCES
           END-IF

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND NOT CREB000-IDREGLST-MAX
               MOVE DB-IDREGLST TO CREB000-IDREGLST
           END-IF
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QPROCES-P        SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET903
                     WHERE IDPRCAPL LIKE :DB-PARMSINP.DB-IDPRCAPL
                       AND DSPRCAPL LIKE :DB-PARMSINP.DB-DSPRCAPL
                       AND(IDAPLICA    = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"         = :DB-IDAPLFLT)
                       AND(IDDIALOG    = :DB-PARMSINP.DB-IDDIALOG
                        OR "N"         = :DB-IDDLGFLT)
                       AND(IDTIPDLG    = :DB-PARMSINP.DB-IDTIPDLG
                        OR "N"         = :DB-TPDLGFLT)
                       AND(MCSITEXE    = :DB-PARMSINP.DB-MCSITEXE
                        OR "N"         = :DB-STEXEFLT)
                       AND(IDPRCDEP    = :DB-PARMSINP.DB-IDPRCDEP
                        OR "N"         = :DB-PRCDPFLT)
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               MOVE DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP907.QPROCES" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2441-CONT-QPROCES-D        SECTION.

           EXEC SQL SELECT COUNT(*) INTO :DB-VARDUMMY
                      FROM CRET904
                     WHERE IDPRCAPL LIKE :DB-PARMSINP.DB-IDPRCAPL
                       AND DSPRCAPL LIKE :DB-PARMSINP.DB-DSPRCAPL
                       AND(IDAPLICA    = :DB-PARMSINP.DB-IDAPLICA
                        OR "N"         = :DB-IDAPLFLT)
                       AND(IDDIALOG    = :DB-PARMSINP.DB-IDDIALOG
                        OR "N"         = :DB-IDDLGFLT)
                       AND("MA"        = :DB-PARMSINP.DB-IDTIPDLG
                        OR "N"         = :DB-TPDLGFLT)
                                                               END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               ADD  DB-VARDUMMY       TO CREB000-NRENTLST
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP907.QPROCES" TO CREB000-NMOBJECT
               SET  CREB000-SLCT      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2441-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2442-OPEN-QPROCES          SECTION.

           EXEC SQL OPEN  QPROCES                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  SQLCODE-OK
               SET  QPROCES-OPEN      TO TRUE
           ELSE
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP907.QPROCES" TO CREB000-NMOBJECT
               SET  CREB000-OPEN-CS   TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P2442-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2443-FTCH-QPROCES          SECTION.

           EXEC SQL FETCH QPROCES
                     INTO :CRET903.IDPRCAPL
                        , :CRET903.DSPRCAPL
                        , :CRET903.IDAPLICA
                        , :CRET903.IDPRGGST
                        , :CRET903.IDDIALOG
                        , :CRET903.IDTIPDLG
                        , :CRET903.MCSITEXE                    END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK     TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREGLST-MAX TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE      TO CREB000-CDRETORN
               MOVE "CREP907.QPROCES"    TO CREB000-NMOBJECT
               SET  CREB000-FTCH-CS      TO TRUE
               SET  CREB000-MSGSGBD      TO TRUE
           END-EVALUATE
           .
       P2443-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2444-LOAD-LSTSEL           SECTION.

           MOVE IDPRCAPL OF CRET903 TO CREB957-IDPRCLIN-V(WS-ARRYNDX1)
                                       DB-IDPRCAPL OF DB-IDREGLST
           MOVE DSPRCAPL OF CRET903 TO CREB957-NMPRCLIN-V(WS-ARRYNDX1)
           MOVE IDAPLICA OF CRET903 TO CREB957-IDAPLLIN-V(WS-ARRYNDX1)
           MOVE IDPRGGST OF CRET903 TO CREB957-IDGSTLIN-V(WS-ARRYNDX1)
           MOVE IDDIALOG OF CRET903 TO CREB957-IDDLGLIN-V(WS-ARRYNDX1)
           MOVE IDTIPDLG OF CRET903 TO CREB957-TPDLGLIN-V(WS-ARRYNDX1)
           MOVE MCSITEXE OF CRET903 TO CREB957-STEXELIN-V(WS-ARRYNDX1)
           .
       P2444-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2445-CLSE-QPROCES          SECTION.

           EXEC SQL CLOSE QPROCES                              END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP907.QPROCES"      TO CREB000-NMOBJECT
               SET  CREB000-CLSE-CS        TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-IF
           .
       P2445-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB957-IDAPLICA-V   TO CREB999-IDAPLICA
           MOVE CREB957-IDDIALOG-V   TO CREB999-IDDIALOG
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PREP-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-IDREGSEL = SPACES
               PERFORM P2610-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2620-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2630-FRMT-IDREGSEL
               END-IF
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2640-FRMT-IDPRCDST
           END-IF
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           INITIALIZE CREB000-NRLINSMC
           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
             OR    CREB000-PROC-ERR
               MOVE CREB957-NROPCLIN-V(WS-ARRYNDX1) TO WS-ARRYNDX2
               EVALUATE TRUE
               WHEN WS-ARRYNDX2 = ZERO
                   CONTINUE
               WHEN CREB000-INDICOPC-ON(WS-ARRYNDX2)
                   ADD  1 TO CREB000-NRLINSMC
               WHEN OTHER
                   COMPUTE CREB957-PCURSOR = CREB957-NRPARAMS-INP
                                           + WS-ARRYNDX1
                   SET  CREB950-VCAMPO-INVALID TO TRUE
                   SET  CREB000-MSGFUNC        TO TRUE
               END-EVALUATE
           END-PERFORM

           IF (CREB000-PROC-OK OR CREB000-MSGINFO)
           AND CREB000-NRLINSMC = ZERO
               SET  CREB957-NROPCL01-PC        TO TRUE
               SET  CREB950-SELECC-NINFORM     TO TRUE
               SET  CREB000-MSGFUNC            TO TRUE
           END-IF
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL CREB957-NROPCLIN-V(WS-ARRYNDX1) > ZERO
               CONTINUE
           END-PERFORM
           MOVE CREB957-NROPCLIN-V(WS-ARRYNDX1) TO CREB000-IDOPCSEL
           COMPUTE CREB957-PCURSOR = CREB957-NRPARAMS-INP + WS-ARRYNDX1
           INITIALIZE CREB957-NROPCLIN-V(WS-ARRYNDX1)
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL
           MOVE CREB957-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           MOVE CREB957-IDPRCLIN-V(WS-ARRYNDX1)
                                   TO WS-IDPRCAPL OF WS-IDREGSEL
           MOVE WS-IDREGSEL        TO CREB000-IDREGSEL
           .
       P2630-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2640-FRMT-IDPRCDST         SECTION.

           CONTINUE
           .
       P2640-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PREP-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2710-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2720-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2730-FRMT-IDREGSEL
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           PERFORM P2410-VRIF-NIVEL01
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           PERFORM P2420-VRIF-NIVEL02
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-IDREGSEL         SECTION.

           INITIALIZE WS-IDREGSEL, WS-COUNTER1
           IF  CREB957-IDAPLICA-V NOT = CREB950-MCPESQGN
               MOVE CREB957-IDAPLICA-V TO WS-IDAPLICA OF WS-IDREGSEL
           END-IF

           INSPECT CREB957-IDPRCAPL-V TALLYING WS-COUNTER1 FOR ALL "*"
           IF  WS-COUNTER1 = ZERO
               MOVE CREB957-IDPRCAPL-V TO WS-IDPRCAPL OF WS-IDREGSEL
           END-IF

           IF  CREB957-IDDIALOG-V NOT = CREB950-MCPESQGN
               MOVE CREB957-IDDIALOG-V TO WS-IDDIALOG OF WS-IDREGSEL
           END-IF

           MOVE WS-IDREGSEL            TO CREB000-IDREGSEL
           .
       P2730-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN OTHER
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB957-MSGDIALG TO CREB000-MSGDLG-L
           END-EVALUATE
           MOVE CREB957-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)

           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB957-IDAPLICA-UP TO TRUE
           SET  CREB957-IDPRCAPL-UP TO TRUE
           SET  CREB957-DSPRCAPL-UP TO TRUE
           SET  CREB957-IDDIALOG-UP TO TRUE
           SET  CREB957-IDTIPDLG-UP TO TRUE
           SET  CREB957-MCSITEXE-UP TO TRUE
           SET  CREB957-IDPRCDEP-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB957-IDAPLICA-PR TO TRUE
           SET  CREB957-IDPRCAPL-PR TO TRUE
           SET  CREB957-DSPRCAPL-PR TO TRUE
           SET  CREB957-IDDIALOG-PR TO TRUE
           SET  CREB957-IDTIPDLG-PR TO TRUE
           SET  CREB957-MCSITEXE-PR TO TRUE
           SET  CREB957-IDPRCDEP-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
             OR    CREB957-IDPRCLIN-V(WS-ARRYNDX1) = SPACES
               SET  CREB957-NROPCLIN-UP(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > CREB957-NRSELLIN-MAX
               SET  CREB957-NROPCLIN-PR(WS-ARRYNDX1) TO TRUE
           END-PERFORM
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           CONTINUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDAPLICA         SECTION.

           MOVE CREB957-IDAPLICA-V TO IDAPLICA OF CRET901
           EXEC SQL SELECT NMAPLICA
                      INTO :CRET901.NMAPLICA
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               MOVE NMAPLICA OF CRET901    TO CREB957-NMAPLICA-V
           WHEN SQLCODE-NOTFOUND
               SET  CREB957-IDAPLICA-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP907.CRET901"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDDIALOG         SECTION.

           MOVE CREB957-IDDIALOG-V TO IDDIALOG OF CRET902
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET902
                     WHERE IDDIALOG = :CRET902.IDDIALOG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB957-IDDIALOG-PC    TO TRUE
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP907.CRET902"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-IDTIPDLG         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-IDTIPDLG-OCC
             OR    KV-IDTIPDLG-ID(WS-ARRYNDX1) = CREB957-IDTIPDLG-V
               CONTINUE
           END-PERFORM

           IF  WS-ARRYNDX1 <= KV-IDTIPDLG-OCC
               MOVE KV-IDTIPDLG-DS(WS-ARRYNDX1) TO CREB957-DSTIPDLG-V
           ELSE
               SET  CREB957-IDTIPDLG-PC         TO TRUE
               SET  CREB950-TIPDLG-INVALID      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-MCSITEXE         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB957-MCSITEXE-V
               CONTINUE
           END-PERFORM

           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB957-DSSITEXE-V
           ELSE
               SET  CREB957-MCSITEXE-PC         TO TRUE
               SET  CREB950-SITEXE-INVALID      TO TRUE
               SET  CREB000-MSGFUNC             TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP907 ------------------------------------*
