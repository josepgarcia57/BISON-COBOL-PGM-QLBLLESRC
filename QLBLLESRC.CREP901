      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP901.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP901
      * OBJECTIVO : IMPLEMENTAR AS REGRAS DE PROCESSAMENTO DO PROCESSO
      *             "CREP901"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
           COPY CREB950.

       01  WS-VARSTRAB.
           05  WS-PARMSINP.
               10  WS-IDAPLICA         PIC X(03).
               10  FILLER              PIC X(77).
           05  WS-IDREGNVG     REDEFINES WS-PARMSINP.
               10  FILLER              PIC X(80).

           05  WS-PARMSLDV             PIC X(80).
           05  WS-LVAPLICA-I   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).
               10  WS-MCSITEXE         PIC X(01).
           05  WS-LVAPLICA-O   REDEFINES WS-PARMSLDV.
               10  WS-IDAPLICA         PIC X(03).

           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-MODEXPLR-LS.
               10  KV-MCMODEXP-TRNS    PIC X       VALUE "T".
               10  FILLER              PIC X(13)   VALUE "On-Line".
               10  KV-MCMODEXP-BTCH    PIC X       VALUE "B".
               10  FILLER              PIC X(13)   VALUE "Batch  ".
               10  KV-MCMODEXP-MIST    PIC X       VALUE "M".
               10  FILLER              PIC X(13)   VALUE
                                                   "Batch/On-Line".
           05  KV-MODEXPLR-LV  REDEFINES KV-MODEXPLR-LS    OCCURS 3.
               10  KV-MODEXPLR-ID      PIC X.
               10  KV-MODEXPLR-DS      PIC X(13).
           05  KV-MODEXPLR-OCC         PIC 9       VALUE 3.
           05  KV-SITEXECU-LS.
               10  KV-MCSITEXE-ACTV    PIC X       VALUE "A".
               10  FILLER              PIC X(07)   VALUE "Activa ".
               10  KV-MCSITEXE-INIB    PIC X       VALUE "I".
               10  FILLER              PIC X(07)   VALUE "Inibida".
           05  KV-SITEXECU-LV  REDEFINES KV-SITEXECU-LS    OCCURS 2.
               10  KV-SITEXECU-ID      PIC X.
               10  KV-SITEXECU-DS      PIC X(07).
           05  KV-SITEXECU-OCC         PIC 9       VALUE 2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB951.
           COPY CREB999.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           EXEC SQL INCLUDE SQLCA                              END-EXEC.

           EXEC SQL BEGIN DECLARE  SECTION                     END-EXEC.
       01  WS-DBGBVARS.
           COPY DDS-CRET901 OF CRET901.
           COPY DDS-CRET902 OF CRET902.
           COPY DDS-CRET903 OF CRET903.
           COPY DDS-CRET906 OF CRET906.
           COPY DDS-CRET909 OF CRET909.

       01  WS-DBLCVARS.
           05  DB-VARDUMMY             PIC S9(4)       BINARY.
           05  DB-DTPRCAPL             PIC X(10).
           EXEC SQL END   DECLARE  SECTION                     END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-IDREGSEL = SPACES
               MOVE CREB000-PARMSINP TO WS-PARMSINP
           ELSE
               MOVE CREB000-IDREGSEL TO WS-PARMSINP
           END-IF

           IF (CREB000-IDREGSEL = SPACES AND CREB000-MSGDLG-P > ZERO)
           OR  CREB000-MSGDLG-P > 1
               MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
                                     TO CREB951-MSGDIALG
           END-IF
           INITIALIZE CREB950-IDMSGAPL, CREB951-PCURSOR, WS-DBGBVARS
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-INITPRC
               PERFORM P2100-INICIALIZA
           WHEN CREB000-RSETPRC
               PERFORM P2200-REINICIALIZA
           WHEN CREB000-PROCLOV
               PERFORM P2300-PROC-LISTVALS
           WHEN CREB000-PROCIDT
               IF  CREB000-INITDLG OR CREB000-NRLINSMC > ZERO
                   PERFORM P2200-REINICIALIZA
               END-IF
               PERFORM P2400-PROC-IDENTREG
           WHEN CREB000-IDREG-NV
               PERFORM P2500-FRMT-IDREGNVG
           WHEN CREB000-PROCCRT
           OR  (CREB000-PROCVRF AND CREB000-IDREG-NF)
               PERFORM P2600-PROC-NOVOREG
           WHEN CREB000-PROCSEL
           OR  (CREB000-PROCVRF AND CREB000-IDREG-OK)
               PERFORM P2700-PROC-REGEXIST
           WHEN CREB000-PROCPRT
               PERFORM P2800-PROC-IMPRESSO
           WHEN CREB000-PROCCNF
               PERFORM P2900-EXEC-ALTERABD
           WHEN CREB000-PROCCAN
               PERFORM P2A00-PROC-CANCELA
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-INICIALIZA            SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB951-MSGDIALG, WS-PARMSINP
           PERFORM P9000-PERMIT-IDT
           PERFORM P9000-PROTEG-DET
           PERFORM P9000-NDISPL-DET
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-REINICIALIZA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB951-MSGDIALG
           MOVE WS-IDAPLICA OF WS-PARMSINP TO CREB951-IDAPLICA-V
           IF  CREB000-RSETPRC
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-IF
           .
       P2200-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-PROC-LISTVALS         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-INITDLG
               PERFORM P2310-FRMT-PARMSLDV
           ELSE
               PERFORM P2320-RECB-IDSELLDV
           END-IF
           .
       P2300-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2310-FRMT-PARMSLDV         SECTION.

           MOVE SPACES                  TO WS-PARMSLDV

           EVALUATE CREB000-IDVARCSR
           WHEN "IDAPLICA"
               MOVE CREB951-IDAPLICA-V  TO WS-IDAPLICA OF WS-LVAPLICA-I
               MOVE CREB950-MCPESQGN    TO WS-MCSITEXE OF WS-LVAPLICA-I
               SET  CREB951-IDAPLICA-PC TO TRUE
           END-EVALUATE

           MOVE WS-PARMSLDV             TO CREB000-PARMSINP
           .
       P2310-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2320-RECB-IDSELLDV         SECTION.

           MOVE CREB000-PARMSINP        TO WS-PARMSLDV

           EVALUATE TRUE
           WHEN CREB000-PARMSINP = SPACES
               CONTINUE
           WHEN CREB000-IDVARCSR = "IDAPLICA"
           AND  WS-IDAPLICA OF WS-LVAPLICA-O NOT = SPACES
               MOVE WS-IDAPLICA OF WS-LVAPLICA-O TO CREB951-IDAPLICA-V
               SET  CREB951-IDAPLICA-PC          TO TRUE
           END-EVALUATE
           .
       P2320-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2400-PROC-IDENTREG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           PERFORM P2410-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2420-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2430-FRMT-PARMSINP
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               PERFORM P2440-FRMT-MSGDIALG
           WHEN CREB000-FUNCCRIA-ON
               PERFORM P2450-INIT-NOVOREG
           END-EVALUATE

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-IDREG-OK
               IF  CREB951-IDAPLICA-V = CREB000-IDAPLICA
                   SET  CREB000-FUNCANUL-OFF TO TRUE
               END-IF
               PERFORM P9000-PROTEG-IDT
               IF  CREB000-FUNCMODF-ON
                   PERFORM P9000-PERMIT-DET
               ELSE
                   PERFORM P9000-PROTEG-DET
               END-IF
               PERFORM P9000-DISPLY-DET
           WHEN CREB000-FUNCCRIA-ON
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PERMIT-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2400-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2410-VRIF-NIVEL01          SECTION.

           IF  CREB951-IDAPLICA-V = SPACES
               SET  CREB951-IDAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P2410-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2420-VRIF-NIVEL02          SECTION.

           MOVE CREB951-IDAPLICA-V TO IDAPLICA OF CRET901
           PERFORM P9000-READ-CRET901
           .
       P2420-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2430-FRMT-PARMSINP         SECTION.

           MOVE CREB951-IDAPLICA-V TO WS-IDAPLICA OF WS-PARMSINP
           .
       P2430-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2440-FRMT-MSGDIALG         SECTION.

           MOVE NMAPLICA OF CRET901       TO CREB951-NMAPLICA-V
           MOVE MCMODEXP OF CRET901       TO CREB951-MCMODEXP-V
           PERFORM P9000-READ-MODEXPLR
           MOVE DTEXEONL OF CRET901       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DATA-X08
           MOVE CREB950-DATA-N08          TO CREB951-DTEXEONL-V
           MOVE DTEXEBTC OF CRET901       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DATA-X08
           MOVE CREB950-DATA-N08          TO CREB951-DTEXEBTC-V
           MOVE DHINISIT OF CRET901       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB951-DHINISIT-V
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DATA-X08
           EVALUATE TRUE
           WHEN CREB950-DATA-N08    <= CREB000-DTPRCAPL
               MOVE MCSITEXE OF CRET901   TO CREB951-MCSITEXE-V
           WHEN MCSITEXE OF CRET901  = KV-MCSITEXE-ACTV
               MOVE KV-MCSITEXE-INIB      TO CREB951-MCSITEXE-V
           WHEN OTHER
               MOVE KV-MCSITEXE-ACTV      TO CREB951-MCSITEXE-V
           END-EVALUATE
           PERFORM P9000-READ-SITEXECU
           MOVE DHCRIREG OF CRET901       TO CREB950-DTHR-ISO
           MOVE CORR CREB950-DTHR-ISO     TO CREB950-DTHR-X14
           MOVE CREB950-DTHR-N14          TO CREB951-DHCRIREG-V
           IF  DHMDFREG OF CRET901 NOT = CREB950-TMSTPMIN
               MOVE DHMDFREG OF CRET901   TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB950-DTHR-N14      TO CREB951-DHMDFREG-V
           END-IF
           MOVE DHMDFREG OF CRET901       TO CREB000-DHMDFREG
           .
       P2440-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2450-INIT-NOVOREG          SECTION.

           MOVE KV-MCMODEXP-TRNS        TO CREB951-MCMODEXP-V
           PERFORM P9000-READ-MODEXPLR
           MOVE CREB000-DTPRCAPL        TO CREB951-DTEXEONL-V
                                           CREB951-DTEXEBTC-V
           MOVE KV-MCSITEXE-INIB        TO CREB951-MCSITEXE-V
           PERFORM P9000-READ-SITEXECU
           .
       P2450-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2500-FRMT-IDREGNVG         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-IDPRCDST     TO CREB999-IDPRCNVG
           INITIALIZE                   CREB999-PARMSINP
           MOVE CREB951-IDAPLICA-V   TO CREB999-IDAPLICA
           CALL CREB950-SRPRENVG USING CREB999-LKGERAL

           IF  CREB999-PROC-OK
               MOVE CREB999-PARMSNVG TO WS-IDREGNVG
           ELSE
               SET  CREB000-MSGSROT  TO TRUE
               MOVE CREB999-CDRETORN TO CREB000-CDRETORN
               MOVE CREB999-NMOBJECT TO CREB000-NMOBJECT
           END-IF
           .
       P2500-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2600-PROC-NOVOREG          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           INITIALIZE CREB951-VARSOUT
           PERFORM P2610-VRIF-NIVEL01

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2620-VRIF-NIVEL02
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P2630-FRMT-MSGDIALG
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCCRT
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-NDISPL-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-NDISPL-DET
           END-EVALUATE
           .
       P2600-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2610-VRIF-NIVEL01          SECTION.

           PERFORM P2710-VRIF-NIVEL01
           .
       P2610-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2620-VRIF-NIVEL02          SECTION.

           CONTINUE
           .
       P2620-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2630-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2630-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2700-PROC-REGEXIST         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCMDF OR CREB000-PROCVRF
               INITIALIZE CREB951-VARSOUT
               PERFORM P2710-VRIF-NIVEL01

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2720-VRIF-NIVEL02
               END-IF

               IF  CREB000-PROC-OK OR CREB000-MSGINFO
                   PERFORM P2730-FRMT-MSGDIALG
               END-IF
           ELSE
               PERFORM P2740-VRIF-RELDPEND
           END-IF

           EVALUATE TRUE
           WHEN CREB000-PROC-ERR
               CONTINUE
           WHEN CREB000-PROCVRF
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PERMIT-DET
               PERFORM P9000-DISPLY-DET
           WHEN OTHER
               PERFORM P9000-PROTEG-IDT
               PERFORM P9000-PROTEG-DET
               PERFORM P9000-DISPLY-DET
           END-EVALUATE
           .
       P2700-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2710-VRIF-NIVEL01          SECTION.

           EVALUATE TRUE
           WHEN CREB951-NMAPLICA-V = SPACES
               SET  CREB951-NMAPLICA-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB951-DTEXEONL-V = ZERO
               SET  CREB951-DTEXEONL-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN CREB951-DTEXEBTC-V = ZERO
               SET  CREB951-DTEXEBTC-PC    TO TRUE
               SET  CREB950-PCAMPO-OBRIGAT TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-EVALUATE
           .
       P2710-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2720-VRIF-NIVEL02          SECTION.

           MOVE CREB950-TMSTPMIN            TO CREB950-DTHR-ISO
           MOVE CREB951-DTEXEONL-V          TO CREB950-DATA-N08
           MOVE CORR CREB950-DATA-X08       TO CREB950-DTHR-ISO
           MOVE CREB950-DTHR-ISO(01:10)     TO DB-DTPRCAPL
           SET  CREB951-DTEXEONL-PC         TO TRUE
           PERFORM P9000-VLID-DTPRCAPL

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               MOVE CREB950-TMSTPMIN        TO CREB950-DTHR-ISO
               MOVE CREB951-DTEXEBTC-V      TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08   TO CREB950-DTHR-ISO
               MOVE CREB950-DTHR-ISO(01:10) TO DB-DTPRCAPL
               SET  CREB951-DTEXEBTC-PC     TO TRUE
               PERFORM P9000-VLID-DTPRCAPL
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               INITIALIZE CREB951-PCURSOR
           END-IF
           .
       P2720-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2730-FRMT-MSGDIALG         SECTION.

           CONTINUE
           .
       P2730-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2740-VRIF-RELDPEND         SECTION.

           PERFORM P9000-VRIF-CRET902

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VRIF-CRET903
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VRIF-CRET906
           END-IF

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               PERFORM P9000-VRIF-CRET909
           END-IF
           .
       P2740-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2800-PROC-IMPRESSO         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CONTINUE
           .
       P2800-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2900-EXEC-ALTERABD         SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               PERFORM P2910-EXEC-TRNCRIAR
           WHEN CREB000-DOMODFY
               PERFORM P2920-EXEC-TRNMODIF
           WHEN CREB000-DONULFY
               PERFORM P2930-EXEC-TRNANULA
           END-EVALUATE

           IF  CREB000-PROC-OK OR CREB000-MSGINFO
               SET  CREB000-TRNDONE TO TRUE
           END-IF
           .
       P2900-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2910-EXEC-TRNCRIAR         SECTION.

           PERFORM P9000-FRMT-CRET901-CHAV
           PERFORM P9000-FRMT-CRET901-ATRB
           PERFORM P9000-INSR-CRET901
           .
       P2910-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2920-EXEC-TRNMODIF         SECTION.

           PERFORM P9000-FRMT-CRET901-CHAV
           PERFORM P9000-FRMT-CRET901-ATRB
           PERFORM P9000-UPDT-CRET901
           .
       P2920-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2930-EXEC-TRNANULA         SECTION.

           PERFORM P9000-FRMT-CRET901-CHAV
           PERFORM P9000-DELT-CRET901
           .
       P2930-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2A00-PROC-CANCELA          SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           IF  CREB000-PROCCRT OR CREB000-FUNCMODF-ON
               PERFORM P9000-PERMIT-DET
           END-IF
           .
       P2A00-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           EVALUATE TRUE
           WHEN CREB000-PROCLOV
               CONTINUE
           WHEN CREB000-IDREGSEL = SPACES
           OR   CREB000-IDREG-NV
               MOVE WS-PARMSINP                TO CREB000-PARMSINP
               MOVE 1                          TO CREB000-MSGDLG-P
               MOVE LENGTH OF CREB951-MSGDIALG TO CREB000-MSGDLG-L
           WHEN CREB000-MSGDLG-P = 1
               COMPUTE CREB000-MSGDLG-P
                     = CREB000-MSGDLG-L + 1
               COMPUTE CREB000-MSGDLG-L
                     = CREB000-MSGDLG-L + LENGTH OF CREB951-MSGDIALG
           END-EVALUATE
           MOVE CREB951-MSGDIALG               TO CREB000-MSGDLG-V
                                                 (CREB000-MSGDLG-P:)
           IF  NOT CREB000-PROC-OK
               MOVE CREB950-IDMSGAPL           TO CREB000-IDMSGAPL
           END-IF
           GOBACK
           .
       P3000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P9000-PERMIT-IDT            SECTION.

           SET  CREB951-IDAPLICA-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-IDT            SECTION.

           SET  CREB951-IDAPLICA-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PERMIT-DET            SECTION.

           SET  CREB951-NMAPLICA-UP TO TRUE
           SET  CREB951-DTEXEONL-UP TO TRUE
           SET  CREB951-DTEXEBTC-UP TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-PROTEG-DET            SECTION.

           SET  CREB951-NMAPLICA-PR TO TRUE
           SET  CREB951-DTEXEONL-PR TO TRUE
           SET  CREB951-DTEXEBTC-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DISPLY-DET            SECTION.

           SET  CREB951-DSMODEXP-PR TO TRUE
           SET  CREB951-DHINISIT-PR TO TRUE
           SET  CREB951-DHCRIREG-PR TO TRUE
           SET  CREB951-DHMDFREG-PR TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-NDISPL-DET            SECTION.

           IF (CREB000-PROCIDT AND CREB000-IDREG-NF
           AND CREB000-FUNCCRIA-ON)
           OR  CREB000-PROCCRT
           OR (CREB000-PROCVRF AND CREB000-IDREG-NF)
               SET  CREB951-DSMODEXP-PR TO TRUE
           ELSE
               SET  CREB951-DSMODEXP-ND TO TRUE
           END-IF
           SET  CREB951-DHINISIT-ND     TO TRUE
           SET  CREB951-DHCRIREG-ND     TO TRUE
           SET  CREB951-DHMDFREG-ND     TO TRUE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-CRET901          SECTION.

           EXEC SQL SELECT NMAPLICA
                         , MCMODEXP
                         , DTEXEONL
                         , DTEXEBTC
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , DHCRIREG
                      INTO :CRET901.NMAPLICA
                         , :CRET901.MCMODEXP
                         , :CRET901.DTEXEONL
                         , :CRET901.DTEXEBTC
                         , :CRET901.MCSITEXE
                         , :CRET901.DHINISIT
                         , :CRET901.DHMDFREG
                         , :CRET901.DHCRIREG
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               SET  CREB000-IDREG-OK       TO TRUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB000-IDREG-NF       TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.CRET901"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-MODEXPLR         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-MODEXPLR-OCC
             OR    KV-MODEXPLR-ID(WS-ARRYNDX1) = CREB951-MCMODEXP-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-MODEXPLR-OCC
               MOVE KV-MODEXPLR-DS(WS-ARRYNDX1) TO CREB951-DSMODEXP-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB951-DSMODEXP-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-READ-SITEXECU         SECTION.

           PERFORM
           VARYING WS-ARRYNDX1 FROM 1 BY 1
             UNTIL WS-ARRYNDX1 > KV-SITEXECU-OCC
             OR    KV-SITEXECU-ID(WS-ARRYNDX1) = CREB951-MCSITEXE-V
               CONTINUE
           END-PERFORM
           IF  WS-ARRYNDX1 <= KV-SITEXECU-OCC
               MOVE KV-SITEXECU-DS(WS-ARRYNDX1) TO CREB951-DSSITEXE-V
           ELSE
               MOVE CREB950-DSABRVNF            TO CREB951-DSSITEXE-V
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VLID-DTPRCAPL         SECTION.

           MOVE CREB000-IDAPLICA           TO IDAPLICA OF CRET901
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA     = :CRET901.IDAPLICA
                       AND CURRENT DATE = :DB-DTPRCAPL         END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT (SQLCODE-OK OR SQLCODE-NOTFOUND)
               SET  CREB950-VCAMPO-INVALID TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET902          SECTION.

           MOVE CREB000-IDAPLICA    TO IDAPLICA OF CRET901
           MOVE CREB951-IDAPLICA-V  TO IDAPLICA OF CRET902
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET902
                     WHERE IDAPLICA = :CRET902.IDAPLICA
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET902"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.EXIST01"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET903          SECTION.

           MOVE CREB000-IDAPLICA    TO IDAPLICA OF CRET901
           MOVE CREB951-IDAPLICA-V  TO IDAPLICA OF CRET903
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET903
                     WHERE IDAPLICA = :CRET903.IDAPLICA
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET903"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.EXIST02"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET906          SECTION.

           MOVE CREB000-IDAPLICA    TO IDAPLICA OF CRET901
           MOVE CREB951-IDAPLICA-V  TO IDAPLICA OF CRET906
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET906
                     WHERE IDAPLICA = :CRET906.IDAPLICA
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET906"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.EXIST03"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-VRIF-CRET909          SECTION.

           MOVE CREB000-IDAPLICA    TO IDAPLICA OF CRET901
           MOVE CREB951-IDAPLICA-V  TO IDAPLICA OF CRET909
           EXEC SQL SELECT 1 INTO :DB-VARDUMMY
                      FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND EXISTS
                   (SELECT * FROM CRET909
                     WHERE IDAPLICA = :CRET909.IDAPLICA
                   )                                           END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
               CONTINUE
           WHEN SQLCODE-OK
               SET  CREB950-FUNCAO-ANUL-NP TO TRUE
               MOVE "CRET909"              TO CREB000-NMOBJECT
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.EXIST04"      TO CREB000-NMOBJECT
               SET  CREB000-SLCT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET901-CHAV     SECTION.

           MOVE CREB951-IDAPLICA-V     TO IDAPLICA OF CRET901
           IF  CREB000-DOMODFY OR CREB000-DONULFY
               MOVE CREB000-DHMDFREG   TO DHMDFREG OF CRET901
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-FRMT-CRET901-ATRB     SECTION.

           MOVE CREB951-NMAPLICA-V     TO NMAPLICA OF CRET901
           MOVE CREB951-DTEXEONL-V     TO DTEXEONL OF CRET901
           MOVE CREB951-DTEXEBTC-V     TO DTEXEBTC OF CRET901
           EVALUATE TRUE
           WHEN CREB000-DOCREAT
               MOVE CREB951-MCMODEXP-V    TO MCMODEXP OF CRET901
               MOVE CREB951-MCSITEXE-V    TO MCSITEXE OF CRET901
               MOVE CREB950-TMSTPMIN      TO CREB950-DTHR-ISO
               MOVE CORR CREB950-DTHR-ISO TO CREB950-DTHR-X14
               MOVE CREB000-DTPRCAPL      TO CREB950-DATA-N08
               MOVE CORR CREB950-DATA-X08 TO CREB950-DTHR-X14
               MOVE CORR CREB950-DTHR-X14 TO CREB950-DTHR-ISO
               MOVE CREB950-DTHR-ISO      TO DHINISIT OF CRET901
               MOVE CREB000-IDUTILIZ      TO IDUTLCRI OF CRET901
               MOVE CREB950-TMSTPMIN      TO DHMDFREG OF CRET901
           WHEN CREB000-DOMODFY
               MOVE CREB000-IDUTILIZ      TO IDUTLMDF OF CRET901
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-INSR-CRET901          SECTION.

           EXEC SQL INSERT INTO CRET901
                         ( IDAPLICA
                         , NMAPLICA
                         , MCMODEXP
                         , DTEXEONL
                         , DTEXEBTC
                         , MCSITEXE
                         , DHINISIT
                         , DHMDFREG
                         , IDUTLMDF
                         , DHCRIREG
                         , IDUTLCRI
                         )
                    VALUES
                         ( :CRET901.IDAPLICA
                         , :CRET901.NMAPLICA
                         , :CRET901.MCMODEXP
                         , :CRET901.DTEXEONL
                         , :CRET901.DTEXEBTC
                         , :CRET901.MCSITEXE
                         , :CRET901.DHINISIT
                         , :CRET901.DHMDFREG
                         , :CRET901.IDUTLMDF
                         , CURRENT TIMESTAMP
                         , :CRET901.IDUTLCRI
                         )                                     END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           IF  NOT SQLCODE-OK
               MOVE CREB950-SQLCODE   TO CREB000-CDRETORN
               MOVE "CREP901.CRET901" TO CREB000-NMOBJECT
               SET  CREB000-INSR      TO TRUE
               SET  CREB000-MSGSGBD   TO TRUE
           END-IF
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-UPDT-CRET901          SECTION.

           EXEC SQL UPDATE CRET901
                       SET NMAPLICA = :CRET901.NMAPLICA
                         , DTEXEONL = :CRET901.DTEXEONL
                         , DTEXEBTC = :CRET901.DTEXEBTC
                         , DHMDFREG = CURRENT TIMESTAMP
                         , IDUTLMDF = :CRET901.IDUTLMDF
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND DHMDFREG = :CRET901.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.CRET901"      TO CREB000-NMOBJECT
               SET  CREB000-UPDT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P9000-DELT-CRET901          SECTION.

           EXEC SQL DELETE FROM CRET901
                     WHERE IDAPLICA = :CRET901.IDAPLICA
                       AND DHMDFREG = :CRET901.DHMDFREG        END-EXEC

           MOVE SQLCODE TO CREB950-SQLCODE
           EVALUATE TRUE
           WHEN SQLCODE-OK
               CONTINUE
           WHEN SQLCODE-NOTFOUND
               SET  CREB950-FUNCAO-INTERRP TO TRUE
               SET  CREB000-MSGFUNC        TO TRUE
           WHEN OTHER
               MOVE CREB950-SQLCODE        TO CREB000-CDRETORN
               MOVE "CREP901.CRET901"      TO CREB000-NMOBJECT
               SET  CREB000-DELT           TO TRUE
               SET  CREB000-MSGSGBD        TO TRUE
           END-EVALUATE
           .
       P9000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP901 ------------------------------------*
