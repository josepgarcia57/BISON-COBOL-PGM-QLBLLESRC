       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BPCSCCIN3   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BPC - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este Interface destina-se a Extrair Info   *
     š*                     Estática de Risco de Instrumento BP        *
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 DADOS-EXT                  PIC 9(01).
          88 COM-DADOS-EXT           VALUE 1.
          88 SEM-DADOS-EXT           VALUE 0.
      *
       01 EXISTE-ALTER-RISCO         PIC 9(01).
          88 SIM-ALTER-RISCO         VALUE 1.
          88 NAO-ALTER-RISCO         VALUE 0.
      *
       01 ENVIADA-INFO-RISCO         PIC 9(01).
          88 SIM-ENVIADA-RISCO       VALUE 1.
          88 NAO-ENVIADA-RISCO       VALUE 0.
      *
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(8)    VALUE "BPSCCIN3".

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".
       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA         PIC 9(04).
           03  FILLER               PIC X VALUE "-".
           03  WS-MESIN-DIA         PIC 9(02).
           03  FILLER               PIC X VALUE "-".
           03  WS-DIAIN-DIA         PIC 9(02).

     š* Contrato de Crédito
       01 WS-IDCONT                PIC X(32).
       01 WX-IDCONT REDEFINES WS-IDCONT.
          05 WS-CTID               PIC X(03).
          05 WS-FILLER6            PIC X(01).
          05 WS-CTCONT             PIC X(07).

     š* Instrumento de Crédito
       01 WS-IDINST                PIC X(32).
       01 WX-IDINT  REDEFINES WS-IDINST.
          05 WS-RACINE             PIC X(07).
          05 WS-FILLER1            PIC X(01).
          05 WS-GRE                PIC X(03).
          05 WS-FILLER2            PIC X(01).
          05 WS-RUB                PIC X(03).
          05 WS-FILLER3            PIC X(01).
          05 WS-MON                PIC X(03).
          05 WS-FILLER4            PIC X(01).
          05 WS-CTID-INST          PIC X(03).
          05 WS-FILLER5            PIC X(01).
          05 WS-CTCONT-INST        PIC X(07).

       01 WS-TST-DATE      PIC X(10).
       01 WS-VAL-DATE      PIC X(10).

       01  R-TCCIN3.
           COPY DDS-ALL-FORMATS OF BPCTCCIN3.

     š* Copy : Tabela de Risco de Instrumento oNova
           EXEC SQL
               INCLUDE BPCHCCIN3N
           END-EXEC.

     š* Copy : Tabela de Risco de Instrumento Antiga
           EXEC SQL
               INCLUDE BPCHCCIN3O
           END-EXEC.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
           COPY BPCBCCIN3.
      *================================================================*
       PROCEDURE DIVISION USING BPCBCCIN3-LKPARMS.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P001-INICIO
           PERFORM P300-GET-BPCTCCIN3
           IF SEM-DADOS-EXT
             PERFORM P999-FIMPGM
           END-IF
           IF COM-DADOS-EXT
             PERFORM P400-MOVE-BPCJCCIN3
             IF BPCBCCIN3-PROC-OK
               PERFORM P550-TESTA-BPCJCCIN3
               PERFORM P550-TRATA-BPCJCCIN3
             END-IF
           END-IF.
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P001-INICIO.
     š*---------------------------------------------------------------------
           INITIALIZE  NEWCCIN3-REC
                    ,  OLDCCIN3-REC
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.
           MOVE ZERO TO EXISTE-ALTER-1.
           MOVE BPCBCCIN3-IDCONT    TO WS-IDCONT
           MOVE BPCBCCIN3-IDINST    TO WS-IDINST
           MOVE WS-IDINST           TO NEWCCIN3-IDINST
           MOVE WS-IDCONT           TO NEWCCIN3-IDCONT.
     š*-
     š*----------------------------------------------------------------
       P300-GET-BPCTCCIN3.
     š*----------------------------------------------------------------
     š*-
     š*- Obtem Informação de Dados Externos Contabilisticos
           INITIALIZE  R-TCCIN3
                       REPLACING ALPHANUMERIC BY SPACES
                                     NUMERIC BY ZEROS.

           EXEC SQL
                SELECT    DTPDINST
                       ,  PDINST
                       ,  TIPOPDINST
                       ,  TPAVALR
                       ,  SISAVALR
                       ,  MODIRBINST
                       ,  NOTACINST
                       ,  LGDINST
                       ,  MODLGDINST

                INTO     :DTPDINST
                       , :PDINST
                       , :TIPOPDINST
                       , :TPAVALR
                       , :SISAVALR
                       , :MODIRBINST
                       , :NOTACINST
                       , :LGDINST
                       , :MODLGDINST

                FROM  BPCTCCIN3 A
                WHERE A.IDCONT     = :WS-IDCONT
                 AND  A.IDINST     = :WS-IDINST
                 AND  A.TPAVALR    <> " "
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET COM-DADOS-EXT TO TRUE
           WHEN SQLCODE-NOTFOUND
              SET SEM-DADOS-EXT TO TRUE
           WHEN OTHER
              MOVE "BPCTCCIN3"          TO W-OBJECT-NAME
              SET CMD-SELECT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P400-MOVE-BPCJCCIN3.
     š*----------------------------------------------------------------
     š*-
           MOVE DTPDINST   OF  R-TCCIN3  TO  NEWCCIN3-DTPDINST
           MOVE PDINST     OF  R-TCCIN3  TO  NEWCCIN3-PDINST
           MOVE TIPOPDINST OF  R-TCCIN3  TO  NEWCCIN3-TIPOPDINST
           MOVE TPAVALR    OF  R-TCCIN3  TO  NEWCCIN3-TPAVALR
           MOVE SISAVALR   OF  R-TCCIN3  TO  NEWCCIN3-SISAVALR
           MOVE MODIRBINST OF  R-TCCIN3  TO  NEWCCIN3-MODIRBINST
           MOVE NOTACINST  OF  R-TCCIN3  TO  NEWCCIN3-NOTACINST
           MOVE LGDINST    OF  R-TCCIN3  TO  NEWCCIN3-LGDINST
           MOVE MODLGDINST OF  R-TCCIN3  TO  NEWCCIN3-MODLGDINST

           PERFORM P450-VALIDA-DATA-RISCO.
     š*-
     š*----------------------------------------------------------------
       P450-VALIDA-DATA-RISCO.
     š*----------------------------------------------------------------
     š*-
           MOVE NEWCCIN3-DTPDINST   TO WS-TST-DATE
           EXEC SQL
               SET :WS-VAL-DATE  = DATE(:WS-TST-DATE)
           END-EXEC
           EVALUATE SQLCODE
              WHEN +0
                  CONTINUE
              WHEN -180
                  MOVE "0001-01-01" TO NEWCCIN3-DTPDINST
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P550-TESTA-BPCJCCIN3.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe

           EXEC SQL
             SELECT  DTPDINST
                   , PDINST
                   , TIPOPDINST
                   , TPAVALR
                   , SISAVALR
                   , MODIRBINST
                   , NOTACINST
                   , LGDINST
                   , MODLGDINST
                   , TMSCRIA
                   , USERCRIA
                   , TMSALT
                   , USERALT
             INTO
                    :OLDCCIN3-DTPDINST
                  , :OLDCCIN3-PDINST
                  , :OLDCCIN3-TIPOPDINST
                  , :OLDCCIN3-TPAVALR
                  , :OLDCCIN3-SISAVALR
                  , :OLDCCIN3-MODIRBINST
                  , :OLDCCIN3-NOTACINST
                  , :OLDCCIN3-LGDINST
                  , :OLDCCIN3-MODLGDINST
                  , :OLDCCIN3-TMSCRIA
                  , :OLDCCIN3-USERCRIA
                  , :OLDCCIN3-TMSALT
                  , :OLDCCIN3-USERALT
             FROM  BPCJCCIN3
               WHERE IDINST   = :NEWCCIN3-IDINST
                AND  IDCONT   = :NEWCCIN3-IDCONT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
     š*-Igual Campos para comparação Global
              SET   SIM-ENVIADA-RISCO TO TRUE
              MOVE  OLDCCIN3-TMSCRIA  TO NEWCCIN3-TMSCRIA
              MOVE  OLDCCIN3-USERCRIA TO NEWCCIN3-USERCRIA
              MOVE  OLDCCIN3-TMSALT   TO NEWCCIN3-TMSALT
              MOVE  OLDCCIN3-USERALT  TO NEWCCIN3-USERALT
              IF   OLDCCIN3-REC  = NEWCCIN3-REC
               SET NAO-ALTER-RISCO TO TRUE
              ELSE
               SET SIM-ALTER-RISCO TO TRUE
              END-IF
           WHEN SQLCODE-NOTFOUND
              SET   NAO-ENVIADA-RISCO TO TRUE
              SET   NAO-ALTER-RISCO   TO TRUE
           WHEN OTHER
              MOVE "BPCJCCIN3"       TO W-OBJECT-NAME
              SET CMD-SELECT         TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
           IF SIM-ALTER-RISCO
              SET SIM-ALTER-1 TO TRUE
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P550-TRATA-BPCJCCIN3.
     š*----------------------------------------------------------------
           IF SIM-ENVIADA-RISCO
             IF SIM-ALTER-RISCO
                PERFORM P550-INSERT-HIST-BPCJCCIN3
                IF BPCBCCIN3-PROC-OK
                   PERFORM P550-UPDATE-BPCJCCIN3
                END-IF
                IF BPCBCCIN3-PROC-OK
                   SET SIM-ALTER-RISCO TO TRUE
                END-IF
             END-IF
           ELSE
             PERFORM P550-INSERT-BPCJCCIN3
             IF BPCBCCIN3-PROC-OK
               SET SIM-ALTER-RISCO TO TRUE
             END-IF
           END-IF.

           IF SIM-ALTER-RISCO
              SET SIM-ALTER-1 TO TRUE
           END-IF.
     š*-
     š*----------------------------------------------------------------
       P550-INSERT-BPCJCCIN3.
     š*----------------------------------------------------------------
     š*- Verifica se o Registo Já Existe
     š*-
           MOVE WS-TMS-NULA  TO NEWCCIN3-TMSALT
           EXEC SQL
                 INSERT INTO BPCJCCIN3

                 VALUES ( :NEWCCIN3-IDCONT
                        , :NEWCCIN3-IDINST
                        , :NEWCCIN3-DTPDINST
                        , :NEWCCIN3-PDINST
                        , :NEWCCIN3-TIPOPDINST
                        , :NEWCCIN3-TPAVALR
                        , :NEWCCIN3-SISAVALR
                        , :NEWCCIN3-MODIRBINST
                        , :NEWCCIN3-NOTACINST
                        , :NEWCCIN3-LGDINST
                        , :NEWCCIN3-MODLGDINST
                        , CURRENT TIMESTAMP
                        , :NEWCCIN3-USERCRIA
                        , :NEWCCIN3-TMSALT
                        , :NEWCCIN3-USERCRIA )
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCCIN3"        TO W-OBJECT-NAME
              SET CMD-INSERT           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*----------------------------------------------------------------
       P550-UPDATE-BPCJCCIN3.
     š*----------------------------------------------------------------
     š*-
     š*- Actualiza a Informação
     š*-
           EXEC SQL
                 UPDATE BPCJCCIN3
                 SET     DTPDINST      = :NEWCCIN3-DTPDINST
                      ,  PDINST        = :NEWCCIN3-PDINST
                      ,  TIPOPDINST    = :NEWCCIN3-TIPOPDINST
                      ,  TPAVALR       = :NEWCCIN3-TPAVALR
                      ,  SISAVALR      = :NEWCCIN3-SISAVALR
                      ,  MODIRBINST    = :NEWCCIN3-MODIRBINST
                      ,  NOTACINST     = :NEWCCIN3-NOTACINST
                      ,  LGDINST       = :NEWCCIN3-LGDINST
                      ,  MODLGDINST    = :NEWCCIN3-MODLGDINST
                      ,  TMSALT        = CURRENT TIMESTAMP
                      ,  USERALT       = "BPCCCIN3"
                 WHERE IDCONT    = :NEWCCIN3-IDCONT
                  AND  IDINST    = :NEWCCIN3-IDINST
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCCIN3"        TO W-OBJECT-NAME
              SET CMD-UPDATE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*----------------------------------------------------------------
       P550-INSERT-HIST-BPCJCCIN3.
     š*----------------------------------------------------------------
     š*- Guarda em Histórico a Informação
     š*-
           EXEC SQL
                 INSERT INTO BPCJHCCIN3
                 VALUES ( :OLDCCIN3-IDCONT
                        , :OLDCCIN3-IDINST
                        , :OLDCCIN3-DTPDINST
                        , :OLDCCIN3-PDINST
                        , :OLDCCIN3-TIPOPDINST
                        , :OLDCCIN3-TPAVALR
                        , :OLDCCIN3-SISAVALR
                        , :OLDCCIN3-MODIRBINST
                        , :OLDCCIN3-NOTACINST
                        , :OLDCCIN3-LGDINST
                        , :OLDCCIN3-MODLGDINST
                        , :OLDCCIN3-TMSCRIA
                        , :OLDCCIN3-USERCRIA
                         ,:OLDCCIN3-TMSALT
                         ,:OLDCCIN3-USERALT
                         , CURRENT TIMESTAMP )
           END-EXEC.
           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             CONTINUE
           WHEN OTHER
              MOVE "BPCJCCIN3"        TO W-OBJECT-NAME
              SET CMD-UPDATE           TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           MOVE WS-RACINE       TO RACINE   OF  KYCT900A
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A
           SET BPCBCCIN3-MSGSGBD  TO TRUE
           MOVE W-OBJECT-NAME   TO BPCBCCIN3-IDPARERR
           MOVE W-SQLCODE       TO BPCBCCIN3-CODERR
           MOVE MSG-ERRO-SQL    TO BPCBCCIN3-DESCERR

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BPCSCCIN3"    TO  ERRPRG OF KYCT900A.
           MOVE   WS-RACINE      TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "INSERT-T900A"   TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.


     š*****************************************************************
       P999-FIMPGM.
     š*****************************************************************
           GOBACK.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BPCSCCIN3
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
