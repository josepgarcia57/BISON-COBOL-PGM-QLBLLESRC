      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP956.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP956
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE956"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE956
                  ASSIGN        TO WORKSTATION-CREE956-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE956
                  CONTROL-AREA  IS CA-CREE956.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE956
           LABEL RECORD OMITTED.
       01  CREE956-HDR.
           COPY DDS-CREE956HDR OF CREE956.
       01  CREE956-D01.
           COPY DDS-CREE956D01 OF CREE956.
       01  CREE956-FTR.
           COPY DDS-CREE956FTR OF CREE956.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE956                  PIC 99.
           88  CREE956-OK                          VALUE ZERO.

       01  CA-CREE956.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE956".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE956HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE956D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE956FTR".
           05  KV-IDDIALOG-HI          PIC 99      VALUE 25.
           05  KV-IDDIALOG-PC          PIC 99      VALUE 26.
           05  KV-IDDIALOG-PR          PIC 99      VALUE 27.
           05  KV-IDAPLICA-HI          PIC 99      VALUE 28.
           05  KV-IDAPLICA-PC          PIC 99      VALUE 29.
           05  KV-IDAPLICA-PR          PIC 99      VALUE 30.
           05  KV-SNPGNVRT-HI          PIC 99      VALUE 31.
           05  KV-SNPGNVRT-PC          PIC 99      VALUE 32.
           05  KV-SNPGNVRT-PR          PIC 99      VALUE 33.
           05  KV-NRPGSHRZ-HI          PIC 99      VALUE 34.
           05  KV-NRPGSHRZ-PC          PIC 99      VALUE 35.
           05  KV-NRPGSHRZ-PR          PIC 99      VALUE 36.
           05  KV-IDGSTDLG-HI          PIC 99      VALUE 37.
           05  KV-IDGSTDLG-PC          PIC 99      VALUE 38.
           05  KV-IDGSTDLG-PR          PIC 99      VALUE 39.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 40.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 40.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB956.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE956, CA-CREE956
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE956-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE956

           IF  NOT CREE956-OK
               MOVE FS-CREE956      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB956-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE956-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE956-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE956-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB956-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB956-IDDIALOG-PR
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PR) TO TRUE
           END-IF
      *
           IF  CREB956-IDAPLICA-PR
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PR) TO TRUE
           END-IF
      *
           IF  CREB956-SNPGNVRT-PR
               SET  CREB000-INDICDLG-ON (KV-SNPGNVRT-PR) TO TRUE
           END-IF
      *
           IF  CREB956-NRPGSHRZ-PR
               SET  CREB000-INDICDLG-ON (KV-NRPGSHRZ-PR) TO TRUE
           END-IF
      *
           IF  CREB956-IDGSTDLG-PR
               SET  CREB000-INDICDLG-ON (KV-IDGSTDLG-PR) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN CREB956-IDDIALOG-PC
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PC) TO TRUE
           WHEN CREB956-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC) TO TRUE
           WHEN CREB956-SNPGNVRT-PC
               SET  CREB000-INDICDLG-ON (KV-SNPGNVRT-PC) TO TRUE
           WHEN CREB956-NRPGSHRZ-PC
               SET  CREB000-INDICDLG-ON (KV-NRPGSHRZ-PC) TO TRUE
           WHEN CREB956-IDGSTDLG-PC
               SET  CREB000-INDICDLG-ON (KV-IDGSTDLG-PC) TO TRUE
           WHEN CREB956-IDDIALOG-UP
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-PC) TO TRUE
           WHEN CREB956-IDAPLICA-UP
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-PC) TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB956-IDDIALOG-PC
               SET  CREB000-INDICDLG-ON (KV-IDDIALOG-HI) TO TRUE
           WHEN CREB956-IDAPLICA-PC
               SET  CREB000-INDICDLG-ON (KV-IDAPLICA-HI) TO TRUE
           WHEN CREB956-SNPGNVRT-PC
               SET  CREB000-INDICDLG-ON (KV-SNPGNVRT-HI) TO TRUE
           WHEN CREB956-NRPGSHRZ-PC
               SET  CREB000-INDICDLG-ON (KV-NRPGSHRZ-HI) TO TRUE
           WHEN CREB956-IDGSTDLG-PC
               SET  CREB000-INDICDLG-ON (KV-IDGSTDLG-HI) TO TRUE
           END-EVALUATE
      *
           IF  CREB956-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  CREB956-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE956-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE956HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE956HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE956HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE956HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE956HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE956HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE956HDR-O
           WRITE CREE956-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE956-OK
               MOVE FS-CREE956      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE956-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE956FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE956FTR-O
           WRITE CREE956-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE956-OK
               MOVE FS-CREE956      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO CREE956-D01
           MOVE CREB956-IDDIALOG-V  TO IDDIALOG OF CREE956D01-O
           MOVE CREB956-IDAPLICA-V  TO IDAPLICA OF CREE956D01-O
           MOVE CREB956-NMAPLICA-V  TO NMAPLICA OF CREE956D01-O
           MOVE CREB956-SNPGNVRT-V  TO SNPGNVRT OF CREE956D01-O
           MOVE CREB956-NRPGSHRZ-V  TO NRPGSHRZ OF CREE956D01-O
           MOVE CREB956-IDGSTDLG-V  TO IDGSTDLG OF CREE956D01-O
           MOVE CREB956-DHCRIREG-V  TO DHCRIREG OF CREE956D01-O
           MOVE CREB956-DHMDFREG-V  TO DHMDFREG OF CREE956D01-O
           WRITE CREE956-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE956-OK
               MOVE FS-CREE956      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE956
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE956-OK
               MOVE IDDIALOG OF CREE956D01-I TO CREB956-IDDIALOG-V
               MOVE IDAPLICA OF CREE956D01-I TO CREB956-IDAPLICA-V
               MOVE SNPGNVRT OF CREE956D01-I TO CREB956-SNPGNVRT-V
               MOVE NRPGSHRZ OF CREE956D01-I TO CREB956-NRPGSHRZ-V
               MOVE IDGSTDLG OF CREE956D01-I TO CREB956-IDGSTDLG-V
               MOVE IDVARCSR OF CREE956D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE956   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE956               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE956

           IF  NOT CREE956-OK
               MOVE FS-CREE956      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP956 ------------------------------------*
