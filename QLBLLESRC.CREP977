      *================================================================*
       IDENTIFICATION DIVISION.
      *================================================================*

       PROGRAM-ID.     CREP977.
      *----------------------------------------------------------------*
      * APLICACAO : GESTAO DE EMPRESTIMOS
      * PROGRAMA .: CREP977
      * OBJECTIVO : GERIR O ECRAN DE DIALOGO "CREE977"
      * CRIADO EM : 2005/FEVEREIRO
      * POR ......: FERNANDO SILVA (CAPGEMINI PORTUGAL)
      *----------------------------------------------------------------*

      *================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       CONFIGURATION               SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.  DECIMAL-POINT  IS COMMA
                       SYSTEM-CONSOLE IS VIDEO
                       LOCAL-DATA     IS LDATA
                       ATTRIBUTE-DATA IS ATTRB.

      *----------------------------------------------------------------*
       INPUT-OUTPUT                SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.

           SELECT CREE977
                  ASSIGN        TO WORKSTATION-CREE977-SI
                  ORGANIZATION  IS TRANSACTION
                  FILE STATUS   IS FS-CREE977
                  CONTROL-AREA  IS CA-CREE977.

      *================================================================*
       DATA DIVISION.
      *================================================================*

      *----------------------------------------------------------------*
       FILE                        SECTION.
      *----------------------------------------------------------------*
       FD  CREE977
           LABEL RECORD OMITTED.
       01  CREE977-HDR.
           COPY DDS-CREE977HDR OF CREE977.
       01  CREE977-D01.
           COPY DDS-CREE977D01 OF CREE977.
       01  CREE977-FTR.
           COPY DDS-CREE977FTR OF CREE977.

      *----------------------------------------------------------------*
       WORKING-STORAGE             SECTION.
      *----------------------------------------------------------------*
       01  FS-CREE977                  PIC 99.
           88  CREE977-OK                          VALUE ZERO.

       01  CA-CREE977.
           05  IDTECLAF                PIC 99.
           05  IDTERMNL                PIC X(10).
           05  FRMTREGT                PIC X(10).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-VARSTRAB.
           05  WS-ARRYNDX1             PIC S9(4)       BINARY.
           05  WS-DTHR-N14             PIC 9(14).
           05  FILLER      REDEFINES WS-DTHR-N14.
               10  WS-DATA-N08         PIC 9(08).
               10  WS-HORA-N06         PIC 9(06).
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       01  WS-CNSTANTS.
           05  KV-IDDIALOG             PIC X(08)   VALUE "CREE977".
           05  KV-IDFMTDLG-HDR         PIC X(10)   VALUE "CREE977HDR".
           05  KV-IDFMTDLG-D01         PIC X(10)   VALUE "CREE977D01".
           05  KV-IDFMTDLG-FTR         PIC X(10)   VALUE "CREE977FTR".
           05  KV-IDUTILIZ-HI          PIC 99      VALUE 25.
           05  KV-IDUTILIZ-PC          PIC 99      VALUE 26.
           05  KV-IDUTILIZ-PR          PIC 99      VALUE 27.
           05  KV-NMUTILIZ-HI          PIC 99      VALUE 28.
           05  KV-NMUTILIZ-PC          PIC 99      VALUE 29.
           05  KV-NMUTILIZ-PR          PIC 99      VALUE 30.
           05  KV-IDEMPRSA-HI          PIC 99      VALUE 31.
           05  KV-IDEMPRSA-PC          PIC 99      VALUE 32.
           05  KV-IDEMPRSA-PR          PIC 99      VALUE 33.
           05  KV-IDBALCAO-HI          PIC 99      VALUE 34.
           05  KV-IDBALCAO-PC          PIC 99      VALUE 35.
           05  KV-IDBALCAO-PR          PIC 99      VALUE 36.
           05  KV-IDGRFUNC-HI          PIC 99      VALUE 37.
           05  KV-IDGRFUNC-PC          PIC 99      VALUE 38.
           05  KV-IDGRFUNC-PR          PIC 99      VALUE 39.
           05  KV-DSSITEXE-ND          PIC 99      VALUE 40.
           05  KV-DHINISIT-ND          PIC 99      VALUE 41.
           05  KV-DTINISIT-HI          PIC 99      VALUE 42.
           05  KV-DTINISIT-PC          PIC 99      VALUE 43.
           05  KV-DHINISIT-PR          PIC 99      VALUE 44.
           05  KV-HRINISIT-HI          PIC 99      VALUE 45.
           05  KV-HRINISIT-PC          PIC 99      VALUE 46.
           05  KV-DHCRIREG-ND          PIC 99      VALUE 47.
           05  KV-DHMDFREG-ND          PIC 99      VALUE 47.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
           COPY CREB977.

      *----------------------------------------------------------------*
       LINKAGE                     SECTION.
      *----------------------------------------------------------------*
           COPY CREB000.

      *================================================================*
       PROCEDURE DIVISION USING CREB000-LKGERAL.
      *================================================================*

      *----------------------------------------------------------------*
       P0000-RAIZ                  SECTION.
      *----------------------------------------------------------------*

           PERFORM P1000-INICIO

           PERFORM P2000-PROCESSO

           PERFORM P3000-FIM
           .
       P0000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P1000-INICIO                SECTION.
      *----------------------------------------------------------------*

           INITIALIZE FS-CREE977, CA-CREE977
           .
       P1000-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P2000-PROCESSO              SECTION.
      *----------------------------------------------------------------*

           IF  CREB000-STDIALOG-OFF
               PERFORM P2100-OPEN-DIALOG
           END-IF

           EVALUATE TRUE
           WHEN NOT CREE977-OK
               CONTINUE
           WHEN CREB000-EXECPRC
               PERFORM P2200-PROC-DIALOG
           WHEN OTHER
               PERFORM P2300-CLSE-DIALOG
           END-EVALUATE
           .
       P2000-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2100-OPEN-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           OPEN I-O CREE977

           IF  NOT CREE977-OK
               MOVE FS-CREE977      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-OPEN-IO TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2100-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2200-PROC-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE CREB000-MSGDLG-V(CREB000-MSGDLG-P:) TO CREB977-MSGDIALG

           PERFORM P2210-PREP-ATRIBS
           PERFORM P2220-WRIT-REGHDR
           IF  CREE977-OK
               PERFORM P2230-WRIT-REGFTR
           END-IF
           IF  CREE977-OK
               PERFORM P2240-WRIT-REGDET
           END-IF
           IF  CREE977-OK
               PERFORM P2250-READ-REGDET
           END-IF

           MOVE CREB977-MSGDIALG TO CREB000-MSGDLG-V(CREB000-MSGDLG-P:)
           .
       P2200-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2210-PREP-ATRIBS           SECTION.

           IF  CREB977-IDUTILIZ-PR
               SET  CREB000-INDICDLG-ON (KV-IDUTILIZ-PR) TO TRUE
           END-IF
      *
           IF  CREB977-NMUTILIZ-PR
               SET  CREB000-INDICDLG-ON (KV-NMUTILIZ-PR) TO TRUE
           END-IF
      *
           IF  CREB977-IDEMPRSA-PR
               SET  CREB000-INDICDLG-ON (KV-IDEMPRSA-PR) TO TRUE
           END-IF
      *
           IF  CREB977-IDBALCAO-PR
               SET  CREB000-INDICDLG-ON (KV-IDBALCAO-PR) TO TRUE
           END-IF
      *
           IF  CREB977-IDGRFUNC-PR
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-PR) TO TRUE
           END-IF
      *
           IF  CREB977-DHINISIT-PR
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-PR) TO TRUE
           END-IF
      *
           EVALUATE TRUE
           WHEN CREB977-IDUTILIZ-PC
               SET  CREB000-INDICDLG-ON (KV-IDUTILIZ-PC) TO TRUE
           WHEN CREB977-NMUTILIZ-PC
               SET  CREB000-INDICDLG-ON (KV-NMUTILIZ-PC) TO TRUE
           WHEN CREB977-IDEMPRSA-PC
               SET  CREB000-INDICDLG-ON (KV-IDEMPRSA-PC) TO TRUE
           WHEN CREB977-IDBALCAO-PC
               SET  CREB000-INDICDLG-ON (KV-IDBALCAO-PC) TO TRUE
           WHEN CREB977-IDGRFUNC-PC
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-PC) TO TRUE
           WHEN CREB977-DHINISIT-UP
               SET  CREB000-INDICDLG-ON (KV-DTINISIT-PC) TO TRUE
           WHEN CREB977-IDUTILIZ-UP
               SET  CREB000-INDICDLG-ON (KV-IDUTILIZ-PC) TO TRUE
           WHEN CREB977-NMUTILIZ-UP
               SET  CREB000-INDICDLG-ON (KV-NMUTILIZ-PC) TO TRUE
           END-EVALUATE
      *
           EVALUATE TRUE
           WHEN CREB000-PROC-OK
               CONTINUE
           WHEN CREB977-IDUTILIZ-PC
               SET  CREB000-INDICDLG-ON (KV-IDUTILIZ-HI) TO TRUE
           WHEN CREB977-NMUTILIZ-PC
               SET  CREB000-INDICDLG-ON (KV-NMUTILIZ-HI) TO TRUE
           WHEN CREB977-IDEMPRSA-PC
               SET  CREB000-INDICDLG-ON (KV-IDEMPRSA-HI) TO TRUE
           WHEN CREB977-IDBALCAO-PC
               SET  CREB000-INDICDLG-ON (KV-IDBALCAO-HI) TO TRUE
           WHEN CREB977-IDGRFUNC-PC
               SET  CREB000-INDICDLG-ON (KV-IDGRFUNC-HI) TO TRUE
           END-EVALUATE
      *
           IF  CREB977-DSSITEXE-ND
               SET  CREB000-INDICDLG-ON (KV-DSSITEXE-ND) TO TRUE
           END-IF
      *
           IF  CREB977-DHINISIT-ND
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-PR) TO TRUE
               SET  CREB000-INDICDLG-ON (KV-DHINISIT-ND) TO TRUE
           END-IF
      *
           IF  CREB977-DHCRIREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHCRIREG-ND) TO TRUE
           END-IF
      *
           IF  CREB977-DHMDFREG-ND
               SET  CREB000-INDICDLG-ON (KV-DHMDFREG-ND) TO TRUE
           END-IF
           .
       P2210-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2220-WRIT-REGHDR           SECTION.

           MOVE SPACES              TO CREE977-HDR
           MOVE CREB000-SGEMPRSA    TO SGEMPRSA OF CREE977HDR-O
           MOVE CREB000-DSAPLICA    TO DSAPLICA OF CREE977HDR-O
           MOVE CREB000-DTPRCAPL    TO DTACTUAL OF CREE977HDR-O
           MOVE CREB000-IDPRCAPL    TO IDPRCAPL OF CREE977HDR-O
           MOVE CREB000-IDDIALOG    TO IDDIALOG OF CREE977HDR-O
           MOVE CREB000-DSPRCAPL    TO DSPRCAPL OF CREE977HDR-O
           MOVE CREB000-NMFUNDLG    TO NMFUNDLG OF CREE977HDR-O
           WRITE CREE977-HDR
                 FORMAT KV-IDFMTDLG-HDR INDIC CREB000-TBINDARA

           IF  NOT CREE977-OK
               MOVE FS-CREE977      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2220-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2230-WRIT-REGFTR           SECTION.

           MOVE SPACES              TO CREE977-FTR
           MOVE CREB000-DSTECLAS    TO DSTECLAS OF CREE977FTR-O
           MOVE CREB000-DSMSGAPL    TO DSMSGAPL OF CREE977FTR-O
           WRITE CREE977-FTR
                 FORMAT KV-IDFMTDLG-FTR INDIC CREB000-TBINDARA

           IF  NOT CREE977-OK
               MOVE FS-CREE977      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2230-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2240-WRIT-REGDET           SECTION.

           MOVE SPACES              TO CREE977-D01
           MOVE CREB977-IDUTILIZ-V  TO IDUTILIZ OF CREE977D01-O
           MOVE CREB977-NMUTILIZ-V  TO NMUTILIZ OF CREE977D01-O
           MOVE CREB977-IDEMPRSA-V  TO IDEMPRSA OF CREE977D01-O
           MOVE CREB977-NMEMPRSA-V  TO NMEMPRSA OF CREE977D01-O
           MOVE CREB977-IDBALCAO-V  TO IDBALCAO OF CREE977D01-O
           MOVE CREB977-NMBALCAO-V  TO NMBALCAO OF CREE977D01-O
           MOVE CREB977-IDGRFUNC-V  TO IDGRFUNC OF CREE977D01-O
           MOVE CREB977-DSGRFUNC-V  TO DSGRFUNC OF CREE977D01-O
           MOVE CREB977-DSSITEXE-V  TO DSSITEXE OF CREE977D01-O
           MOVE CREB977-DHINISIT-V  TO WS-DTHR-N14
           MOVE WS-DATA-N08         TO DTINISIT OF CREE977D01-O
           MOVE WS-HORA-N06         TO HRINISIT OF CREE977D01-O
           MOVE CREB977-DHCRIREG-V  TO DHCRIREG OF CREE977D01-O
           MOVE CREB977-DHMDFREG-V  TO DHMDFREG OF CREE977D01-O
           WRITE CREE977-D01
                 FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA

           IF  NOT CREE977-OK
               MOVE FS-CREE977      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-WRIT    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2240-EXIT.                 EXIT.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2250-READ-REGDET           SECTION.

           READ CREE977
                FORMAT KV-IDFMTDLG-D01 INDIC CREB000-TBINDARA
                AT END CONTINUE
           END-READ

           IF  CREE977-OK
               MOVE IDUTILIZ OF CREE977D01-I TO CREB977-IDUTILIZ-V
               MOVE NMUTILIZ OF CREE977D01-I TO CREB977-NMUTILIZ-V
               MOVE IDEMPRSA OF CREE977D01-I TO CREB977-IDEMPRSA-V
               MOVE IDBALCAO OF CREE977D01-I TO CREB977-IDBALCAO-V
               MOVE IDGRFUNC OF CREE977D01-I TO CREB977-IDGRFUNC-V
               MOVE DTINISIT OF CREE977D01-I TO WS-DATA-N08
               MOVE HRINISIT OF CREE977D01-I TO WS-HORA-N06
               MOVE WS-DTHR-N14              TO CREB977-DHINISIT-V
               MOVE IDVARCSR OF CREE977D01-I TO CREB000-IDVARCSR
               MOVE IDTECLAF OF CA-CREE977   TO CREB000-IDTECLAF
           ELSE
               MOVE FS-CREE977               TO CREB000-CDRETORN
               MOVE KV-IDDIALOG              TO CREB000-NMOBJECT
               SET  CREB000-READ             TO TRUE
               SET  CREB000-MSGDLOG          TO TRUE
           END-IF
           .
       P2250-EXIT.                 EXIT.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P2300-CLSE-DIALOG           SECTION.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           CLOSE CREE977

           IF  NOT CREE977-OK
               MOVE FS-CREE977      TO CREB000-CDRETORN
               MOVE KV-IDDIALOG     TO CREB000-NMOBJECT
               SET  CREB000-CLSE    TO TRUE
               SET  CREB000-MSGDLOG TO TRUE
           END-IF
           .
       P2300-EXIT.                 EXIT.

      *----------------------------------------------------------------*
       P3000-FIM                   SECTION.
      *----------------------------------------------------------------*

           GOBACK
           .
       P3000-EXIT.                 EXIT.
      *--- FIM DO PROGRAMA CREP977 ------------------------------------*
