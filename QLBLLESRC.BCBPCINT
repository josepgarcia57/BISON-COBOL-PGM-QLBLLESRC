       IDENTIFICATION DIVISION.
      *================================================================*
       PROGRAM-ID.    BCBPCINT   INITIAL.
       AUTHOR.        PEDRO GARCIA
       INSTALLATION.  BBI SA.
       DATE-WRITTEN.  2017-MAR-13.
     š*----------------------------------------------------------------*
     š*   APLICACO .......: BCB - Centralização de Responsabilidades   *
     š*   OBJECTIVO ......: Este programa destina-se a Extrair Info    *
     š*                     a enviar ao Banco de Portugal Interv.      *
     š*   ANALISTA .......: PEDRO GARCIA                               *
     š*   PROGRAMADOR.....:                                            *
     š*================================================================*
       ENVIRONMENT DIVISION.
      *================================================================*
      *----------------------------------------------------------------*
       CONFIGURATION SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.    IBM-AS400.
       OBJECT-COMPUTER.    IBM-AS400.
       SPECIAL-NAMES.   DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       DATA DIVISION.
      *================================================================*
       FILE SECTION.
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       WORKING-STORAGE SECTION.
      *-----------------------------------------------------------------
      *
       01 TRATA-INFO                 PIC 9(01).
          88 SIM-TRATA               VALUE 1.
          88 NAO-TRATA               VALUE 0.
      *
       01 EXECUTA-P001               PIC 9(01).
          88 INICIO-P001             VALUE 0.
          88 FIM-P001                VALUE 1.
      *
       01 NR-ADM                     PIC 9(06) VALUE ZEROS.
       01 NR-READ                    PIC 9(06) VALUE ZEROS.
       01 NR-PROC                    PIC 9(06) VALUE ZEROS.
       01 NR-ERR                     PIC 9(06) VALUE ZEROS.

       01 W-DATA-SYS                 PIC 9(08).
       01 W-DATA-SYS-R REDEFINES W-DATA-SYS.
          05 W-DATA-SYS-SC           PIC 9(02).
          05 W-DATA-SYS-AAMMDD       PIC 9(06).

       01 W-DATA-EDITADA             PIC X(10) VALUE ZEROS.
       01 W-DATA-R2 REDEFINES W-DATA-EDITADA.
          05 WS-ANOIN                PIC X(04).
          05 W-DATA-SEP1             PIC X(01).
          05 WS-MESIN                PIC X(02).
          05 W-DATA-SEP2             PIC X(01).
          05 WS-DIAIN                PIC X(02).
     š*
       01 WS-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 FILLER                  PIC X VALUE "-".
          03 WS-MESIN                PIC X(02).
          03 FILLER                  PIC X VALUE "-".
          03 WS-DIAIN                PIC X(02).
     š*
       01 WX-DATA-CONVER.
          03 WS-ANOIN                PIC X(04).
          03 WS-MESIN                PIC X(02).
          03 WS-DIAIN                PIC X(02).
       01 WR-DATA-CONVER  REDEFINES  WX-DATA-CONVER     PIC X(8).
     š*
       01 WX-ANO-CONVER.
          03 WS-ANO-12               PIC X(02).
          03 WS-ANO-34               PIC X(02).

       01  W-CONSTANTS.
           05  K-APLICACAO           PIC X(3)    VALUE "BPC".
           05  K-PROGRAMA            PIC X(8)    VALUE "BCBPCINT".

      *----------------------------------------------------------------*
      * AREAS LIGACAO C/ SUB-ROTINAS
      *----------------------------------------------------------------*
     š* Parametros p/ Intervenientes
           COPY BCBBCINT.
      *----------------------------------------------------------------*
      *    DECLARACOES P/ INTERFACE C/ DB2
      *----------------------------------------------------------------*
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.

           EXEC SQL
                INCLUDE SQLCA
           END-EXEC.

      * COPY BOOK DO MODULO DE ERROS
           EXEC SQL INCLUDE CPYW999 END-EXEC.

       01  WS-TMS-NULA     PIC  X(26)
                           VALUE "0001-01-01-00.00.00.000000".
       01  WS-DATA-DIA.
           03  WS-ANOIN-DIA         PIC 9(04).
           03  FILLER               PIC X VALUE "-".
           03  WS-MESIN-DIA         PIC 9(02).
           03  FILLER               PIC X VALUE "-".
           03  WS-DIAIN-DIA         PIC 9(02).

     š* Copy : Tabela BCB BP - CNTL - Controlo
       01  R-BCBJCNTL.
           COPY DDS-ALL-FORMATS OF BCBJCNTL.

     š* Copy : Tabela de KYCT900A
       01  R-KYCT900A.
           COPY DDS-ALL-FORMATS OF KYCT900A.

       01 WS-NURACI                PIC X(07).
       01 WS-NOVA-VERSAO           PIC XXX.

     š*--Selecção de Intervenientes BBI
           EXEC SQL DECLARE CURCINT CURSOR FOR
           SELECT DISTINCT(NURACI)
             FROM
              ( SELECT   REFORIG AS NURACI
                         FROM  BCBJCCNTI
                UNION
                SELECT   NURACI  AS NURACI
                         FROM  BCBJCCNTI   ) AS X
                WHERE NURACI <> " "
           END-EXEC.

           EXEC SQL END  DECLARE SECTION END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE SECTION.
      *----------------------------------------------------------------*
      *================================================================*
       PROCEDURE DIVISION.
      *================================================================*
       P000-INICIO.
           EXEC SQL
                 WHENEVER  SQLERROR  CONTINUE
           END-EXEC.
           PERFORM P001-INICIO
           PERFORM P300-GET-BCBJCNTL
           PERFORM P100-OPEN-CURSOR-CURINT
           PERFORM P200-FETCH-CURSOR-CURINT
           PERFORM P300-PROCESSO-GERAL
                           UNTIL FIM-P001
           PERFORM P400-CLOSE-CURSOR-CURINT
           PERFORM P999-FIMPGM.
     š*---------------------------------------------------------------------
       P001-INICIO.
     š*---------------------------------------------------------------------
           SET  INICIO-P001         TO  TRUE.
           EXEC SQL SET :S-SYSTMST = CURRENT TIMESTAMP END-EXEC.
           MOVE S-SYSTMST(1:10) TO  WS-DATA-DIA.
     š*---------------------------------------------------------------------
       P300-GET-BCBJCNTL.
     š*---------------------------------------------------------------------
     š*- Último Envio

           EXEC SQL
             SELECT     X.ANO
                     ,  X.MES
                     ,  X.VERSAOBCB
                     ,  X.REMET
                     ,  X.REPOR
                     ,  X.DESTI
                     ,  X.DTCRIACAO
                     ,  X.VERSAO
                     ,  X.VERSUB
                     ,  X.NOMEFICH
                     ,  X.ESTADO
                     ,  X.NREC
                     ,  X.NREJ
                     ,  X.NINT
                     ,  X.NALERTA
                     ,  X.TMSCRIA
                     ,  X.TMSRET
             INTO      :BCBJCNTL.ANO
                     , :BCBJCNTL.MES
                     , :BCBJCNTL.VERSAOBCB
                     , :BCBJCNTL.REMET
                     , :BCBJCNTL.REPOR
                     , :BCBJCNTL.DESTI
                     , :BCBJCNTL.DTCRIACAO
                     , :BCBJCNTL.VERSAO
                     , :BCBJCNTL.VERSUB
                     , :BCBJCNTL.NOMEFICH
                     , :BCBJCNTL.ESTADO
                     , :BCBJCNTL.NREC
                     , :BCBJCNTL.NREJ
                     , :BCBJCNTL.NINT
                     , :BCBJCNTL.NALERTA
                     , :BCBJCNTL.TMSCRIA
                     , :BCBJCNTL.TMSRET
             FROM  BCBJCNTL  X
                WHERE TMSCRIA = ( SELECT MAX(TMSCRIA)
                                  FROM BCBJCNTL Y
                                  WHERE X.MES = Y.MES
                                   AND  X.ANO = Y.ANO )
                  AND ESTADO = "ENV"
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.
           EVALUATE TRUE
           WHEN SQLCODE-OK
              MOVE VERSAO OF BCBJCNTL TO WS-NOVA-VERSAO
           WHEN OTHER
              MOVE "BCBPCINT"        TO W-OBJECT-NAME
              SET CMD-SELECT         TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

           MOVE S-SYSTMST(1:10) TO WS-DATA-DIA.
           MOVE ANO OF BCBJCNTL TO WS-ANOIN-DIA.
           MOVE MES OF BCBJCNTL TO WS-MESIN-DIA.
     š*---------------------------------------------------------------------
       P100-OPEN-CURSOR-CURINT.
     š*---------------------------------------------------------------------
     š*-
           EXEC SQL
                OPEN CURCINT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET  INICIO-P001         TO TRUE
           WHEN OTHER
              SET     FIM-P001         TO TRUE
              MOVE "CURCINT  "         TO W-OBJECT-NAME
              SET CMD-OPEN-CURSOR      TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P200-FETCH-CURSOR-CURINT.
     š*****************************************************************
     š*-
           EXEC SQL FETCH CURCINT
                     INTO
                    :WS-NURACI
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
             ADD 1 TO NR-READ
     š*-
           WHEN SQLCODE-NOTFOUND
              SET  FIM-P001          TO TRUE
           WHEN OTHER
              SET   FIM-P001         TO TRUE
              MOVE "CURINT   "       TO W-OBJECT-NAME
              SET   CMD-FETCH-CURSOR TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.
     š*-
     š*****************************************************************
       P300-PROCESSO-GERAL.
     š*****************************************************************
     š*-
           IF SQLCODE-OK
              PERFORM P400-CALL-BCBSCINT
           END-IF.
           PERFORM P200-FETCH-CURSOR-CURINT.
     š*----------------------------------------------------------------
       P400-CALL-BCBSCINT.
     š*----------------------------------------------------------------
           INITIALIZE              BCBBCINT-LKPARMS.
           MOVE WS-NURACI      TO  BCBBCINT-RACINE
           MOVE WS-NOVA-VERSAO TO  BCBBCINT-VERSAO
           MOVE WS-DATA-DIA    TO  BCBBCINT-DTPROC

           CALL "BCBSCINT" USING  BCBBCINT-LKPARMS
           IF BCBBCINT-PROC-OK
              CONTINUE
           ELSE
             SET  FIM-P001          TO TRUE
             INITIALIZE   KYCT900A
             MOVE BCBBCINT-DESCERR  TO ERRMSG   OF  KYCT900A
             MOVE BCBBCINT-CODERR   TO ERRCOD   OF  KYCT900A
             MOVE "BCBSCINT"        TO ERRPRG   OF  KYCT900A
             MOVE "BCB"             TO ERRAPL   OF  KYCT900A
             MOVE BCBBCINT-IDMSGEXE TO ERROBJ   OF  KYCT900A
             MOVE  1                TO ERRRTC   OF  KYCT900A
             PERFORM P995-INSERT-ERROS
           END-IF.
     š*-
     š*****************************************************************
       P400-CLOSE-CURSOR-CURINT.
     š*****************************************************************
     š*-
           EXEC SQL
               CLOSE CURCINT
           END-EXEC.

           MOVE SQLCODE  TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              SET     FIM-P001         TO TRUE
           WHEN OTHER
              SET     FIM-P001         TO TRUE
              MOVE "CURSOR-CURINT"     TO W-OBJECT-NAME
              SET CMD-CLOSE-CURSOR     TO TRUE
              PERFORM  P990-ERRO-DB2
           END-EVALUATE.

      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P990-ERRO-DB2.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE  SQLCODE                 TO  W-SQLCODE

           EVALUATE TRUE
           WHEN SQLCODE-NOTFOUND
                  STRING "ERRO DE DADOS: CHAVE INEXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                         W-OBJECT-NAME            DELIMITED BY SPACE
                         ">"                      DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DUPLKEY
                 STRING "ERRO DE DADOS: CHAVE JÁ EXISTENTE NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
           WHEN SQLCODE-URESOURC
                STRING "ERRO DE SISTEMA: TABELA <"
                                                  DELIMITED BY SIZE
                       W-OBJECT-NAME              DELIMITED BY SPACE
                       "> INDISPONÍVEL. CONTACTE RESPONSÁVEL"
                                                  DELIMITED BY SIZE
                  INTO MSG-ERRO-SQL
           WHEN SQLCODE-DLKTMOUT
               STRING "INFO: TABELA <"            DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      "> MOMENTANEAMENTE INDISPONÍVEL. TENTE DE NOVO"
                                                  DELIMITED BY SIZE
                 INTO MSG-ERRO-SQL
           WHEN OTHER
               STRING "ERRO: OBJECTO <"           DELIMITED BY SIZE
                      W-OBJECT-NAME               DELIMITED BY SPACE
                      ">, FUNCAO <"               DELIMITED BY SIZE
                      W-CMDEXEC                   DELIMITED BY SPACE
                      ">, C/ SQLCODE <"
                      W-EDTSQLC
                      ">."                        DELIMITED BY SIZE
                      INTO MSG-ERRO-SQL
           END-EVALUATE.
     š*
           INITIALIZE   KYCT900A
           MOVE MSG-ERRO-SQL    TO ERRMSG   OF  KYCT900A
           MOVE W-SQLCODE       TO ERRCOD   OF  KYCT900A
           MOVE "SQL"           TO ERRAPL   OF  KYCT900A
           MOVE W-OBJECT-NAME   TO ERROBJ   OF  KYCT900A
           MOVE  2              TO ERRRTC   OF  KYCT900A

           PERFORM    P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
       P995-INSERT-ERROS.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *

           MOVE   "BCBPCINT"      TO  ERRPRG OF KYCT900A.
           MOVE   WS-NURACI      TO  RACINE OF KYCT900A.

           EXEC SQL  INSERT
                INTO KYCT900A
                VALUES  (
                           :KYCT900A.RACINE
                         , :KYCT900A.REFOPE
                         , CURRENT TIMESTAMP
                         , :KYCT900A.ERRRTC
                         , :KYCT900A.ERRCOD
                         , :KYCT900A.ERRMSG
                         , :KYCT900A.ERRPRG
                         , :KYCT900A.ERRAPL
                         , :KYCT900A.ERROBJ
                         , :KYCT900A.ERRFLD
                         , :KYCT900A.ERRCMD
                         , :KYCT900A.ERRKEY1
                         , :KYCT900A.ERRVAL1
                         , :KYCT900A.ERRKEY2
                         , :KYCT900A.ERRVAL2 )
           END-EXEC.

     š*-
           MOVE SQLCODE          TO  W-SQLCODE, W-EDTSQLC.

           EVALUATE TRUE
           WHEN SQLCODE-OK
              ADD      1            TO  NR-ERR
           WHEN OTHER
              MOVE "KYCT900A"       TO W-OBJECT-NAME
              SET  CMD-INSERT       TO TRUE
              STRING "ERRO DE DADOS AO INSERIR ERRO  NA TABELA <"
                                                  DELIMITED BY SIZE
                        W-OBJECT-NAME             DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                        W-SQLCODE                 DELIMITED BY SPACE
                        ">"                       DELIMITED BY SIZE
                   INTO MSG-ERRO-SQL
              DISPLAY MSG-ERRO-SQL
           END-EVALUATE.


     š*****************************************************************
       P999-FIMPGM.
     š*****************************************************************
           EXEC SQL    COMMIT    HOLD         END-EXEC
           EXIT PROGRAM.
           STOP RUN.
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *    FIM DO PROGRAMA BCBPCINT
      *- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
